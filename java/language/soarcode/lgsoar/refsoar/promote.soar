# File: promote.soar
# Author: William Taysom
# Created: July 7, 2001
#
# Desc: Promotes semantics from ref to object.
#
# Promotions:
#		basic (70-60), feature (70)
#
# 7 June 2011: soar9 conversion: DWL removed attr prefs, added o-support
#

## basic promotion

sp {promote*propose*direct-promotion
   (state <s> ^current-refset <refs> ^model <m>)
   (<refs> ^ref <main> <arg>)
   (<main> ^pointer <imain> ^head.wcount <main-wcount> ^head.wvalue <main-wval>)
   (<arg> ^pointer <iarg> ^head.wcount <arg-wcount> ^head.wvalue <arg-wval>)
   (<imain> -^<aug> <iarg>)
   # this is different than the indirect case
   (<main> ^<aug> <arg>)
-->
   (<s> ^operator <o> +)
   (<o> ^name basic ^priority 70 ^imain <imain> ^aug <aug> ^iarg <iarg> ^ref <main>
   	 ^order <arg-wcount> ^level-two-order <main-wcount> 
     ^debug-info (concat |directly promoting: idea for "|<main-wval>|" gets a | <aug> | of "| <arg-wval> |"|))
}

sp {promote*propose*indirect-promotion
   (state <s> ^current-refset <refs> ^model <m>)
   (<refs> ^ref <main> <arg>)
   (<main> ^pointer <imain> ^head.wcount <main-wcount> ^head.wvalue <main-wval>)
   (<arg> ^pointer <iarg> ^head.wcount <arg-wcount> ^head.wvalue <arg-wval>)
   (<imain> -^aug <iarg>)
   # this is different than the direct case
   (<iarg> ^{<> aug <aug>} <imain>)
-->
   (<s> ^operator <o> +)
   (<o> ^name basic ^priority 60 ^imain <imain> ^aug aug ^iarg <iarg> ^ref <main>
   	 ^order <arg-wcount> ^level-two-order <main-wcount> 
     ^debug-info (concat |indirectly promoting: idea for "|<main-wval>|" gets an aug of "| <arg-wval> |"|))
}

sp {promote*apply*direct-promotion
 #  :o-support
   (state <s> ^operator <o>)
   (<o> ^name basic ^imain <imain> ^aug <aug> ^iarg <iarg>)
-->
   (<imain> ^<aug> <iarg> +)
}

sp {promote*apply*direct-promotion*preposition-flag
   (state <s> ^operator <o>)
   (<o> ^name basic ^imain <imain> ^ref.preposition-lex <prep>)
-->
   (<imain> ^preposition-lex <prep>)
}

## feature promotion

sp {promote*propose*feature-promotion*infl
   (state <s> ^current-refset <refs> ^model <m>)
   (<refs> ^ref <main>)
   (<main> ^pointer <imain> ^infl <feature> ^head.wcount <main-wcount>)
   (<imain> -^infl <feature>)
-->
   (<s> ^operator <o> +)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug infl ^feature <feature>
   	 ^order <main-wcount> ^level-two-order 0 )}

sp {promote*propose*feature-promotion*neg
   (state <s> ^current-refset <refs> ^model <m>)
   (<refs> ^ref <main>)
   (<main> ^pointer <imain> ^neg not ^head.wcount <main-wcount>)
   (<imain> -^neg not)
-->
   (<s> ^operator <o> +)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug neg ^feature not
   	 ^order <main-wcount> ^level-two-order 1 )}

sp {promote*propose*feature-promotion*mode
   (state <s> ^current-refset <refs> ^model <m>)
   (<refs> ^ref <main>)
   (<main> ^pointer <imain> ^mode <mode> ^head.wcount <main-wcount>)
   (<imain> -^mode <mode>)
-->
   (<s> ^operator <o> +)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug mode ^feature <mode>
   	 ^order <main-wcount> ^level-two-order 2 )}

sp {promote*propose*feature-promotion*questioned
   (state <s> ^current-refset <refs> ^model <m>)
   (<refs> ^ref <main>)
   (<main> ^pointer <imain> ^questioned <feature> ^head.wcount <main-wcount>)
   (<imain> -^questioned <feature>)
-->
   (<s> ^operator <o> +)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug questioned ^feature <feature>
   	 ^order <main-wcount> ^level-two-order 0 )}

sp {promote*apply*feature-promotion
#  :o-support
   (state <s> ^operator <o>)
   (<o> ^name feature ^priority 70 ^imain <imain> ^aug <aug> ^feature <feature> )
-->
   (<imain> ^<aug> <feature>)
}

# role preposition structures added for BOLT
# ONLY for "of" for now..

# "The color of the block is red"
# DEF(N2)
# DEF(N3)
# of(N2,N3)
# block(N3)
# color(N2)
# color-of(N2,N3) <== adds this
# red(N2)
# This makes "the color of the block is red" different from "the red of the
# block is color"

sp {global*apply*generate-predicates*role-preposition
   (state <s> ^model.idea <idea> ^final-predicates <fp> 
              ^operator.name generate-predicates) 
   (<idea> ^nuc <nucval> ^preposition-lex <plex> ^<plex> <pobjval>)
   (<pobjval> ^nuc <nuc2val> ^aug <aug2val> ^preposition-lex {of <pl2>} ^<pl2> <prep> ^sentence-count <sc>)
-->
   (<pobjval> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer1 <nuc2val>
           ^outer2 <pl2>
           ^inner1 <prep>
           ^inner2 <pobjval>
           ^originating-sentence <sc>)
}

sp {global*apply*generate-predicates*role-preposition*copula*ext
   (state <s> ^model.idea <idea> ^final-predicates <fp> 
              ^operator.name generate-predicates) 
   (<idea> ^ext <ext> ^nuc <nucval> ^annotation copula) 
   (<ext> ^nuc <thisnucval> ^preposition-lex {of <pl>} ^<pl> <prep> ^sentence-count <sc>)
-->
   (<ext> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer1 <thisnucval>
           ^outer2 <pl>
           ^inner1 <prep>
           ^inner2 <ext>
           ^originating-sentence <sc>)
}

sp {global*apply*generate-predicates*role-preposition*copula*int
   (state <s> ^model.idea <idea> ^final-predicates <fp> 
              ^operator.name generate-predicates) 
   (<idea> ^int <int> ^ext <ext> ^nuc <nucval> ^annotation copula) 
   (<int> ^nuc <thisnucval> ^preposition-lex {of <pl>} ^<pl> <prep>)
   (<ext> ^sentence-count <sc>)
-->
   (<int> ^annotation pred-dumped +)
   (<fp> ^predicate <pred>)
   (<pred> ^outer1 <thisnucval>
           ^outer2 <pl>
           ^inner1 <prep>
           ^inner2 <ext>
           ^originating-sentence <sc>)
}

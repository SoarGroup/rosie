sp {generate-preposition-message*propose*init-state
   (state <s> ^superstate.operator <o>)
   (<o> ^name generate-preposition-message
        ^results <r>)
-->
   (<s> ^problem-space message-interpretation
        ^name generate-preposition-message
        ^results <r>)
}

sp {generate-preposition-message*propose*init-message
   (state <s> ^name generate-preposition-message
              ^results <r>
             -^msg <m>)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-message)
}

sp {generate-preposition-message*apply*init-message
   (state <s> ^name generate-preposition-message
              ^results <r>
              ^operator <o>)
   (<o> ^name init-message)
-->
   (<s> ^msg <msg>)
   (<msg> ^first <w1>
          ^word <w1> <w2> <w3> <w4>
          ^last <w4>)
   (<w1> ^value I
         ^next <w2>)
   (<w2> ^value know
         ^next <w3>)
   (<w3> ^value the
         ^next <w4>)
   (<w4> ^value |prepositions:|)
}

sp {generate-preposition-message*prefer*init-message*not-empty
   (state <s> ^name generate-preposition-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name init-message-empty)
   (<o2> ^name init-message)
-->
   (<s> ^operator <o2> < <o1>)
}

sp {generate-preposition-message*propose*init-message-empty
   (state <s> ^name generate-preposition-message
             -^results.result.name <n>
             -^msg)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-message-empty)
}

sp {generate-preposition-message*apply*init-message-empty
   (state <s> ^name generate-preposition-message
              ^results <r>
              ^operator <o>)
   (<o> ^name init-message-empty)
-->
   (<s> ^msg <msg>)
   (<msg> ^first <w1>
          ^word <w1> <w2> <w3> <w4> <w5> <w6>
          ^last <w6>)
   (<w1> ^value I
         ^next <w2>)
   (<w2> ^value |don't|
         ^next <w3>)
   (<w3> ^value know
         ^next <w4>)
   (<w4> ^value any
         ^next <w5>)
   (<w5> ^value prepositions
         ^next <w6>)
   (<w6> ^value |.|)
}

sp {generate-preposition-message*prefer*lower*add-word-to-msg
   (state <s> ^name generate-preposition-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-word-to-msg
         ^word <w1>)
   (<o2> ^name add-word-to-msg
         ^word {<w2> < <w1>})
-->
   (<s> ^operator <o1> < <o2>)
}

sp {generate-preposition-message*propose*add-word-to-msg
   (state <s> ^name generate-preposition-message
              ^msg <msg>
              ^results.result <r>)
   (<r> ^name <word>
       -^handled true)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-word-to-msg
        ^word <word>
        ^result <r>)
}

sp {generate-preposition-message*apply*add-word-to-msg
   (state <s> ^name generate-preposition-message
              ^msg <msg>
              ^operator <o>)
   (<o> ^name add-word-to-msg
        ^word <word>
        ^result <r>)
   (<msg> ^last <oldlast>)
-->
   (<msg> ^last <oldlast> -)
   (<msg> ^last <wnew>)
   (<msg> ^word <wnew>)
   (<wnew> ^value <word>)
   (<oldlast> ^next <wnew>)
   (<r> ^handled true)
}

sp {generate-preposition-message*propose*copy-to-output-link
   (state <s> ^name generate-preposition-message
              ^msg <msg>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name copy-to-output-link
        ^msg <msg>)
}

sp {generate-preposition-message*apply*copy-to-output-link
   (state <s> ^name generate-preposition-message
              ^operator <o>
              ^top-state.io.output-link <ol>
              ^results <r>)
   (<o> ^name copy-to-output-link
        ^msg <msg>)
-->
   (<ol> ^message <msg>)
   (<r> ^message-generated true)
}



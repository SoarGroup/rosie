# This runs after the parser, before LGSoar.
# 
# - If the parse fails, try reparsing as a noun phrase
# - If there are multiple parses, allow Soar knowledge to select a non-default
# parse. Specifically:
# "Pick up the square to the right of the triangle"
# This parses by attaching the preposition to the verb, not the object.
# Need to detect the verb ("pick up"), get knowledge that it has no PP. Then,
# select best parse that doesn't give it one.
#
# "Describe the relationship between the arch and the triangle."
# Similarly, the "between" attaches to "describe" wrongly in the first parse.

# elaborations to help parse matching
# NOT attached to this subgoal, but used here
sp {elaborate*parse*left
   (state <s> ^superstate nil
              ^io.input-link.lg.parsed-sentence <sent>)
   (<sent> ^links.link <lk>
           ^words.word <wd>)
   (<wd> ^wcount <ct>)
   (<lk> ^lwleft <ct>)
-->
   (<lk> ^left <wd>)
}

sp {elaborate*parse*right
   (state <s> ^superstate nil
              ^io.input-link.lg.parsed-sentence <sent>)
   (<sent> ^links.link <lk>
           ^words.word <wd>)
   (<wd> ^wcount <ct>)
   (<lk> ^lwright <ct>)
-->
   (<lk> ^right <wd>)
}

sp {select-parse*elaborate*init
   (state <s> ^name select-parse
              ^superstate.operator.sentence-count <ct>
              ^top-state.io.input-link.lg.parsed-sentence <sent>)
   (<sent> ^sentence-count <ct>
           ^parse-count <pc>
           ^expensive-link-cost <elc>
           ^unused-word-cost <uwc>)
-->
   (<s> ^parse <ps>)
   (<ps> ^sentence <sent>
         ^parse-count <pc>
         ^expensive-link-cost <elc>
         ^unused-word-cost <uwc>
         ^extra-cost 0)
}

sp {select-parse*elaborate*context*which-question*from-stack
   (state <s> ^name select-parse
              ^top-state.interaction.stack.segment.purpose.type which-question)
-->
   (<s> ^context which-question)
}

# Allow the input to force a context (for debugging) by prepending
# context_which_question to the sentence. Preprocessing catches this and maps
# it to null, use the presence of that mapping to adjust context here.
sp {select-parse*elaborate*context*which-question*from-input
   (state <s> ^name select-parse
              ^top-state.sentence-mapping.word.real-wvalue context_which_question)
-->
   (<s> ^context which-question)
}

sp {select-parse*elaborate*report-context
   (state <s> ^name select-parse
              ^context <context>)
-->
   (write (crlf) |evaluating under context: | <context>)
}

sp {select-parse*elaborate*acceptable-parse
   (state <s> ^name select-parse
              ^parse <parse>)
   (<parse> -^rejected)
-->
   (<s> ^acceptable-parse <parse>)
}

sp {select-parse*elaborate*noun-phrase-cost*true
   (state <s> ^name select-parse
              ^parse <parse>)
   (<parse> ^sentence.words.word.wvalue NOUN-PHRASE-WALL)
-->
   (<parse> ^noun-phrase-cost 1)
}

sp {select-parse*elaborate*noun-phrase-cost*false
   (state <s> ^name select-parse
              ^parse <parse>)
   (<parse> -^sentence.words.word.wvalue NOUN-PHRASE-WALL)
-->
   (<parse> ^noun-phrase-cost 0)
}

sp {select-parse*elaborate*weighting*baseline
   (state <s> ^name select-parse
             -^context which-question
              ^parse <parse>)
   (<parse> ^expensive-link-cost <elc>
            ^extra-cost <ec>
            ^noun-phrase-cost <npc>)
-->
   (<parse> ^cost (+ <elc> <npc> <ec>))
}

# don't penalize noun phrases if we are expecting one
sp {select-parse*elaborate*weighting*which-question
   (state <s> ^name select-parse
              ^context which-question
              ^parse <parse>)
   (<parse> ^expensive-link-cost <elc>
            ^extra-cost <ec>
            ^noun-phrase-cost <npc>)
-->
   (<parse> ^cost (+ <elc> <ec>))
}

# eliminate all sentences completely that have excess unused words
# Can't think of cases where the correct parse has more unused words than an
# incorrect parse..
# If one occurs, we will have to integrate unused-word-cost into the overall
# score. This causes problems with pp-attachment reasoning, though, e.g., "the square
# to the right". The pp system ignores all scores other than the minimal, so
# will elevate the version where "to" attaches to the verb ("square" with "the"
# unused) over the correct noun-phrase parse.

sp {select-parse*elevate*excess-unused-words
   (state <s> ^name select-parse
              ^parse <p1>
              ^parse.unused-word-cost < <uwc>)
   (<p1> ^unused-word-cost <uwc>)
-->
   (<p1> ^rejected true)
}

# make red.a square.n preferable to red.n square.a

sp {select-parse*propose*adjust-extra-cost*Ma
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <map>)
   (<map> ^sentence.links.link <lk>
         -^extra-cost-adjusted Ma)
   (<lk> ^ltype M
         ^ltypesub a)
-->
   (<s> ^operator <o> + >)
   (<o> ^name adjust-extra-cost
        ^parse <map>
        ^amount 2
        ^type Ma)
}


# fix "is the block on the table red?"
sp {select-parse*propose*adjust-extra-cost*AN
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <map>)
   (<map> ^sentence.links.link <lk>
         -^extra-cost-adjusted AN)
   (<lk> ^ltype AN)
-->
   (<s> ^operator <o> + >)
   (<o> ^name adjust-extra-cost
        ^parse <map>
        ^amount .1
        ^type AN)
}

# if there is a parse with an idiom, give it a bonus (negative added cost)
sp {select-parse*propose*adjust-extra-cost*idiom
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <map>)
   (<map> ^sentence.links.link <lk>
         -^extra-cost-adjusted ID)
   (<lk> ^ltype ID)
-->
   (<s> ^operator <o> + >)
   (<o> ^name adjust-extra-cost
        ^parse <map>
        ^amount -.1
        ^type ID)
}

sp {select-parse*adjust-extra-cost*indifferent
   (state <s> ^operator <o1> +
              ^operator {<> <o1> <o2>} +)
   (<o1> ^name adjust-extra-cost)
   (<o2> ^name adjust-extra-cost)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {select-parse*apply*adjust-extra-cost
   (state <s> ^name select-parse
              ^operator <op>)
   (<op> ^name adjust-extra-cost
         ^parse <p>
         ^amount <a>
         ^type <ty>)
   (<p> ^extra-cost <old>)
-->
   (<p> ^extra-cost <old> - 
        ^extra-cost (+ <old> <a>)
        ^extra-cost-adjusted <ty>)
}


sp {select-parse*elaborate*minimal-acceptable-parse
   (state <s> ^name select-parse
              ^acceptable-parse <parse>)
   (<parse> ^cost <pc>)
  -{(<s> ^acceptable-parse <parse2>)
    (<parse2> ^cost < <pc>)}
-->
   (<s> ^minimal-acceptable-parse <parse>)
}

sp {select-parse*propose*approve-minimal-parse
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <parse>)
-->
   (<s> ^operator <o> + < =)
   (<o> ^name approve
        ^kind interpretation
        ^parse <parse>)
}

sp {select-parse*apply*approve
   (state <s> ^name select-parse
              ^operator <o>)
   (<o> ^name approve
        ^parse.sentence <sent>)
-->
  (<sent> ^parse-valid true)
}

# "get the book on the table in the room" should be:
# book (on (the table (in (the room)))
# not book (on (the table)) (in (the room))
# LG gives these both the same cost

sp {select-parse*elaborate*multiple-m
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <mp>)
   (<mp> ^sentence.links.link <l1> {<> <l1> <l2>})
   (<l1> ^lwleft <left>
         ^ltype M)
   (<l2> ^lwleft <left>
         ^ltype M)
-->
   (<mp> ^has-multiple-m true)
}

# similarly, for O links, "describe the relationship between X and Y" should
# apply the "and" inside the relationship, not as two things to describe
# In general, then, disprefer multiple verb objects if there is an alternative.
sp {select-parse*elaborate*multiple-o
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <mp>)
   (<mp> ^sentence.links.link <l1> {<> <l1> <l2>})
   (<l1> ^lwleft <left>
         ^ltype O)
   (<l2> ^lwleft <left>
         ^ltype O)
-->
   (<mp> ^has-multiple-o true)
}

sp {select-parse*prefer*parse-without-multiple-M
   (state <s> ^name select-parse
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name approve
         ^parse <mp1>)
   (<o2> ^name approve
         ^parse <mp2>)
   (<mp1> ^has-multiple-m true
          ^parse-count <pc1>)
   (<mp2> -^has-multiple-m
           ^parse-count <pc2>)
-->
   (<s> ^operator <o2> > <o1>)
   (write (crlf) |Preferring | <pc2> | over | <pc1> | due to lack of multiple M links to a word.|)
}

sp {select-parse*prefer*parse-without-multiple-0
   (state <s> ^name select-parse
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name approve
         ^parse <mp1>)
   (<o2> ^name approve
         ^parse <mp2>)
   (<mp1> ^has-multiple-o true
          ^parse-count <pc1>)
   (<mp2> -^has-multiple-o
           ^parse-count <pc2>)
-->
   (<s> ^operator <o2> > <o1>)
   (write (crlf) |Preferring | <pc2> | over | <pc1> | due to lack of multiple O links to a word (likely an innermost "and" case).|)
}

# prefer adjectives over prepositions in general, this occurs with
# "This is (unlearned word)"
sp {select-parse*prefer*Pa*versus*others
   (state <s> ^name select-parse
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name approve
         ^parse <pa>)
   (<o2> ^name approve
         ^parse <other>)
   (<pa> ^sentence.links.link <pal>
         ^parse-count <pac>)
   (<pal> ^lvalue Pa
          ^left.global-wcount <lc>
          ^right.global-wcount <rc>)
   (<other> ^sentence.links.link <ol>
            ^parse-count <oc>)
   (<ol> ^lvalue << Pp MVp Ost >>
         ^left.global-wcount <lc>
         ^right.global-wcount <rc>)
-->
   (<s> ^operator <o1> > <o2>)
   (write (crlf) |Preferring parse |<pac> | over |<oc> | due to Pa link preference.|)
}

# Reject minimal-acceptable-parses with bad pp attachments, even though LG may
# regard the proper attachment as worse (higher dis cost)

# MV links are verb->preposition, M links are noun->preposition

# "move (the sandwich) to (the plate  on  the table)" (1) to:MV on:M
# "move (the sandwich  on  the plate) to (the table)" (2) on:M  to:MV
# "move (the sandwich  on  the table) to (the plate)" (3) on:M  to:MV
# "move (the sandwich) to (the table) on (the plate)" (4) to:MV on:MV
# "move (the sandwich  on  the plate  on  the table)" (5) on:M  on:M
# "pick up (the fork   on  the left   of  the plate)" (6) on:M  of:M
# "pick up (the fork   to  the left   of  the plate)" (7) to:M  of:M
#        
# heuristic: "to" is an MV, "on" is an M
#  Case 4 doesn't work, needs knowledge that tables don't sit on plates, and that
# things can be moved on plates.
#
#  Case 7 doesn't work, needs knowledge that "pick(up)" is a verb that doesn't
#  normally take a preposition.
#
# We'll at least do better than LGP alone, since that tends to assign both as MV

# propose a resolution operator if there is a link that could be M or MV, and
# one of those possiblities is in the highest-ranked parse

# use global-wcounts here, which don't always start at 0 or correspond with the
# links, but are consistent parse-to-parse (where extra words may have been
# added)

# M links have a DIS cost of 1 more than the equivalent MV
sp {select-parse*elaborate*expensive-link-cost*plus-1
   (state <s> ^name select-parse
              ^parse <ps>)
   (<ps> ^cost <elc>)
-->
   (<ps> ^cost-plus-1 (+ <elc> 1))
}

sp {select-parse*elaborate*preposition-attachment-ambiguity*first-MV
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <p1>
              ^acceptable-parse <p2>)
   (<p1> ^sentence.links.link <mv>
         ^cost-plus-1 <elc>)
   (<p2> ^cost <ct>
         ^sentence.links.link <m>
         ^cost <elc>)
   (<mv> ^ltype MV
         ^left.wvalue <mv-left-word>
         ^right.wvalue <right-val>
         ^right.global-wcount <right-count>)
   (<m> ^ltype M
        ^left.wvalue <m-left-word>
        ^right.global-wcount <right-count>)
-->
   (<s> ^preposition-attachment-ambiguity <paa>)
   (<paa> ^parse-rank <ct>
          ^prep-rank <right-count>
          ^m-left <m-left-word>
          ^mv-left <mv-left-word>
          ^right <right-val>
          ^m-parse <p2>
          ^mv-parse <p1>)
}


sp {select-parse*elaborate*preposition-attachment-ambiguity*first-M
   (state <s> ^name select-parse
              ^acceptable-parse <p1>
              ^minimal-acceptable-parse <p2>)
   (<p1> ^cost <ct>
         ^sentence.links.link <mv>
         ^cost-plus-1 <elc>)
   (<p2> ^sentence.links.link <m>
         ^cost <elc>)
   (<mv> ^ltype MV
         ^left.wvalue <mv-left-word>
         ^right.wvalue <right-val>
         ^right.global-wcount <right-count>)
   (<m> ^ltype M
        ^left.wvalue <m-left-word>
        ^right.global-wcount <right-count>)
-->
   (<s> ^preposition-attachment-ambiguity <paa>)
   (<paa> ^parse-rank <ct>
          ^prep-rank <right-count>
          ^m-left <m-left-word>
          ^mv-left <mv-left-word>
          ^right <right-val>
          ^m-parse <p2>
          ^mv-parse <p1>)
}

sp {select-parse*elaborate*leftmost-ambiguity
   (state <s> ^name select-parse
             -^preposition-attachment-ambiguity.prep-rank < <pr>
              ^preposition-attachment-ambiguity <paa>)
   (<paa> ^prep-rank <pr>)
-->
   (<paa> ^minimal-prep-rank true)
}

sp {select-parse*elaborate*lowest-parse-ambiguity
   (state <s> ^name select-parse
              ^preposition-attachment-ambiguity <paa>)
   (<paa> ^parse-rank <pr>
          ^minimal-prep-rank true)
  -{(<s> ^preposition-attachment-ambiguity <paa2>)
    (<paa2> ^minimal-prep-rank true
            ^parse-rank < <pr>)}
-->
   (<paa> ^minimal-parse-rank true)
}

sp {select-parse*propose*resolve-ambiguous-preposition-attachment
   (state <s> ^name select-parse
              ^preposition-attachment-ambiguity <paa>)
   (<paa> ^minimal-parse-rank true
          ^minimal-prep-rank true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name resolve-ambiguous-preposition-attachment
        ^info <paa>)
}

# choose the M version, unless the word is "to"

sp {select-parse*apply*resolve-ambiguous-preposition-attachment*m
   (state <s> ^name select-parse
              ^operator <o>)
   (<o> ^name resolve-ambiguous-preposition-attachment
        ^info <paa>)
   (<paa> -^right << to >>
          -^mv-left << put >>
         ^right <right>
        ^mv-parse <parse>
        ^mv-parse.parse-count <rc>
        ^m-parse.parse-count <gc>)
-->
   (<parse> ^rejected true)
   (write (crlf) |rejected parse | <rc> | in favor of | <gc> | due to | <right> | link|)
}

sp {select-parse*apply*resolve-ambiguous-preposition-attachment*mv
   (state <s> ^name select-parse
              ^operator <o>)
   (<o> ^name resolve-ambiguous-preposition-attachment
        ^info <paa>)
   (<paa> ^right << to >>
        ^right <right>
        ^m-parse <parse>
        ^m-parse.parse-count <rc>
        ^mv-parse.parse-count <gc>)
-->
   (<parse> ^rejected true)
   (write (crlf) |rejected parse | <rc> | in favor of | <gc> | due to |
         <right> | link|)
}

sp {select-parse*apply*resolve-ambiguous-preposition-attachment*mv*put
   (state <s> ^name select-parse
              ^operator <o>)`
   (<o> ^name resolve-ambiguous-preposition-attachment
        ^info <paa>)
   (<paa> ^mv-left << put >>
        ^right <right>
        ^m-parse <parse>
        ^m-parse.parse-count <rc>
        ^mv-parse.parse-count <gc>)
-->
   (<parse> ^rejected true)
   (write (crlf) |rejected parse | <rc> | in favor of | <gc> | due to |
         <right> | link|)
}

sp {select-parse*elaborate*report-chosen
   (state <s> ^name select-parse
              ^parse <parse>)
   (<parse> ^sentence.parse-valid true
            ^parse-count <ct>)
-->
   (write (crlf) |select-parse chose parse | <ct> )
}

sp {select-parse*elaborate*report-not-chosen
   (state <s> ^name select-parse
              ^minimal-acceptable-parse <map>
              ^parse {<> <map> <chosen>})
   (<chosen> ^sentence.parse-valid true)
   (<map> ^parse-count <pc>)
-->
   (write (crlf) |indifferent selection could have chosen | <pc> | instead.|)
}

# For unlearned words,
# there will be one pending-dictionary-example for each parse-class passed to
# the parser. Convert that for the chosen parse to a dictionary-example, and
# remove all the pending structures.

sp {select-parse*apply*approve*save-word-example
   (state <s> ^name select-parse
              ^top-state.language <lang>
              ^operator <o>)
   (<o> ^name approve
        ^parse.sentence <sent>
        ^parse.parse-count <pc>)
   (<sent> ^words.word <wd>
        ^sentence-count <sc>)
   (<wd> ^wvalue <val>  # e.g., *nounjective3
         ^global-wcount <wc>)
   (<lang> ^pending-dictionary-example <pde>)
   (<pde> ^sentence-count <sc>
          ^wcount <wc>
          ^word <orig>           # e.g., red
          ^parser-wvalue <val>   # e.g., *nounjective3
          ^parse-class <pclass>) # e.g., *nounjective
-->
   (<lang> ^dictionary-example <de>)
   (<de> ^word <orig>
         ^parse-class <pclass>
         ^sentence-count <sc>
         ^parse-count <pc>) # if this sentence or parse is later rejected, this example should be removed
}

sp {select-parse*apply*approve*remove-pending
   (state <s> ^name select-parse
              ^top-state.language <lang>
              ^operator <o>)
   (<o> ^name approve
        ^parse.sentence.sentence-count <sc>)
   (<lang> ^pending-dictionary-example <pde>)
   (<pde> ^sentence-count <sc>)
-->
   (<lang> ^pending-dictionary-example <pde> -)
}

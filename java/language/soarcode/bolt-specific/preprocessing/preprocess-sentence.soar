# examine the words before parsing, possibly swapping them out based on Soar
# knowledge.
# Soar knowledge to mess with the sentence before the parser sees it goes here.
# Learned words will be looked up in semantic memory here, and replaced with
# generics. Possibly all words.


# preprocessing flow:
# -sentence appears on io.input-link.lg.sentence
#  structure: sentence.words.word [wvalue wcount]
# -preprocessing goal takes over, builds io.output-link.preprocessed-sentence
#  structure: preprocessed-sentence.start [word next] [word next] ..

# copy the sentence to the state, deep copy so it can be modified

sp {preprocess-sentence*elaborate*init
  (state <s> ^name preprocess-sentence
             ^superstate.operator.original-sentence <sent>)
   (<sent> ^sentence-count <sc>)
-->
   (<s> ^sentence <copy>)
   (<copy> ^sentence-count <sc>)
   (<s> ^learnable-parse-class *nounjective
        ^learnable-parse-class *preposition
        ^learnable-parse-class *verb+with+nothing
        ^learnable-parse-class *verb+with+preposition
        ^learnable-parse-class *verb+with+direct+object
        ^learnable-parse-class *verb+with+direct+object+and+preposition)
}

sp {preprocess-sentence*elaborate*copy-word
   (state <s> ^name preprocess-sentence
              ^superstate.operator.original-sentence.words.word <wd>
              ^sentence <copy>)
   (<wd> ^wcount <wc>
         ^wvalue <wv>)
-->
   (<copy> ^word <copy-word>)
   (<copy-word> ^wcount <wc>
                ^real-wvalue <wv>)
}

sp {preprocess-sentence*elaborate*real-wvalue*fixed
   (state <s> ^name preprocess-sentence
              ^sentence.word <wd>)
   (<wd> ^real-wvalue { <wv> << what which where is a an the this that those these and are done
                                color shape size of one object you your me my I yes no goal should must be 
                                pick up put pantry stove garbage table |.| |?| >>}) # fixed words
-->
   (<wd> ^parser-wvalue <wv>)
}

# excise this to enable parser learning mode
sp {preprocess-sentence*elaborate*no-learning
   (state <s> ^name preprocess-sentence
              ^sentence.word <wd>)
   (<wd> ^real-wvalue <rv>)
-->
   (<wd> ^parser-wvalue <rv>)
}

# issue a sentence-done command to remove obsolete original-sentence and
# parsed-sentences from input

# this is done here since at this time we are sure an alternate parse for the
# previous sentence shouldn't be selected

# message-predicate-sets are cleaned up as part of finish-interpretation
sp {preprocess-sentence*propose*cleanup
   (state <s> ^name preprocess-sentence
              ^io.input-link.lg.original-sentence.sentence-count <sc>
              ^top-state.language.message-predicate-set <mps>)
   (<mps> ^finished true
          ^sentence-count <sc>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name clean-old-input
        ^kind interpretation
        ^sentence-count <sc>)
}

# guessed words and skipped words and others that don't get to the UM stuff
# should set invalid true on the original-sentence
sp {preprocess-sentence*propose*cleanup
   (state <s> ^name preprocess-sentence
              ^io.input-link.lg.original-sentence <sent>)
   (<sent> ^sentence-count <sc>
           ^invalid true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name clean-old-input
        ^kind interpretation
        ^sentence-count <sc>)
}

sp {preprocess-sentence*apply*clean-old-input
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name clean-old-input
        ^sentence-count <sc>)
-->
   (<ol> ^sentence-done <sd>)
   (<sd> ^sentence-count <sc>)
}
# proposal will unmatch since the effect of this will be removing the
# original-sentence (done in Java land)

sp {preprocess-sentence*clean-old-input*over*others
   (state <s> ^name preprocess-sentence
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name clean-old-input)
   (<o2> ^name << substitute lookup-word >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {preprocess-sentence*elaborate*context-tag*which-question
   (state <s> ^name preprocess-sentence
              ^sentence.word <wd>)
   (<wd> ^real-wvalue context_which_question
        -^parser-wvalue | |)
-->
   (<s> ^operator <o> + >)
   (<o> ^name substitute
        ^word <wd>
        ^old-value context_which_question
        ^new-value | |)
}

sp {preprocess-sentence*apply*substitute*new
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^sentence.word <wd>)
   (<o> ^name substitute
        ^word <wd>
        ^new-value <new>)
-->
   (<wd> ^parser-wvalue <new>)
   (<s> ^substituted <wd>)
}

sp {preprocess-sentence*apply*substitute*old
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^sentence.word <wd>)
   (<wd> ^parser-wvalue <old>)
   (<o> ^name substitute
        ^word <wd>
        ^old-value <old>)
-->
   (<wd> ^parser-wvalue <old> -)
}

sp {preprocess-sentence*propose*output-preprocessed
   (state <s> ^name preprocess-sentence
              ^sentence <sent>)
-->
   (<s> ^operator <o> + <) # only after all preprocessing done
   (<o> ^name output-preprocessed)
}

sp {preprocess-sentence*apply*output-preprocessed*first-word
   (state <s> ^name preprocess-sentence
              ^operator.name output-preprocessed
              ^top-state.io.output-link <ol>
              ^sentence.sentence-count <ct>
              ^sentence.word <wd>)
   (<wd> ^wcount 0)
-->
   (<ol> ^preprocessed-sentence <ps>)
   (<ps> ^start <st>
         ^sentence-count <ct>)
   (<st> ^wcount 0)
   (<s> ^output-word <st>)
}

sp {preprocess-sentence*apply*output-preprocessed*next-word
   (state <s> ^name preprocess-sentence
              ^operator.name output-preprocessed
              ^sentence.word <next-word>
              ^output-word <last-word>)
   (<last-word> ^wcount <last-count>)
   (<next-word> ^wcount <next-count> > <last-count>)
  -{(<s> ^sentence.word <intervening-word>)
   (<intervening-word> ^wcount > <last-count>
                       ^wcount < <next-count>)}
-->
   (<last-word> ^next <xt>)
   (<xt> ^wcount <next-count>)
   (<s> ^output-word <xt>)
}

sp {preprocess-sentence*apply*output-preprocessed*word-value
   (state <s> ^name preprocess-sentence
              ^operator.name output-preprocessed
              ^sentence.word <wd>
              ^output-word <ow>)
   (<ow> ^wcount <wc>)
   (<wd> ^wcount <wc>
         ^parser-wvalue <val>)
-->
   (<ow> ^word <val>)
}

sp {preprocess-sentence*apply*output-preprocessed*last-word
   (state <s> ^name preprocess-sentence
              ^top-state <ts>
              ^operator.name output-preprocessed
              ^superstate.operator.original-sentence <os>
              ^sentence <sent>
              ^output-word <last-word>)
   (<last-word> ^wcount <last-count>)
  -{(<s> ^sentence.word.wcount > <last-count>)}
-->
   (<os> ^preprocessed true)
   (<ts> ^sentence-mapping <sent>)
}



# if there is an expectation that a word is being implicitly quoted, substitute
# it with a generic mass noun (word.n.3)
# this happens if the interaction stack indicates "I just told the mentor to
# define a word for me", that word should be quoted in the response
sp {preprocess-sentence*elaborate*implicit-quote
   (state <s> ^name preprocess-sentence
              ^top-state.interaction.stack.segment.purpose.parameters.message <q>)
   (<q> ^type category-of-word
        ^fields.word <wd>)
-->
   (<s> ^implicitly-quoted-word <wd>)
   (write (crlf) |"| <wd> |" will be implicitly quoted due to dialog context|)
}

# special word that is always implicitly quoted
sp {preprocess-sentence*iqw
   (state <s> ^name preprocess-sentence)
-->
   (<s> ^implicitly-quoted-word iqw)
}

sp {preprocess-sentence*propose*implicit-quote
   (state <s> ^name preprocess-sentence
              ^implicitly-quoted-word <wd>
             -^substituted <wrd>
              ^sentence.word <wrd>)
   (<wrd> ^wcount <wc>
          ^real-wvalue <wd>)
-->
   (<s> ^operator <o> +)
   (<o> ^name substitute
        ^word <wrd>
        ^new-value (concat |*noun| <wc>))
}

sp {preprocess-sentence*propose*lookup-word
   (state <s> ^name preprocess-sentence
             -^queried <ct>
              ^sentence.word <wd>)
   (<wd> ^real-wvalue <wv>
        -^parser-wvalue
         ^wcount <ct>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-word
        ^word <wv>
        ^wcount <ct>)
}

sp {preprocess-sentence*apply*lookup-word
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^smem.command <sc>)
   (<sc> -^<existing-command> <any>)
   (<o> ^name lookup-word
        ^word <wd>
        ^wcount <ct>)
-->
   (<sc> ^query <qr>)
   (<qr> ^word <wd>
         ^parse-class <class>)
}

# there could be an old store command from a commit-dictionary-examples
# sp {preprocess-sentence*apply*lookup-word*clear-previous
#    (state <s> ^name preprocess-sentence
#               ^operator <o>
#               ^smem.command <sc>
#               ^smem.result.success)
#    (<sc> ^store <cmd>)
# -->
#    (<sc> ^store <cmd> -)
# }
   
sp {preprocess-sentence*apply*lookup-word*after-retrieval
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^smem <sm>
              ^sentence.word <wd>)
   (<o> ^name lookup-word
        ^wcount <ct>)
   (<sm> ^command <c>
         ^result.retrieved <ret>)
   (<ret> ^word <wval>
          ^parse-class <pc>)
   (<wd> ^wcount <ct>)
   (<c> ^query <qr>)
-->
   (<s> ^queried <ct>)
   (<c> ^query <qr> -)
   (<wd> ^parser-wvalue (concat <pc> <ct>))
   (write (crlf) |Retrieved class | <pc> | for "| <wval> |"|)
}

sp {preprocess-sentence*apply*lookup-word*fail
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^smem <sm>
              ^sentence.word <wd>)
   (<o> ^name lookup-word
        ^wcount <ct>)
   (<sm> ^command <c>
         ^result.failure)
   (<c> ^query <qr>)
   (<wd> ^wcount <ct>
         ^real-wvalue <wval>)
-->
   (<s> ^queried <ct>)
   (<c> ^query <qr> -)
   (write (crlf) |Retrieval failed for "| <wval> |"|)
   (<s> ^failed-lookup <fl>)
   (<fl> ^wvalue <wval>
         ^wcount <ct>)
}

sp {preprocess-sentence*propose*handle-unlearned-word
   (state <s> ^name preprocess-sentence
              ^failed-lookup <fl>
              ^sentence.word <wd>)
   (<fl> ^wvalue <wv>
         ^wcount <wc>)
   (<wd> ^wcount <wc>
        -^parser-wvalue)
-->
   (<s> ^operator <o> +)
   (<o> ^name handle-unlearned-word
        ^wvalue <wv>
        ^wcount <wc>)
}

sp {preprocess-sentence*apply*handle-unlearned-word
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^top-state.language <lan>
              ^sentence.sentence-count <sc>
              ^learnable-parse-class <lpc> # many matches due to this
              ^sentence.word <wd>)
   (<o> ^name handle-unlearned-word
        ^wcount <ct>)
   (<wd> ^wcount <ct>
         ^real-wvalue <wval>)
-->
   # assume word is all possible types
   (<wd> ^parser-wvalue (concat <lpc> <ct>))
   (<lan> ^pending-dictionary-example <pde>)
   (<pde> ^sentence-count <sc>
          ^wcount <ct>
          ^word <wval>
          ^parse-class <lpc>
          ^parser-wvalue (concat <lpc> <ct>))
}

sp {preprocess-sentence*prefer*lookup-word*over*handle-unlearned-word
   (state <s> ^name preprocess-sentence
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name lookup-word)
   (<o2> ^name handle-unlearned-word)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {preprocess-sentence*prefer*substitute*over*lookup-word
   (state <s> ^name preprocess-sentence
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name lookup-word)
   (<o2> ^name substitute)
-->
   (<s> ^operator <o1> <  <o2>)
}

sp {preprocess-sentence*prefer*lower-wc
   (state <s> ^name preprocess-sentence
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name {<< lookup-word handle-unlearned-word commit-dictionary-examples >> <name>} ^wcount <wc>)
   (<o2> ^name <name> ^wcount > <wc>)
-->
   (<s> ^operator <o1> > <o2>)
}

# commit examples to smem if there is more than one failed lookup and an
# example exists for one of the words

sp {preprocess-sentence*propose*commit-dictionary-examples
   (state <s> ^name preprocess-sentence
              ^failed-lookup <f1>
              ^failed-lookup {<> <f1> <f2>}
              ^top-state.language.dictionary-example <de>)
   (<f1> ^wvalue <word>
         ^wcount <ct>)
   (<de> ^word <word>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name commit-dictionary-examples
        ^word <word>
        ^failed-lookup <f1>
        ^wcount <ct>)
}

# combine all examples into a single structure to add to smem
sp {preprocess-sentence*elaborate*construct-smem-entry*init
   (state <s> ^name preprocess-sentence
              ^operator <o> +)
   (<o> ^name commit-dictionary-examples
        ^word <wd>)
-->
   (<o> ^example <ex>)
   (<ex> ^word <wd>)
}

sp {preprocess-sentence*elaborate*construct-smem-entry*example
   (state <s> ^name preprocess-sentence
              ^operator <o> +
              ^top-state.language.dictionary-example <de>)
   (<o> ^name commit-dictionary-examples
        ^example <ex>
        ^word <word>)
   (<de> ^word <word>
         ^parse-class <class>)
-->
   (<ex> ^parse-class <class>)
}

sp {preprocess-sentence*apply*commit-dictionary-examples
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^queried <ct>
              ^top-state.smem.command <c>)
   (<o> ^name commit-dictionary-examples
        ^example <de>
        ^wcount <ct>)
   (<de> ^word <wd>
         ^parse-class <pc>)
-->
   (write (crlf) |Committing learned example of "| <wd> |" before proceeding.|)
   (<c> ^store <de>)
   (<s> ^queried <ct> -) # re-query it
}
# Note we do the store on top-state.smem, this avoids invoking the GDS when the
# examples are removed (below)

sp {preprocess-sentence*apply*commit-dictionary-examples*cleanup*examples
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^top-state.language <lg>
              ^top-state.smem.result.success)
   (<o> ^name commit-dictionary-examples
        ^word <wd>)
   (<lg> ^dictionary-example <de>)
   (<de> ^word <wd>)
-->
   (<lg> ^dictionary-example <de> -)
}

sp {preprocess-sentence*apply*commit-dictionary-examples*cleanup
   (state <s> ^name preprocess-sentence
              ^operator <o>
              ^top-state.smem <smem>
              ^failed-lookup <fl>)
   (<smem> ^command <cmd>
           ^result.success)
   (<cmd> ^store <st>)
   (<o> ^name commit-dictionary-examples
        ^failed-lookup <fl>)
-->
   (<s> ^failed-lookup <fl> -) # retract operator
   (<cmd> ^store <st> -) 
}



# happens if there are multiple examples available
sp {preprocess-sentence*commit-dictionary-examples*same-word-indifferent
   (state <s> ^name preprocess-sentence
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name commit-dictionary-examples
         ^word <wd>)
   (<o2> ^name commit-dictionary-examples
         ^word <wd>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {preprocess-sentence*prefer*commit-dictionary-examples*over*handle-unlearned-word
   (state <s> ^name preprocess-sentence
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name commit-dictionary-examples)
   (<o2> ^name handle-unlearned-word)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {elaborate*preprocessing*operators*are*interpretation*operators
   (state <s> ^name preprocess-sentence
              ^operator <o> +)
   (<o> ^name << lookup-word substitute output-preprocessed commit-dictionary-examples handle-unlearned-word >>)
-->
   (<o> ^kind interpretation)
}

# smem --set learning on
# smem --add {
#   (<wd1> ^word red
#         ^parse-class *nounjective)
#   (<wd2> ^word square
#         ^parse-class *nounjective)
#   (<wd3> ^word get
#         ^parse-class *verb+with+direct+object)
#   (<wd4> ^word move
#         ^parse-class *verb+with+direct+object+and+preposition
#         ^parse-class *verb+with+direct+object
#         ^parse-class *verb+with+preposition)
# }


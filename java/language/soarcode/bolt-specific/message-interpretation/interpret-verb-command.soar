# eventually, this should handle conditional actions, too: "pick up the block
# if it is on the table."

sp {interpret-verb-command*propose*collect-predicates
   (state <s> ^name interpret-verb-command
             -^predicates-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-predicates)
}

sp {interpret-verb-command*apply*collect-predicates*root
   (state <s> ^name interpret-verb-command
              ^operator.name collect-predicates)
-->
   (<s> ^verb-predicates <op>)
}

sp {interpret-verb-command*apply*collect-predicates
   (state <s> ^name interpret-verb-command
              ^operator.name collect-predicates
              ^verb-predicates <op>
              ^predicates.predicate <p>)
-->
   (<op> ^predicate <p>)
   (<s> ^predicates-collected true)
}

sp {interpret-verb-command*propose*interpret-verb
   (state <s> ^name interpret-verb-command
              ^verb-predicates <p>
              ^predicates-collected
             -^verb-interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-verb
        ^predicates <p>)
}

sp {interpret-verb-command*elaborate*return-interpretation
   (state <s> ^name interpret-verb-command
              ^predicates <p>
              ^verb-predicates.interpretation <oi>) # comes from interpret-verb
-->
   (<p> ^interpretation.verb-command.verb <oi>)
}

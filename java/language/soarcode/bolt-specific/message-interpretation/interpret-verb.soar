
sp {interpret-verb*elaborate*init
   (state <s> ^name interpret-verb)
-->
   (<s> ^interpretation <it>)
}

sp {interpret-verb*propose*identify-verb-word
   (state <s> ^name interpret-verb
             -^interpretation.word <vb>)
-->
   (<s> ^operator <o> +)
   (<o> ^name identify-verb-word)
}

sp {interpret-verb*apply*identify-verb-word*direct-object
   (state <s> ^name interpret-verb
              ^operator.name identify-verb-word
              ^interpretation <int>
              ^predicates.predicate <hearer>
              ^predicates.predicate {<> <hearer> <vw>} )
   (<hearer> ^outer SELF
            -^is-verb
             ^inner1 <hearer-obj>)
   (<vw> ^outer <word>
         ^inner1 <hearer-obj>
         ^inner2 <do>)
-->
   (<int> ^word <word>
          ^direct-object <doint>)
   (<s> ^verb-predicate <vw>
        ^direct-object <do>
        ^verb-word <word>)
   (<vw> ^accounted-for true
         ^is-verb true)
   (<hearer> ^accounted-for true)
}

# in the no-direct-object case, the verb predicate isn't on the state:
# the hearer was already interpreted as a primitive object
#
# find the predicate on the object itself, rather than the state
sp {interpret-verb*apply*identify-verb-word*no-direct-object
   (state <s> ^name interpret-verb
              ^operator.name identify-verb-word
              ^interpretation <int>
              ^predicates.predicate <hearer>)
   (<hearer> ^outer SELF
            -^is-verb
             ^inner1 <hearer-obj>)
   (<hearer-obj> ^predicates.predicate <pred>)
   (<pred> ^outer <word> <> SELF)
-->
   (<int> ^word <word>)
   (<s> ^verb-predicate <pred>
        ^no-direct-object true
        ^verb-word <word>)
   (<hearer> ^accounted-for true
             ^is-verb true)
}



sp {interpret-verb*elaborate*no-preposition
   (state <s> ^name interpret-verb
              ^predicates <ap>
              ^verb-word <vw>
              ^verb-predicate <verbp>)
  -{(<ap> ^predicate {<> <verbp> <pred>} )
    (<pred> ^inner1 <vw>)}
-->
   (<s> ^has-preposition false)
}

sp {interpret-verb*elaborate*has-preposition
   (state <s> ^name interpret-verb
              ^predicates <ap>
              ^verb-word <vw>
              ^verb-predicate <verbp>)
    (<ap> ^predicate {<> <verbp> <pred>})
    (<pred> ^inner1 <vw>
            ^inner2 <ob>)
-->
   (<s> ^has-preposition true
        ^preposition-object <ob>
        ^preposition-predicate <pred>)
}

sp {interpret-verb*propose*collect-object-predicates
   (state <s> ^name interpret-verb
              ^direct-object <do>
              ^interpretation.word <exists>
             -^object-predicates-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-object-predicates)
}

sp {interpret-verb*apply*collect-object-predicates*init
   (state <s> ^name interpret-verb
              ^operator.name collect-object-predicates)
-->
   (<s> ^object-predicates <rp>)
}

sp {interpret-verb*apply*collect-object-predicates*collect
   (state <s> ^name interpret-verb
              ^operator.name collect-object-predicates
             -^preposition-predicate <pred>
              ^predicates.predicate <pred>
              ^object-predicates <rp>)
-->
   (<rp> ^predicate <pred>)
}

sp {interpret-verb*apply*collect-object-predicates*done
   (state <s> ^name interpret-verb
              ^operator.name collect-object-predicates
              ^object-predicates <rp>)
-->
   (<s> ^object-predicates-collected true)
}

sp {interpret-verb*propose*interpret-object
   (state <s> ^name interpret-verb
              ^object-predicates-collected true
              ^object-predicates <op>
              ^direct-object <po>
             -^object-predicates.interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object
        ^primary-object <po>
        ^predicates <op>)
}

sp {interpret-object*propose*collect-preposition-predicates
   (state <s> ^name interpret-verb
              ^has-preposition true
             -^preposition-predicates-collected
              ^object-predicates.interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-preposition-predicates)
}

sp {interpret-object*propose*collect-preposition-predicates*no-object
   (state <s> ^name interpret-verb
              ^no-direct-object
             -^preposition-predicates-collected
              ^has-preposition)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-preposition-predicates)
}

sp {interpret-verb*apply*collect-preposition-predicates*init
   (state <s> ^name interpret-verb
              ^operator.name collect-preposition-predicates)
-->
   (<s> ^preposition-subpredicates <rp>)
}

sp {interpret-verb*apply*collect-preposition-predicates*collect
   (state <s> ^name interpret-verb
              ^operator.name collect-preposition-predicates
              ^preposition-subpredicates <rp>
              ^verb-word <vw>
              ^predicates.predicate <pred>)
   (<pred> ^inner1 <> <vw>)
-->
   (<rp> ^predicate <pred>)
}

sp {interpret-verb*apply*collect-preposition-predicates*done
   (state <s> ^name interpret-verb
              ^operator.name collect-preposition-predicates
              ^preposition-subpredicates <rp>)
-->
   (<s> ^preposition-predicates-collected true)
}

sp {interpret-verb*propose*interpret-preposition
   (state <s> ^name interpret-verb
              ^preposition-predicates-collected true
              ^preposition-subpredicates <op>
              ^preposition-predicate <pp>
             -^preposition-subpredicates.interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-preposition
        ^predicates <op>
        ^preposition <pp>)
}

sp {interpret-verb*elaborate*preposition-interpretation
   (state <s> ^name interpret-verb
              ^preposition-subpredicates.interpretation.preposition <ri>
              ^interpretation <int>)
-->
   (<int> ^preposition <ri>)
}

sp {interpret-verb*elaborate*direct-object*object
   (state <s> ^name interpret-verb
              ^object-predicates.interpretation.object <ri>
              ^interpretation.direct-object <do>)
-->
   (<do> ^object <ri>)
}

sp {interpret-verb*elaborate*direct-object*relation
   (state <s> ^name interpret-verb
              ^object-predicates.interpretation.relation <ri>
              ^interpretation.direct-object <do>)
-->
   (<do> ^relation <ri>)
}

sp {interpret-verb*elaborate*fully-interpreted*simple
   (state <s> ^name interpret-verb
              ^no-direct-object
              ^has-preposition false
              ^interpretation.word <exists>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*fully-interpreted*both
   (state <s> ^name interpret-verb
              ^direct-object
              ^has-preposition true
              ^interpretation.direct-object.object <exists>
              ^interpretation.preposition <exists2>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*fully-interpreted*do
   (state <s> ^name interpret-verb
              ^direct-object
              ^has-preposition false
              ^interpretation.direct-object.object <exists>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*fully-interpreted*prep
   (state <s> ^name interpret-verb
              ^no-direct-object
              ^has-preposition true
              ^interpretation.preposition <exists>)
-->
   (<s> ^fully-interpreted true)
}

sp {interpret-verb*elaborate*return-interpretation
   (state <s> ^name interpret-verb
              ^predicates <ap>
              ^interpretation <int>
              ^fully-interpreted true)
-->
   (<ap> ^interpretation <int>)
}

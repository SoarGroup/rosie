sp {interpret-message*elaborate*init
   (state <s> ^name interpret-message
              ^superstate.operator.predicate-set <pred>)
-->
   (<s> ^predicate-set <pred>)
}

# primitive objects are always interpreted first
# all primitive objects are interpreted regardless of originating sentence
# TODO: to do multi-sentence references, need to:
# - keep around interpreted primitive-object structures for some amount of time
# - allow interpret-primitive-object to add new information to an existing interpretation

sp {interpret-message*elaborate*primitive-object-symbol
   (state <s> ^name interpret-message
              ^objects <ob>
             -^ignored-object <val>
              ^predicate-set.predicate <pr>)
   (<pr> ^<< inner1 inner2 inner3 >> <val>)
   (<val> ^<is-an-identifier>)
-->
   (<ob> ^object <val>)
}

sp {interpret-message*elaborate*object*predicates
   (state <s> ^name interpret-message
              ^objects.object <po>)
-->
   (<po> ^predicates <pr>)
}


sp {interpret-message*elaborate*object*collect
   (state <s> ^name interpret-message
              ^objects.object <po>
              ^predicate-set.predicate <pr>)
   (<pr> ^inner1 <po>
        -^outer goal
        -^inner2)
   (<po> ^predicates <prp>)
-->
   (<prp> ^predicate <pr>)
}

sp {interpret-message*propose*interpret-primitive-object
   (state <s> ^name interpret-message
              ^objects.object <pi>)
   (<pi> ^predicates <pr>)
   (<pr> -^interpretation)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interpret-primitive-object
        ^predicates <pr>)
}

# once primitive-objects are interpreted, interpret each original sentence into
# a message structure


# verb-commands have the SELF
sp {interpret-message*propose*interpret-verb-command
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <ob>)
   (<pred> -^interpretation
            ^predicate <p1> <p2>)
   (<p1> ^outer SELF
         ^inner1 <hearer>
        -^ignored)
   (<p2> -^outer SELF 
        ^inner1 <hearer>
       -^ignored)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-verb-command
        ^predicates <pred>
        ^objects <ob>)
}

# anything might be an object-message
sp {interpret-message*propose*interpret-object-message
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <ob>)
   (<pred> -^interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object-message
        ^predicates <pred>
        ^objects <ob>)
}

sp {interpret-message*propose*interpret-goal-object-message
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <ob>)
   (<pred> -^interpretation
            ^predicate.outer goal)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-goal-object-message
        ^predicates <pred>
        ^objects <ob>)
}

sp {interpret-message*propose*interpret-object-is-question
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <ob>)
   (<pred> -^interpretation
            ^predicate <pr>)
   (<pr> ^outer QUESTIONED
        -^ignored)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object-is-question
        ^predicates <pred>
        ^objects <ob>)
}


# relation-message must have a 2-parameter predicate
# DEPRECATED

# sp {interpret-message*propose*interpret-relation-message
#    (state <s> ^name interpret-message
#               ^predicate-set <pred>)
#    (<pred> -^interpretation
#             ^predicate <p1>)
#    (<p1> ^inner1 <i1>
#          ^inner2 <i2>
#         -^ignored)
# -->
#    (<s> ^operator <o> +)
#    (<o> ^name interpret-relation-message
#         ^predicates <pred>)
# }

# goal-relation-message same but with goal predicate
sp {interpret-message*propose*interpret-goal-relation-message
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <on>)
   (<pred> -^interpretation
            ^predicate <p1>
            ^predicate.outer goal)
   (<p1> ^inner1 <i1>
         ^inner2 <i2>
        -^ignored)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interpret-goal-relation-message
        ^predicates <pred>
        ^objects <ob>)
}

# object-questions must have a what or which or where pred
sp {interpret-message*propose*interpret-object-question
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <ob>)
   (<pred> -^interpretation
            ^predicate <p1>)
   (<p1> ^outer << what which where >>
        -^ignored)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object-question
        ^predicates <pred>
        ^objects <ob>)
}

# relation-questions must have a what or which pred and a relation
sp {interpret-message*propose*interpret-relation-question
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <ob>)
   (<pred> -^interpretation
            ^predicate <p1>
            ^predicate <p2>)
   (<p1> ^outer << what which >>
        -^ignored)
   (<p2> ^inner1 <i1>
         ^inner2 <i2>
        -^ignored)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-relation-question
        ^predicates <pred>
        ^objects <ob>)
}

sp {interpret-message*propose*interpret-single-word-response
   (state <s> ^name interpret-message
              ^predicate-set <pred>)
   (<pred> -^interpretation
            ^predicate <p1>)
   (<p1> ^outer single-word
        -^ignored)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-single-word-response
        ^predicates <pred>)
}

# single-word-responses are done in place here
sp {interpret-message*apply*interpret-single-word-response
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-single-word-response
              ^predicates <pred>)
   (<pred> ^predicate.inner1 <word>)
-->
   (<pred> ^interpretation <int>)
   (<int> ^single-word-response.response <word>)
}

# verb-completion-message: revised from verb command if past tense is detected
# (could be "you" based, but "you should put the block on the table" has same
# DRS as "you put the block on the table")

# single-word response:  need to add metalevel for unparsable sentences -> raw
# word representation


sp {interpret-message*prefer*anything-over-object-message
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-object-message)
   (<o2> ^kind message-interpretation
        -^name interpret-object-message)
-->
   (<s> ^operator <o2> > <o1>)
}

sp {interpret-message*prefer*relation-question-over-relation
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-relation-message)
   (<o2> ^name interpret-relation-question)
-->
   (<s> ^operator <o2> > <o1>)
}

sp {interpret-message*prefer*goal-relation-over-relation
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-relation-message)
   (<o2> ^name interpret-goal-relation-message)
-->
   (<s> ^operator <o2> > <o1>)
}

sp {interpret-message*prefer*goal-relation-over-goal-object
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-goal-object-message)
   (<o2> ^name interpret-goal-relation-message)
-->
   (<s> ^operator <o2> > <o1>)
}

# "to the right of" has two 2d preds
sp {interpret-message*prefer*indifferent-relation
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-relation-message)
   (<o2> ^name interpret-relation-message)
-->
   (<s> ^operator <o2> = <o1>)
}

sp {interpret-message*prefer*indifferent-question
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-relation-question)
   (<o2> ^name interpret-relation-question)
-->
   (<s> ^operator <o2> = <o1>)
}

sp {interpret-message*prefer*relation-question-over-object-question
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-object-question)
   (<o2> ^name interpret-relation-question)
-->
   (<s> ^operator <o2> > <o1>)
}

sp {interpret-message*prefer*verb-command-over-any
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interpret-verb-command)
   (<o2> ^kind message-interpretation
        -^name interpret-primitive-object
        -^name interpret-verb-command)
-->
   (<s> ^operator <o1> > <o2>)
}

# primitive objects are interpreted first thing in message interpretation
# interpretation structures are stored right on the model

sp {interpret-message*elaborate*objects
   (state <s> ^name interpret-message)
-->
   (<s> ^objects <ob>)
}

# role prepositions will cause redundant predicates
# e.g.: color-of(N7,N4) color(N4) of(N4,N7)
# ignore the last two

sp {elaborate*ignore-redundant
   (state <s> ^name interpret-message
              ^predicate-set <pred>)
   (<pred> ^predicate <rp>
           ^predicate <propp>
           ^predicate <predp>)
   (<rp> ^outer1 <property>
         ^outer2 <preposition>
         ^inner1 <i2>
         ^inner2 <i1>)
   (<propp> ^outer <property>
            ^inner1 <i1>)
   (<predp> ^outer <preposition>
            ^inner1 <i1>
            ^inner2 <i2>)
-->
   (<propp> ^ignored true)
   (<predp> ^ignored true)
}

sp {interpret-message*propose*identify-to-the-x-of-relation
   (state <s> ^name interpret-message
              ^predicate-set <ap>)
   (<ap> ^predicate <to> <the> <x-of>)
   (<to> ^outer << to on >>
         ^inner1 <p1>
         ^inner2 <x-obj>
        -^ignored)
   (<the> ^outer DEF
          ^inner1 <x-obj>
         -^ignored)
   (<x-of> ^outer1 <x-word>
           ^outer2 of
           ^inner1 <p2>
           ^inner2 <x-obj>
          -^ignored)
-->
   (<s> ^operator <o> + =)
   (<o> ^name identify-to-the-x-of-relation
        ^outer (concat <x-word> |-of|)
        ^inner1 <p1>
        ^inner2 <p2>
        ^inhibit <to> <the> <x-of>)
}

sp {interpret-message*apply*identify-to-the-x-of-relation
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^operator <o>)
   (<o> ^name identify-to-the-x-of-relation
        ^outer <out>
        ^inner1 <in1>
        ^inner2 <in2>)
-->
   (<pred> ^predicate <ttxo>)
   (<ttxo> ^outer <out>
           ^inner1 <in1>
           ^inner2 <in2>)
}

sp {interpret-message*apply*identify-to-the-x-of-relation*inhibit
   (state <s> ^name interpret-message
              ^predicate-set.predicate <pred>
              ^operator <o>)
   (<o> ^name identify-to-the-x-of-relation
        ^inhibit <pred>)
-->
   (<pred> ^ignored true)
}

sp {interpret-message*prefer*identify-to-the-x-of-relation
   (state <s> ^name interpret-message
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name identify-to-the-x-of-relation)
   (<o2> ^kind message-interpretation
        -^name identify-to-the-x-of-relation)
-->
   (<s> ^operator <o1> > <o2>)
}

# make a new QUESTIONED predicate for the collapsed reln
sp {interpret-message*apply*identify-to-the-x-of-relation*questioned
   (state <s> ^name interpret-message
              ^predicate-set <ps>
              ^operator <o>)
   (<ps> ^predicate <pred>)
   (<pred> ^outer QUESTIONED
           ^inner1 <to-or-on>)
   (<o> ^name identify-to-the-x-of-relation
        ^outer <out>
        ^inhibit <too-pred>)
   (<too-pred> ^outer <to-or-on>)
-->
   (<pred> ^ignored true)
   (<ps> ^predicate <p2>)
   (<p2> ^outer QUESTIONED
         ^inner1 <out>)
}

# noun phrases get reparsed as commands with a fake verb,
# postprocessing removes the fake verb but not the SELF object

# remove any SELF object that isn't referred to elsewhere
sp {interpret-message*elaborate*ignore-phantom-hearer
   (state <s> ^name interpret-message
              ^predicate-set <pred>
              ^objects <ob>)
   (<pred> -^interpretation
            ^predicate <p1>)
   (<p1> ^outer SELF
         ^inner1 <hearer>)
  -{(<pred> ^predicate {<> <p1> <p2>})
    (<p2> ^<< inner1 inner2 inner3 >> <hearer>)}
-->
   (<p1> ^ignored true)
   (<s> ^ignored-object <hearer>)
}

# replace "you" and HEARER with SELF
sp {interpret-message*elaborate*you-to-hearer
   (state <s> ^name interpret-message
              ^predicate-set <pred>)
   (<pred> ^predicate <you>)
   (<you> ^outer << you HEARER >>
          ^inner1 <you-obj>
         -^inner2)
-->
   (<pred> ^predicate <you> -
           ^predicate <hearer>)
   (<hearer> ^outer SELF
             ^inner1 <you-obj>)
}

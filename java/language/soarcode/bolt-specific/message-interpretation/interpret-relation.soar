sp {interpret-relation*elaborate*init
   (state <s> ^name interpret-relation)
-->
   (<s> ^interpretation <it>)
   (<it> ^relation <rm>)
   (<rm> ^p1 <p1>
         ^p2 <p2>)
}

sp {interpret-relation*propose*identify-relation
   (state <s> ^name interpret-relation
             -^relation-identified
              ^superstate.operator.relation <pr>)
-->
   (<s> ^operator <o> +)
   (<o> ^name identify-relation
        ^relation <pr>)
}

sp {interpret-relation*apply*identify-relation
   (state <s> ^name interpret-relation
              ^operator <o>
              ^interpretation.relation <rm>)
   (<o> ^name identify-relation
        ^relation <pr>)
   (<pr> ^outer <relation>
         ^inner1 <p1-val>
         ^inner2 <p2-val>)
-->
   (<s> ^relation-identified true
        ^p1 <p1-val>
        ^p2 <p2-val>)
   (<rm> ^word <relation>)
   (<pr> ^accounted-for true)
}

sp {interpret-relation*apply*identify-relation*two-part-outer*of
   (state <s> ^name interpret-relation
              ^operator <o>
              ^interpretation.relation <rm>)
   (<o> ^name identify-relation
        ^relation <pr>)
   (<pr> ^outer1 <o1>
         ^outer2 {<o2> of}
         ^inner1 <p1-val>
         ^inner2 <p2-val>)
-->
   (<s> ^relation-identified true
        ^p1 <p1-val>
        ^p2 <p2-val>)
   (<rm> ^word (concat <o1> |-| <o2>))
   (<pr> ^accounted-for true)
}

sp {interpret-relation*propose*collect-p1
   (state <s> ^name interpret-relation
              ^relation-identified true
             -^p1-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-p1)
}

sp {interpret-relation*apply*collect-p1*base
   (state <s> ^name interpret-relation
              ^p1 <p1>
              ^operator.name collect-p1)
-->
   (<s> ^p1-predicates <cp>)
}

sp {interpret-relation*apply*collect-p1
   (state <s> ^name interpret-relation
              ^operator.name collect-p1
              ^p1-predicates <cp>
              ^predicates.predicate <pred>)
-->
   (<cp> ^predicate <pred>)
}

sp {interpret-relation*apply*collect-p1*done
   (state <s> ^name interpret-relation
              ^operator.name collect-p1
              ^p1-predicates <cp>)
-->
   (<s> ^p1-collected true)
}

sp {interpret-relation*propose*interpret-p1
   (state <s> ^name interpret-relation
              ^p1 <p1>
              ^p1-collected true
              ^p1-predicates <cp>)
   (<cp> -^interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object
        ^primary-object <p1>
        ^predicates <cp>)
}

sp {interpret-relation*propose*collect-p2
   (state <s> ^name interpret-relation
              ^relation-identified true
              ^p1-predicates.interpretation
             -^p2-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-p2)
}

sp {interpret-relation*apply*collect-p2*base
   (state <s> ^name interpret-relation
              ^p2 <p2>
              ^operator.name collect-p2)
-->
   (<s> ^p2-predicates <cp>)
}

sp {interpret-relation*apply*collect-p2
   (state <s> ^name interpret-relation
              ^operator.name collect-p2
              ^p2-predicates <cp>
              ^predicates.predicate <pred>)
-->
   (<cp> ^predicate <pred>)
}


sp {interpret-relation*apply*collect-p2*done
   (state <s> ^name interpret-relation
              ^operator.name collect-p2
              ^p2-predicates <cp>)
-->
   (<s> ^p2-collected true)
}


sp {interpret-relation*propose*interpret-p2
   (state <s> ^name interpret-relation
              ^p2 <p2>
              ^p2-collected true
              ^p2-predicates <cp>)
   (<cp> -^interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object
        ^primary-object <p2>
        ^predicates <cp>)
}

sp {interpret-relation*elaborate*p1-interpretation
   (state <s> ^name interpret-relation
              ^p1-predicates <p1p>
              ^interpretation.relation.p1 <rm>)
   (<p1p> ^interpretation.object <int>)
-->
   (<rm> ^object <int>)
}

sp {interpret-relation*elaborate*p2-interpretation
   (state <s> ^name interpret-relation
              ^p2-predicates <p2p>
              ^interpretation.relation.p2 <rm>)
   (<p2p> ^interpretation.object <int>)
-->
   (<rm> ^object <int>)
}

sp {interpret-relation*elaborate*p1-interpretation*subrelation
   (state <s> ^name interpret-relation
              ^p1-predicates <p1p>
              ^interpretation.relation.p1 <rm>)
   (<p1p> ^interpretation.relation <int>)
-->
   (<rm> ^relation <int>)
}

sp {interpret-relation*elaborate*p2-interpretation*subrelation
   (state <s> ^name interpret-relation
              ^p2-predicates <p1p>
              ^interpretation.relation.p2 <rm>)
   (<p1p> ^interpretation.relation <int>)
-->
   (<rm> ^relation <int>)
}

sp {interpret-relation*elaborate*return-interpretation
   (state <s> ^name interpret-relation
              ^predicates <p>
              ^interpretation <i>)
   (<i> ^relation <rm>)
   (<rm> ^p1.object <p1>
         ^p2.object <p2>)
-->
   (<p> ^interpretation <i>)
}

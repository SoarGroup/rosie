# TODO: mask out the DEF from "The goal"
# Better yet, keep the goal as a separate thing so _the_ goal is _a_ property
# isn't conflicted.


sp {interpret-goal-object-message*propose*mark-goal-predicate
   (state <s> ^name interpret-goal-object-message
             -^goal-predicate)
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-goal-predicate)
}

sp {interpret-goal-object-message*apply*mark-goal-predicate
   (state <s> ^name interpret-goal-object-message
              ^operator.name mark-goal-predicate
              ^predicates.predicate <p>)
   (<p> ^outer goal)
-->
   (<s> ^goal-predicate <p>)
   (<p> ^ignored true)
}


sp {interpret-goal-object-message*propose*collect-predicates
   (state <s> ^name interpret-goal-object-message
              ^goal-predicate
             -^predicates-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-predicates)
}

sp {interpret-goal-object-message*apply*collect-predicates*root
   (state <s> ^name interpret-goal-object-message
              ^operator.name collect-predicates)
-->
   (<s> ^object-predicates <op>)
}

sp {interpret-goal-object-message*apply*collect-predicates
   (state <s> ^name interpret-goal-object-message
              ^operator.name collect-predicates
              ^object-predicates <op>
              ^predicates.predicate <p>)
   (<p> -^ignored)
-->
   (<op> ^predicate <p>)
}

sp {interpret-goal-object-message*apply*collect-predicates*done
   (state <s> ^name interpret-goal-object-message
              ^operator.name collect-predicates
              ^object-predicates <op>)
-->
   (<s> ^predicates-collected true)
}

sp {interpret-goal-object-message*propose*interpret-object
   (state <s> ^name interpret-goal-object-message
              ^object-predicates <p>
              ^predicates-collected
             -^object-interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object
        ^predicates <p>)
}

sp {interpret-goal-object-message*elaborate*return-interpretation
   (state <s> ^name interpret-goal-object-message
              ^predicates <p>
              ^object-predicates.interpretation <oi>) # comes from interpret-object
-->
   (<p> ^interpretation.goal-object-message <oi>)
}


# test sentence: Which is the yellow block?
# PASSED: DEF(N2) block(N2) which(N2) yellow(N2)

sp {interpret-object-question*elaborate*init
   (state <s> ^name interpret-object-question)
-->
   (<s> ^interpretation <int>)
}

sp {interpret-object-question*propose*identify-question-word
   (state <s> ^name interpret-object-question
             -^interpretation.question-word)
-->
   (<s> ^operator <o> +)
   (<o> ^name identify-question-word)
}

sp {interpret-object-question*apply*identify-question-word
   (state <s> ^name interpret-object-question
              ^operator.name identify-question-word
              ^interpretation <int>
              ^predicates.predicate <pred>)
   (<pred> ^outer {<< what which where >> <qword>}
           ^inner1 <obj>)
-->
   (<int> ^question-word <qword>)
   (<s> ^exclude <pred>
        ^questioned-object <obj>)
   (<pred> ^accounted-for true)
}

sp {interpret-object-question*propose*collect-predicates
   (state <s> ^name interpret-object-question
              ^interpretation.question-word
             -^predicates-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-predicates)
}

sp {interpret-object-question*apply*collect-predicates*root
   (state <s> ^name interpret-object-question
              ^operator.name collect-predicates)
-->
   (<s> ^object-predicates <op>)
}

sp {interpret-object-question*apply*collect-predicates
   (state <s> ^name interpret-object-question
              ^operator.name collect-predicates
              ^object-predicates <op>
             -^exclude <p>
              ^predicates.predicate <p>)
-->
   (<op> ^predicate <p>)
}

sp {interpret-object-question*apply*collect-predicates*done
   (state <s> ^name interpret-object-question
              ^operator.name collect-predicates
              ^object-predicates <op>)
-->
   (<s> ^predicates-collected true)
}

sp {interpret-object-question*propose*interpret-object
   (state <s> ^name interpret-object-question
              ^object-predicates <p>
              ^predicates-collected
              ^questioned-object <po>
             -^object-interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object
        ^primary-object <po>
        ^predicates <p>)
}

sp {interpret-object-question*elaborate*return-interpretation
   (state <s> ^name interpret-object-question
              ^predicates <p>
              ^interpretation <int>
              ^object-predicates.interpretation.object <oi>) # comes from interpret-object
-->
   (<int> ^object <oi>)
   (<p> ^interpretation.object-question <int>)
}

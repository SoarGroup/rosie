
sp {interpret-relation-question*elaborate*init
   (state <s> ^name interpret-relation-question)
-->
   (<s> ^interpretation <int>)
}

sp {interpret-relation-question*propose*identify-question-word
   (state <s> ^name interpret-relation-question
             -^interpretation.question-word)
-->
   (<s> ^operator <o> +)
   (<o> ^name identify-question-word)
}

sp {interpret-relation-question*apply*identify-question-word
   (state <s> ^name interpret-relation-question
              ^operator.name identify-question-word
              ^interpretation <int>
              ^predicates.predicate <pred>
              ^predicates.predicate <pred2>)
   (<pred> ^outer {<< what which >> <qword>}
           ^inner1 <qo>)
   (<pred2> ^<< inner1 inner2 >> <qo>
            ^inner2 <any>) 
-->
   (<int> ^question-word <qword>)
   (<s> ^question-predicate <pred>)
   (<s> ^question-relation-predicate <pred2>)
   (<s> ^question-object <qo>)
   (<pred> ^accounted-for true)
}

sp {interpret-relation-question*propose*collect-predicates
   (state <s> ^name interpret-relation-question
              ^interpretation.question-word
             -^predicates-collected)
-->
   (<s> ^operator <o> +)
   (<o> ^name collect-predicates)
}

sp {interpret-relation-question*apply*collect-predicates*root
   (state <s> ^name interpret-relation-question
              ^operator.name collect-predicates)
-->
   (<s> ^relation-predicates <op>)
}

sp {interpret-relation-question*apply*collect-predicates
   (state <s> ^name interpret-relation-question
              ^operator.name collect-predicates
              ^predicates.predicate <p>
              -^question-relation-predicate <p>
              -^question-predicate <p>
              ^relation-predicates <op>)
-->
   (<op> ^predicate <p>)
}

sp {interpret-relation-question*apply*collect-predicates*done
   (state <s> ^name interpret-relation-question
              ^operator.name collect-predicates
              ^relation-predicates <op>)
-->
   (<s> ^predicates-collected true)
}

sp {interpret-relation-question*propose*interpret-relation
   (state <s> ^name interpret-relation-question
              ^relation-predicates <p>
              ^question-relation-predicate <pd>
              ^predicates-collected
             -^relation-interpretation)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-relation
        ^predicates <p>
        ^relation <pd>)
}

sp {interpret-relation-question*elaborate*return-interpretation
   (state <s> ^name interpret-relation-question
              ^predicates <p>
              ^interpretation <int>
              ^relation-predicates.interpretation.relation <oi>) # comes from interpret-relation
-->
   (<int> ^relation <oi>)
   (<p> ^interpretation.relation-question <int>)
}

# If we did not find any contained fragments, check to see if it is occluded

# occlusion-command: see agent/svs-command-operators
sp {attend-to-perception*missing-object*propose*occlusion-command
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^object-info <obj-info>
             -^found-contained-fragment true
             -^contained-by
             -^object-occluded false)
   (<obj-info> ^belief-id <bel-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name occlusion-command
        ^object-id <bel-id>
        ^remove-id robot
        ^object-source perception)
}

# If occluded, add to the status, removing the missing discrepancy for some time
sp {attend-to-perception*missing-object*apply*occlusion-command*is-occluded*mark*occlusion-timeout
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^operator.name occlusion-command
              ^object-info <obj-info>
              ^object-occluded true
              ^io.input-link.time.seconds <secs>
              ^agent-params.occlusion-check-period <t>)
-->
   (<obj-info> ^occlusion-timeout (+ <secs> <t>))
}


# In rare cases this command will fail, so we redo it
sp {attend-to-perception*missing-object*propose*redo-occlusion-command
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^occlusion-command-result failure)
-->
   (<s> ^operator <o> + >)
   (<o> ^name redo-occlusion-command)
}

sp {attend-to-perception*missing-object*apply*redo-occlusion-command
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^operator.name redo-occlusion-command
              ^occlusion-command-result failure)
-->
   (<s> ^occlusion-command-result failure -)
}


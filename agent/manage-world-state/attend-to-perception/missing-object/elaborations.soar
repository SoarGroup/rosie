sp {attend-to-perception*missing-object*elaborate*object-info
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^discrepancy-info.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

#sp {attend-attend-to-perception*missing-object*elaboate*aggressive-match 
#   (state <s> ^name attend-to-perception
#              ^discrepancy-type missing-object
#              ^superstate.superstate.operator.aggressive-match <m>)
#-->
#   (<s> ^aggressive-match <m>)
#}


sp {attend-to-perception*missing-object*elaborate*agent-params*contained-threshold
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

sp {attend-to-perception*missing-object*elaborate*agent-params*contained-by-threshold
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^agent-params.contained-by-threshold <t>)
-->
   (<s> ^contained-by-threshold <t>)
}

# Intersection Results
sp {attend-to-perception*missing-object*elaborate*intersected-perc-id*from*intersect-command
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^intersect-command.result { <obj-id> <> none })
-->
   (<s> ^intersected-perc-id <obj-id>)
}

# Overlap Results
sp {attend-to-perception*missing-object*elaborate*overlap-results
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object)
-->
   (<s> ^overlap-results <res>)
}

sp {attend-to-perception*missing-object*elaborate*contained-fragment
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^object-info.belief-obj.id <missing-id>
              ^overlap-results.result <res>
              ^contained-threshold <t>)
   (<res> ^object-a <cont-id>
          ^object-b <missing-id>
          ^overlap > <t>)
-->
   (<s> ^contained-fragment-id <cont-id>)
}

sp {attend-to-perception*missing-object*elaborate*contained-by
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^object-info.belief-obj.id <missing-id>
              ^overlap-results <overlap-results>
              ^contained-by-threshold <t>
              ^top-state.perception-monitor.object-monitor.object-info <cont-obj-info>)
   (<overlap-results> ^result <res>)
   (<res> ^object-a <missing-id>
          ^object-b <cont-id>
          ^overlap { <overlap> > <t> })
   (<cont-obj-info> ^perception-obj.id <cont-id>
                   -^world-obj.predicates.affordance1 receptacle1)
   -{(<overlap-results> ^result <better-res>)
     (<better-res> ^object-a <missing-id>
                   ^overlap > <overlap>)}
-->
   (<s> ^contained-by <cont-obj-info>)
}

sp {attend-to-perception*missing-object*elaborate*found-contained-fragment
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^contained-fragment-id <id>)
-->
   (<s> ^found-contained-fragment true)
}

### object-occluded true/false
# Occluded = true if inside a closed receptacle

sp {attend-to-perception*missing-object*elaborate*object-occluded*true
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^agent-params.occlusion-threshold <threshold>
              ^occlusion-command-result { >= <threshold> <> failure })
-->
   (<s> ^object-occluded true)
}

sp {attend-to-perception*missing-object*elaborate*object-occluded*false
   (state <s> ^name attend-to-perception
              ^discrepancy-type missing-object
              ^agent-params.occlusion-threshold <threshold>
              ^occlusion-command-result { < <threshold> <> failure })
-->
   (<s> ^object-occluded false)
}

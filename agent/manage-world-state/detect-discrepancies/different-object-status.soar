#########################################################
#
# Discrepancy: different-object-status
#   One of the status predicates on the object-info doesn't match the world object
# Handling: attend-to-perception/different-object-status
#
#########################################################

sp {perception-monitor*detect-discrepancies*different-object-status*is-confirmed1
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^discrepancies <discs>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^status.is-confirmed1 <conf>
               ^world-obj.predicates <preds>)
   (<preds> ^category << object person location >>
           -^is-confirmed1 <conf>)
-->
   (<discs> ^different-object-status <disc>)
   (<disc> ^object-info <obj-info> 
           ^property-handle is-confirmed1
           ^predicate-handle <conf>)
}

sp {perception-monitor*detect-discrepancies*different-object-status*is-visible1
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^discrepancies <discs>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^status.is-visible1 <vis>
               ^world-obj.predicates <preds>)
   (<preds> ^category << object person location >>
           -^is-visible1 <vis>)
-->
   (<discs> ^different-object-status <disc>)
   (<disc> ^object-info <obj-info> 
           ^property-handle is-visible1
           ^predicate-handle <vis>)
}

sp {perception-monitor*detect-discrepancies*different-object-status*is-reachable1
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^discrepancies <discs>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^status.is-reachable1 <reach>
               ^world-obj.predicates <preds>)
   (<preds> ^category << object person >>
           -^is-reachable1 <reach>)
-->
   (<discs> ^different-object-status <disc>)
   (<disc> ^object-info <obj-info> 
           ^property-handle is-reachable1
           ^predicate-handle <reach>)
}

sp {perception-monitor*detect-discrepancies*different-object-status*is-grabbed1
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^discrepancies <discs>
               ^object-monitor.object-info <obj-info>)
   (<obj-info> ^status.is-grabbed1 <grab>
               ^world-obj.predicates <preds>)
   (<preds> ^affordance1 grabbable1
           -^is-grabbed1 <grab>)
-->
   (<discs> ^different-object-status <disc>)
   (<disc> ^object-info <obj-info> 
           ^property-handle is-grabbed1
           ^predicate-handle <grab>)
}


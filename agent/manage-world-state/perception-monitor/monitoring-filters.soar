# For domains where we have a view volume, intersect it with all belief objects in the scene
#   to see which are in-view

sp {topstate*manage-world-state*perception-monitor*elaborate*robot-view-filter
   (state <s> ^superstate nil
              ^agent-params.domain << magicbot cozmo >>
              ^perception-monitor <pmon>
              ^svs.command <cmd>)
-->
   (<pmon> ^robot-view-filter <filter>)
   (<filter> ^type contain
             ^a <robot-view>
             ^b <belief-objs>
             ^contain_type point)
   (<robot-view> ^type node
                 ^id robot_view)
   (<belief-objs> ^type tag_select
                  ^tag_name object-source
                  ^tag_value belief
                  ^a <all>)
   (<all> ^type all_nodes)
   (<cmd> ^extract <filter>)
}

# For the magicbot domain, we also extract the distance from each object to the robot
sp {topstate*manage-world-state*perception-monitor*elaborate*object-distance-filter
   (state <s> ^superstate nil
              ^agent-params.domain magicbot 
              ^perception-monitor <pmon>
              ^svs.command <cmd>)
-->
   (<pmon> ^obj-distance-filter <filter>)
   (<filter> ^type distance
             ^a <robot>
             ^b <perc-objs>
             ^distance_type hull)
   (<robot> ^type node
            ^id robot_body)
   (<perc-objs> ^type tag_select
                ^tag_name object-source
                ^tag_value perception
                ^a <all>)
   (<all> ^type all_nodes)
   (<cmd> ^extract <filter>)
}

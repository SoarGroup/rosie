# Reject an attend-to-scene-change operator if attending to an object being moved

sp {anystate*reject*attend-to-scene-change*moving*object
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change <chg>)
   (<chg> ^info.category object
          ^object-info.wm-obj.predicates.arm-status << moving grabbed >>
          ^type <> newly-visible-object <> newly-not-visible-object <> changed-arm-status)
-->
   (<s> ^operator <o> -)
}
#
## Don't delete objects that are part of the current task
#sp {anystate*reject*delete-object*task*object
#   (state <s> ^operator <o> +)
#   (<o> ^name attend-to-scene-change
#        ^change.type << delete-object >>
#        ^change.object-info.wm-obj <obj>)
#   (<obj> ^task-object true)
#-->
#   (<s> ^operator <o> -)
#}

# Ordering of scene change operators

sp {anystate*attend-to-scene-change*prefer*higher*preference-order
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change
         ^preference-order <pref1>)
   (<o2> ^name attend-to-scene-change
         ^preference-order < <pref1>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {anystate*attend-to-scene-change*prefer*equal*same*preference-order
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change
         ^preference-order <pref>)
   (<o2> ^name attend-to-scene-change
         ^preference-order <pref>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {anystate*attend-to-scene-change*elaborate*default*preference-order
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
       -^preference-order > 0)
-->
   (<o> ^preference-order 0)
}


# Preference Orders:

# 200 update-pose
# 200 delete-object 
# 200 create-belief-object
# 190 new-perception-object
# 190 new-object
# 180 changed-moving-status
# 180 changed-arm-moving-status
# 160 changed-waypoint 
# 140 stability-timer-expired
# 120 moved-object
# 100 newly-visible-object
# 100 newly-not-visible-object
# 100 new-object-predicate
#  80 missing-object

# 200 update-pose
# 200 delete-object 
sp {anystate*scene-change*attend*preference*200
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << update-pose delete-object create-belief-object >>)
-->
   (<o> ^preference-order 200)
}

# 190 new-perception-object, new-object
sp {anystate*scene-change*attend*preference*190
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << new-perception-object new-object >>)
-->
   (<o> ^preference-order 190)
}

# 180 changed-moving-status, changed-arm-moving-status
sp {anystate*scene-change*attend*preference*180
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << changed-moving-status changed-arm-moving-status >>)
-->
   (<o> ^preference-order 180)
}

# 160 attend-to-changed-waypoint 
sp {anystate*scene-change*attend*preference*160
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << changed-waypoint >>)
-->
   (<o> ^preference-order 160)
}

# 140 stability-timer-expired
sp {anystate*scene-change*attend*preference*140
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << stability-timer-expired >>)
-->
   (<o> ^preference-order 140)
}

# 120 moved-object
sp {anystate*scene-change*attend*preference*120
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << moved-object >>)
-->
   (<o> ^preference-order 120)
}

# 100 newly-visible-object
# 100 newly-not-visible-object
# 100 new-object-predicate
sp {anystate*scene-change*attend*preference*100
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << newly-visible-object newly-not-visible-object new-object-predicate >>)
-->
   (<o> ^preference-order 100)
}

#  80 missing-object
sp {anystate*scene-change*attend*preference*80
   (state <s> ^operator <o> +)
   (<o> ^name attend-to-scene-change
        ^change.type << missing-object >>)
-->
   (<o> ^preference-order 80)
}

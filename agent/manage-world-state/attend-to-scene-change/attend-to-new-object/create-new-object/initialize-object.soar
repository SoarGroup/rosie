sp {create-new-object*propose*initialize-object
   (state <s> ^name create-new-object
             -^new-object-rep.handle <h>)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialize-object)
}

sp {create-new-object*apply*initialize-object*create*new*rep
   (state <s> ^name create-new-object
              ^operator.name initialize-object)
-->
   (<s> ^new-object-rep <rep>)
   (<rep> ^item-type object
          ^predicates <preds>)
}

sp {create-new-object*apply*initialize-object*copy*handle
   (state <s> ^name create-new-object
              ^operator.name initialize-object
              ^new-object-rep <rep>
              ^object-handle <h>)
-->
   (<rep> ^handle <h>)
}

sp {create-new-object*apply*initialize-object*mark*in-smem
   (state <s> ^name create-new-object
              ^operator.name initialize-object
              ^smem-obj <smem>
              ^new-object-rep <rep>)
-->
   (<rep> ^in-smem true)
}

sp {create-new-object*apply*initialize-object*mark*arm-status*not-grabbed
   (state <s> ^name create-new-object
              ^operator.name initialize-object
              ^agent-params.domain <> ai2thor
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^arm-status not-grabbed)
}

sp {create-new-object*apply*initialize-object*mark*arm-status*not-grabbed*ai2thor
   (state <s> ^name create-new-object
              ^operator.name initialize-object
              ^agent-params.domain ai2thor
              ^new-object-rep.predicates <preds>
              ^object-info.properties.property-info.predicate-handle grabbable)
-->
   (<preds> ^arm-status not-grabbed)
}

sp {create-new-object*apply*initialize-object*mark*object-status*confirmed
   (state <s> ^name create-new-object
              ^operator.name initialize-object
              ^source-type perception-obj
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^object-status confirmed
            ^visible true)
}

sp {create-new-object*apply*initialize-object*mark*object-status*unconfirmed
   (state <s> ^name create-new-object
              ^operator.name initialize-object
              ^source-type <> perception-obj
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^object-status unconfirmed
            ^visible false)
}


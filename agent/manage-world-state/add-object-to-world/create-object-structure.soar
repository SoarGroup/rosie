sp {add-object-to-world*propose*create-object-structure
   (state <s> ^name add-object-to-world
              ^object-handle <h>
              ^category-info.root-category-lti.handle <root>
             -^created-object <obj>
             -^matches-existing-object)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-object-structure
        ^object-handle <h>
        ^root-category <root>)
}

sp {add-object-to-world*apply*create-object-structure*base
   (state <s> ^name add-object-to-world
              ^operator <o>)
   (<o> ^name create-object-structure
        ^object-handle <h>
        ^root-category <root>)
-->
   (<s> ^new-object-rep <obj>)
   (<obj> ^item-type object
          ^handle <h>
          ^predicates <preds>
          ^root-category <root>)
}

sp {add-object-to-world*apply*create-object-structure*mark*created
   (state <s> ^name add-object-to-world
              ^operator.name create-object-structure
              ^new-object-rep <obj>)
-->
   (<s> ^created-object <obj>)
}

sp {add-object-to-world*apply*create-object-structure*copy*predicates
   (state <s> ^name add-object-to-world
              ^operator.name create-object-structure
              ^new-object-predicates.<prop-h> <pred-h>
              ^new-object-rep.predicates <preds>)
-->
   (<preds> ^<prop-h> <pred-h>)
}

sp {add-object-to-world*apply*create-object-structure*copy*waypoint
   (state <s> ^name add-object-to-world
              ^operator.name create-object-structure
              ^source-obj.waypoint <wp-h>
              ^new-object-rep <obj>)
-->
   (<obj> ^waypoint <wp-h>)
}

sp {add-object-to-world*apply*create-object-structure*copy*waypoint*from*smem
   (state <s> ^name add-object-to-world
              ^operator.name create-object-structure
              ^matches-smem-object.waypoint <wp-h>
              ^new-object-rep <obj>)
-->
   (<obj> ^waypoint <wp-h>)
}


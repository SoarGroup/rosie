##### add-object-to-world application ####
# These rules fire if there is an immediate result

# The object being added is already there
sp {anystate*apply*add-object-to-world*already*there
   (state <s> ^operator <o>
              ^top-state.world.objects.object <obj>)
   (<o> ^name add-object-to-world
        ^source-obj <obj>)
-->
   (<s> ^add-object-result <res>)
   (<res> ^source-obj <obj>
          ^world-obj <obj>)
}

# Adding an imagined object that already has an attached world-object
sp {anystate*apply*add-object-to-world*imagined*world-object
   (state <s> ^operator <o>
             -^add-object-result.source-obj <obj>)
   (<o> ^name add-object-to-world
        ^source-type imagined
        ^source-obj <obj>)
   (<obj> ^world-object <wobj>)
-->
   (<s> ^add-object-result <res>)
   (<res> ^source-type imagined
          ^source-obj <obj>
          ^world-obj <wobj>)
}


### matches-object-relation
# match an existing object based on a given relation

sp {add-object-to-world*elaborate*match-relation
   (state <s> ^name add-object-to-world
              ^source-obj.relation <rel>)
   (<rel> ^handle <rel-h>
          ^2 <obj2>)
-->
   (<s> ^match-relation <mat>)
   (<mat> ^handle <rel-h>
          ^source-obj2 <obj2>)
}

sp {add-object-to-world*propose*add-object-to-world*match-relation*obj2
   (state <s> ^name add-object-to-world
              ^match-relation <mat>)
   (<mat> ^source-obj2 <src-obj2>
         -^2 <obj2>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-object-to-world 
        ^source-obj <src-obj2>)
}

sp {add-object-to-world*elaborate*match-relation*object*2
   (state <s> ^name add-object-to-world
              ^match-relation <mat>
              ^add-object-result <res>)
   (<mat> ^handle <rel-h>
          ^source-obj2 <src-obj2>)
   (<res> ^source-obj <src-obj2>
          ^world-obj <world-obj2>)
-->
   (<mat> ^2 <world-obj2>)
}

sp {add-object-to-world*elaborate*match-relation*candidate
   (state <s> ^name add-object-to-world
              ^match-relation <mat>
              ^object-monitor.object-info <obj-info>
              ^top-state.world.predicates.predicate <pred>)
   (<mat> ^handle <pred-h>
          ^2 <obj2>)
   (<obj-info> ^world-obj <obj1>)
   (<pred> ^handle <pred-h>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<mat> ^candidate <obj-info>)
}


### matches-overlapping-object: The new-perception-object matches
#     an existing object based on overlapping the bounding volume

### Case 1: The new-perception-obj overlaps an object without a input-link-obj

sp {add-object-to-world*propose*matches-overlapping-object*matches*not-visible*object
   (state <s> ^name add-object-to-world
              ^overlap-results.result <res>
             -^matches-existing-object)
   (<res> ^overlapper-info <obj-info>
          ^overlap <val>)
   (<obj-info> ^status.is-visible1 not-visible1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name matches-overlapping-object
        ^overlapping-object <obj-info>
        ^overlap <val>)
}

### Case 2: The new-perception-obj overlaps an object with a shared-input-link-obj
# (The object shares a input-link-obj with another, meaning they might be segmented together)

sp {add-object-to-world*propose*matches-overlapping-object*shared-input-link-obj
   (state <s> ^name add-object-to-world
              ^overlap-results.result <res>
              ^agent-params.contained-threshold <thresh>
             -^matches-existing-object)
   (<res> ^overlapper-info <obj-info>
          ^overlap { <val> > <thresh> })
   (<obj-info> ^status.shared-input-link-obj true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name matches-overlapping-object
        ^overlapping-object <obj-info>
        ^overlap <val>)
}

### Case 3: The new-perception-obj is contained within an existing belief-obj
#     which is not a receptacle (receptacles can hold other items inside)

sp {add-object-to-world*propose*matches-overlapping-object*contained*by*other
   (state <s> ^name add-object-to-world
              ^overlap-results.result <res>
              ^agent-params.contained-by-threshold <thresh>
             -^matches-existing-object)
   (<res> ^overlapper-info <obj-info>
          ^overlap { <val> > <thresh> })
   (<obj-info> ^world-obj.predicates <preds>
               ^status.is-visible1 visible1)
   (<preds> -^affordance1 receptacle1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name matches-overlapping-object
        ^overlapping-object <obj-info>
        ^overlap <val>)
}

### Preferences

# Prefer more overlap when matching
sp {add-object-to-world*prefer*matches-overlapping-object*with*more*overlap
   (state <s> ^name add-object-to-world
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name matches-overlapping-object
         ^overlap <val1>)
   (<o2> ^name matches-overlapping-object
         ^overlap {<val2> < <val1>})
-->
   (<s> ^operator <o1> > <o2>)
}

# Reject match if it differs on color
sp {match-exisiting-object*reject*matches-overlapping-object*different*color
   (state <s> ^name add-object-to-world
              ^operator <o> +
              ^new-object-predicates.color <color>)
   (<o> ^name matches-overlapping-object
        ^overlapping-object.world-obj.predicates.color <> <color>)
-->
   (<s> ^operator <o> -)
}

# Reject match if differs on category
sp {match-exisiting-object*reject*matches-overlapping-object*different*category
   (state <s> ^name add-object-to-world
              ^operator <o> +
              ^category-info.root-category <cat>)
   (<o> ^name matches-overlapping-object
        ^overlapping-object.world-obj.predicates <opreds>)
   (<opreds> -^category <cat>)
-->
   (<s> ^operator <o> -)
}

### Application

# Report the match to the superstate
sp {add-object-to-world*apply*matches-overlapping-object
   (state <s> ^name add-object-to-world
              ^operator <o>)
   (<o> ^name matches-overlapping-object
        ^overlapping-object <obj-info>)
-->
   (<s> ^matches-existing-object <obj-info>
        ^overlap-match <obj-info>)
}

# If print-perception is on, print a message about the match
sp {add-object-to-world*apply*matches-overlapping-object*print-perception
   (state <s> ^name add-object-to-world
              ^operator <o>
              ^object-handle <new-obj>
              ^agent-params.print-perception true)
   (<o> ^name matches-overlapping-object
        ^overlapping-object.object-handle <matching-obj>)
-->
   (write |add-object-to-world: Object | <new-obj> | matches | <matching-obj> | via overlap|(crlf))
}

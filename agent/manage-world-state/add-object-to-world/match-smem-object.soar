
sp {add-object-to-world*match-smem-object*elaborate*smem-query
   (state <s> ^name add-object-to-world
              ^source-type <> smem
              ^category-info.root-category <cat>
              ^source-predicates <src>
             -^matches-smem-object)
   (<src> ^{ << name cardinal-direction1 modifier1 possessive >> <prop1> } <pred1>)
   (<src> -^{ << name cardinal-direction1 modifier1 possessive >> <> <prop1> } <pred1>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^destination <s>
        ^result-name matches-smem-object)
   (<cue> ^item-type object
          ^object-instance true
          ^category <cat>
          ^<prop1> <pred1>)
}

sp {add-object-to-world*match-smem-object*elaborate*smem-query*2*preds
   (state <s> ^name add-object-to-world
              ^source-type <> smem
              ^category-info.root-category <cat>
              ^source-predicates <src>
             -^matches-smem-object)
   (<src> ^{ << name cardinal-direction1 modifier1 possessive >> <prop1> } <pred1>)
   (<src> ^{ << name cardinal-direction1 modifier1 possessive >> <prop2> > <prop1> } <pred2>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^destination <s>
        ^result-name matches-smem-object)
   (<cue> ^item-type object
          ^object-instance true
          ^category <cat>
          ^<prop1> <pred1>
          ^<prop2> <pred2>)
}

sp {add-object-to-world*match-smem-object*elaborate*smem-query*cardinal-direction
   (state <s> ^name add-object-to-world
              ^source-type <> smem
              ^category-info.root-category <cat>
              ^source-predicates.cardinal-direction1 <dir>
             -^matches-smem-object)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^destination <s>
        ^result-name matches-smem-object)
   (<cue> ^item-type object
          ^object-instance true
          ^category <cat>
          ^cardinal-direction1 <dir>)
}


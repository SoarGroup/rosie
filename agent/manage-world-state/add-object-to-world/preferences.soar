
# First find intersecting objects, then overlapping ones
sp {add-object-to-world*prefer*intersect-command*over*overlap-command
   (state <s> ^name add-object-to-world
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name intersect-command)
   (<o2> ^name overlap-command)
-->
   (<s> ^operator <o1> > <o2>)
}

# Wait to match until all svs-commands have finished
sp {add-object-to-world*prefer*svs-commands*over*matches-overlapping-object
   (state <s> ^name add-object-to-world
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << intersect-command overlap-command >>)
   (<o2> ^name << matches-overlapping-object matches-object-predicates create-object-structure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

# If there is a relation, add the relation object before matching
sp {add-object-to-world*prefer*add-object-to-world*over*matching
   (state <s> ^name add-object-to-world
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << add-object-to-world >>)
   (<o2> ^name << matches-overlapping-object matches-object-predicates create-object-structure >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {add-object-to-world*prefer*matching*over*create
   (state <s> ^name add-object-to-world
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << matches-overlapping-object matches-object-predicates >>)
   (<o2> ^name << create-object-structure change-world-state >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {add-object-to-world*prefer*matching*overlapping*to*predicates
   (state <s> ^name add-object-to-world
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name matches-overlapping-object)
   (<o2> ^name matches-object-predicates)
-->
   (<s> ^operator <o1> > <o2>)
}


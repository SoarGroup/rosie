### change-world-state
# If the new-perception-obj does match an existing one, 
#   change the world to resolve the discrepancy


### Case 1: If the matched object has no perception object, change the perception-id
sp {attend-to-perception*new-perception-object*propose*change-world-state*change-perception-id*no*input-link-obj
   (state <s> ^name attend-to-perception
              ^discrepancy-type new-perception-object
              ^input-link-obj.object-handle <perc-id>
              ^matches-existing-object { <existing-obj> <> none })
   (<existing-obj> ^status.is-visible1 not-visible1)
-->
   (<s> ^operator <o> +)
   (<o> ^name change-world-state
        ^change-perception-id <chg>)
   (<chg> ^object-info <existing-obj>
          ^perception-id <perc-id>)
}

### Case 2: If the matched object has a shared-input-link-obj, change the perception-id
sp {attend-to-perception*new-perception-object*propose*change-world-state*change-perception-id*shared-input-link-obj
   (state <s> ^name attend-to-perception
              ^discrepancy-type new-perception-object
              ^input-link-obj.object-handle <perc-id>
              ^matches-existing-object { <existing-obj> <> none })
   (<existing-obj> ^status.shared-input-link-obj true)
-->
   (<s> ^operator <o> +)
   (<o> ^name change-world-state
        ^change-perception-id <chg>)
   (<chg> ^object-info <existing-obj>
          ^perception-id <perc-id>)
}

### Case 3: If the matched object has a input-link-obj, merge the perception-objs
sp {attend-to-perception*new-perception-object*propose*change-world-state*merge-perception-objects
   (state <s> ^name attend-to-perception
              ^discrepancy-type new-perception-object
              ^input-link-obj.object-handle <perc-id>
              ^matches-existing-object { <existing-obj> <> none })
   (<existing-obj> -^status.shared-input-link-obj true
                    ^input-link-obj.object-handle <other-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name change-world-state
        ^merge-perception-objects <merge>)
   (<merge> ^core-perception-id <other-id>
            ^merging-perception-id <perc-id>)
}

### create-belief-object
# If the matched object has no belief-obj, create one

sp {attend-to-perception*new-perception-object*elaborate*change-world-state*create-belief-object
   (state <s> ^name attend-to-perception
              ^discrepancy-type new-perception-object
              ^input-link-obj.object-handle <perc-id>
              ^matches-existing-object { <existing-obj> <> none }
              ^operator <o> +)
   (<o> ^name change-world-state)
   (<existing-obj> ^object-handle <obj-handle>
                   ^belief-id <bel-id>
                  -^belief-obj)
-->
   (<o> ^create-belief-object <bel>)
   (<bel> ^perception-id <perc-id>
          ^belief-id <bel-id>)
}


### Case 4: If the object doesn't match anything, create one and add it to the world
sp {attend-to-perception*new-perception-object*propose*change-world-state*add-object-to-world
   (state <s> ^name attend-to-perception
              ^discrepancy-type new-perception-object
              ^input-link-obj.object-handle <perc-id>
              ^constructed-world-object.result <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name change-world-state
        ^add-object-to-world <add>)
   (<add> ^object <obj>
          ^perception-id <perc-id>)
}


### Apply change-world-state: clean up the new-object-info on the object-monitor
sp {attend-to-perception*new-perception-object*apply*change-world-state*remove*new-object-info
   (state <s> ^name attend-to-perception
              ^discrepancy-type new-perception-object
              ^operator.name change-world-state
              ^object-info <obj-info>
              ^top-state.perception-monitor.object-monitor <obj-mon>)
   (<obj-mon> ^new-object-info <obj-info>)
-->
   (<obj-mon> ^new-object-info <obj-info> -)
}

# also remove the svs filters
sp {attend-to-perception*new-perception-object*apply*change-world-state*remove*monitoring*filters
   (state <s> ^name attend-to-perception
              ^discrepancy-type new-perception-object
              ^operator.name change-world-state
              ^superstate.operator.new-object-info <obj-info>
              ^top-state.svs.command <cmd>)
   (<obj-info> ^position-monitor <pos>
               ^volume-monitor <vol>)
   (<cmd> ^extract <pos>
          ^extract <vol>)
-->
   (<cmd> ^extract <pos> -
          ^extract <vol> -)
}

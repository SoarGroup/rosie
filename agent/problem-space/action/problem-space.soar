sp {problem-space*action*elaborate*default*problem-space
   (state <s> ^superstate.problem-space <ss-ps>
             -^default-problem-space no)
   (<ss-ps> ^name action)
-->
   (<s> ^problem-space <ps>)
   (<ps> ^name action
         ^world <world>
         ^learning <learn>
         ^subtasks <subs>
         ^super-problem-space <ss-ps>)
}

sp {problem-space*action*copy*default*subproblem*from*superstate
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^super-problem-space.subproblem <subp>
        -^subproblem <> <subp>)
-->
   (<ps> ^subproblem <subp>)
}

sp {problem-space*action*copy*default*execution-type*from*superstate
   (state <s> ^problem-space <ps>
             -^name selection
             -^name evaluate-operator)
   (<ps> ^name action
         ^super-problem-space.execution-type <type>
        -^execution-type <> <type>)
-->
   (<ps> ^execution-type <type>)
}


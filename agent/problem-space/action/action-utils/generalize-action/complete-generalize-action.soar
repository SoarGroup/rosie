sp {generalize-action*propose*complete-generalize-action
   (state <s> ^name generalize-action)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-generalize-action)
}

# Copy the perform flag
sp {generalize-action*elaborate*complete-generalize-action*perform*from*source-id
   (state <s> ^name generalize-action
              ^operator <o> +
              ^source-id.perform <perf>)
   (<o> ^name complete-generalize-action)
-->
   (<o> ^perform <perf>)
}

# Default perform flag
sp {generalize-action*elaborate*complete-generalize-action*perform*default
   (state <s> ^name generalize-action
              ^operator <o> +
             -^source-id.perform)
   (<o> ^name complete-generalize-action)
-->
   (<o> ^perform multiple)
}

# Create the root structure
sp {generalize-action*apply*complete-generalize-action*create*generalized-action
   (state <s> ^name generalize-action
              ^source-action.name <op-name>
              ^source-id.handle <handle>
              ^operator <o>)
   (<o> ^name complete-generalize-action
        ^perform <perf>)
-->
   (<s> ^generalized-action <gen>)
   (<gen> ^op_name <op-name>
          ^action-handle <handle>
          ^perform <perf>)
}

# Copy the arg-status flag onto the argument
sp {generalize-action*apply*complete-generalize-action*copy*arg-status
   (state <s> ^name generalize-action
              ^operator.name complete-generalize-action
              ^source-id.procedural.<arg-name>.arg-status <arg-status>
              ^generalize-argument <arg>)
   (<arg> ^arg-name <arg-name>
          ^generalized-rep <gen-arg>)
-->
   (<gen-arg> ^arg-status <arg-status>)
}


# Copy the arguments onto the generalized-action and copy the generalized-action to the superstate
# (Will create the chunked application rule)
# ENUMERATE: 0-5 arguments

sp {generalize-action*apply*complete-generalize-action*copy*generalized-action*superstate*0*arguments
   (state <s> ^name generalize-action
              ^operator.name complete-generalize-action
              ^num-args 0
              ^generalized-action <gen>
              ^ss-generalize-info <info>)
-->
   (<info> ^generalized-action <gen>)
}

sp {generalize-action*apply*complete-generalize-action*copy*generalized-action*superstate*1*arguments
   (state <s> ^name generalize-action
              ^operator.name complete-generalize-action
              ^num-args 1
              ^generalized-action <gen>
              ^generalize-argument   <arg1>
              ^ss-generalize-info <info>)
   (<arg1> ^arg-name <arg1-name> ^generalized-rep <gen-arg1>)
-->
   (<info> ^generalized-action <gen>)
   (<gen> ^<arg1-name> <gen-arg1>)
}

sp {generalize-action*apply*complete-generalize-action*copy*generalized-action*superstate*2*arguments
   (state <s> ^name generalize-action
              ^operator.name complete-generalize-action
              ^num-args 2
              ^generalized-action <gen>
              ^generalize-argument   <arg1>
              ^generalize-argument { <arg2> > <arg1>}
              ^ss-generalize-info <info>)
   (<arg1> ^arg-name <arg1-name> ^generalized-rep <gen-arg1>)
   (<arg2> ^arg-name <arg2-name> ^generalized-rep <gen-arg2>)
-->
   (<info> ^generalized-action <gen>)
   (<gen> ^<arg1-name> <gen-arg1>
          ^<arg2-name> <gen-arg2>)
}

sp {generalize-action*apply*complete-generalize-action*copy*generalized-action*superstate*3*arguments
   (state <s> ^name generalize-action
              ^operator.name complete-generalize-action
              ^num-args 3
              ^generalized-action <gen>
              ^generalize-argument   <arg1>
              ^generalize-argument { <arg2> > <arg1>}
              ^generalize-argument { <arg3> > <arg2>}
              ^ss-generalize-info <info>)
   (<arg1> ^arg-name <arg1-name> ^generalized-rep <gen-arg1>)
   (<arg2> ^arg-name <arg2-name> ^generalized-rep <gen-arg2>)
   (<arg3> ^arg-name <arg3-name> ^generalized-rep <gen-arg3>)
-->
   (<info> ^generalized-action <gen>)
   (<gen> ^<arg1-name> <gen-arg1>
          ^<arg2-name> <gen-arg2>
          ^<arg3-name> <gen-arg3>)
}

sp {generalize-action*apply*complete-generalize-action*copy*generalized-action*superstate*4*arguments
   (state <s> ^name generalize-action
              ^operator.name complete-generalize-action
              ^num-args 4
              ^generalized-action <gen>
              ^generalize-argument   <arg1>
              ^generalize-argument { <arg2> > <arg1>}
              ^generalize-argument { <arg3> > <arg2>}
              ^generalize-argument { <arg4> > <arg3>}
              ^ss-generalize-info <info>)
   (<arg1> ^arg-name <arg1-name> ^generalized-rep <gen-arg1>)
   (<arg2> ^arg-name <arg2-name> ^generalized-rep <gen-arg2>)
   (<arg3> ^arg-name <arg3-name> ^generalized-rep <gen-arg3>)
   (<arg4> ^arg-name <arg4-name> ^generalized-rep <gen-arg4>)
-->
   (<info> ^generalized-action <gen>)
   (<gen> ^<arg1-name> <gen-arg1>
          ^<arg2-name> <gen-arg2>
          ^<arg3-name> <gen-arg3>
          ^<arg4-name> <gen-arg4>)
}

sp {generalize-action*apply*complete-generalize-action*copy*generalized-action*superstate*5*arguments
   (state <s> ^name generalize-action
              ^operator.name complete-generalize-action
              ^num-args 5
              ^generalized-action <gen>
              ^generalize-argument   <arg1>
              ^generalize-argument { <arg2> > <arg1>}
              ^generalize-argument { <arg3> > <arg2>}
              ^generalize-argument { <arg4> > <arg3>}
              ^generalize-argument { <arg5> > <arg4>}
              ^ss-generalize-info <info>)
   (<arg1> ^arg-name <arg1-name> ^generalized-rep <gen-arg1>)
   (<arg2> ^arg-name <arg2-name> ^generalized-rep <gen-arg2>)
   (<arg3> ^arg-name <arg3-name> ^generalized-rep <gen-arg3>)
   (<arg4> ^arg-name <arg4-name> ^generalized-rep <gen-arg4>)
   (<arg5> ^arg-name <arg5-name> ^generalized-rep <gen-arg5>)
-->
   (<info> ^generalized-action <gen>)
   (<gen> ^<arg1-name> <gen-arg1>
          ^<arg2-name> <gen-arg2>
          ^<arg3-name> <gen-arg3>
          ^<arg4-name> <gen-arg4>
          ^<arg5-name> <gen-arg5>)
}

####################### predicate ###########################

### Expand the predicate to generalize its sub-components

sp {generalize-action*generalize-argument*predicate*expand*state*predicate
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate
         ^instance <i>)
  (<i> ^type state
       ^handle <handle>
       ^1 <obj1>)
-->
  (<s> ^generalize-argument <gen-obj1> <gen-pred>)

  (<gen-pred> ^instance <pred-i>)
  (<pred-i> ^arg-type predicate-handle
            ^handle <handle>)

  (<gen-obj1> ^instance <obj1-i>)
  (<obj1-i> ^arg-type object
            ^id <obj1>)

  (<arg> ^generalize-handle <gen-pred>
         ^generalize-obj1 <gen-obj1>)
}


sp {generalize-action*generalize-argument*predicate*expand*relation*predicate
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate
         ^instance <i>)
  (<i> ^type relation
       ^handle <handle>
       ^1 <obj1>
       ^2 <obj2>)
-->
  (<s> ^generalize-argument <gen-pred> <gen-obj1> <gen-obj2>)

  (<gen-pred> ^instance <pred-i>)
  (<pred-i> ^arg-type predicate-handle
            ^handle <handle>)

  (<gen-obj1> ^instance <obj1-i>)
  (<obj1-i> ^arg-type object
            ^id <obj1>)

  (<gen-obj2> ^instance <obj2-i>)
  (<obj2-i> ^arg-type object
            ^id <obj2>)

  (<arg> ^generalize-handle <gen-pred>
         ^generalize-obj1 <gen-obj1>
         ^generalize-obj2 <gen-obj2>)
}

sp {generalize-action*generalize-argument*predicate*expand*predicate*arg-type
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^instance <i>)
  (<i> ^arg-type predicate
       ^handle <handle>
       ^2 <obj>)
-->
  (<s> ^generalize-argument <gen-obj> <gen-pred>)

  (<gen-pred> ^instance <pred-i>)
  (<pred-i> ^arg-type predicate-handle
            ^handle <handle>)

  (<gen-obj> ^instance <obj-i>)
  (<obj-i> ^arg-type object
           ^id <obj>)

  (<arg> ^generalize-handle <gen-pred>
         ^generalize-obj2 <gen-obj>)
}

sp {generalize-action*generalize-argument*predicate*elaborate*object*reference-info
   (state <s> ^name generalize-action
              ^generalize-argument <arg>)
   (<arg> ^arg-type predicate
          ^generalize-obj2 <gen-obj1>
          ^instance.reference-info <info>)
-->
   (<gen-obj1> ^reference-info <info>)
}


### When all the subparts are generalized, generalize the whole predicate

# generalize state predicate
sp {generalize-action*propose*generalize-argument*predicate*state
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate
         ^instance.type state
         ^generalize-handle.generalized-rep.id <pred-slot>
         ^generalize-obj1.generalized-rep.id <obj1-slot>
         -^generalized-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type state
       ^id <pred-slot>
       ^1 <obj1-slot>)
}

sp {generalize-action*apply*generalize-argument*predicate*state
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type state
       ^id <pred-slot>
       ^1 <obj1-slot>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^type state
         ^id <pred-slot>
         ^1 <obj1-slot>)
}


# generalize relation predicate
sp {generalize-action*propose*generalize-argument*predicate*relation
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^arg-type predicate
         ^instance.type relation
         ^generalize-handle.generalized-rep.id <pred-slot>
         ^generalize-obj1.generalized-rep.id <obj1-slot>
         ^generalize-obj2.generalized-rep.id <obj2-slot>
         -^generalized-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^id <pred-slot>
       ^1 <obj1-slot>
       ^2 <obj2-slot>)
}

sp {generalize-action*apply*generalize-argument*predicate*relation
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^id <pred-slot>
       ^1 <obj1-slot>
       ^2 <obj2-slot>)
-->
  (<arg> ^generalized-rep <rep>)
  (<rep> ^type relation
         ^id <pred-slot>
         ^1 <obj1-slot>
         ^2 <obj2-slot>)
}


# generalize partial predicate (from action operator)
sp {generalize-action*propose*generalize-argument*predicate*predicate
  (state <s> ^name generalize-action
             ^generalize-argument <arg>)
  (<arg> ^instance.arg-type predicate
         ^generalize-handle.generalized-rep.id <pred-slot>
         ^generalize-obj2.generalized-rep.id <obj-slot>
        -^generalized-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type partial
       ^id <pred-slot>
       ^2 <obj-slot>)
}

sp {generalize-action*apply*generalize-argument*predicate*procedural
  (state <s> ^name generalize-action
             ^operator <o>)
  (<o> ^name generalize-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type partial
       ^id <pred-slot>
       ^2 <obj-slot>)
-->
  (<arg> ^generalized-rep <gen>)
  (<gen> ^arg-type predicate
         ^id <pred-slot>
         ^2 <obj-slot>)
}



sp {anystate*copy-action*elaborate*performed*info
   (state <s> ^copy-action <ca>)
   (<ca> ^source-action.performed <sub>
         ^destination <dest>
         ^copy-name <name>)
   (<dest> ^<name> <copy>)
-->
   (<copy> ^performed <sub>)
}

sp {state*no-change*copy-action*elaborate*state*name*learn-copy-action-elaboration-rule
   (state <s> ^superstate.copy-action-present true
              ^impasse no-change
              ^attribute state)
-->
   (<s> ^name learn-copy-action-elaboration-rule)
}

sp {learn-copy-action-elaboration-rule*elaborate*copy-action*from*superstate
   (state <s> ^name learn-copy-action-elaboration-rule
              ^superstate.copy-action <ssca>)
-->
   (<s> ^copy-action <ca>)
   (<ca> ^ss-copy-action <ssca>)
}

sp {copy-action*op-no-change*elaborate*ss-copy-action*superoperator
   (state <s> ^name copy-action
              ^superstate.operator <o>)
-->
   (<s> ^name learn-copy-action-elaboration-rule
        ^copy-action <ca>)
   (<ca> ^ss-copy-action <o>)
}

sp {learn-copy-action-elaboration-rule*elaborate*info
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^ss-copy-action <ssca>)
   (<ssca> ^source-action <src-action>
           ^destination <dest>
           ^copy-name <name>)
-->
   (<ca> ^source-action <src-action>
         ^destination <dest>
         ^copy-name <name>)
}

sp {learn-copy-action-elaborate-rule*elaborate*copy-action*not-copied*true
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^copy-name <name>
        -^destination.<name>)
-->
   (<ca> ^not-copied true)
}

sp {learn-copy-action-elaboration-rule*elaborate*object-map*superoperator
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^ss-copy-action <ssca>)
   (<ssca> ^object-map <map>)
-->
   (<ca> ^object-map <map>)
}

sp {learn-copy-action-elaboration-rule*elaborate*object-map*none
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^ss-copy-action <ssca>)
   (<ssca> -^object-map <map>)
-->
   (<ca> ^no-object-map true)
}

# elaborate argument information 
## ENUMERATE: 0-5 arguments
sp {learn-copy-action-elaboration-rule*elaborate*no-args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^source-action <a>)
   (<a> -^{ << arg1 arg2 arg3 arg4 arg5 until-clause >> } <arg>)
-->
   (<ca> ^num-args 0)
}

sp {learn-copy-action-elaboration-rule*elaborate*arguments*1*arg
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^source-action <a>)
   (<a> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
       -^{ <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>)
   (<a1> ^arg-type <t1>)
--> 
   (<ca> ^num-args 1
        ^copy-argument <ca1>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
}

sp {learn-copy-action-elaboration-rule*elaborate*arguments*2*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
       -^{ <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
-->
   (<ca> ^num-args 2
        ^copy-argument <ca1> <ca2>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
}

sp {learn-copy-action-elaboration-rule*elaborate*arguments*3*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
       -^{ <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
-->
   (<ca> ^num-args 3
        ^copy-argument <ca1> <ca2> <ca3>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
}

sp {learn-copy-action-elaboration-rule*elaborate*arguments*4*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2> < <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
        ^{ <n4> > <n3>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
       -^{ <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
-->
   (<ca> ^num-args 4
        ^copy-argument <ca1> <ca2> <ca3> <ca4>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
   (<ca4> ^arg-type <t4>
          ^arg-name <n4>
          ^source-arg <a4>)
}


sp {learn-copy-action-elaboration-rule*elaborate*arguments*5*args
   (state <s> ^name learn-copy-action-elaboration-rule
              ^copy-action <ca>)
   (<ca> ^source-action <a>)
   (<a> ^{ <n1>        < <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> < <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2> < <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
        ^{ <n4> > <n3> < <n5> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
        ^{ <n5> > <n4>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>
       -^{ <n6> <> <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a6>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
   (<a5> ^arg-type <t5>)
-->
   (<ca> ^num-args 5
        ^copy-argument <ca1> <ca2> <ca3> <ca4> <ca5>)
   (<ca1> ^arg-type <t1>
          ^arg-name <n1>
          ^source-arg <a1>)
   (<ca2> ^arg-type <t2>
          ^arg-name <n2>
          ^source-arg <a2>)
   (<ca3> ^arg-type <t3>
          ^arg-name <n3>
          ^source-arg <a3>)
   (<ca4> ^arg-type <t4>
          ^arg-name <n4>
          ^source-arg <a4>)
   (<ca5> ^arg-type <t5>
          ^arg-name <n5>
          ^source-arg <a5>)
}

###############################################################
#####                      concept                        #####

sp {ground-action*propose*ground-argument*concept*matches*procedural
   (state <s> ^name ground-action
              ^ground-argument <arg>
              ^reference-tcn-proc.<arg-name> <parg>
              ^reference-action.<arg-name> <op-arg>)
   (<arg> ^generalized-rep <gen>
         -^instance)
   (<gen> ^arg-type concept
          ^id <conc-slot>)
   (<parg> ^arg-type concept
           ^id <conc-slot>)
   (<op-arg> ^arg-type concept
             ^handle <conc-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name ground-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^handle <conc-handle>)
}

sp {ground-action*propose*ground-argument*concept*matches*default
   (state <s> ^name ground-action
              ^ground-argument <arg>)
   (<arg> ^generalized-rep <gen>
         -^instance)
   (<gen> ^arg-type concept
          ^id.default.predicate-handle <conc-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name ground-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^handle <conc-handle>)
}

sp {ground-action*apply*ground-argument*concept*create*instance
  (state <s> ^operator <o>)
  (<o> ^name ground-argument
       ^arg-type concept
       ^argument <arg>
       ^handle <conc-handle>)
-->
  (<arg> ^instance <i>)
  (<i> ^arg-type concept
       ^handle <conc-handle>)
}

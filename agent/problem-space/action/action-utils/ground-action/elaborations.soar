sp {ground-action*elaborate*operator*info
   (state <s> ^name ground-action
              ^superstate.operator <ss-op>)
   (<ss-op> ^name ground-action
            ^info <info>
            ^source-id <src-id>
            ^reference-action <ref-action>
            ^reference-tcn-proc <ref-proc>
            ^world <w>)
-->
   (<s> ^ss-ground-info <info>
        ^source-id <src-id>
        ^reference-action <ref-action>
        ^reference-tcn-proc <ref-proc>
        ^world <w>)
}

sp {ground-action*elaborate*perform*from*source-id
   (state <s> ^name ground-action
              ^source-id.perform <perf>)
-->
   (<s> ^perform <perf>)
}

sp {ground-action*elaborate*perform*default
   (state <s> ^name ground-action
              ^source-id <src-id>)
   (<src-id> -^perform)
-->
   (<s> ^perform multiple)
}

# elaborate argument information 
## ENUMERATE: 0-5 arguments
sp {ground-action*elaborate*no-args
   (state <s> ^name ground-action
              ^source-id <a>)
   (<a> -^{ << arg1 arg2 arg3 arg4 arg5 until-clause >> } <arg>)
-->
   (<s> ^num-args 0)
}

sp {ground-action*elaborate*arguments*1*arg
   (state <s> ^name ground-action
              ^source-id <a>)
   (<a> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
       -^{ <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>)
   (<a1> ^arg-type <t1>)
-->
   (<s> ^num-args 1
        ^ground-argument <ga1>)
   (<ga1> ^arg-type <t1>
          ^arg-name <n1>
          ^generalized-rep <a1>)
}

sp {ground-action*elaborate*arguments*2*args
   (state <s> ^name ground-action
              ^source-id <a>)
   (<a> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
       -^{ <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
-->
   (<s> ^num-args 2
        ^ground-argument <ga1> <ga2>)
   (<ga1> ^arg-type <t1>
          ^arg-name <n1>
          ^generalized-rep <a1>)
   (<ga2> ^arg-type <t2>
          ^arg-name <n2>
          ^generalized-rep <a2>)
}

sp {ground-action*elaborate*arguments*3*args
   (state <s> ^name ground-action
              ^source-id <a>)
   (<a> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
       -^{ <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
-->
   (<s> ^num-args 3
        ^ground-argument <ga1> <ga2> <ga3>)
   (<ga1> ^arg-type <t1>
          ^arg-name <n1>
          ^generalized-rep <a1>)
   (<ga2> ^arg-type <t2>
          ^arg-name <n2>
          ^generalized-rep <a2>)
   (<ga3> ^arg-type <t3>
          ^arg-name <n3>
          ^generalized-rep <a3>)
}

sp {ground-action*elaborate*arguments*4*args
   (state <s> ^name ground-action
              ^source-id <a>)
   (<a> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
        ^{ <n4> > <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
       -^{ <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
-->
   (<s> ^num-args 4
        ^ground-argument <ga1> <ga2> <ga3> <ga4>)
   (<ga1> ^arg-type <t1>
          ^arg-name <n1>
          ^generalized-rep <a1>)
   (<ga2> ^arg-type <t2>
          ^arg-name <n2>
          ^generalized-rep <a2>)
   (<ga3> ^arg-type <t3>
          ^arg-name <n3>
          ^generalized-rep <a3>)
   (<ga4> ^arg-type <t4>
          ^arg-name <n4>
          ^generalized-rep <a4>)
}


sp {ground-action*elaborate*arguments*5*args
   (state <s> ^name ground-action
              ^source-id <a>)
   (<a> ^{ <n1>        << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a1>
        ^{ <n2> > <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a2>
        ^{ <n3> > <n2> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a3>
        ^{ <n4> > <n3> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a4>
        ^{ <n5> > <n4> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a5>
       -^{ <n6> <> <n5> <> <n4> <> <n3> <> <n2> <> <n1> << arg1 arg2 arg3 arg4 arg5 until-clause >> } <a6>)
   (<a1> ^arg-type <t1>)
   (<a2> ^arg-type <t2>)
   (<a3> ^arg-type <t3>)
   (<a4> ^arg-type <t4>)
   (<a5> ^arg-type <t5>)
-->
   (<s> ^num-args 5
        ^ground-argument <ga1> <ga2> <ga3> <ga4> <ga5>)
   (<ga1> ^arg-type <t1>
          ^arg-name <n1>
          ^generalized-rep <a1>)
   (<ga2> ^arg-type <t2>
          ^arg-name <n2>
          ^generalized-rep <a2>)
   (<ga3> ^arg-type <t3>
          ^arg-name <n3>
          ^generalized-rep <a3>)
   (<ga4> ^arg-type <t4>
          ^arg-name <n4>
          ^generalized-rep <a4>)
   (<ga5> ^arg-type <t5>
          ^arg-name <n5>
          ^generalized-rep <a5>)
}

# @SPCASE
# Prefer go-to over explore (for fetch)
sp {action*prefer*go-to*over*explore
   (state <s> ^current-action.task-operator.task-handle fetch1
              ^current-action.task-operator.arg2 <arg2>
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name op_go-to1)
   (<o2> ^name op_explore1)
-->
   (<s> ^operator <o1> > <o2>)
}

# Reject ask operator if current-action.asked <msg> is on the state
sp {action*reject*ask*if*asked
   (state <s> ^current-action.task-operator.task-handle ask1
              ^operator <o> +
              ^current-action.asked <msg>)
   (<o> ^name op_ask1
        ^arg1.id.predicates.sentence <msg>)
-->
   (<s> ^operator <o> -)
}

# Reject ask operator if ^asked <msg> is on state
sp {action*reject*ask*if*asked*on*state
   (state <s> ^problem-space.name action
              ^operator <o> +
              ^asked <msg>)
   (<o> ^name op_ask1
        ^arg1.id.predicates.sentence <msg>)
-->
   (<s> ^operator <o> -)
}
          

# If the until-clause is satisfied, reject the operator
sp {reject*wait*if*until-clause*satisfied
   (state <s> ^problem-space.name action
              ^operator <o> +)
   (<o> ^name op_wait1
        ^until-clause <until>)
   (<until> ^satisfied true)
-->
   (<s> ^operator <o> -)
}

 

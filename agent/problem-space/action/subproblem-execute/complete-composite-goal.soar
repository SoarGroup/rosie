
### Once we have been told that we are done learning the composite goal
# (The instructor says 'You are done')
# interpret-message/object-description adds a flag ^finish-composite-goal true to the segment
# 
# Operator complete-composite-goal will remove this flag and the ^learning-composite-goal flag
sp {subproblem*execute*propose*complete-composite-goal
   (state <s> ^problem-space.subproblem execute
              ^current-task-segment <seg>)
   (<seg> ^learning-composite-goal <node-h>
          ^finish-composite-goal true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-composite-goal)
}

sp {subproblem*execute*apply*complete-composite-goal*remove*flags
   (state <s> ^problem-space.subproblem execute
              ^operator.name complete-composite-goal
              ^current-task-segment <seg>)
   (<seg> ^learning-composite-goal <node-h>
          ^finish-composite-goal true)
-->
   (<seg> ^learning-composite-goal <node-h> -
          ^finish-composite-goal true -)
}


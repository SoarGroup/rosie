
### If the agent is currently learning a composite goal, 
# add a new subtask goal predicate onto the current goal

sp {learn-new-subtask*propose*add-composite-goal-predicate*composite-goal*predicate
   (state <s> ^name learn-new-subtask
              ^current-goal-id <goal>
              ^superstate.current-task-segment.learning-composite-goal true
              ^subtask-handle <sub-h>
             -^subtask-predicate)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-composite-goal-predicate
        ^current-goal <goal>
        ^subtask-handle <sub-h>)
}

sp {learn-new-subtask*apply*add-composite-goal-predicate*composite-goal*predicate
   (state <s> ^name learn-new-subtask
              ^operator <o>
              ^task-operator.task-handle <task-h>)
   (<o> ^name add-composite-goal-predicate
        ^current-goal <goal>
        ^subtask-handle <sub-h>)
   (<goal> ^pred-count <c>)
-->
   (<goal> ^pred-count <c> -
           ^pred-count (+ <c> 1)
           ^(+ <c> 1) <gp>)
   (<gp> ^type subtask
         ^subtask-handle <sub-h>)
   (<s> ^subtask-predicate <gp>
        ^to-store <goal> <gp>)
}

# If the subtask was given when the agent didn't have a goal
# (Finished with the current goal and doesn't have a next one)
# Then add a new procedural goal onto the goal-graph after the current goal

sp {learn-new-subtask*propose*add-subtask-goal
   (state <s> ^name learn-new-subtask
              ^current-goal-id <goal>
              ^superstate.desired <des>
              ^subtask-handle <sub-h>
             -^subtask-goal)
   (<goal> ^handle <goal-h>)
   (<des> ^handle <goal-h>
          ^satisfied true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-subtask-goal
        ^current-goal <goal>
        ^subtask-handle <sub-h>)
}

sp {learn-new-subtask*apply*add-subtask-goal
   (state <s> ^name learn-new-subtask
              ^operator <o>
              ^task-operator.task-handle <task-h>)
   (<o> ^name add-subtask-goal
        ^current-goal <goal>
        ^subtask-handle <sub-h>)
-->
   (<goal> ^next <next>)
   (<next> ^goal <st-goal>)
   (<st-goal> ^handle (make-constant-symbol (concat <task-h> |goal|))
              ^pred-count 1
              ^1 <st-pred>)
   (<st-pred> ^type subtask
              ^subtask-handle <sub-h>)

   (<s> ^subtask-goal <st-goal>
        ^to-store <goal> <next> <st-goal> <st-pred>)
}

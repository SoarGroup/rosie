
# Check if the subtask being pushed matches one we've already learned
sp {learn-new-subtask*elaborate*match-task-operator*pushed-task-operator
   (state <s> ^name learn-new-subtask
              ^subtask-operator <sub-op>)
-->
   (<s> ^match-task-operator <mto>)
   (<mto> ^task-operator <sub-op>
          ^candidate-tasks <cands>)
}

# Elaborate proposed operators in the superstate as match candidates
sp {learn-new-subtask*elaborate*match-task-operator*candidate-task-operators
   (state <s> ^name learn-new-subtask
              ^match-task-operator <mto>
              ^superstate.operator <o> +)
   (<mto> ^candidate-tasks <cands>)
   (<o> ^item-type task-operator)
-->
   (<cands> ^candidate-task-operator <o>)
}

# If no candidates match, elaborate no-matching-operators onto the state
sp {learn-new-subtask*elaborate*no-matching-operators
   (state <s> ^name learn-new-subtask
              ^match-task-operator <mto>)
   (<mto> ^no-matches true)
-->
   (<s> ^no-matching-operators true)
}



# Use the generalize-task-operator (found in task-utils) 
# to generate a generalized smem rep for the task based on the subtask operator

sp {learn-new-subtask*elaborate*generalize-task-operator
   (state <s> ^name learn-new-subtask
              ^no-matching-operators true
              ^task-concept-network.procedural <proc>
              ^task-operator <task-op>
              ^subtask-operator <sub-op>
              ^subtask-tcn <sub-tcn>)
-->
   (<s> ^generalize-task-operator <gto>)
   (<gto> ^task-operator <sub-op>
          ^task-concept-network <sub-tcn>
          ^reference-task-op <task-op>
          ^reference-tcn-proc <proc>)
}

sp {learn-new-subtask*elaborate*generalize-task-operator*goal*info
   (state <s> ^name learn-new-subtask
              ^generalize-task-operator <gto>
              ^task-goal <goal>
              ^current-goal-id <goal-id>)
-->
   (<gto> ^reference-goal <goal>
          ^reference-goal-id <goal-id>)
}

# Copy the result onto the state as generalized-subtask
sp {learn-new-subtask*elaborate*generalize-task-operator*result*as*generalized-subtask
   (state <s> ^name learn-new-subtask
              ^generalize-task-operator.general-task-rep <gen-subtask>)
-->
   (<s> ^generalized-subtask <gen-subtask>)
}

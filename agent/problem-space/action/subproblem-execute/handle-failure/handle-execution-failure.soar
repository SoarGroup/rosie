
# If a command comes back with a failure, there was some error when trying to execute the command

sp {subproblem*execute*propose*handle-execution-failure
   (state <s> ^problem-space.subproblem execute
              ^current-task-segment.<cmd-name> <cmd>)
   (<cmd> ^status << failure interrupted failed >>)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name handle-execution-failure
        ^category failure-handling
        ^command-name <cmd-name>
        ^command <cmd>)
}

### Elaboration info on how to handle the failure

# By default - terminate the task segment
sp {subproblem*execute*elaborate*handle-execution-failure*handler*default
   (state <s> ^problem-space.subproblem execute
              ^operator <o> +
             -^handle-failure-info.execution-failure.<cmd-name>)
   (<o> ^name handle-execution-failure
        ^command-name <cmd-name>)
-->
   (<o> ^handler.action terminate)
}

# If handler info is present on the state, use that
sp {subproblem*execute*elaborate*handle-execution-failure*handler*from*handle-failure-info
   (state <s> ^problem-space.subproblem execute
              ^operator <o> +
              ^handle-failure-info.execution-failure.<cmd-name> <handler>)
   (<o> ^name handle-execution-failure
        ^command-name <cmd-name>)
-->
   (<o> ^handler <handler>)
}

### Application

# Print info if the print-task-failures flag is set
sp {subproblem*execute*apply*handle-execution-failure*debug*print
   (state <s> ^problem-space.subproblem execute
              ^agent-params.print-task-failures true
              ^operator <o>)
   (<o> ^name handle-execution-failure
        ^category failure-handling
        ^command-name <cmd-name>
        ^command <cmd>)
-->
   (write |The following | <cmd-name> | encountered a failure during execution:|  (crlf))
   (write (cmd print <cmd> -d 3)  (crlf))
}

sp {subproblem*execute*apply*handle-execution-failure*terminate
   (state <s> ^problem-space.subproblem execute
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> ^name handle-execution-failure
        ^handler.action terminate
        ^command-name <cmd-name>
        ^command <cmd>)
-->
   (<seg> ^status failure)
}


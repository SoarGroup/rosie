
# By default, elaborated from the task-operator
sp {subproblem*execute*elaborate*predicate-test*from*end-clause
   (state <s> ^problem-space.subproblem execute
              ^task-operator.end-clause <cl>)
-->
   (<s> ^predicate-test <cl>)
}

# If the end clause is not satisfied, elaborate waiting-to-end
sp {subproblem*execute*elaborate*waiting-to-end*true
   (state <s> ^problem-space.subproblem execute
              ^desired.item-type task-goal
              ^task-operator.end-clause <cl>
             -^satisfied <cl>)
-->
   (<s> ^waiting-to-end true)
}


# If the end clause is satisfied, mark the goal as satisfied
sp {subproblem*execute*elaborate*desired*satisfied*if*end-clause*satisfied
   (state <s> ^problem-space.subproblem execute
              ^task-operator.end-clause <cl>
              ^satisfied <cl>
              ^desired <des>)
-->
   (<des> ^satisfied true)
}

### If the goal is to execute-command and the end-clause is satisfied, 
# send a stop command to the output link (magicbot only for now)

sp {subproblem*execute*propose*send-stop-command*end-clause*satisfied
   (state <s> ^problem-space.subproblem execute
              ^agent-params.domain magicbot
              ^desired.<pnum> <gpred>
              ^current-task-segment.<cmd-name> <cmd>
              ^task-operator.end-clause <end>
              ^satisfied <end>)
   (<gpred> ^type execute-command
            ^command-name <cmd-name>)
   (<cmd> ^status executing)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-stop-command
        ^item-type command-operator
        ^command-name stop-command)
}

sp {subproblem*execute*apply*send-stop-command*send*command
   (state <s> ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain magicbot
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-stop-command
        ^command-name <cmd-name>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name stop
          ^item-type output-command
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}

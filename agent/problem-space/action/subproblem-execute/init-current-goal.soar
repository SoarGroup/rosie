
sp {subproblem*execute*propose*init-current-goal
   (state <s> ^name <name>
              ^problem-space.subproblem execute
              ^task-operator <task-op>)
   (<task-op> ^name <name>
             -^current-goal)
-->
   (<s> ^operator <o> + =)
   (<o> ^name init-current-goal
        ^task-operator <task-op>)
}

sp {init-current-goal*substate*elaborate*task-handle
   (state <s> ^name init-current-goal
              ^superstate.operator.task-operator <task-op>)
   (<task-op> ^task-handle <task-h>)
-->
   (<s> ^task-operator <task-op>
        ^task-handle <task-h>)
}

sp {init-current-goal*elaborate*problem-space
   (state <s> ^name init-current-goal)
-->
   (<s> ^default-problem-space no)
#        ^problem-space.learning.use chunking)
}

sp {init-current-goal*retrieve*task-concept-network
   (state <s> ^name init-current-goal
              ^task-handle <task-h>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue.handle <task-h>
        ^depth 10
        ^destination <s>
        ^result-name task-concept-network)
}

sp {init-current-goal*propose*create-initial-goal
   (state <s> ^name init-current-goal
              ^task-concept-network <tcn>
              ^task-handle <task-h>)
   (<tcn> ^goal-graph.handle <goal-h>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name create-initial-goal
        ^goal-handle <goal-h>)
}

sp {__INIT__GOAL__*apply*create-initial-goal
   (state <s> ^name init-current-goal
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name create-initial-goal
        ^goal-handle <goal-h>)
-->
   (<task-op> ^current-goal <goal-h>)
}

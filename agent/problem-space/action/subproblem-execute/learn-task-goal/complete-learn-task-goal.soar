# Once the goal has been created and stored, finish the operator
sp {learn-task-goal*propose*complete-learn-task-goal
   (state <s> ^name learn-task-goal
              ^task-goal <goal>
              ^added-goal-node <goal-h>)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-learn-task-goal)
}

# Notify the interaction stack that the goal was learned
sp {learn-task-goal*apply*complete-learn-task-goal
   (state <s> ^name learn-task-goal
              ^operator.name complete-learn-task-goal
              ^top-state.interaction.status <status>)
-->
   (<status> ^performed.task-event.type learned-task-goal)
}

# Remove the new-task-goal from the current task segment
sp {learn-task-goal*apply*complete-learn-task-goal*remove*new-task-goal
   (state <s> ^name learn-task-goal
              ^operator.name complete-learn-task-goal
              ^superstate.current-task-segment <seg>
              ^task-goal <goal>)
   (<seg> ^new-task-goal <goal>)
-->
   (<seg> ^new-task-goal <goal> -)
}

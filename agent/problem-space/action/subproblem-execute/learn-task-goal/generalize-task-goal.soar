# use generalize-task-goal utility to turn the grounded goal representation
# into one that can be added to the TCN and connected to various slots

sp {learn-task-goal*elaborate*generalize-task-goal
   (state <s> ^name learn-task-goal
              ^task-operator <task-op>
              ^task-goal <goal>
              ^task-concept-network.procedural <tcn-proc>)
-->
   (<s> ^generalize-task-goal <gen-goal>)
   (<gen-goal> ^reference-task-op <task-op>
               ^reference-tcn-proc <tcn-proc>
               ^task-goal <goal>)
}

# copy the result of the generalization process
sp {learn-task-goal*elaborate*generalized-goal*from*generalize-task-goal
  (state <s> ^name learn-task-goal
             ^generalize-task-goal.general-goal-rep <rep>)
-->
  (<s> ^generalized-goal <rep>)
}

# Elaborate any reference-info for objects in the goal
# (Set of predicates explicitly mentioned by the instructor)
sp {learn-task-goal*generalize-task-goal*elaborate*generalize-task-argument*object*reference-info
   (state <s> ^name generalize-task-goal
              ^superstate.name learn-task-goal
              ^generalize-task-argument <arg>)
   (<arg> ^arg-type object
          ^instantiation <i>)
-->
   (<i> ^reference-info <info>)
}

sp {learn-task-goal*generalize-task-goal*elaborate*generalize-task-argument*object*reference-info*constraints
   (state <s> ^name generalize-task-goal
              ^superstate.name learn-task-goal
              ^generalize-task-argument <pred-arg>
              ^generalize-task-argument <obj-arg>)
   (<pred-arg> ^arg-type predicate
               ^instantiation <pred-ins>
               ^<gen-obj> <obj-arg>)
   (<pred-ins> ^reference-info <ref-info>
               ^<num> <obj>)
   (<ref-info> ^num <num>
               ^{ <prop-h> <> num } <pred-h>)
   (<obj-arg> ^arg-type object
              ^instantiation <obj-ins>)
   (<obj-ins> ^id <obj>
              ^reference-info <obj-ref-info>)
-->
   (<obj-ref-info> ^<prop-h> <pred-h>)
}


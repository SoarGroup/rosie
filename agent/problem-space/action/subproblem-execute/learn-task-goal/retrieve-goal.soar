
sp {learn-task-goal*elaborate*smem-query*task-concept-network
   (state <s> ^name learn-task-goal
              ^task-operator.task-handle <task-handle>)
-->
  (<s> ^smem-query <query>)
  (<query> ^cue.handle <task-handle>
           ^depth 8
           ^destination <s>
           ^result-name task-concept-network)
}

sp {learn-task-goal*elaborate*smem-query*current-goal-id
   (state <s> ^name learn-task-goal
              ^task-operator.current-goal <goal-h>)
-->
  (<s> ^smem-query <query>)
  (<query> ^cue.handle <goal-h>
           ^depth 5
           ^destination <s>
           ^result-name current-goal-id)
}

sp {learn-task-goal*elaborate*smem-query-cue*predicate
   (state <s> ^name learn-task-goal
              ^goal-predicate-info.predicate.handle <pred-handle>)
-->
  (<s> ^smem-query <query>)
  (<query> ^cue <cue>
            ^depth 2)
   (<cue> ^handle <pred-handle>)
}

sp {learn-task-goal*elaborate*predicate-id*smem-query-cue*retrieved
   (state <s> ^name learn-task-goal
              ^goal-predicate-info.predicate <pred>
              ^smem-query <query>)
  (<query> ^cue.handle <pred-handle>
           ^result <id>)
   (<pred> ^handle <pred-handle>)
-->
   (<pred> ^predicate-id <id>)
}

# If the instructor told us the task is done, add a terminal node

# Using a double negative to avoid chunking ^finish-task flag
sp {task-state-no-change*elaborate*not-finished-yet
   (state <s> ^name task-state-no-change
              ^desired.satisfied true
              ^goal-id <goal>
              ^superstate.current-task-segment <seg>)
   (<seg> -^finish-task true)
-->
   (<s> ^not-finished-yet true)
}

sp {task-state-no-change*propose*create-terminal-goal-node
   (state <s> ^name task-state-no-change
              ^desired.satisfied true
              ^goal-id <goal>
             -^not-finished-yet true)
   (<goal> -^next.goal.item-type terminal-goal)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-terminal-goal-node
        ^current-goal-node <goal>)
}

sp {task-state-no-change*apply*create-terminal-goal-node*create
   (state <s> ^name task-state-no-change
              ^task-operator.task-handle <task-h>
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name create-terminal-goal-node
        ^current-goal-node <goal>)
-->
   (<goal> ^next <next>)
   (<next> ^goal <term>)
   (<term> ^handle (make-constant-symbol (concat <task-h> |term|))
           ^item-type terminal-goal)

   (<cmd> ^store <goal> <next> <term>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*copy-task-operator
   (state <s> ^top-subtask-search-state true
              ^copy-of-state.task-operator <op>
              ^deep-copy-world.copy-objects-map <map>)
   (<op> ^name <name>)
-->
   (<s> ^copy-task-operator <cto>)
   (<cto> ^task-operator <op>
          ^destination <s>
          ^copy-name task-operator
          ^object-map <map>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*copy-task-goal
   (state <s> ^top-subtask-search-state true
              ^copy-of-state.desired <des>)
-->
   (<s> ^copy-task-goal <des>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*copy-task-operator*successful-operator
   (state <s> ^top-subtask-search-state true
              ^successful-operator <op>)
-->
   (<s> ^copy-task-operator <cto>)
   (<cto> ^task-operator <op>
          ^destination <s>
          ^copy-name copied-successful-operator
          ^object-map <new-map>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*ignore-operator*subtask*performed
   (state <s> ^top-subtask-search-state true
              ^best-pref-eval <op>
              ^desired.<pnum>.subtask-handle <sub-h>
              ^current-task-segment.performed-subtask <sub-h>)
   (<op> ^subtask-handle <sub-h>)
-->
   (<s> ^ignore-operator <op>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*copy-task-operator*best-pref-eval
   (state <s> ^top-subtask-search-state true
              ^best-pref-eval <op>
             -^successful-operator <op>
             -^ignore-operator <op>)
-->
   (<s> ^copy-task-operator <cto>)
   (<cto> ^task-operator <op>
          ^destination <s>
          ^copy-name copied-successful-operator
          ^object-map <new-map>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*copy-task-operator*successful-operator*object-map
   (state <s> ^top-subtask-search-state true
              ^copy-task-operator <cto>
              ^deep-copy-world.copy-objects-map.<src-obj> <copy-obj>)
   (<cto> ^copy-name copied-successful-operator
          ^object-map <new-map>)
-->
   (<new-map> ^<copy-obj> <src-obj>)
}

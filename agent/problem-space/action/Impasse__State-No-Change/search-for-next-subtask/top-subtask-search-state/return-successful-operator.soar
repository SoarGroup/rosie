# If the subtask-search was successful, return the operator copy to the superstate

sp {search-for-next-subtask*top-subtask-search-state*propose*return-successful-operator
   (state <s> ^top-subtask-search-state true
              ^copied-successful-operator <sub-op>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name return-successful-operator
        ^successful-operator <sub-op>)
}

sp {search-for-next-subtask*top-subtask-search-state*prefer*return-successful-operator
   (state <s> ^top-subtask-search-state true
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name return-successful-operator)
   (<o2> ^name <> return-successful-operator)
-->
   (<s> ^operator <o2> -)
}

#sp {search-for-next-subtask*top-subtask-search-state*apply*return-successful-operator*interrupt
#   (state <s> ^top-subtask-search-state true
#              ^operator.name return-successful-operator
#              ^agent-params.interrupt-task-search true)
#-->
#   (interrupt)
#}

sp {search-for-next-subtask*top-subtask-search-state*apply*return-successful-operator*copy*superstate
   (state <s> ^top-subtask-search-state true
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name return-successful-operator
        ^successful-operator <sub-op>)
-->
   (<ss> ^successful-operator <sub-op>)
}


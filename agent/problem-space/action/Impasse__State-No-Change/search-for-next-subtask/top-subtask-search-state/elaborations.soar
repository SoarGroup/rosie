sp {search-for-next-subtask*task-operator*elaborate*top-subtask-search-state
   (state <s> ^problem-space.name action
              ^superstate.operator <ss-op>)
   (<ss-op> ^top-subtask-search-state true
            ^copy-of-state <src-state>)
-->
   (<s> ^top-subtask-search-state true
        ^copy-of-state <src-state>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*state*name
   (state <s> ^top-subtask-search-state true
              ^copy-of-state.task-operator.name <name>)
-->
   (<s> ^name <name>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*current-task-segment
   (state <s> ^top-subtask-search-state true)
-->
   (<s> ^current-task-segment <seg>)
}

sp {search-for-next-subtask*top-subtask-search-state*elaborate*performed-subtask*copy-of-state
   (state <s> ^top-subtask-search-state true
              ^copy-of-state.current-task-segment.performed-subtask <sub-h>
              ^current-task-segment <seg>)
-->
   (<seg> ^performed-subtask <sub-h>)
}

# current-search-depth, the depth of the search where 1 is the top, and increments 1 per SNC
sp {search-for-next-subtask*top-subtask-search-state*elaborate*current-search-depth*1
   (state <s> ^top-subtask-search-state true)
-->
   (<s> ^current-search-depth 1)
}

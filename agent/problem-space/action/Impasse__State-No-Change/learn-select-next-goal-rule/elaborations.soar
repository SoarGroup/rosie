sp {learn-select-next-goal-rule*elaborate*superoperator*params
   (state <s> ^name learn-select-next-goal-rule
              ^superstate.operator <ssop>)
   (<ssop> ^goal-handle <goal-h>
           ^next-goal-link <link>)
-->
   (<s> ^goal-handle <goal-h>
        ^next-goal-link <link>)
}

sp {learn-select-next-goal-rule*elaborate*superstate*params
   (state <s> ^name learn-select-next-goal-rule
              ^superstate <ss>)
   (<ss> ^task-operator <task-op>
         ^task-concept-network <tcn>)
-->
   (<s> ^task-operator <task-op>
        ^task-concept-network <tcn>)
}

sp {learn-select-next-goal-rule*elaborate*conditions
   (state <s> ^name learn-select-next-goal-rule
              ^next-goal-link.conditions <conds>)
-->
   (<s> ^conditions <conds>)
}

sp {learn-select-next-goal-rule*elaborate*can-select-goal*true*no*conditions
   (state <s> ^name learn-select-next-goal-rule
              ^next-goal-link <link>)
   (<link> -^conditions)
-->
   (<s> ^can-select-goal true)
}

sp {learn-select-next-goal-rule*elaborate*can-select-goal*true*conditions*met
   (state <s> ^name learn-select-next-goal-rule
              ^conditions <conds>
              ^predicate-test.satisfied true)
-->
   (<s> ^can-select-goal true)
}

sp {learn-select-next-goal-rule*elaborate*superoperator*params
   (state <s> ^name learn-select-next-goal-rule
              ^superstate.operator <ssop>)
   (<ssop> ^goal-handle <goal-h>
           ^next-goal-handle <next-h>)
-->
   (<s> ^goal-handle <goal-h>
        ^next-goal-handle <next-h>)
}

sp {learn-select-next-goal-rule*elaborate*superstate*params
   (state <s> ^name learn-select-next-goal-rule
              ^superstate <ss>)
   (<ss> ^task-operator <task-op>
         ^task-concept-network <tcn>
         ^superstate.current-task-segment <seg>)
-->
   (<s> ^task-operator <task-op>
        ^task-concept-network <tcn>
        ^current-task-segment <seg>)
}

sp {learn-select-next-goal-rule*elaborate*next-goal-link
   (state <s> ^name learn-select-next-goal-rule
              ^goal-id-copy.next <next>
              ^next-goal-handle <next-h>)
   (<next> ^goal.handle <next-h>)
-->
   (<s> ^next-goal-link <next>)
}

sp {learn-select-next-goal-rule*elaborate*has-conditions*true
   (state <s> ^name learn-select-next-goal-rule
              ^next-goal-link.conditions <conds>)
-->
   (<s> ^has-conditions true)
}

sp {learn-select-next-goal-rule*elaborate*has-conditions*false
   (state <s> ^name learn-select-next-goal-rule
              ^next-goal-link <next>)
   (<next> -^conditions <conds>)
-->
   (<s> ^has-conditions false)
}

sp {learn-select-next-goal-rule*elaborate*dont-chunk*superoperator
   (state <s> ^name learn-select-next-goal-rule
              ^superstate.operator.dont-chunk true)
-->
   (<s> ^dont-chunk true)
}

sp {learn-select-next-goal-rule*elaborate*can-select-goal*true*no*conditions
   (state <s> ^name learn-select-next-goal-rule
              ^has-conditions false)
-->
   (<s> ^can-select-goal true)
}

sp {learn-select-next-goal-rule*elaborate*can-select-goal*true*conditions*met
   (state <s> ^name learn-select-next-goal-rule
              ^has-conditions true
              ^predicate-test <pt>
              ^satisfied <pt>)
-->
   (<s> ^can-select-goal true)
}

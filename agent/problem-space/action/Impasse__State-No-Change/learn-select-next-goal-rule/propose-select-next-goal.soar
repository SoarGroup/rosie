sp {learn-select-next-goal-rule*propose*propose-select-next-goal
   (state <s> ^name learn-select-next-goal-rule
              ^next-goal-link.goal <g>
              ^can-select-goal true)
   (<g> ^handle <goal-h>
        ^item-type <goal-t>)
-->
   (<s> ^operator <o> +)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>)
}

sp {learn-select-next-goal-rule*elaborate*propose-select-next-goal*has-conditions*false
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o> +
             -^conditions)
   (<o> ^name propose-select-next-goal)
-->
   (<o> ^has-conditions false)
}

sp {learn-select-next-goal-rule*elaborate*propose-select-next-goal*has-conditions*true
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o> +
              ^conditions <conds>
             -^next-goal-link.loop-exit)
   (<o> ^name propose-select-next-goal)
-->
   (<o> ^has-conditions true)
}

### Learn a proposal rule for select-next-goal

sp {__SEL_NEXT_GOAL__*propose*select-next-goal*superstate
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
             -^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>
        ^has-conditions <has-cond>)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>
           ^has-conditions <has-cond>)
}

### If we shouldn't chunk the rule, check for quiescence
         
sp {learn-select-next-goal-rule*apply*propose-select-next-goal*dont-chunk
   (state <s> ^name learn-select-next-goal-rule
              ^operator <o>
              ^quiescence t
              ^dont-chunk true
              ^superstate.superstate <ss>)
   (<o> ^name propose-select-next-goal
        ^next-goal-handle <goal-h>
        ^next-goal-type <goal-t>
        ^has-conditions <has-cond>)
#   (<ss> ^problem-space.subproblem execute)
-->
   (<ss> ^operator <ssop> + =)
   (<ssop> ^name select-next-goal
           ^next-goal-handle <goal-h>
           ^next-goal-type <goal-t>
           ^has-conditions <has-cond>)
}



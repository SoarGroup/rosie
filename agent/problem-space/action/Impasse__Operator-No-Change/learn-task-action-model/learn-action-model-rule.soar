

### If there is custom code in an innate task that learns an action mode (such as recall)
#     then it should elaborate the ^custom-model-learning true flag to disable default behavior
sp {learn-task-action-model*propose*learn-action-model-rule
   (state <s> ^name learn-task-action-model
             -^custom-model-learning true)
   #^created-desired <d>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name learn-action-model-rule)
}

sp {execution-type*internal*apply*task-operator*mark*tried-tied-operator
   (state <s> ^name learn-task-action-model
              ^operator.name learn-action-model-rule
              ^superstate <ss>)
   (<ss> ^operator <o>
         ^untried-tied-operator <o>)
-->
   (<ss> ^untried-tied-operator <o> -
         ^tried-tied-operator <o>)
}

sp {execution-type*internal*apply*task-operator*update*current-evaluation-depth
   (state <s> ^name learn-task-action-model
              ^operator.name learn-action-model-rule
              ^superstate <ss>)
   (<ss> ^operator <o>
         ^current-evaluation-depth <ced>)
-->
   (<ss> ^current-evaluation-depth <ced> -
         ^current-evaluation-depth (- <ced> 1))
}

sp {__TASK_MODEL__*problem-space*action*interal*apply*task*action-model*unary*predicate
   (state <s> ^name learn-task-action-model
              ^operator.name learn-action-model-rule
              ^superstate.problem-space <ps>
              ^created-desired <des>)
   (<ps> ^name action
         ^execution-type internal)
   (<des> ^pred-count 1
          ^<gpnum> <gp>)
   (<gp> ^type unary
         ^handle <pred-h>
         ^property-handle <prop-h>
         ^1 <obj>)
   (<obj> ^predicates <obj-preds>)
-->
   (<obj-preds> ^<prop-h> <pred-h>)
}

sp {__TASK_MODEL__*problem-space*action*internal*apply*task*action-model*remove*other*predicate
   (state <s> ^name learn-task-action-model
              ^operator.name learn-action-model-rule
              ^superstate.problem-space <ps>
              ^created-desired <des>)
   (<ps> ^name action
         ^execution-type internal)
   (<des> ^pred-count 1
          ^<gpnum> <gp>)
   (<gp> ^type unary
         ^handle <pred-h>
         ^property-handle <prop-h>
         ^1 <obj>)
   (<obj> ^predicates <obj-preds>)
   (<obj-preds> ^<prop-h> { <other-pred> <> <pred-h> })
-->
   (<obj-preds> ^<prop-h> <other-pred> -)
}

sp {__TASK_MODEL__*problem-space*action*interal*apply*task*action-model*1*relation
   (state <s> ^name learn-task-action-model
              ^operator.name learn-action-model-rule
              ^superstate.problem-space <ps>
              ^created-desired <des>
              ^world.predicates.predicate <wpred>)
   (<ps> ^name action
         ^execution-type internal)
   (<des> ^pred-count 1
          ^<gpnum> <gp>)
   (<gp> ^type relation
         ^handle <rel-h>
         ^1 <obj1>
         ^2 <obj2>)
   (<wpred> ^handle <rel-h>)
   -{(<wpred> ^instance <i>)
     (<i> ^1 <obj1> ^2 <obj2>)}
-->
    (<wpred> ^instance <i>)
    (<i> ^1 <obj1> ^2 <obj2>)
}


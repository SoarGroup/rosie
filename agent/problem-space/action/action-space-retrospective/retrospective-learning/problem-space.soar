sp {retrospective-learning*elaborate*problem-space
   (state <s> ^name retrospective-learning)
-->
   (<s> ^problem-space <ps>)
   (<ps> ^world <world>)
}

# When we are learning for each subaction, elaborate the world from its episode
sp {retrospective-learning*elaborate*problem-space*world
   (state <s> ^name retrospective-learning
              ^problem-space.world <world>
              ^subactions-stack.next.episode.world <source>
             -^replayed-all-subactions true)
-->
   (<world> ^source <source>
            ^copy-type deep)
}

# When we are learning the proposal rule, elaborate the world from the initial episode
sp {retrospective-learning*elaborate*problem-space*world*after*replayed
   (state <s> ^name retrospective-learning
              ^problem-space.world <world>
              ^replayed-all-subactions true
              ^initial-episode.world <source>)
-->
   (<world> ^source <source>
            ^copy-type deep)
}

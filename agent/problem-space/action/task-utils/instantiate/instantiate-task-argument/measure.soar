### INSTANTIATE MEASURE

sp {task-utils*instantiate-task-operator*propose*instantiate-task-argument*measure*matches*procedural
   (state <s> ^instantiate-task-argument <arg>
              ^reference-tcn-proc.<arg-name> <parg>
              ^reference-task-op.<arg-name> <op-arg>)
   (<arg> ^arg-type measure
          ^general-arg-rep.id <conc-slot>
         -^instantiation)
   (<parg> ^arg-type measure
           ^id {@ <conc-slot> }) 
   (<op-arg> ^arg-type measure
             ^number <num>
             ^unit <unit>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type measure
       ^match-type procedural
       ^number <num>
       ^unit <unit>)
}

sp {task-utils*instantiate-task-operator*propose*instantiate-task-argument*measure*matches*default
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type measure
          ^general-arg-rep.id.default <def>
         -^instantiation)
   (<def> ^number <num>
          ^unit <unit>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type measure
       ^match-type default
       ^number <num>
       ^unit <unit>)
}

sp {task-utils*instantiate-task-operator*apply*instantiate-task-argument*measure*create*instantiation
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type measure
       ^number <num>
       ^unit <unit>)
-->
  (<arg> ^instantiation <i>)
  (<i> ^number <num>
       ^unit <unit>)
}


### INSTANTIATE CONCEPT

sp {task-utils*instantiate-task-operator*propose*instantiate-task-argument*concept*matches*procedural
   (state <s> ^instantiate-task-argument <arg>
              ^reference-tcn-proc.<arg-name> <parg>
              ^reference-task-op.<arg-name> <op-arg>)
   (<arg> ^arg-type concept
          ^general-arg-rep.id <conc-slot>
         -^instantiation)
   (<parg> ^arg-type concept
           ^id <conc-slot>)
   (<op-arg> ^arg-type concept
             ^handle <conc-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type procedural
       ^handle <conc-handle>)
}

sp {task-utils*instantiate-task-operator*propose*instantiate-task-argument*concept*matches*default
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type concept
          ^general-arg-rep.id.default.predicate-handle <conc-handle>
         -^instantiation)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type concept
       ^match-type default
       ^handle <conc-handle>)
}

sp {task-utils*instantiate-task-operator*apply*instantiate-task-argument*concept*create*instantiation
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type concept
       ^handle <conc-handle>)
-->
  (<arg> ^instantiation <i>)
  (<i> ^handle <conc-handle>)
}


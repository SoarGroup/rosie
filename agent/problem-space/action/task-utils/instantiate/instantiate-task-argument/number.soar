### INSTANTIATE NUMBER

sp {task-utils*instantiate-task-operator*propose*instantiate-task-argument*number*matches*procedural
   (state <s> ^instantiate-task-argument <arg>
              ^reference-tcn-proc.<arg-name> <parg>
              ^reference-task-op.<arg-name> <op-arg>)
   (<arg> ^arg-type number
          ^general-arg-rep.id <conc-slot>
         -^instantiation)
   (<parg> ^arg-type number
           ^id {@ <conc-slot> }) 
   (<op-arg> ^arg-type number
             ^number <num>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type number
       ^match-type procedural
       ^number <num>)
}

sp {task-utils*instantiate-task-operator*propose*instantiate-task-argument*number*matches*default
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type number
          ^general-arg-rep.id.default.number <num>
         -^instantiation)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type number
       ^match-type default
       ^number <num>)
}

sp {task-utils*instantiate-task-operator*apply*instantiate-task-argument*number*create*instantiation
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type number
       ^number <num>)
-->
  (<arg> ^instantiation <i>)
  (<i> ^number <num>)
}


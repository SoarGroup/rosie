### INSTANTIATE PREDICATE HANDLE

sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*predicate-handle*matches*procedural
   (state <s> ^instantiate-task-argument <arg>
              ^reference-tcn-proc.<arg-name> <parg>
              ^reference-task-op.<arg-name> <op-arg>)
   (<arg> ^arg-type predicate-handle
          ^general-arg-rep.id <pred-slot>
          -^instantiation)
   (<parg> ^arg-type partial-predicate
           ^id <pred-slot>)
   (<op-arg> ^arg-type partial-predicate
             ^handle <pred-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type procedural
       ^handle <pred-handle>
       ^no-property-handle true)
}

sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*predicate-handle*matches*default
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type predicate-handle
          ^no-property-handle true
          ^general-arg-rep.id.default.predicate-handle <pred-handle>
         -^instantiation)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^handle <pred-handle>
       ^no-property-handle true)
}

sp {task-utils*instantiate-task-argument*apply*instantiate-task-argument*predicate-handle*create*instantiation
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^arg-type predicate-handle
       ^argument <arg>
       ^handle <pred-handle>
       ^no-property-handle true)
-->
  (<arg> ^instantiation <i>)
  (<i> ^handle <pred-handle>)
}


sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*predicate-handle*matches*default*property-handle
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type predicate-handle
          ^general-arg-rep.id.default <def>
         -^instantiation)
   (<def> ^predicate-handle <pred-handle>
          ^property-handle <prop-handle>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name instantiate-task-argument
       ^argument <arg>
       ^arg-type predicate-handle
       ^match-type default
       ^handle <pred-handle>
       ^property-handle <prop-handle>)
}

sp {task-utils*instantiate-task-argument*apply*instantiate-task-argument*predicate-handle*create*instantiation*property-handle
  (state <s> ^operator <o>)
  (<o> ^name instantiate-task-argument
       ^arg-type predicate-handle
       ^argument <arg>
       ^handle <pred-handle>
       ^property-handle <prop-handle>)
-->
  (<arg> ^instantiation <i>)
  (<i> ^handle <pred-handle>
       ^property-handle <prop-handle>)
}


### INSTANTIATE CONDITIONS

sp {task-utils*instantiate-task-argument*elaborate*instantiate-task-argument*conditions*as*predicate-set
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type conditions
          ^general-arg-rep <rep>)
-->
   (<s> ^instantiate-task-argument <ita>)
   (<ita> ^arg-type predicate-set
          ^general-arg-rep <rep>)
   (<arg> ^ins-pred-set <ita>)
}

sp {task-utils*instantiate-task-argument*propose*instantiate-task-argument*conditions
   (state <s> ^instantiate-task-argument <arg>)
   (<arg> ^arg-type conditions
          ^ins-pred-set.instantiation <ins>
         -^instantiation)
-->
   (<s> ^operator <o> + =)
   (<o> ^name instantiate-task-argument
        ^arg-type conditions
        ^argument <arg>
        ^instantiation <ins>)
}

sp {task-utils*instantiate-task-argument*apply*instantiate-task-argument*conditions
   (state <s> ^operator <o>)
   (<o> ^name instantiate-task-argument
        ^arg-type conditions
        ^argument <arg>
        ^instantiation <ins>)
-->
   (<arg> ^instantiation <ins>)
}

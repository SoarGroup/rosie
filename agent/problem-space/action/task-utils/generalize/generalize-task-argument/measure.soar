## GENERALIZE MEASURE

# procedural match - the measure matches one on the reference-task-op
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*measure*procedural
   (state <s> ^generalize-task-argument <arg>
              ^reference-tcn-proc.<arg-name> <parg>
              ^reference-task-op.<arg-name> <ref-arg>)
   (<arg> ^arg-type measure
          ^instantiation <ins>
         -^general-arg-rep)
   (<ins> ^number <num>
          ^unit <unit>)
   (<ref-arg> ^number <num>
              ^unit <unit>)
   (<parg> ^arg-type measure
           ^id <num-slot>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name generalize-task-argument
        ^argument <arg>
        ^arg-type measure
        ^match-type procedural
        ^id <num-slot>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*measure*procedural
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type measure
       ^match-type procedural
       ^id <num-slot>)
-->
  (<arg> ^general-arg-rep <gen>)
  (<gen> ^id <num-slot>)
}

# default match - copy the predicates onto a default rep
sp {task-utils*generalize-task-argument*propose*generalize-task-argument*measure*default
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type measure
         ^instantiation <ins>
        -^general-arg-rep)
   (<ins> ^number <num>
          ^unit <unit>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type measure
       ^match-type default
       ^number <num>
       ^unit <unit>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*measure*default
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type measure
       ^match-type default
       ^number <num>
       ^unit <unit>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^id <num-slot>)
  (<num-slot> ^default <def>)
  (<def> ^number <num>
         ^unit <unit>)
}

### GENERALIZE STATUS PREDICATE

sp {task-utils*generalize-task-argument*expand*status*predicate*current-location
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation <i>)
  (<i> ^type status
       ^name current-location
       ^1 <obj1>)
-->
  (<s> ^generalize-task-argument <gen-obj1>)

  (<gen-obj1> ^arg-type object
              ^force-default true
              ^instantiation <obj1-i>)
  (<obj1-i> ^id <obj1>)

  (<arg> ^generalize-obj1 <gen-obj1>)
}

sp {task-utils*generalize-task-argument*expand*status*predicate*current-waypoint
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation <i>)
  (<i> ^type status
       ^name current-waypoint
       ^1 <wp1>)
-->
  (<s> ^generalize-task-argument <gen-obj1>)

  (<gen-obj1> ^arg-type waypoint
              ^instantiation <wp1i>)
  (<wp1i> ^id <wp1>)

  (<arg> ^generalize-obj1 <gen-obj1>)
}

sp {task-utils*generalize-task-argument*propose*generalize-task-argument*predicate*status
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation <i>
         ^generalize-obj1.general-arg-rep.id <obj1-slot>
        -^general-arg-rep)
   (<i> ^type status
        ^name <status-name>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type status
       ^status-name <status-name>
       ^1 <obj1-slot>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*predicate*status
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type status
       ^status-name <status-name>
       ^1 <obj1-slot>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^type status
         ^name <status-name>
         ^1 <obj1-slot>)
}



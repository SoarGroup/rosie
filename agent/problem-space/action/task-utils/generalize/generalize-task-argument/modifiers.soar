### GENERALIZE MODIFIER

sp {task-utils*generalize-task-argument*propose*generalize-task-argument*modifiers*default
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type modifiers
        -^general-arg-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^arg-type modifiers
       ^argument <arg>)
}

# ENUMERATE: 1-3 task modifiers

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*modifiers*1
   (state <s> ^operator <o>)
   (<o> ^name generalize-task-argument
        ^arg-type modifiers
        ^argument <arg>)
   (<arg> ^instantiation <ins>)
   (<ins> ^handle <mod1>
         -^handle { <mod2> <> <mod1> })
-->
   (<arg> ^general-arg-rep <rep>)
   (<rep> ^mod-handle <mod1>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*modifiers*2
   (state <s> ^operator <o>)
   (<o> ^name generalize-task-argument
        ^arg-type modifiers
        ^argument <arg>)
   (<arg> ^instantiation <ins>)
   (<ins> ^handle <mod1>
          ^handle { <mod2> > <mod1> }
         -^handle { <mod3> <> <mod2> <> <mod1> })
-->
   (<arg> ^general-arg-rep <rep>)
   (<rep> ^mod-handle <mod1> <mod2>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*modifiers*3
   (state <s> ^operator <o>)
   (<o> ^name generalize-task-argument
        ^arg-type modifiers
        ^argument <arg>)
   (<arg> ^instantiation <ins>)
   (<ins> ^handle <mod1>
          ^handle { <mod2> > <mod1> }
          ^handle { <mod3> > <mod2> }
         -^handle { <mod4> <> <mod3> <> <mod2> <> <mod1> })
-->
   (<arg> ^general-arg-rep <rep>)
   (<rep> ^mod-handle <mod1> <mod2> <mod3>)
}


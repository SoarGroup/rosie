### GENERALIZE RELATION PREDICATE

sp {task-utils*generalize-task-argument*expand*relation*predicate
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation <i>)
  (<i> ^type relation
       ^handle <handle>
       ^1 <obj1>
       ^2 <obj2>)
-->
  (<s> ^generalize-task-argument <gen-pred> <gen-obj1> <gen-obj2>)

  (<gen-pred> ^arg-type predicate-handle
              ^instantiation <pred-i>)
  (<pred-i> ^handle <handle>
            ^no-property-handle true)

  (<gen-obj1> ^arg-type object
              ^instantiation <obj1-i>)
  (<obj1-i> ^id <obj1>)

  (<gen-obj2> ^arg-type object
              ^instantiation <obj2-i>)
  (<obj2-i> ^id <obj2>)

  (<arg> ^generalize-handle <gen-pred>
         ^generalize-obj1 <gen-obj1>
         ^generalize-obj2 <gen-obj2>)
}

sp {task-utils*generalize-task-argument*propose*generalize-task-argument*predicate*relation
  (state <s> ^generalize-task-argument <arg>)
  (<arg> ^arg-type predicate
         ^instantiation.type relation
         ^generalize-handle.general-arg-rep.id <pred-slot>
         ^generalize-obj1.general-arg-rep.id <obj1-slot>
         ^generalize-obj2.general-arg-rep.id <obj2-slot>
         -^general-arg-rep)
-->
  (<s> ^operator <o> + =)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^id <pred-slot>
       ^1 <obj1-slot>
       ^2 <obj2-slot>)
}

sp {task-utils*generalize-task-argument*apply*generalize-task-argument*predicate*relation
  (state <s> ^operator <o>)
  (<o> ^name generalize-task-argument
       ^argument <arg>
       ^arg-type predicate
       ^pred-type relation
       ^id <pred-slot>
       ^1 <obj1-slot>
       ^2 <obj2-slot>)
-->
  (<arg> ^general-arg-rep <rep>)
  (<rep> ^type relation
         ^id <pred-slot>
         ^1 <obj1-slot>
         ^2 <obj2-slot>)
}



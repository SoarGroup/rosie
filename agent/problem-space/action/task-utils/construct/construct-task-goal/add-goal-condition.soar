### Operator: add-goal-condition

# once a goal condition was constructed via construct-task-argument, 
#   add it to the task-goal structure being created

sp {task-utils*construct-task-goal*propose*add-goal-condition
   (state <s> ^name construct-task-goal
              ^construct-task-argument <cta>
              ^task-goal <goal>)
   (<cta> ^arg-type predicate
          ^result-type goal-condition
          ^created-arg <pred>
         -^added-to-goal true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-goal-condition
        ^goal-condition <cta>
        ^predicate <pred>)
}

# If there is no conditions wme on the goal, add it
sp {task-utils*construct-task-goal*add-goal-condition*create*conditions
   (state <s> ^name construct-task-goal
              ^operator.name add-goal-condition
              ^task-goal <goal>)
   (<goal> -^conditions)
-->
   (<goal> ^conditions <conds>)
   (<conds> ^pred-count 0)
}

# The number of the predicate we are adding to the goal
sp {task-utils*construct-task-goal*add-goal-condition*elaborate*pred-number
   (state <s> ^name construct-task-goal
              ^operator <o> +
              ^task-goal.conditions <conds>)
   (<o> ^name add-goal-condition)
   (<conds> ^pred-count <n>)
-->
   (<o> ^pred-number (+ <n> 1))
}

# Add the predicate to the goal and increment pred-count
sp {task-utils*construct-task-goal*apply*add-goal-condition
   (state <s> ^name construct-task-goal
              ^operator <o>
              ^task-goal.conditions <conds>)
   (<o> ^name add-goal-condition
        ^goal-condition <gcd>
        ^predicate <pred>
        ^pred-number <pred-num>)
   (<conds> ^pred-count <n>)
-->
   (<conds> ^<pred-num> <pred>
            ^pred-count <n> -
            ^pred-count (+ <n> 1))
   (<gcd> ^added-to-goal true)
}


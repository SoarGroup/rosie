##### operator create-goal-base
# creates the basic structure for the operator as task-goal

#### PROPOSAL

# Propose if there is no task-goal
sp {task-utils*construct-task-goal*propose*create-goal-base
   (state <s> ^name construct-task-goal
              ^goal-message <msg>
             -^task-goal)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-goal-base
        ^goal <goal>)
} 

#### ELABORATIONS

#### APPLICATION

# create the task-goal structure
sp {task-utils*construct-task-goal*apply*create-goal-base
   (state <s> ^name construct-task-goal
              ^operator <o>)
   (<o> ^name create-goal-base
        ^goal <goal>)
-->
   (<s> ^task-goal <goal>)
   (<goal> ^item-type task-goal
           ^pred-count 0)
}

# If the person say this was the entire/only goal, mark it as final-goal
sp {task-utils*construct-task-goal*apply*create-goal-base*mark*final-goal
   (state <s> ^name construct-task-goal
              ^goal-message.arg1.handle << only1 entire1 final >>
              ^operator <o>)
   (<o> ^name create-goal-base
        ^goal <goal>)
-->
   (<goal> ^final-goal true)
}


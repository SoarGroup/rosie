#### Goal Clauses

# Unary goal clause
sp {task-utils*construct-task-goal*elaborate*construct-task-argument*unary*goal-clause
   (state <s> ^name construct-task-goal
              ^goal-clause <gcl>)
   (<gcl> ^predicate.handle <pred-h>
          ^arg1 <obj1>
         -^arg2 <arg2>
         -^predicate.{2} <obj2>
          ^reference-info <ref1>)
   (<ref1> ^arg-name arg1)
-->
   (<s> ^construct-task-argument <cta>)
   (<cta> ^arg-type predicate
          ^result-type goal-clause
          ^source <src>
          ^reference-info <ref-info>)
   (<src> ^type unary
          ^handle <pred-h>
          ^1 <obj1>)
   (<ref-info> ^1 <ref1>)
}

# Relation goal clause
sp {task-utils*construct-task-goal*elaborate*construct-task-argument*relation*goal-clause
   (state <s> ^name construct-task-goal
              ^goal-clause <gcl>)
   (<gcl> ^arg1 <obj1>
          ^{ << arg2 predicate relation >> } <arg2>
          ^reference-info <ref1> <ref2>)
   (<arg2> ^handle <pred-h>
           ^2 <obj2>)
   (<ref1> ^arg-name arg1)
   (<ref2> ^arg-name << arg2 predicate relation >>)
-->
   (<s> ^construct-task-argument <cta>)
   (<cta> ^arg-type predicate
          ^result-type goal-clause
          ^source <src>
          ^reference-info <ref-info>)
   (<src> ^type relation
          ^handle <pred-h>
          ^1 <obj1>
          ^2 <obj2>)
   (<ref-info> ^1 <ref1> ^2 <ref2>)
}

# Special Case: Construct a relation goal predicate for heard (goal is that the person heard the message)
sp {task-utils*construct-task-goal*elaborate*construct-task-argument*relation*predicate*heard
   (state <s> ^name construct-task-goal
              ^goal-clause <gcl>)
   (<gcl> ^action.handle heard1
          ^agent <obj1>
          ^arg1 <obj2>
          ^reference-info <ref1>)
   (<ref1> ^arg-name arg1)
-->
   (<s> ^construct-task-argument <cta>)
   (<cta> ^arg-type predicate
          ^result-type goal-clause
          ^source <src>
          ^reference-info <ref-info>)
   (<src> ^type relation
          ^handle heard2
          ^1 <obj1>
          ^2 <obj2>)
   (<ref-info> ^1 <ref1>)
}

#### Goal Conditions

sp {task-utils*construct-task-goal*elaborate*construct-task-argument*unary*condition
   (state <s> ^name construct-task-goal
              ^conditions <conds>)
   (<conds> ^action.handle is1
            ^arg1 <obj1>
           -^arg2
            ^predicate.handle <pred-h>
            ^reference-info <ref1>)
   (<ref1> ^arg-name arg1)
-->
   (<s> ^construct-task-argument <cta>)
   (<cta> ^arg-type predicate
          ^result-type goal-condition
          ^source <src>
          ^reference-info <ref-info>)
   (<src> ^type unary
          ^handle <pred-h>
          ^1 <obj1>)
   (<ref-info> ^1 <ref1>)
}


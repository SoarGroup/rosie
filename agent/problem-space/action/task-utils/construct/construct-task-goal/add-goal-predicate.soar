
### Operator: add-goal-predicate

# once a goal predicate was constructed via construct-task-argument, 
#   add it to the task-goal structure being created

sp {task-utils*construct-task-goal*propose*add-goal-predicate
   (state <s> ^name construct-task-goal
              ^goal-clause <gcl>
              ^construct-task-argument <cta>
              ^task-goal <goal>)
   (<gcl> -^added)
   (<cta> ^arg-type predicate
          ^goal-clause <gcl>
          ^created-arg <pred>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-goal-predicate
        ^goal-clause <gcl>
        ^predicate <pred>)
}

# The number of the predicate we are adding to the goal
sp {task-utils*construct-task-goal*add-goal-predicate*elaborate*pred-number
   (state <s> ^name construct-task-goal
              ^operator <o> +
              ^task-goal <goal>)
   (<o> ^name add-goal-predicate)
   (<goal> ^pred-count <n>)
-->
   (<o> ^pred-number (+ <n> 1))
}

# Add the predicate to the goal and increment pred-count
sp {task-utils*construct-task-goal*apply*add-goal-predicate
   (state <s> ^name construct-task-goal
              ^operator <o>
              ^task-goal <goal>)
   (<o> ^name add-goal-predicate
        ^goal-clause <gcl>
        ^predicate <pred>
        ^pred-number <pred-num>)
   (<goal> ^pred-count <n>)
-->
   (<goal> ^<pred-num> <pred>
           ^pred-count <n> -
           ^pred-count (+ <n> 1))
   (<gcl> ^added true)
}

### Adding reference-info for each predicate object
sp {task-utils*construct-task-goal*elaborate*add-goal-predicate*copy-reference-info*obj1
   (state <s> ^name construct-task-goal
              ^operator <o> +)
   (<o> ^name add-goal-predicate
        ^goal-clause.obj1-ref-info <ref1>)
-->
   (<o> ^copy-reference-info <cri>)
   (<cri> ^arg-num 1
          ^source <ref1>
          ^destination <dest>)
}

sp {task-utils*construct-task-goal*elaborate*add-goal-predicate*copy-reference-info*obj2
   (state <s> ^name construct-task-goal
              ^operator <o> +)
   (<o> ^name add-goal-predicate
        ^goal-clause.obj2-ref-info <ref2>)
-->
   (<o> ^copy-reference-info <cri>)
   (<cri> ^arg-num 2
          ^source <ref2>
          ^destination <dest>)
}

sp {task-utils*construct-task-goal*apply*add-goal-predicate*copy-reference-info*root
   (state <s> ^name construct-task-goal
              ^operator <o>)
   (<o> ^name add-goal-predicate
        ^copy-reference-info <cri>
        ^predicate <pred>)
   (<cri> ^arg-num <num>
          ^destination <ref-info>)
-->
   (<pred> ^reference-info <ref-info>)
   (<ref-info> ^num <num>)
}

sp {task-utils*construct-task-goal*apply*add-goal-predicate*copy-reference-info*constraint
   (state <s> ^name construct-task-goal
              ^operator <o>)
   (<o> ^name add-goal-predicate
        ^copy-reference-info <cri>)
   (<cri> ^source.<prop-h> <pred-h>
          ^destination <ref-info>)
-->
   (<ref-info> ^<prop-h> <pred-h>)
}



# If the argument is not on the task-concept-network.procedural link, add it
sp {update-tcn*propose*add-argument-to-tcn
   (state <s> ^name update-tcn
              ^task-operator <task-op>
              ^task-concept-network.procedural <proc>)
   (<task-op> ^<arg-name> <arg>)
   (<arg> ^arg-type { <arg-type> << object concept partial-predicate >> })
   (<proc> -^<arg-name> <parg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument <arg>)
}

# arg-type concept
sp {update-tcn*apply*add-argument-to-tcn*concept
   (state <s> ^name update-tcn
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument.arg-type concept)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type concept
           ^id <new-conc-slot>)
   (<s> ^store-concept <proc> <parg>)
}

# arg-type object
sp {update-tcn*apply*add-argument-to-tcn*object
   (state <s> ^name update-tcn
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument.arg-type object)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type object
           ^id <new-obj-slot>)
   (<s> ^store-concept <proc> <parg>)
}

# arg-type partial-predicate
sp {update-tcn*apply*add-argument-to-tcn*predicate
   (state <s> ^name update-tcn
              ^operator <o>
              ^task-concept-network.procedural <proc>)
   (<o> ^name add-argument-to-tcn
        ^arg-name <arg-name>
        ^argument.arg-type partial-predicate)
-->
   (<proc> ^<arg-name> <parg>)
   (<parg> ^arg-type partial-predicate
           ^id <new-pred-slot>
           ^2 <new-obj-slot>)
   (<s> ^store-concept <proc> <parg>)
}


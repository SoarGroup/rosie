sp {comprehension*elaborate*process-semantics*reference-info*DP
   (state <s> ^operator <o> +)
   (<o> ^name process-semantics
        ^assigner <asg>
        ^value <obj>)
   (<obj> ^item-type object)
   (<asg> ^structure-type DP
          ^parent-receiver.semantics <sem>
          ^merged-receiver <info>)
-->
   (<o> ^reference-info <new-info>)
   (<new-info> ^source <info>
               ^destination <sem>
               ^root <root>)
}

sp {comprehension*elaborate*process-semantics*reference-info*PP
   (state <s> ^operator <o> +)
   (<o> ^name process-semantics
        ^assigner <asg>
        ^<arg-name> <pred>)
   (<asg> ^structure-type PP
          ^parent-receiver.semantics <sem>
          ^pp-obj-reference-info <info>)
   (<info> ^referent <obj>)
   (<pred> ^2 <obj>)
-->
   (<o> ^reference-info <new-info>)
   (<new-info> ^source <info>
               ^destination <sem>
               ^root <root>)
}

sp {comprehension*elaborate*process-semantics*reference-info*relation
   (state <s> ^operator <o> +)
   (<o> ^name process-semantics
        ^assigner <asg>
        ^attribute relation
        ^relation.{2} <obj>)
   (<asg> ^parent-receiver.semantics <sem>
          ^pp-obj-reference-info <info>)
   (<info> ^referent <obj>)
-->
   (<o> ^reference-info <new-info>)
   (<new-info> ^source <info>
               ^destination <sem>
               ^root <root>)
}

sp {comprehension*apply*process-semantics*copy*reference-info*to*destination
   (state <s> ^operator <o>
             -^top-state.dialog-context
             -^agent-params.hypothetical true)
   (<o> ^name process-semantics
        ^reference-info <ref-info>)
   (<ref-info> ^source.referent <ref>
               ^destination <dest>
               ^root <root>)
-->
   (<dest> ^reference-info <root>)
   (<root> ^referent <ref>)
}

sp {comprehension*apply*process-semantics*copy*reference-info*constraint
   (state <s> ^operator <o>)
   (<o> ^name process-semantics
        ^reference-info <ref-info>)
   (<ref-info> ^source.constraint <c>
               ^root <root>)
-->
   (<root> ^constraint <c>)
}

sp {comprehension*apply*merge*PP*copy*object*reference-info
   (state <s> ^operator <o>)
   (<o> ^name merge
        ^receiver.head <head>
        ^assigner <asg>)
   (<head> ^structure-type DP)
   (<asg> ^structure-type PP)
-->
   (<asg> ^pp-obj-reference-info <head>)
}



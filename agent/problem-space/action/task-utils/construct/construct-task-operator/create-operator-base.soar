##### operator create-operator-base
# creates the basic structure for the operator as task-operator

#### PROPOSAL

# propose if there is procedural info on the TCN
sp {task-utils*construct-task-operator*propose*create-operator-base
   (state <s> ^name construct-task-operator
              ^task-concept-network <tcn>
             -^task-operator <op>)
   (<tcn> ^handle <task-handle>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-operator-base
        ^task-handle <task-handle>)
} 

#### ELABORATIONS

# elaborate operator-name from the TCN
sp {task-utils*construct-task-operator*elaborate*operator-name*TCN
   (state <s> ^name construct-task-operator
              ^operator <o> +
              ^task-concept-network.procedural.op_name <op-name>)
   (<o> ^name create-operator-base)
-->
   (<o> ^operator-name <op-name>)
}

# create a new operator-name if it isn't on the TCN
sp {task-utils*construct-task-operator*elaborate*operator-name*new
   (state <s> ^name construct-task-operator
              ^operator <o> +
              ^task-concept-network <tcn>)
   (<o> ^name create-operator-base
        ^task-handle <handle>)
   (<tcn> -^procedural.op_name <op-name>)
-->
   (<o> ^operator-name (make-constant-symbol (concat |op_| <handle>)))
}

#### APPLICATION

# create the task-operator strucutre with task-handle
sp {task-utils*construct-task-operator*apply*create-operator-base
   (state <s> ^name construct-task-operator
              ^operator <o>)
   (<o> ^name create-operator-base
        ^task-handle <task-handle>
        ^operator-name <op-name>)
-->
   (<s> ^task-operator <task-op>)
   (<task-op> ^item-type task-operator
              ^task-handle <task-handle>
              ^name <op-name>)
}


### Operator: add-arguments-to-task-operator
# If there are uncopied arguments and all arguments have been created, 
# Propose add-arguments-to-task-operator to copy all the arguments onto the task-operator

sp {task-utils*construct-task-operator*propose*add-arguments-to-task-operator
   (state <s> ^name construct-task-operator
              ^task-operator <task-op>
             -^added-arguments true)
-->
   (<s> ^operator <o> + = <)
   (<o> ^name add-arguments-to-task-operator)
}

sp {task-utils*construct-task-operator*apply*add-arguments-to-task-operator
   (state <s> ^name construct-task-operator
              ^operator.name add-arguments-to-task-operator)
-->
   (<s> ^added-arguments true)
}

sp {task-utils*construct-task-operator*apply*add-arguments-to-task-operator*copy*argument
   (state <s> ^name construct-task-operator
              ^operator.name add-arguments-to-task-operator
              ^task-operator <task-op>
              ^construct-task-argument <cta>)
   (<cta> ^arg-name <arg-name>
          ^arg-type <arg-type>
          ^created-arg <arg>)
-->
   (<task-op> ^<arg-name> <arg>)
   (<arg> ^arg-type <arg-type>)
}

sp {task-utils*construct-task-operator*apply*write*warning*unconstructed-argument
   (state <s> ^name construct-task-operator
              ^operator.name add-arguments-to-task-operator
              ^construct-task-argument <cta>)
   (<cta> -^created-arg <arg>)
-->
   (interrupt)
   (write (crlf) |WARNING: construct-task-operator has an unconstructed argument: | (crlf))
   (write (cmd p <cta> -d 4) (crlf))
}

sp {task-utils*construct-task-operator*propose*add-arguments-to-task-operator
   (state <s> ^name construct-task-operator
              ^task-operator <task-op>
             -^added-arguments true)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-arguments-to-task-operator)
}

sp {task-utils*construct-task-operator*elaborate*add-arguments-to-task-operator*copy-reference-info
   (state <s> ^name construct-task-operator
              ^operator <o> +
              ^command-message.reference-info <info>)
   (<o> ^name add-arguments-to-task-operator)
   (<info> ^referent <ref>)
-->
   (<o> ^copy-reference-info <copy>)
   (<copy> ^source <info>
           ^referent <ref>
           ^destination <dest>)
}

sp {task-utils*construct-task-operator*apply*add-arguments-to-task-operator*mark*added
   (state <s> ^name construct-task-operator
              ^operator.name add-arguments-to-task-operator)
-->
   (<s> ^added-arguments true)
}

sp {task-utils*construct-task-operator*apply*add-arguments-to-task-operator*add*arguments
   (state <s> ^name construct-task-operator
              ^operator.name add-arguments-to-task-operator
              ^task-operator <task-op>
              ^construct-task-argument <cta>)
   (<cta> ^arg-name <arg-name>
          ^arg-type <arg-type>
          ^created-arg <arg>)
-->
   (<task-op> ^<arg-name> <arg>)
   (<arg> ^arg-type <arg-type>)
}

### Add reference-info for objects

sp {task-utils*construct-task-operator*apply*add-arguments-to-task-operator*copy*reference-info*object*arg
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^construct-task-argument <cta>)
   (<o> ^name add-arguments-to-task-operator
        ^copy-reference-info <copy-ri>)
   (<cta> ^arg-type object
          ^source <src-obj>
          ^created-arg <arg>)
   (<copy-ri> ^referent <src-obj>
              ^destination <dest>)
-->
   (<arg> ^reference-info <dest>)
}

sp {task-utils*construct-task-operator*apply*add-arguments-to-task-operator*copy*reference-info*partial-predicate*arg
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^construct-task-argument <cta>)
   (<o> ^name add-arguments-to-task-operator
        ^copy-reference-info <copy-ri>)
   (<cta> ^arg-type partial-predicate
          ^source.{2} <src-obj>
          ^created-arg <arg>)
   (<copy-ri> ^referent <src-obj>
              ^destination <dest>)
-->
   (<arg> ^reference-info <dest>)
}

sp {task-utils*construct-task-operator*apply*add-arguments-to-task-operator*copy*reference-info*constraints
   (state <s> ^name construct-task-operator
              ^operator <o>)
   (<o> ^name add-arguments-to-task-operator
        ^copy-reference-info <copy>)
   (<copy> ^source.constraint <c>
           ^destination <dest>)
-->
   (<dest> ^constraint <c>)
}


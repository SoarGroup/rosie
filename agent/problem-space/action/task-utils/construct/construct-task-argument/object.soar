#### constructing object arguments

### First we add the object to the world

sp {task-utils*construct-task-argument*object*propose*add-object-to-world
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^source <obj>
         -^world-object)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-object-to-world
        ^source-obj <obj>)
}

sp {task-utils*construct-task-argument*elaborate*object*argument*world-object*from*add-object-result
   (state <s> ^construct-task-argument <arg>
              ^add-object-result <res>)
   (<arg> ^arg-type object
          ^source <obj>)
   (<res> ^source-obj <obj>
          ^world-obj <wobj>)
-->
   (<arg> ^world-object <wobj>)
}

### construct-task-argument object

sp {task-utils*construct-task-argument*propose*construct-task-argument*object
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^world-object <obj>
         -^created-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type object
        ^object <obj>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*object
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type object
        ^object <obj>)
-->
   (<arg> ^created-arg <carg>)
   (<carg> ^id <obj>)
}

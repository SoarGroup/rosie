### construct conditions argument 

sp {task-utils*construct-task-argument*conditions*expand*as*predicate-set
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type conditions
          ^source <src>)
-->
   (<s> ^construct-task-argument <cps>)
   (<cps> ^arg-type predicate-set
          ^source <src>)
   (<arg> ^construct-pred-set <cps>)
}

sp {task-utils*construct-task-argument*propose*construct-task-argument*conditions
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type conditions
          ^construct-pred-set.created-arg <carg>
         -^created-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type conditions
        ^result <carg>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*conditions
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type conditions
        ^result <res>)
-->
   (<arg> ^created-arg <res>)
}


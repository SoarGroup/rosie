### construct temporal-clause argument (until-clause after-clause etc.)

sp {task-utils*construct-task-argument*temporal-clause*construct*using*clause
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type temporal-clause
          ^source <src>)
-->
   (<s> ^construct-task-argument <cta>)
   (<cta> ^arg-type clause
          ^source <src>)
   (<arg> ^construct-clause <cta>)
}

#sp {task-utils*construct-task-argument*temporal-clause*copy*construct-clause*result
#   (state <s> ^construct-task-argument <arg>)
#   (<arg> ^arg-type temporal-clause
#          ^construct-clause.created-arg <temp-cl>)
#-->
#   (<arg> ^created-arg <temp-cl>)
#}

sp {task-utils*construct-task-argument*propose*construct-task-argument*temporal-clause
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type temporal-clause
          ^construct-clause.created-arg <created>
         -^created-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type temporal-clause
        ^created-arg <created>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*temporal-clause
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type temporal-clause
        ^created-arg <created>)
-->
   (<arg> ^created-arg <created>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*temporal-clause*copy*negated
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument.negated true
        ^arg-type temporal-clause
        ^created-arg <created>)
-->
   (<created> ^modifiers.mod-handle negated1)
}


### construct partial-predicate

sp {task-utils*construct-task-argument*expand*partial-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^arg-name <arg-name>)
-->
   (<s> ^construct-task-argument <obj2-arg>)
   (<obj2-arg> ^arg-type object
               ^parent-arg-name <arg-name>)
   (<arg> ^create-obj2 <obj2-arg>)
}

sp {task-utils*construct-task-argument*expand*partial-predicate*elaborate*objects
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^create-obj2 <obj2-arg>
          ^source.{2} <obj2>)
-->
   (<obj2-arg> ^source <obj2>)
}

sp {task-utils*construct-task-argument*propose*construct-task-argument*partial-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type partial-predicate
          ^source.handle <handle>
          ^create-obj2.created-arg <obj2-arg>
         -^created-arg)
   (<obj2-arg> ^id <obj2>
               ^reference-info <ref-info>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type partial-predicate
        ^handle <handle>
        ^2 <obj2>
        ^reference-info2 <ref-info>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*partial-predicate
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type partial-predicate
        ^handle <handle>
        ^2 <obj2>
        ^reference-info2 <ref-info>)
-->
   (<arg> ^created-arg <carg>)
   (<carg> ^handle <handle>
           ^2 <obj2>
           ^reference-info <ref-info>)
}

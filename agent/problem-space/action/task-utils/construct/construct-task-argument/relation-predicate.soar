### construct relation-predicate

sp {task-utils*construct-task-argument*expand*relation-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source <src>)
   (<src> ^type relation
          ^1 <obj1>
          ^2 <obj2>)
-->
   (<arg> ^pred-type relation)

   (<s> ^construct-task-argument <obj1-arg>)
   (<obj1-arg> ^arg-type object
               ^source <obj1>)
   (<arg> ^create-obj1 <obj1-arg>)

   (<s> ^construct-task-argument <obj2-arg>)
   (<obj2-arg> ^arg-type object
               ^source <obj2>)
   (<arg> ^create-obj2 <obj2-arg>)
}
          
### copy the reference-info for each object
sp {task-utils*construct-task-argument*relation-predicate*copy*object1*reference-info
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source.type relation
          ^reference-info <ref-info>
          ^create-obj1 <obj1-arg>)
   (<ref-info> ^1 <ref1>)
-->
   (<obj1-arg> ^reference-info <ref1>)
}

sp {task-utils*construct-task-argument*relation-predicate*copy*object2*reference-info
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^source.type relation
          ^reference-info <ref-info>
          ^create-obj2 <obj2-arg>)
   (<ref-info> ^2 <ref2>)
-->
   (<obj2-arg> ^reference-info <ref2>)
}

### If both objects were created, then create the relation predicate
sp {task-utils*construct-task-argument*propose*construct-task-argument*relation-predicate
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type predicate
          ^pred-type relation
          ^source.handle <handle>
          ^create-obj1.created-arg <obj1arg>
          ^create-obj2.created-arg <obj2arg>
         -^created-arg)
   (<obj1arg> ^id <obj1> ^reference-info <ref1>)
   (<obj2arg> ^id <obj2> ^reference-info <ref2>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type relation
        ^handle <handle>
        ^1 <obj1>
        ^2 <obj2>
        ^ref1 <ref1>
        ^ref2 <ref2>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*relation-predicate
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type predicate
        ^pred-type relation
        ^handle <handle>
        ^1 <obj1>
        ^2 <obj2>
        ^ref1 <ref1>
        ^ref2 <ref2>)
-->
   (<arg> ^created-arg <carg>)
   (<carg> ^type relation
           ^handle <handle>
           ^1 <obj1>
           ^2 <obj2>
           ^reference-info <ref-info>)
   (<ref-info> ^1 <ref1> ^2 <ref2>)
}


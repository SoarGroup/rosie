######### If you elaborate ^store-task-operator or ^store-predicate-set on to a state, 
# this will recursively elaborate ^store-concept wmes onto the state to store the entire task
# via smem-operators/smem-store.soar
### Make sure the subtask operator is fully stored

##### EXPAND store-task-operator

# store the task-operator root
sp {task-utils*store-task-operator*elaborate*store-concept*root
   (state <s> ^store-task-operator <task>)
-->
   (<s> ^store-concept <task>)
}

# store any task arguments
sp {task-utils*store-task-operator*elaborate*store-concept*argument
   (state <s> ^store-task-operator <task>)
   (<task> ^<arg-name> <arg>)
   (<arg> ^arg-type <type>)
-->
   (<s> ^store-concept <arg>)
}

# store any task argument slots
sp {task-utils*store-task-operator*elaborate*store-task-arg-slot
   (state <s> ^store-task-operator <task>)
   (<task> ^<arg-name> <arg>)
   (<arg> ^arg-type { <type> <> conditions <> temporal-clause <> predicate-set }
          ^{ << id 1 2 3 4 5 predicate >> } <slot>)
-->
   (<s> ^store-task-arg-slot <slot>)
}

# store any task predicate-set arguments
sp {task-utils*store-task-operator*elaborate*store-predicate-set
   (state <s> ^store-task-operator <task>)
   (<task> ^<arg-name> <arg>)
   (<arg> ^arg-type << conditions temporal-clause predicate-set >>)
-->
   (<s> ^store-predicate-set <arg>)
}


##### EXPAND store-predicate-set

# store the predicate-set root
sp {task-utils*store-task-operator*store-predicate-set*elaborate*store-concept*root
   (state <s> ^store-predicate-set <pset>)
-->
   (<s> ^store-concept <pset>)
}

# store each predicate
sp {task-utils*store-task-operator*store-predicate-set*elaborate*store-concept*predicate
   (state <s> ^store-predicate-set.{ <n> << 1 2 3 4 5 6 7 8 9 >> } <pred>)
-->
   (<s> ^store-concept <pred>)
}

# store each predicate argument slot
sp {task-utils*store-task-operator*store-predicate-set*elaborate*store-task-arg-slot
   (state <s> ^store-predicate-set.{ << 1 2 3 4 5 6 7 8 9 >> } <pred>)
   (<pred> ^{ << id 1 2 3 4 5 >> } <arg>)
-->
   (<s> ^store-task-arg-slot <arg>)
}

##### EXPAND store-task-arg-slot

# store the arg-slot root
sp {task-utils*store-task-operator*store-task-arg-slot*elaborate*store-concept*root
   (state <s> ^store-task-arg-slot <slot>)
-->
   (<s> ^store-concept <slot>)
}

# store any slot default values
sp {task-utils*store-task-operator*store-task-arg-slot*elaborate*store-concept*default
   (state <s> ^store-task-arg-slot <slot>)
   (<slot> ^{ << default imagined >> } <val>)
-->
   (<s> ^store-concept <val>)
}

# store default value substructure
sp {task-utils*store-task-operator*store-task-arg-slot*elaborate*store-concept*default*children
   (state <s> ^store-task-arg-slot <slot>)
   (<slot> ^{ << default imagined >> }.{ << predicates relation >> } <val>)
-->
   (<s> ^store-concept <val>)
}

# store any relations on task argument slots
sp {task-utils*store-task-operator*store-task-arg-slot*elaborate*store-task-arg-slot*default
   (state <s> ^store-task-arg-slot <slot>)
   (<slot> ^{ << default imagined >> }.relation.{ << 1 2 >> } <rel-obj-slot>)
-->
   (<s> ^store-task-arg-slot <rel-obj-slot>)
}


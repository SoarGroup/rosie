# Delete any task objects that are not confirmed and are not on any other task segment
sp {pop-task-segment*elaborate*old-objects
   (state <s> ^name pop-task-segment
              ^segment-to-pop <seg>
              ^task-stack.segments <segs>)
   (<seg> ^task-objects.object <obj>)
   (<obj> -^predicates.is-confirmed1)
  -{(<segs> ^segment { <other-seg> <> <seg> })
    (<other-seg> ^task-objects.object <obj>)}
-->
   (<s> ^old-object <obj>)
}

sp {pop-task-segment*elaborate*old-locations
   (state <s> ^name pop-task-segment
              ^segment-to-pop <seg>
              ^task-stack.segments <segs>)
   (<seg> ^task-objects.object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^category location
           -^modifier1 current1)
  -{(<segs> ^segment { <other-seg> <> <seg> })
    (<other-seg> ^task-objects.object <obj>)}
-->
   (<s> ^old-object <obj>)
}

# Delete any abstract objects if we are popping the top segment
sp {pop-task-segment*elaborate*old-objects*abstract*top
   (state <s> ^name pop-task-segment
              ^segment-to-pop.prev none
              ^top-state.world.objects <objs>)
   (<objs> ^object <obj>)
   (<obj> -^predicates.is-confirmed1 confirmed1)
-->
   (<s> ^old-object <obj>)
}

# Delete any abstract objects not on a segment
sp {pop-task-segment*elaborate*old-objects*abstract*no*segment
   (state <s> ^name pop-task-segment
              ^segment-to-pop <seg>
              ^task-stack.segments <segs>
              ^top-state.world.objects <objs>)
  -{(<segs> ^segment { <other-seg> <> <seg> })
    (<other-seg> ^task-objects.object <obj>)}
   (<objs> ^object <obj>)
   (<obj> -^predicates.is-confirmed1 confirmed1)
-->
   (<s> ^old-object <obj>)
}

sp {pop-task-segment*elaborate*old-object-info
   (state <s> ^name pop-task-segment
              ^old-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^world-obj <obj>)
-->
   (<s> ^old-object-info <obj-info>)
}


sp {pop-task-segment*propose*change-world-state*delete-object
   (state <s> ^name pop-task-segment
              ^old-object-info <obj-info>
              ^world.objects.object <obj>)
   (<obj-info> ^world-obj <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name change-world-state
        ^delete-object <obj-info>)
}

sp {pop-task-segment*apply*change-world-state*delete-object*remove*task-object
   (state <s> ^name pop-task-segment
              ^operator <o>
              ^segment-to-pop.task-objects <objs>)
   (<o> ^name change-world-state
        ^delete-object <obj-info>)
   (<obj-info> ^world-obj <obj>)
   (<objs> ^<att> <obj>)
-->
   (<objs> ^<att> <obj> -)
   (<s> ^old-object <obj> # Add so that they remain even when removed from task-objects
        ^old-object-info <obj-info>)
}


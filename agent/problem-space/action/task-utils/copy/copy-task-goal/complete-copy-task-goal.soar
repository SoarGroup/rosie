sp {task-utils*copy-task-goal*propose*complete-copy-task-goal
   (state <s> ^copy-task-goal-workspace <ws>)
-->
   (<s> ^operator <o> + < =)
   (<o> ^name complete-copy-task-goal
        ^workspace <ws>)
}

sp {__COPY__GOAL__*anystate*copy-task-goal*elaborate*copy*no*modifiers
   (state <s> ^operator <o>
              ^superstate <ss>)
   (<o> ^name complete-copy-task-goal
        ^workspace <ws>)
   (<ws> ^source-goal <src>
         ^copy-predicate-set.arg-copy <copy>
         ^copy-modifiers none)
   (<src> ^handle <h>)
-->
   (<ss> ^desired <copy>)
   (<copy> ^duplicate-of <src>
           ^handle <h>)
}

sp {__COPY__GOAL__*anystate*copy-task-goal*elaborate*copy*with*modifiers
   (state <s> ^operator <o>
              ^superstate <ss>)
   (<o> ^name complete-copy-task-goal
        ^workspace <ws>)
   (<ws> ^source-goal <src>
         ^copy-predicate-set.arg-copy <copy>
         ^copy-modifiers.arg-copy <copy-mods>)
   (<src> ^handle <h>)
-->
   (<ss> ^desired <copy>)
   (<copy> ^duplicate-of <src>
           ^handle <h>
           ^modifiers <copy-mods>)
}

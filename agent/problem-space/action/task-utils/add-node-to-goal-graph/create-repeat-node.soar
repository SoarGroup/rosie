### Operator create-repeat-node
# Proposed if node-type = repeat
# Creates a node with ^item-type repeat-goal and 0 predicates

sp {add-node-to-goal-graph*propose*create-repeat-node
   (state <s> ^name add-node-to-goal-graph
              ^node-type repeat
              ^task-handle <task-h>
             -^created-node
             -^closing-repeat-block)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-repeat-node
        ^item-type create-node-operator
        ^task-handle <task-h>)
}

sp {add-node-to-goal-graph*apply*create-repeat-node
   (state <s> ^name add-node-to-goal-graph
              ^operator <o>)
   (<o> ^name create-repeat-node
        ^task-handle <task-h>)
-->
   (<s> ^created-node <node>
        ^new-node <node>
        ^repeat-node <node>)
   (<node> ^item-type repeat-goal
           ^handle (make-constant-symbol (concat <task-h> |repeat|))
           ^pred-count 0)
}

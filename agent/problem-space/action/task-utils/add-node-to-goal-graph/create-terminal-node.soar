
### Operator create-terminal-node
# Proposed if node-type = terminal
# Creates a node with ^item-type terminal-goal and adds it after the previous-node
sp {add-node-to-goal-graph*propose*create-terminal-node
   (state <s> ^name add-node-to-goal-graph
              ^node-type terminal
              ^task-handle <task-h>
             -^created-node)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-terminal-node
        ^item-type create-node-operator
        ^task-handle <task-h>
        ^result-name created-node)
}

### Also proposed if ^final-goal true flag is present
sp {add-node-to-goal-graph*propose*create-terminal-node*final-goal
   (state <s> ^name add-node-to-goal-graph
              ^node-type << predicate-set >>
              ^final-goal true
              ^task-handle <task-h>
             -^created-terminal-node)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-terminal-node
        ^item-type create-node-operator
        ^task-handle <task-h>
        ^result-name created-terminal-node)
}

# Create the terminal node and add a next pointer from the previous node
sp {add-node-to-goal-graph*apply*create-terminal-node
   (state <s> ^name add-node-to-goal-graph
              ^operator <o>)
   (<o> ^name create-terminal-node
        ^task-handle <task-h>
        ^result-name <res-name>)
-->
   (<s> ^<res-name> <node>
        ^new-node <node>
        ^store-node <node>)
   (<node> ^handle (make-constant-symbol (concat <task-h> |term|)) ^item-type terminal-goal)
}



### Operator create-start-node
# Proposed if node-type = start
# Creates a node with ^item-type start-goal and adds it directly to the TCN as ^goal-graph <start-node>
sp {add-node-to-goal-graph*propose*create-start-node
   (state <s> ^name add-node-to-goal-graph
              ^node-type start
              ^task-handle <task-h>
              ^task-concept-network <tcn>
             -^created-node)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-start-node
        ^item-type create-node-operator
        ^task-handle <task-h>)
}

sp {add-node-to-goal-graph*apply*create-start-node
   (state <s> ^name add-node-to-goal-graph
              ^operator <o>
              ^task-concept-network <tcn>)
   (<o> ^name create-start-node
        ^task-handle <task-h>)
-->
   (<s> ^created-node <node>
        ^new-node <node>
        ^store-concept <tcn>)
   (<tcn> ^goal-graph <node>)
   (<node> ^handle (make-constant-symbol (concat <task-h> |start|)) ^item-type start-goal)
}

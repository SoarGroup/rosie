
### When the node has been created and stored, copy the handle
#     onto the superoperator as ^add-node-result <node-h>
sp {add-node-to-goal-graph*propose*complete-add-node-to-goal-graph
   (state <s> ^name add-node-to-goal-graph
              ^created-node.handle <node-h>)
-->
   (<s> ^operator <o> + = <)
   (<o> ^name complete-add-node-to-goal-graph
        ^node-handle <node-h>)
}

# If we are closing a repeat block, propose complete
sp {add-node-to-goal-graph*propose*complete-add-node-to-goal-graph*closing-repeat-block
   (state <s> ^name add-node-to-goal-graph
              ^closing-repeat-block true
              ^repeat-node.handle <node-h>)
-->
   (<s> ^operator <o> + = <)
   (<o> ^name complete-add-node-to-goal-graph
        ^node-handle <node-h>)
}

sp {add-node-to-goal-graph*apply*complete-add-node-to-goal-graph*return*result
   (state <s> ^name add-node-to-goal-graph
              ^operator <o>
              ^superstate.operator <ssop>)
   (<o> ^name complete-add-node-to-goal-graph
        ^node-handle <node-h>)
-->
   (<ssop> ^add-node-result <node-h>)
}

sp {add-node-to-goal-graph*apply*complete-add-node-to-goal-graph*return*new-node-handle
   (state <s> ^name add-node-to-goal-graph
              ^operator.name complete-add-node-to-goal-graph
              ^new-node.handle <node-h>
              ^superstate.operator <ssop>)
-->
   (<ssop> ^new-node-handle <node-h>)
}

sp {add-node-to-goal-graph*apply*complete-add-node-to-goal-graph*return*new-node-handle*modified
   (state <s> ^name add-node-to-goal-graph
              ^operator.name complete-add-node-to-goal-graph
              ^modified-node.handle <node-h>
              ^superstate.operator <ssop>)
-->
   (<ssop> ^new-node-handle <node-h>)
}

sp {add-node-to-goal-graph*apply*complete-add-node-to-goal-graph*return*renamed-node
   (state <s> ^name add-node-to-goal-graph
              ^operator.name complete-add-node-to-goal-graph
              ^insert-after-node <old-h>
              ^superstate.operator <ssop>
              ^changed-handle <chg>)
   (<chg> ^old-handle <old-h>
          ^goal.handle <new-h>)
-->
   (<ssop> ^renamed-node <new-h>)
}

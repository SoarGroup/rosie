
# Best preference for command operators, because they will likely achieve the goal
sp {subproblem*retrospective*best*preference*command*operators
   (state <s> ^problem-space.subproblem retrospective
              ^operator <o> +)
   (<o> ^item-type command-operator)
-->
   (<s> ^operator <o> >)
}

# Even if the depth is exceeded, allow command-operators to fire
sp {subproblem*retrospective*prefer*command-operator*over*exceed-depth
   (state <s> ^problem-space.subproblem retrospective
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^item-type command-operator)
   (<o2> ^name exceed-depth)
-->
   (<s> ^operator <o1> > <o2>)
}

# Reject an operator with the same name as the parent task
sp {subproblem*retrospective*reject*same*task*as*supertask
   (state <s> ^problem-space.subproblem retrospective
              ^task-operator <task-op>
              ^operator { <o> <> <task-op> } +)
   (<task-op> ^task-handle <handle>)
   (<o> ^task-handle <handle>)
-->
   (<s> ^operator <o> -)
}



# First, try to see if we have a representation for the object in semantic memory 
# using the handle
sp {op_remember1*elaborate*smem-query*match*object*handle
   (state <s> ^name op_remember1
              ^query-object-workspace <ws>)
   (<ws> ^source.handle <obj-h>
        -^match-handle-result)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2
        ^destination <ws>
        ^result-name match-handle-result)
   (<cue> ^item-type object
          ^object-instance true
          ^handle <obj-h>)
}

# If the handle doesn't match, try to retrieve based on predicates
sp {op_remember1*elaborate*smem-query*match*object*predicates
   (state <s> ^name op_remember1
              ^query-object-workspace <ws>)
   (<ws> ^source.root-category <cat>
         ^match-handle-result failure
        -^match-predicates-result)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2
        ^destination <ws>
        ^result-name match-predicates-result)
   (<cue> ^item-type object
          ^object-instance true
          ^root-category <cat>)
   (<ws> ^predicates-cue <cue>)
}

sp {op_remember1*elaborate*smem-query*match*object*predicates*copy*predicates
   (state <s> ^name op_remember1
              ^query-object-workspace <ws>)
   (<ws> ^source.predicates <preds>
         ^predicates-cue <cue>)
   (<preds> ^{ <prop> << name >> } <pred>)
-->
   (<cue> ^<prop> <pred>)
}

# If the handle/predicates doesn't match, try to retrieve the category lti
sp {op_remember1*elaborate*smem-query*category*lti
   (state <s> ^name op_remember1
              ^query-object-workspace <ws>)
   (<ws> ^reference-info.category <cat>
         ^match-handle-result failure
         ^match-predicates-result failure
        -^retrieved-category)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2
        ^destination <ws>
        ^result-name retrieved-category)
   (<cue> ^handle <cat>)
}

sp {op_remember1*elaborate*smem-query*category*lti*root-category
   (state <s> ^name op_remember1
              ^query-object-workspace <ws>)
   (<ws> -^reference-info
         ^source.root-category <cat>
         ^match-handle-result failure
         ^match-predicates-result failure)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2
        ^destination <ws>
        ^result-name retrieved-category)
   (<cue> ^handle <cat>)
}


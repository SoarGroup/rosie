# If the word 'permanently' was used, use long-term memory, otherwise working memory
sp {op_remember1*elaborate*operator*memory-type*ltm
   (state <s> ^operator <o> +)
   (<o> ^name op_remember1
        ^modifiers.handle permanent1)
-->
   (<o> ^memory-type ltm)
}

sp {op_remember1*elaborate*operator*memory-type*wm
   (state <s> ^operator <o> +)
   (<o> ^name op_remember1
       -^modifiers.handle permanent1)
-->
   (<o> ^memory-type wm)
}

### Copy memory-type to substate
sp {op_remember1*elaborate*memory-type*superoperator
   (state <s> ^name op_remember1
              ^superstate.operator.memory-type <t>)
-->
   (<s> ^memory-type <t>)
}

# Turn "Remember the current location as the office of Mary"
# Into an edge in the smem graph, where
# (arg3) ----(arg2)---> (arg1), e.g., 
# (Mary) ---(office)--> (cur-loc)
sp {op_remember1*memory-type*ltm*elaborate*objects
   (state <s> ^name op_remember1
              ^memory-type ltm
              ^task-operator <task-op>)
   (<task-op> ^arg1.id <tail>
              ^arg2.id <edge>
              ^arg3.id <head>)
-->
   (<s> ^head-object <head>
        ^edge-object <edge>
        ^tail-object <tail>)
}

sp {op_remember1*elaborate*query-object-workspace
   (state <s> ^name op_remember1
              ^memory-type ltm
              ^problem-space.execution-type external
              ^desired.handle remember1goal1
              ^{ <type> << head-object tail-object >> } <obj>)
-->
   (<s> ^query-object-workspace <ws>)
   (<ws> ^type <type>
         ^source <obj>)
}

sp {op_remember1*elaborate*query-object-workspace*reference-info
   (state <s> ^name op_remember1
              ^memory-type ltm
              ^query-object-workspace <ws>
              ^task-operator.<arg-name> <arg>)
   (<ws> ^source <obj>)
   (<arg> ^id <obj>
          ^reference-info <ref-info>)
-->
   (<ws> ^reference-info <ref-info>)
}

sp {op_remember1*elaborate*query-object-workspace*ltm-object
   (state <s> ^name op_remember1
              ^query-object-workspace <ws>)
   (<ws> ^{ << match-handle-result match-predicates-result retrieved-category >> } { <obj> <> failure })
-->
   (<ws> ^ltm-object <obj>)
}

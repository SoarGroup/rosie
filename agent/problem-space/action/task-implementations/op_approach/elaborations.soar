
sp {op_approach1*elaborate*target-object*superoperator
   (state <s> ^name op_approach1
              ^task-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

sp {op_approach1*elaborate*goal-coord*current-task-segment
   (state <s> ^name op_approach1
              ^current-task-segment.goal-coord <coord>)
-->
   (<s> ^goal-coord <coord>)
}

### CHECK PRECONDITIONS
# If these are violated, quit the current task and re-plan in the parent task

# violated-precondition: the target object is missing
sp {op_approach1*elaborate*violated-precondition*missing-object*target
   (state <s> ^name op_approach1
              ^target-object <obj>)
   (<obj> ^predicates.is-confirmed1 not-confirmed1)
-->
   (<s> ^violated-precondition <vp>)
   (<vp> ^type missing-object
         ^object <obj>)
}


# For the magicbot: 
# Calculate the position to drive to, 1.0 meters in front of target-object

# theta from the robot to the target object
sp {op_approach*elaborate*angle-to-target
   (state <s> ^name op_approach1
              ^current-task-segment <seg>)
   (<seg> ^robot-coord <rc>
          ^target-coord <tc>)
   (<rc> ^x <rx> ^y <ry>)
   (<tc> ^x <tx> ^y <ty>)
-->
   (<s> ^angle-to-target (atan2 (- <ty> <ry>) (- <tx> <rx>) ) )
}

# calculate a point 1 meter from the object's position towards the robot
sp {op_approach*elaborate*goal-coord
   (state <s> ^name op_approach1
              ^angle-to-target <theta>
              ^current-task-segment.target-coord <tc>)
   (<tc> ^x <tx> ^y <ty>)
-->
   (<s> ^goal-coord <gc>)
   (<gc> ^x (- <tx> (* 1.0 (cos <theta>) ) ) )
   (<gc> ^y (- <ty> (* 1.0 (sin <theta>) ) ) )
}


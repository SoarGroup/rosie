# Extract the robot's position from SVS

sp {op_approach*propose*extract-robot-position
   (state <s> ^name op_approach1
              ^agent-params.domain magicbot
              ^problem-space.execution-type external
             -^robot-coord
             -^goal-coord)
-->
   (<s> ^operator <o> + =)
   (<o> ^name extract-robot-position)
}

sp {op_approach*apply*extract-robot-position*create*robot*position*filter
   (state <s> ^name op_approach1
              ^operator.name extract-robot-position
              ^top-state.svs.command <cmd>)
-->
    (<cmd> ^extract_once <rob-pos>)
    (<rob-pos> ^type node_position
               ^a <rob>)
    (<rob> ^type node
           ^id robot)
    (<s> ^robot-position-filter <rob-pos>)
}

sp {op_approach*apply*extract-robot-position*copy*result
   (state <s> ^name op_approach1
              ^operator.name extract-robot-position
              ^robot-position-filter <filter>
              ^top-state.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
   (<filter> ^result.record.value <rob-pos>)
   (<rob-pos> ^x <rx> ^y <ry>)
-->
   (<s> ^robot-coord <coord>)
   (<coord> ^x <rx> ^y <ry>)

   (<s> ^robot-position-filter <filter> -)
   (<cmd> ^extract_once <filter> -)
}


#### GOAL SATISFACTION ####

sp {op_scan1*elaborate*desired
   (state <s> ^name op_scan1
             -^copy-task-goal
              ^task-operator.current-goal scan1goal1)
-->
   (<s> ^desired <des>)
   (<des> ^handle scan1goal1 ^pred-count 0)
}

# No until-clause, wait until the scan-command is successful
sp {op_scan1*elaborate*desired*satisfied*no*until-clause
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^desired <des>
              ^current-task-segment.scan-command.status << success succeeded >>
             -^task-operator.until-clause)
   (<des> ^handle scan1goal1)
-->
   (<des> ^satisfied true)
}

# The until-clause is satisfied and the agent has stopped
sp {op_scan1*elaborate*desired*satisfied*until-clause
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^desired <des>
              ^current-task-segment.stop-command.status << success succeeded >>
              ^task-operator.until-clause <until>
              ^satisfied <until>)
   (<des> ^handle scan1goal1)
-->
   (<des> ^satisfied true)
}

# The until-clause is satisfied even before the agent starts driving
sp {op_scan1*elaborate*desired*satisfied*until-clause*no*command*sent
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^desired <des>
             -^current-task-segment.scan-command
              ^task-operator.until-clause <until>
              ^satisfied <until>)
   (<des> ^handle scan1goal1)
-->
   (<des> ^satisfied true)
}


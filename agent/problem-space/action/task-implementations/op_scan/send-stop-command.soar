### send the low-level stop command to the output-link when the until-clause is satisfied ###


sp {op_scan1*propose*send-stop-command
   (state <s> ^name op_scan1
              ^current-task-segment.scan-command
              ^task-operator.until-clause.satisfied true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-stop-command
        ^item-type command-operator
        ^command-name stop-command)
}

sp {op_scan1*reject*complete-task*not*stopped
   (state <s> ^name op_scan1
              ^current-task-segment.scan-command <scan>
              ^operator <o> +)
   (<o> ^name complete-task)
   (<scan> ^status running)
-->
   (<s> ^operator <o> -)
}


### magicbot ###

sp {op_scan1*external*apply*send-stop-command*magicbot
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain magicbot
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-stop-command
        ^command-name <cmd-name>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name stop
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}

sp {op_scan1*external*apply*send-stop-command*cozmo
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-stop-command
        ^command-name <cmd-name>)
-->
   (<out> ^stop <stop>)
   (<stop> ^item-type output-command)
   (<seg> ^<cmd-name> <stop>)
}

### send the low-level scan command to the output-link until the end-clause is satisfied ###

sp {op_scan1*propose*send-scan-command
   (state <s> ^name op_scan1
             -^current-task-segment.stop-command)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-scan-command
        ^item-type command-operator
        ^command-name scan-command)
}


### internal ###

# internal - we don't actually do anything, just mark as complete
sp {op_scan1*external*apply*send-scan-command*internal
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain internal
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-scan-command
        ^command-name <cmd-name>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name scan
         ^item-type output-command
         ^status success)
   (<seg> ^<cmd-name> <pa>)
}


### cozmo ###

sp {op_scan1*external*apply*send-scan-command*cozmo
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-scan-command
        ^command-name <cmd-name>)
-->
   (<out> ^turn-in-place <tip>)
   (<tip> ^item-type output-command
          ^angle 360
          ^speed 20)
   (<seg> ^<cmd-name> <tip>)
}

### magicbot ###

sp {op_scan1*apply*send-scan-command*magicbot
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-scan-command
        ^command-name <cmd-name>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name turn
          ^item-type output-command
          ^parameters <params>
          ^termination-condition.name stabilized)
   (<params> ^direction -1
             ^yaw 6.28)
   (<seg> ^<cmd-name> <law>)
}

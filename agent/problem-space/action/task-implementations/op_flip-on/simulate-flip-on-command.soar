### For the internal environment, simulate the turn-on command on the top-state world ###

sp {op_flip-on1*propose*simulate-turn-on-command
   (state <s> ^name op_flip-on1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^turn-on-command <cmd>)
   (<cmd> -^status success)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-turn-on-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_flip-on1*apply*simulate-turn-on-command*mark*command*success
   (state <s> ^name op_flip-on1
              ^operator <o>)
   (<o> ^name simulate-turn-on-command
        ^command <cmd>)
-->
   (<cmd> ^status success)
}


### simulate-perception = false, change on the object directly ###

sp {op_flip-on1*apply*simulate-turn-on-command*change*to*activated1
   (state <s> ^name op_flip-on1
              ^operator <o>
             -^agent-params.simulate-perception true)
   (<o> ^name simulate-turn-on-command
        ^command.object.predicates <preds>)
   (<preds> ^<prop> not-activated1)
-->
   (<preds> ^<prop> not-activated1 -
            ^<prop> activated1)
}


### simulate-perception = true, change on the perception-monitor ###

sp {op_flip-on1*apply*simulate-turn-on-command*simulate-perception*change*to*activated1
   (state <s> ^name op_flip-on1
              ^operator <o>
              ^agent-params.simulate-perception true
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name simulate-turn-on-command
        ^command.object <obj>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^world-obj <obj>)
   (<il-prop> ^predicate-handle not-activated1)
-->
   (<il-prop> ^predicate-handle not-activated1 -
              ^predicate-handle activated1)
}


#####################################################
# General purpose action models 

sp {action*apply*subtask*mark*performed-subtask
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> -^no-default-application
         ^subtask-handle { <sub-handle> <> none })
-->
   (<seg> ^performed-subtask <sub-handle>)
}

sp {action*apply*subtask*mark*performed*duplicate-of
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^current-task-segment <seg>)
   (<o> -^no-default-application
         ^duplicate-of.subtask-handle { <sub-handle> <> none })
-->
   (<seg> ^performed-subtask <sub-handle>)
}

## If a command-operator is selected, mark the goal as satisfied
#  (We assume executing the command would achieve the goal)
sp {action*execution-type*internal*apply*command-operator*mark*desired*satisfied
   (state <s> ^problem-space <ps>
              ^operator.item-type command-operator
              ^desired <d>)
   (<ps> ^name action
         ^execution-type internal)
-->
   (<d> ^satisfied true)
}

### If there is a predicate-based end-clause, add the predicates to the world as a stand-in action model

sp {action*execution-type*internal*apply*task-operator*end-clause*add*unary*predicate
   (state <s> ^problem-space <ps>
              ^operator <o>)
   (<ps> ^name action
         ^execution-type internal)
   (<o> ^item-type task-operator
        ^end-clause <end>)
   (<end> ^<pnum> <pred>)
   (<pred> ^type unary
           ^handle <pred-h>
           ^property-handle <prop-h>
           ^{1}.predicates <preds1>)
-->
   (<preds1> ^<prop-h> <pred-h>)
}

sp {action*execution-type*internal*apply*task-operator*end-clause*add*relation*predicate
   (state <s> ^problem-space <ps>
              ^operator <o>
              ^world.predicates.predicate <rel>)
   (<ps> ^name action
         ^execution-type internal)
   (<o> ^item-type task-operator
        ^end-clause <end>)
   (<end> ^<pnum> <pred>)
   (<pred> ^type relation
           ^handle <rel-h>
           ^1 <obj1>
           ^2 <obj2>)
   (<rel> ^handle <rel-h>)
-->
   (<rel> ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
}


### For the internal environment, simulate the open command on the top-state world ###

sp {op_open1*propose*simulate-open-command
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^open-command <cmd>)
   (<cmd> -^status success)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-open-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_open1*apply*simulate-open-command*mark*command*success
   (state <s> ^name op_open1
              ^operator <o>)
   (<o> ^name simulate-open-command
        ^command <cmd>)
-->
   (<cmd> ^status success)
}


### simulate-perception = false, change on the object directly ###

sp {op_open1*apply*simulate-open-command*add*open*predicate
   (state <s> ^name op_open1
              ^operator <o>
             -^agent-params.simulate-perception true)
   (<o> ^name simulate-open-command
        ^command.object.predicates <preds>)
   (<preds> ^is-open1 not-open1)
-->
   (<preds> ^is-open1 not-open1 -
            ^is-open1 open2)
}


### simulate-perception = true, change on the perception-monitor ###

sp {op_open1*apply*simulate-open-command*simulate-perception*change*predicate*to*open
   (state <s> ^name op_open1
              ^operator <o>
              ^agent-params.simulate-perception true
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name simulate-open-command
        ^command.object <obj>)
   (<obj-info> ^input-link-obj.source-obj.predicates <src-preds>
               ^world-obj <obj>)
   (<src-preds> ^is-open1 not-open1)
-->
   (<src-preds> ^is-open1 not-open1 -
                ^is-open1 open2)
}



### Send the low-level open command when the door is visible and reachable ###

sp {op_open1*propose*send-open-command
   (state <s> ^name op_open1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-open-command
        ^item-type command-operator
        ^command-name open-command
        ^arg1.id <obj>)
}

### internal ###

sp {op_open1*external*apply*send-open-command*internal
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-open-command
        ^command-name <cmd-name>
        ^arg1.id <obj>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name open
         ^object <obj>)
   (<seg> ^<cmd-name> <pa>)
}

### ai2thor ###

sp {op_open1*external*apply*send-open-command*ai2thor
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor 
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-open-command
        ^command-name <cmd-name>
        ^arg1.id.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name open
         ^object <h>)
   (<seg> ^<cmd-name> <pa>)
}


### tabletop ###

sp {op_open1*external*apply*send-open-command*tabletop
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain tabletop
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-open-command
        ^command-name <cmd-name>
        ^arg1.id.handle <h>)
-->
   (<ol> ^set-state <set>)
   (<set> ^object-handle <h>
          ^name is-open1
          ^value open2)
   (<seg> ^<cmd-name> <set>)
}


### magicbot ###

sp {op_open1*external*apply*send-open-command*magicbot
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-open-command
        ^command-name <cmd-name>
        ^arg1.id.perception-id <perc-id>)
-->
   (<ol> ^do-control-law <cmd>)
   (<cmd> ^name change-state
          ^item-type output-command
          ^parameters <params>
          ^termination-condition <term>)
   (<term> ^name object-state
           ^parameters <params>)
   (<params> ^object-id (int <perc-id>)
             ^property is-open1
             ^value open2)

   (<seg> ^<cmd-name> <cmd>)
}

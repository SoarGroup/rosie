### Internally modeling the remove task ###

sp {problem-space*action*internal*elaborate*op_remove1*dont-learn-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_remove1)
-->
   (<o> ^dont-learn-action-model true)
}

# Remove the object from the world
sp {problem-space*action*internal*apply*op_remove1*remove*object
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.objects <objs>)
   (<o> ^name op_remove1 
        ^arg1.id <obj>)
   (<objs> ^object <obj>)
-->
   (<objs> ^object <obj> -)
}

# Remove any predicates for that object
sp {problem-space*action*internal*apply*op_remove1*remove*predicates
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name op_remove1
        ^arg1.id <obj>)
   (<pred> ^instance <i>)
   (<i> ^<p-num> <obj>)
-->
   (<pred> ^instance <i> -)
}

### For the internal environment, simulate the remove command on the top-state world ###

sp {op_remove1*propose*simulate-remove-command
   (state <s> ^name op_remove1
              ^problem-space.execution-type external
              ^current-task-segment <seg>)
   (<seg> ^remove-command <cmd>)
   (<cmd> -^status success)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-remove-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_remove1*apply*simulate-remove-command*mark*command*success
   (state <s> ^name op_remove1
              ^operator <o>)
   (<o> ^name simulate-remove-command
        ^command <cmd>)
-->
   (<cmd> ^status success)
}

sp {op_remove1*apply*simulate-remove-command*remove-object
   (state <s> ^name op_remove1
              ^operator <o>
              ^top-state.world.objects <objs>)
   (<o> ^name simulate-remove-command
        ^command.object <obj>)
   (<objs> ^object <obj>)
-->
   (<objs> ^object <obj> -)
}

sp {op_remove1*apply*simulate-remove-command*remove-predicates
   (state <s> ^name op_remove1
              ^operator <o>
              ^top-state.world.predicates.predicate <pp>)
   (<o> ^name simulate-remove-command
        ^command.object <obj>)
   (<pp> ^instance <in>)
   (<in> ^<p-num> <obj>)
-->
   (<pp> ^instance <in> -)
}

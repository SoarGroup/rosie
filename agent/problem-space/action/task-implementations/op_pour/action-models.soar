### Internally modeling the pour task ###

sp {problem-space*action*internal*elaborate*op_pour1*dont-learn-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_pour1)
-->
   (<o> ^dont-learn-action-model true)
}

### Action Model: Pouring a dispenser into an empty fillable object will fill it
sp {problem-space*action*internal*apply*op_pour1*dispenser*fillable
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <op>)
   (<op> ^name op_pour1
         ^arg1.id.predicates <p1>
         ^arg2.{2}.predicates <p2>)
   (<p1> ^affordance1 dispenser1
         ^contents1 { <con> <> empty1 })
   (<p2> ^affordance1 fillable1
         ^contents1 empty1)
-->
   (<p2> ^contents1 empty1 -
         ^contents1 <con>)
}

### Action Model: Pouring a filled object into a drain will empty the object
sp {problem-space*action*internal*apply*op_pour1*fillable*drain
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <op>)
   (<op> ^name op_pour1
         ^arg1.id.predicates <p1>
         ^arg2.{2}.predicates <p2>)
   (<p1> ^affordance1 fillable1
         ^contents1 { <liq> <> empty1 })
   (<p2> ^affordance1 drain1)
-->
   (<p1> ^contents1 <liq> -
         ^contents1 empty1)
}

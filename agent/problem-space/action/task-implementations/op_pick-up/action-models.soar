################################################
### pick-up

sp {problem-space*action*internal*elaborate*op_pick-up1*dont-learn-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_pick-up1)
-->
   (<o> ^dont-learn-action-model true)
}

# Add the grabbed predicate
sp {action*apply*op_pick-up1*do*action-model*mark*grabbed
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <op>)
   (<op> ^name op_pick-up1 
         ^arg1.id.predicates <preds>)
   (<preds> ^is-grabbed1 not-grabbed1)
-->
   (<preds> ^is-grabbed1 grabbed1
            ^is-grabbed1 not-grabbed1 -)
}

sp {action*apply*op_pick-up1*do*action-model*remove*predicates
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name op_pick-up1 
        ^arg1.id <obj>)
   (<pred> ^instance <i>)
   (<i> ^<num> <obj>)
-->
   (<pred> ^instance <i> -)
}

# Change the robot arm holding-object flag to true
sp {action*apply*op_pick-up1*do*action-model*change*robot*holding-object*true
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator.name op_pick-up1 
              ^world.robot.arm <arm>)
   (<arm> ^holding-object false)
-->
   (<arm> ^holding-object false -
          ^holding-object true)
}

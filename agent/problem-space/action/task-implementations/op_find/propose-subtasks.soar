
# Known location in the world, propose go-to that location
sp {op_find1*propose*op_go-to1*location
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>
              ^world.robot.current-location <cur-loc>
              ^world.objects.object { <loc> <> <cur-loc> }
              ^world.predicates.predicate <in-pred>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
   (<loc> ^predicates.category location)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_go-to-location1
        ^item-type task-operator
        ^task-handle go-to-location1
         ^arg2 <arg2>)
   (<arg2> ^arg-type partial-predicate
           ^handle to1
           ^2 <loc>)
}

# If the object is inside a closed receptacle, open it
sp {op_find1*propose*op_open1
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>
              ^world.predicates.predicate <in-pred>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <rec>)
   (<rec> ^predicates <rec-preds>)
   (<rec-preds> ^affordance1 receptacle1
                ^affordance1 openable1
                ^is-confirmed1 confirmed1
                ^<prop> closed2)
-->
   (<s> ^operator <open-op> + =)
   (<open-op> ^name op_open1
              ^item-type task-operator
              ^task-handle open1
              ^arg1 <rec-arg>)
   (<rec-arg> ^arg-type object
              ^id <rec>)
}

# Scan the room to see if you can see it
sp {op_find1*propose*op_scan
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^agent-params.domain << magicbot cozmo >>
              ^target-object <obj>
             -^current-task-segment.tried-scan true)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_scan1
        ^item-type task-operator
        ^task-handle scan1
        ^until-clause <until>)
   (<until> ^arg-type until-clause
            ^pred-count 1
            ^1 <pred>)
   (<pred> ^type unary
           ^handle visible1
           ^property-handle is-visible1
           ^1 <obj>)
}

# Ask the instructor if they know where the object is 
sp {op_find1*propose*initiate-interaction*get-find-help
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>
             -^current-task-segment.stop-asking true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name initiate-interaction
        ^item-type command-operator
        ^type get-find-help
        ^object <obj>)
}


# Explore the building to try and find it
sp {op_find1*propose*op_explore
   (state <s> ^name op_find1
              ^problem-space.execution-type external
              ^target-object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_explore1
        ^item-type task-operator
        ^task-handle explore1
        ^until-clause <until>)
   (<until> ^arg-type until-clause
            ^pred-count 1
            ^1 <pred>)
   (<pred> ^type unary
           ^handle visible1
           ^property-handle is-visible1
           ^1 <obj>)
}

sp {op_find1*apply*complete-push-task-segment*op_explore*mark*completed-scan
   (state <s> ^name op_find1
              ^operator.name op_explore1
              ^child-task-segment <child>
              ^current-task-segment <seg>)
   (<seg> ^tried-scan true
         -^marked-explored-scan true)
-->
   (<child> ^completed-scan true)
   (<seg> ^marked-explore-scan true)
}
              

###############################################
## put-down

sp {problem-space*action*internal*elaborate*op_put-down1*dont-learn-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_put-down1)
-->
   (<o> ^dont-learn-action-model true)
}

# Mark the object as not-grabbed
sp {action*apply*op_put-down1*internal*do*action-model*is-grabbed1
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
               ^operator <o>)
   (<o> ^name op_put-down1 
        ^arg1.id.predicates <obj1-preds>)
   (<obj1-preds> ^is-grabbed1 grabbed1)
-->
   (<obj1-preds> ^is-grabbed1 grabbed1 -)
   (<obj1-preds> ^is-grabbed1 not-grabbed1)
}

# Change the robot arm holding-object flag to false
sp {action*apply*op_put-down1*do*action-model*change*robot*holding-object*false
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator.name op_put-down1 
              ^world.robot.arm <arm>)
   (<arm> ^holding-object true)
-->
   (<arm> ^holding-object true -
          ^holding-object false)
}

# Add the expected relation from the arg2
sp {action*apply*op_put-down1*internal*do*action-model*on
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name op_put-down1 
        ^arg1.id <obj1>
        ^arg2 <arg2>)
   (<arg2> ^handle <pred-handle>
           ^2 <obj2>)
   (<pred> ^handle <pred-handle>)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
}

# The object is also in the current location
sp {action*apply*op_put-down1*do*action-model*add*in*relation*current-location
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.robot.current-location { <loc> <> none }
              ^world.predicates.predicate <pred>)
   (<o> ^name op_put-down1 
        ^arg1.id <obj>
       -^arg2)
   (<pred> ^handle in1)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

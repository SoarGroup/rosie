################### propose op_put-down1 ######################

# put down on the ground
sp {problem-space*action*propose*op_put-down1*on*ground
   (state <s> ^problem-space <ps>
              ^world <w>)
   (<w> ^objects.object <obj>)
   (<ps> ^name action
         ^propose-subtasks true)
   (<obj> ^predicates.is-grabbed1 grabbed1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down1
         ^item-type task-operator
         ^task-handle put-down1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

# put-down on surface (e.g. table, countertop)
sp {problem-space*action*propose*op_put-down1*on1*surface1
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>
              ^world.objects.object { <obj2> <> <obj> })
   (<ps> ^name action
         ^propose-subtasks true)
   (<obj> ^predicates.is-grabbed1 grabbed1)
   (<obj2> ^predicates <obj-preds>)
   (<obj-preds> ^is-confirmed1 confirmed1
                ^affordance1 surface1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down1
         ^item-type task-operator
         ^task-handle put-down1
         ^arg1 <arg1>
         ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj>)
   (<arg2> ^arg-type partial-predicate
           ^handle on1
           ^2 <obj2>)
}

# put-down inside receptacle without a door (e.g. sink)
sp {problem-space*action*propose*op_put-down1*in1*receptacle
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>
              ^world.objects.object { <obj2> <> <obj> })
   (<ps> ^name action
         ^propose-subtasks true)
   (<obj> ^predicates.is-grabbed1 grabbed1)
   (<obj2> ^predicates <obj2-preds>)
   (<obj2-preds> ^is-confirmed1 confirmed1
                 ^affordance1 receptacle1
                 ^affordance1 always-open1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down1
         ^item-type task-operator
         ^task-handle put-down1
         ^arg1 <arg1>
         ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj>)
   (<arg2> ^arg-type partial-predicate
           ^handle in1
           ^2 <obj2>)
}

# Put down inside a receptacle that can be closed (but is currently open)
sp {problem-space*action*propose*op_put-down1*in1*open*openable*receptacle
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>
              ^world.objects.object { <obj2> <> <obj> })
   (<ps> ^name action
         ^propose-subtasks true)
   (<obj> ^predicates.is-grabbed1 grabbed1)
   (<obj2> ^predicates <obj2-preds>)
   (<obj2-preds> ^is-confirmed1 confirmed1
                 ^affordance1 openable1
                 ^affordance1 receptacle1
                 ^is-open1 open2)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down1
         ^item-type task-operator
         ^task-handle put-down1
         ^arg1 <arg1>
         ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj>)
   (<arg2> ^arg-type partial-predicate
           ^handle in1
           ^2 <obj2>)
}

### propose put-down on a block
sp {problem-space*action*propose*op_put-down1*on*block
   (state <s> ^problem-space <ps>
              ^world.objects <objs>)
   (<ps> ^name action
         ^propose-subtasks true)
   (<objs> ^object <obj1> { <obj2> <> <obj1> })
   (<obj1> ^predicates.is-grabbed1 grabbed1)
   (<obj2> ^predicates <obj2-preds>)
   (<obj2-preds> ^is-confirmed1 confirmed1
                 ^category block)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_put-down1
        ^item-type task-operator
        ^task-handle put-down1
        ^arg1 <arg1>
        ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj1>)
   (<arg2> ^arg-type partial-predicate
           ^handle on1
           ^2 <obj2>)
}

sp {problem-space*action*propose*op_put-down1*on1*anything
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>
              ^world.objects.object { <obj2> <> <obj> })
   (<ps> ^name action
         ^propose-subtasks true)
   (<obj> ^predicates.is-grabbed1 grabbed1)
 #  (<obj2> -^predicates.affordance1 surface1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down1
         ^item-type task-operator
         ^task-handle put-down1
         ^perform multiple
         ^arg1 <arg1>
         ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj>)
   (<arg2> ^arg-type partial-predicate
           ^handle on1
           ^2 <obj2>)
}

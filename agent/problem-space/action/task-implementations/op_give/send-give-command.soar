### Send the give command to the output link when the person is visible and reachable

sp {op_give1*propose*send-give-command
   (state <s> ^name op_give1
              ^target-object <obj>
              ^person <person>)
   (<obj> ^predicates.is-grabbed1 grabbed1)
   (<person> ^predicates <per-preds>)
   (<per-preds> ^is-visible1 visible1
                ^is-reachable1 reachable1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-give-command
        ^item-type command-operator
        ^command-name give-command
        ^arg1.id <obj>
        ^arg2 <arg2>)
   (<arg2> ^2 <person>)
}

### internal ###

sp {op_give1*external*apply*send-give-command*internal
   (state <s> ^name op_give1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-give-command
        ^command-name <cmd-name>
        ^arg1.id <obj>
        ^arg2.{2} <person>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name give
         ^object <obj>
         ^person <person>)
   (<seg> ^<cmd-name> <pa>)
}


### magicbot ###

sp {op_give1*external*apply*send-give-command*put-on-object
   (state <s> ^name op_give1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-give-command
        ^arg1.id.perception-id <perc-id>
        ^arg2.{2}.perception-id <perc-id2>
        ^command-name <cmd-name>)
-->
   (<ol> ^do-control-law <cmd>)
   (<cmd> ^name put-on-object
          ^item-type output-command
          ^parameters <params>
          ^termination-condition <term>)
   (<params> ^destination-id (int <perc-id2>)
             ^relation to1)
   (<term> ^name holding
           ^parameters <term-params>)
   (<term-params> ^object-id (int <perc-id>)
                  ^negated true)
   (<seg> ^<cmd-name> <cmd>)
}

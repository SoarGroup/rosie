###############################################
## give

# Mark the object as not-grabbed
sp {action*apply*op_give1*internal*do*action-model*is-grabbed1
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
               ^operator <o>)
   (<o> ^name << op_give1 send-give-command >>
        ^arg1.id.predicates <obj1-preds>)
   (<obj1-preds> ^is-grabbed1 grabbed1)
-->
   (<obj1-preds> ^is-grabbed1 grabbed1 -)
   (<obj1-preds> ^is-grabbed1 not-grabbed1)
}

# Add the expected relation from the arg2
sp {action*apply*op_give1*internal*do*action-model*on
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.predicates.predicate <pred>)
   (<o> ^name << op_give1 send-give-command >>
        ^arg1.id <obj>
        ^arg2.{2} <person>)
   (<pred> ^handle holding1)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <person> ^2 <obj>)
}


# Change the robot arm holding-object flag to false
sp {action*apply*op_give1*do*action-model*change*robot*holding-object*false
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator.name << op_give1 send-give-command >>
              ^world.robot.arm <arm>)
   (<arm> ^holding-object true)
-->
   (<arm> ^holding-object true -
          ^holding-object false)
}

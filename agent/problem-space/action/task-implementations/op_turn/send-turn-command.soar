### send the low-level turn command to the output-link ###

sp {op_turn1*propose*send-turn-command
   (state <s> ^name op_turn1
              ^direction-id <dir>
             -^degrees)
   (<dir> ^yaw <rad> ^side <side>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-turn-command
        ^item-type command-operator
        ^command-name turn-command
        ^radians <rad>
        ^direction <side>)
}

sp {op_turn1*propose*send-turn-command*specified*degrees
   (state <s> ^name op_turn1
              ^direction-id.side <side>
              ^degrees <deg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-turn-command
        ^item-type command-operator
        ^command-name turn-command
        ^radians (* (/ <deg> 180.0) 3.14159 <side>)
        ^direction <side>)
}

### internal ###

# internal - we don't actually do anything, just mark as complete
sp {op_turn1*external*apply*send-turn-command*internal
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain internal
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-command
        ^command-name <cmd-name>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name turn
         ^item-type output-command
         ^status success)
   (<seg> ^<cmd-name> <pa>)
}


### magicbot ###

sp {op_turn1*external*apply*send-turn-command*magicbot
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain magicbot
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-command
        ^command-name <cmd-name>
        ^radians <rad>
        ^direction <dir>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^item-type output-command
          ^name turn
          ^parameters <params>
          ^termination-condition.name stabilized)
   (<params> ^direction <dir>
             ^yaw <rad>)
   (<seg> ^<cmd-name> <law>)
}


### ai2thor ###

sp {op_turn1*external*apply*send-turn-command*ai2thor
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain ai2thor
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-command
        ^command-name <cmd-name>
        ^direction.handle <dir>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name turn
         ^direction <dir>)
   (<seg> ^<cmd-name> <pa>)
}


### cozmo ###

sp {op_turn1*external*apply*send-turn-command*cozmo
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-command
        ^command-name <cmd-name>
        ^radians <rad>)
-->
   (<out> ^turn-in-place <tip>)
   (<tip> ^item-type output-command
          ^angle (* <rad> 57.3)
          ^speed 45)
   (<seg> ^<cmd-name> <tip>)
}


sp {op_recall1*elaborate*memory-type*smem
   (state <s> ^name op_recall1
              ^task-operator <task-op>)
   (<task-op> ^arg1.id <obj1>
              ^arg2 <arg2>
             -^when-clause)
   (<arg2> ^id <obj2>)
-->
   (<s> ^memory-type smem
        ^head-object <obj2>
        ^head-object-arg <arg2>
        ^edge-object <obj1>)
}

sp {op_recall1*elaborate*memory-type*epmem
   (state <s> ^name op_recall1
              ^task-operator <task-op>
              ^desired.handle recall1goal1)
   (<task-op> ^arg1.id <obj1>
              ^when-clause <when>)
   (<when> ^1 <p1>)
-->
   (interrupt)
   (<s> ^memory-type epmem
        ^object-to-retrieve <obj1>
        ^query-info <p1>)
}

### Setup smem queries
sp {op_recall1*elaborate*query-object-workspace
   (state <s> ^name op_recall1
              ^memory-type smem
              ^problem-space.execution-type external
              ^desired.handle recall1goal1
              ^head-object <obj>
              ^head-object-arg <arg>)
-->
   (<s> ^query-object-workspace <ws>)
   (<ws> ^type head-object
         ^source <obj>
         ^source-arg <arg>)
}

sp {op_recall1*elaborate*query-object-workspace*smem-object
   (state <s> ^name op_recall1
              ^query-object-workspace <ws>)
   (<ws> ^{ << match-handle-result match-predicates-result retrieved-category >> } { <obj> <> failure })
-->
   (<ws> ^smem-object <obj>)
}

sp {op_recall1*smem*elaborate*matching-smem-edge
   (state <s> ^name op_recall1
              ^memory-type smem
              ^query-object-workspace <ws>
              ^edge-object.predicates <preds>)
   (<ws> ^type head-object
         ^smem-object.semantic-edge <se>)
   # There are no predicates on the edge-object that are not on the semantic-edge
  -{(<preds> ^<prop1> <pred1>)
    (<se> -^<prop1> <pred1>)}
   # There are no predicates on the semantic-edge that are not on the edge-object
  -{(<se> ^{ <prop2> <> tail } <pred2>)
    (<preds> -^<prop2> <pred2>)}
   (<se> ^tail <tail>)
-->
   (<s> ^matching-smem-edge <se>)
}

sp {op_recall1*smem*elaborate*smem-tail-object*from*matching-smem-edge
   (state <s> ^name op_recall1
              ^memory-type smem
              ^matching-smem-edge <se>)
   (<se> ^tail <tail>)
   (<tail> ^<anything>)
-->
   (<s> ^smem-tail-object <tail>)
}

### WRITE RESULT

sp {op_recall*write*smem-tail-object
   (state <s> ^name op_recall1
              ^memory-type smem
              ^agent-params.print-test-output true
              ^smem-tail-object.handle <h>)
-->
   (write (crlf) |@TEST: ### Task op_recall1 retrieved | <h> (crlf))
}




sp {op_recall1*propose*mark-object-recalled
   (state <s> ^name op_recall1
              ^added-object <obj>)
   (<obj> -^predicates.modifier1 recalled1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name mark-object-recalled
        ^object <obj>)
}

sp {op_recall1*apply*mark-object-recalled
   (state <s> ^name op_recall1
              ^operator <o>)
   (<o> ^name mark-object-recalled
        ^object <obj>)
   (<obj> ^predicates <preds>)
-->
   (<preds> ^modifier1 recalled1)
}

sp {op_recall1*smem*apply*mark-object-recalled*copy*edge*predicates
   (state <s> ^name op_recall1
              ^operator <o>
              ^edge-object.predicates.{ <prop> <> category } <pred>)
   (<o> ^name mark-object-recalled
        ^object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> -^category nothing1)
-->
   (<preds> ^<prop> <pred>)
}

# Finish recall

sp {op_recall1*propose*finish-recall
   (state <s> ^name op_recall1
              ^added-object <obj>
              ^current-task-segment <seg>)
   (<obj> ^predicates.modifier1 recalled1)
   (<seg> -^recall-command.status success)
-->
   (<s> ^operator <o> + =)
   (<o> ^name finish-recall)
}

sp {op_recall1*apply*finish-recall*remove*recalled*predicate*from*others
   (state <s> ^name op_recall1
              ^operator.name finish-recall
              ^added-object <obj>
              ^world.objects.object { <obj2> <> <obj> })
   (<obj2> ^predicates <preds>)
   (<preds> ^modifier1 recalled1)
-->
   (<preds> ^modifier1 recalled1 -)
}

sp {op_recall*apply*finish-recall*add*object*to*parent*task-objects*added-object
   (state <s> ^name op_recall1
              ^operator.name finish-recall
              ^added-object <obj>
              ^parent-task-segment.task-objects <objs>)
-->
   (<objs> ^added-object <obj>)
}

# Mark the recall-command as successful (will complete the goal)
sp {op_recall*apply*finish-recall*mark*command*complete
   (state <s> ^name op_recall1
              ^operator.name finish-recall
              ^current-task-segment <seg>)
-->
   (<seg> ^recall-command.status success)
}

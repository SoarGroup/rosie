
### Internally modeling the recall task ###

#sp {problem-space*action*internal*elaborate*op_recall1*dont-learn-action-model
#   (state <s> ^problem-space.execution-type internal
#              ^operator <o> +)
#   (<o> ^name op_recall1)
#-->
#   (<o> ^dont-learn-action-model true)
#}


#### learn-action-model
# For recall, instead of having a pre-built model, we learn one
# where we take the answer that was given when teaching, 
# and create an action model that creates that recalled object on the world
# This is learned once per subtask, so different recalls will result in different models

sp {op_recall1*learn-action-model*reject*performance*operators
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^operator <o> +)
   (<o> ^name << init-current-goal smem-query retrieve-epmem-episode >>)
-->
   (<s> ^operator <o> -)
}

#### retrieve-previous-result
# Retrieve from epmem an episode when the agent recalled something from smem

sp {op_recall1*learn-action-model*propose*retrieve-previous-result
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^memory-type smem
             -^retrieved-episode)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-previous-result)
}

sp {op_recall1*learn-action-model*apply*retrieve-previous-result
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^operator.name retrieve-previous-result
              ^epmem.command <cmd>
              ^superstate.task-operator.task-handle <par-h>
              ^superstate.operator.subtask-handle <sub-h>)
-->
   (<cmd> ^query <q>)
   (<q> ^task-stack.bottom <bot>
        ^world.objects.object <obj>)
   (<bot> ^task-operator <recall-op>
          ^prev.task-operator.task-handle <par-h>)
   (<recall-op> ^task-handle recall1
                ^subtask-handle <sub-h>)
   (<obj> ^predicates.modifier1 recalled1)
}

# Success :) Copy the episode to the state
sp {op_recall1*learn-action-model*apply*retrieve-previous-result*success
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^operator.name retrieve-previous-result
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^result.retrieved <episode>
            ^command <cmd>)
   (<res> ^success <q>
          ^retrieved <episode>
          ^memory-id <ep-id>
          ^normalized-match-score 1.0)
   (<cmd> ^query <q>)
-->
   (<s> ^retrieved-episode <episode>)
   (<cmd> ^query <q> -)
}

# Failure :(  There is no matching episode 
sp {op_recall1*learn-action-model*apply*retrieve-previous-result*failure
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^operator.name retrieve-previous-result
              ^epmem <epmem>)
   (<epmem> ^result.normalized-match-score < 1.0
            ^command <cmd>)
   (<cmd> ^query <q>)
-->
   (<s> ^retrieved-episode none)
   (<cmd> ^query <q> -)
}

# The recalled object that was retrieved
sp {op_recall1*learn-action-model*elaborate*retrieved-object*from*retrieved-episode
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^retrieved-episode.world.objects.object <obj>)
   (<obj> ^predicates.modifier1 recalled1)
-->
   (<s> ^retrieved-object <obj>)
}

# Copy the retrieved object to the superstate as the action model
sp {op_recall1*learn-action-model*propose*learn-action-model-rule
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^retrieved-object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name learn-action-model-rule
        ^object <obj>)
}

sp {__TASK_MODEL__problem-space*action*internal*apply*op_recall1*add*recalled*object
   (state <s> ^name op_recall1
              ^name learn-task-action-model
              ^operator <o>
              ^superstate.world.objects <objs>)
   (<o> ^name learn-action-model-rule
        ^object <obj>)
-->
   (<objs> ^object <obj>)
}


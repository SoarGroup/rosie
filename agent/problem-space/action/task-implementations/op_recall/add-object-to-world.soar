### Once the retrieval is finished, there are three possiblities: 

# 1. The retrieved object from smem is an actual object-instance, add it to the world
sp {op_recall1*propose*add-object-to-world*smem*object-instance
   (state <s> ^name op_recall1
              ^memory-type smem
              ^smem-tail-object <tail>
             -^added-object)
   (<tail> ^object-instance true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj <tail>)
}

# 1. The retrieved object from epmem is an actual object, add it to the world
sp {op_recall1*propose*add-object-to-world*epmem*matching-epmem-object
   (state <s> ^name op_recall1
              ^memory-type epmem
              ^matching-epmem-object <obj>
             -^added-object)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name add-object-to-world
        ^source-obj <obj>)
}

# 2. The tail object is a category, add a new object with that category as the root
sp {op_recall1*propose*add-object-to-world*smem*category
   (state <s> ^name op_recall1
              ^memory-type smem
              ^smem-tail-object <tail>
             -^added-object)
   (<tail> ^handle <cat>
           ^property.handle category)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj <src>)
   (<src> ^root-category <cat>
          ^predicates.category <cat>)
}

# 3. The retrieval failed, add a nothing object
sp {op_recall1*propose*add-object-to-world*smem*failure*add*nothing*object
   (state <s> ^name op_recall1
              ^memory-type smem
              ^retrieval-failed true
             -^added-object)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj.predicates <preds>)
   (<preds> ^category nothing1)
}

sp {op_recall1*propose*add-object-to-world*epmem*failure*add*nothing*object
   (state <s> ^name op_recall1
              ^memory-type epmem
              ^retrieved-episode none
             -^added-object)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj.predicates <preds>)
   (<preds> ^category nothing1)
}

sp {op_recall1*propose*add-object-to-world*epmem*failure*no*match*add*nothing*object
   (state <s> ^name op_recall1
              ^memory-type epmem
              ^retrieved-episode <> none
             -^matching-epmem-object
             -^added-object)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj.predicates <preds>)
   (<preds> ^category nothing1)
}

sp {op_recall1*apply*add-object-to-world*nothing*write*message
   (state <s> ^name op_recall1
              ^agent-params.print-test-output true
              ^operator <o>)
   (<o> ^name add-object-to-world
        ^source-obj.predicates.category nothing1)
-->
   (write (crlf) |@TEST: ### Task op_recall1 retrieved nothing!| (crlf))
}

# Copy the result onto the state as ^added-object
sp {op_recall1*apply*add-object-to-world*copy*added-object
   (state <s> ^name op_recall1
              ^operator <o>
              ^add-object-result <res>)
   (<o> ^name add-object-to-world
        ^source-obj <src>)
   (<res> ^source-obj <src>
          ^world-obj <obj>)
-->
   (<s> ^added-object <obj>)
}


### Once the retrieval is finished, there are three possiblities: 

# 1. The tail object is an actual smem object-instance, add it to the world
sp {op_recall1*propose*add-object-to-world*smem*object-instance
   (state <s> ^name op_recall1
              ^memory-type smem
              ^smem-tail-object <tail>
             -^tail-object)
   (<tail> ^object-instance true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj <tail>)
}

# 2. The tail object is a category, add a new object with that category as the root
sp {op_recall1*propose*add-object-to-world*smem*category
   (state <s> ^name op_recall1
              ^memory-type smem
              ^smem-tail-object <tail>
             -^tail-object)
   (<tail> ^handle <cat>
           ^property.handle category)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj.predicates.category <cat>)
}

# 3. The retrieval failed, add a nothing object
sp {op_recall1*propose*add-object-to-world*smem*failure*nothing
   (state <s> ^name op_recall1
              ^memory-type smem
              ^retrieval-failed true
             -^tail-object)
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-object-to-world
        ^source-obj.predicates <preds>)
   (<preds> ^category nothing1)
}

sp {op_recall1*apply*add-object-to-world*nothing*write*message
   (state <s> ^name op_recall1
              ^memory-type smem
              ^agent-params.print-test-output true
              ^operator <o>)
   (<o> ^name add-object-to-world
        ^source-obj.predicates.category nothing1)
-->
   (write (crlf) |@TEST: ### Task op_recall1 retrieved nothing!| (crlf))
}

# Copy the result onto the state as ^tail-object
sp {op_recall1*apply*add-object-to-world*copy*tail-object
   (state <s> ^name op_recall1
              ^memory-type smem
              ^operator <o>
              ^add-object-result <res>)
   (<o> ^name add-object-to-world
        ^source-obj <src>)
   (<res> ^source-obj <src>
          ^world-obj <obj>)
-->
   (<s> ^tail-object <obj>)
}


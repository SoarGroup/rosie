
# First, try to see if we have a representation for the object in semantic memory 
# using the handle
sp {op_recall1*elaborate*smem-query*match*object*handle
   (state <s> ^name op_recall1
              ^query-object-workspace <ws>)
   (<ws> ^source.handle <obj-h>
        -^match-handle-result)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 4
        ^destination <ws>
        ^result-name match-handle-result)
   (<cue> ^item-type object
          ^object-instance true
          ^handle <obj-h>)
}

# If the handle doesn't match, try to retrieve based on predicates
sp {op_recall1*elaborate*smem-query*match*object*predicates
   (state <s> ^name op_recall1
              ^query-object-workspace <ws>)
   (<ws> ^source.root-category <cat>
         ^match-handle-result failure
        -^match-predicates-result)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 4
        ^destination <ws>
        ^result-name match-predicates-result)
   (<cue> ^item-type object
          ^object-instance true
          ^root-category <cat>)
   (<ws> ^predicates-cue <cue>)
}

sp {op_recall1*elaborate*smem-query*match*object*predicates*copy*predicates
   (state <s> ^name op_recall1
              ^query-object-workspace <ws>)
   (<ws> ^source.predicates <preds>
         ^predicates-cue <cue>)
   (<preds> ^{ <prop> << name >> } <pred>)
-->
   (<cue> ^<prop> <pred>)
}

# If the handle/predicates doesn't match, try to retrieve the category lti
sp {op_recall1*elaborate*smem-query*category*lti
   (state <s> ^name op_recall1
              ^query-object-workspace <ws>)
   (<ws> ^source.root-category <cat>
         ^match-handle-result failure
         ^match-predicates-result failure
        -^retrieved-category)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 4
        ^destination <ws>
        ^result-name retrieved-category)
   (<cue> ^handle <cat>)
}

# If for some reason we didn't retrieve the tail, do so
sp {op_recall1*elaborate*smem-query*tail-object
   (state <s> ^name op_recall1
              ^matching-smem-edge <se>
             -^smem-tail-object)
   (<se> ^tail <tail>)
   (<tail> -^<anything>)
-->
   (<s> ^smem-retrieve <ret>)
   (<ret> ^identifier <tail>
          ^depth 2)
}
sp {op_recall1*apply*smem-retrieve-lti*copy*result
   (state <s> ^name op_recall1
              ^operator.name smem-retrieve-lti
              ^matching-smem-edge.tail <tail>
              ^smem.result <res>)
   (<res> ^success <tail> ^retrieved <smem-tail>)
-->
   (<s> ^smem-tail-object <smem-tail>)
}

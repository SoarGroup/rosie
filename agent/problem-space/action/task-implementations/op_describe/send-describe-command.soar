### Describe the object via the interaction stack ###

sp {op_describe1*propose*send-describe-command*no*person
   (state <s> ^name op_describe1
              ^target-obj <obj>
             -^person)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-describe-command
        ^item-type command-operator
        ^command-name describe-command
        ^arg1.id <obj>)
}

sp {op_describe1*propose*send-describe-command*with*person
   (state <s> ^name op_describe1
              ^target-obj <obj>
              ^person <person>)
   (<person> ^predicates.is-visible1 visible1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-describe-command
        ^item-type command-operator
        ^command-name describe-command
        ^arg1.id <obj>
        ^arg2 <arg2>)
   (<arg2> ^handle to1
           ^2 <person>)
}

### For all domains we simulate this command
sp {op_describe1*external*apply*send-describe-command
   (state <s> ^name op_describe1
              ^problem-space.execution-type external
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-describe-command
        ^command-name <cmd-name>
        ^arg1.id <obj>
       -^arg2)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name describe
         ^object <obj>)
   (<seg> ^<cmd-name> <pa>)
}

sp {op_describe1*external*apply*send-describe-command*with*arg2
   (state <s> ^name op_describe1
              ^problem-space.execution-type external
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-describe-command
        ^command-name <cmd-name>
        ^arg1.id <obj>
        ^arg2.{2} <person>)

-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name describe
         ^object <obj>
         ^person <person>)
   (<seg> ^<cmd-name> <pa>)
}

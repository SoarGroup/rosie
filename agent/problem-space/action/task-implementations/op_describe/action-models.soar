### Internally modeling the describe task ###

sp {problem-space*action*internal*elaborate*op_describe1*dont-learn-action-model
   (state <s> ^problem-space.execution-type internal
              ^operator <o> +)
   (<o> ^name op_describe1)
-->
   (<o> ^dont-learn-action-model true)
}

sp {problem-space*action*internal*apply*op_describe1*mark*heard
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.predicates.predicate <heard>)
   (<o> ^name op_describe1 
        ^arg1.id <obj>
        ^arg2.{2} <person>)
   (<heard> ^handle heard2)
   -{(<heard> ^instance <heard-i>)
     (<heard-i> ^1 <person> ^2 <obj>)}
-->
   (<heard> ^instance <i>)
   (<i> ^1 <person> ^2 <obj>)
}

sp {problem-space*action*internal*apply*op_describe1*no*arg2*anyone*heard
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world <w>)
   (<o> ^name op_describe1 
        ^arg1.id <obj>
       -^arg2)
   (<w> ^objects.object <person>
        ^predicates.predicate <heard>)
   (<person> ^predicates.is-visible1 visible1)
   (<heard> ^handle heard2)
   -{(<heard> ^instance <heard-i>)
     (<heard-i> ^1 <person> ^2 <obj>)}
-->
   (<heard> ^instance <i>)
   (<i> ^1 <person> ^2 <obj>)
}


sp {op_face1*propose*send-face-command*object
   (state <s> ^name op_face1
              ^target-object.predicates.is-confirmed1 confirmed1
              ^yaw-to-target <yaw>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-face-command
        ^item-type command-operator
        ^command-name face-command
        ^yaw <yaw>)
}

sp {op_face1*propose*send-face-command*coordinate
   (state <s> ^name op_face1
              ^task-operator.arg1.arg-type coordinate
              ^yaw-to-target <yaw>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-face-command
        ^item-type command-operator
        ^command-name face-command
        ^yaw <yaw>)
}


### cozmo ###

sp {op_face1*external*apply*send-face-command*cozmo*coord
   (state <s> ^name op_face1
              ^problem-space.execution-type external
              ^agent-params.domain cozmo
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-face-command
        ^command-name <cmd-name>
        ^yaw <yaw>)
-->
   (<out> ^turn-in-place <tip>)
   (<tip> ^item-type output-command
          ^absolute true
          ^angle <yaw>
          ^speed 1.57)
   (<seg> ^<cmd-name> <tip>)
}

# magicbot

sp {op_face1*apply*send-face-command
   (state <s> ^name op_face1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-face-command
        ^command-name <cmd-name>
        ^yaw <yaw>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name orient
          ^item-type output-command
          ^parameters.yaw <yaw>
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}

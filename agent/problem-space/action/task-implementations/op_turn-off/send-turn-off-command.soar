### Send the low-level turn-off command when the target is visible and reachable ###

sp {op_turn-off1*propose*send-turn-off-command
   (state <s> ^name op_turn-off1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^is-activated1 activated1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-turn-off-command
        ^item-type command-operator
        ^command-name turn-off-command
        ^arg1.id <obj>)
}

### internal ###

sp {op_turn-off1*external*apply*send-turn-off-command*internal
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-off-command
        ^command-name <cmd-name>
        ^arg1.id <obj>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name turn-off
         ^object <obj>)
   (<seg> ^<cmd-name> <pa>)
}

### ai2thor ###

sp {op_turn-off1*external*apply*send-turn-off-command*ai2thor
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor 
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-off-command
        ^command-name <cmd-name>
        ^arg1.id.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name turn-off
         ^object <h>)
   (<seg> ^<cmd-name> <pa>)
}


### tabletop ###

sp {op_turn-off1*external*apply*send-turn-off-command*tabletop
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain tabletop
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-off-command
        ^command-name <cmd-name>
        ^arg1.id.handle <h>)
-->
   (<ol> ^set-state <set>)
   (<set> ^object-handle <h>
          ^name is-activated1
          ^value not-activated1)
   (<seg> ^<cmd-name> <set>)
}

### magicbot ###

sp {op_turn-off1*external*apply*send-turn-off-command*magicbot
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-turn-off-command
        ^command-name <cmd-name>
        ^arg1.id.perception-id <perc-id>)
-->
   (<ol> ^do-control-law <cmd>)
   (<cmd> ^name change-state
          ^item-type output-command
          ^parameters <params>
          ^termination-condition <term>)
   (<term> ^name object-state
           ^parameters <params>)
   (<params> ^object-id (int <perc-id>)
             ^property is-activated1
             ^value not-activated1)

   (<seg> ^<cmd-name> <cmd>)
}

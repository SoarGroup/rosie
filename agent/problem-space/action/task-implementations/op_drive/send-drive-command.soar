
sp {op_drive1*propose*send-drive-command
   (state <s> ^name op_drive1
             -^current-task-segment.stop-command
             -^desired-waypoint)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-drive-command
        ^item-type command-operator
        ^command-name drive-command)
}

sp {op_drive1*elaborate*send-drive-command*distance
   (state <s> ^name op_drive1
              ^operator <o> +
              ^distance <dist>)
   (<o> ^name send-drive-command)
-->
   (<o> ^distance <dist>)
}

### internal ###

# internal - we don't actually do anything, just mark as complete
sp {op_drive1*external*apply*send-drive-command*internal
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain internal
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name drive
         ^item-type output-command
         ^status success)
   (<seg> ^<cmd-name> <pa>)
}


### magicbot ###

sp {op_drive1*external*apply*send-drive-command*magicbot
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
             -^distance
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>
       -^distance)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name drive-forward
          ^item-type output-command
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}

sp {op_drive1*external*apply*send-drive-command*magicbot*distance-traveled
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>
        ^distance <dist>)
   (<dist> ^number <num>
           ^unit <unit>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^name drive-forward
          ^item-type output-command
          ^termination-condition <cond>)
   (<cond> ^name distance-traveled
           ^parameters.<unit> (float <num>))
   (<seg> ^<cmd-name> <law>)
}

### ai2thor ###

sp {op_drive1*apply*send-drive-command*ai2thor
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name drive-forward
          ^item-type output-command)
   (<seg> ^<cmd-name> <pa>)
}


### cozmo ###

sp {op_drive1*external*apply*send-drive-command*cozmo
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>)
-->
   (<out> ^drive-forward <df>)
   (<df> ^item-type output-command
         ^distance 300
         ^speed 100)
   (<seg> ^<cmd-name> <df>)
}

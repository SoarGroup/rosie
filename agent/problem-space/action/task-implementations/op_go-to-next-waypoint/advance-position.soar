sp {op_go-to-next-waypoint1*propose*advance-position*go-to-xy-command*success
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^current-task-segment.go-to-xy-command.status success
              ^current-task-segment.position-set.next { <next> <> none })
-->
   (<s> ^operator <o> + =)
   (<o> ^name advance-position)
}

sp {op_go-to-next-waypoint1*apply*advance-position*change*next*position
   (state <s> ^name op_go-to-next-waypoint1
              ^operator.name advance-position
              ^current-task-segment.position-set <set>)
   (<set> ^next <cur>)
   (<cur> ^next <next>)
-->
   (<set> ^next <cur> -
          ^next <next>)
}

sp {op_go-to-next-waypoint1*apply*advance-position*remove*go-to-xy-command
   (state <s> ^name op_go-to-next-waypoint1
              ^operator.name advance-position
              ^io.output-link <out>
              ^current-task-segment <seg>)
   (<seg> ^go-to-xy-command <cmd>)
-->
   (<seg> ^go-to-xy-command <cmd> -)
}

sp {op_go-to-next-waypoint1*apply*advance-position*remove*face-command
   (state <s> ^name op_go-to-next-waypoint1
              ^operator.name advance-position
              ^io.output-link <out>
              ^current-task-segment <seg>)
   (<seg> ^face-command <cmd>)
-->
   (<seg> ^face-command <cmd> -)
}
 

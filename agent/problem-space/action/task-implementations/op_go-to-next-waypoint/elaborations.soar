sp {op_go-to-next-waypoint1*elaborate*waypoint
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^task-operator.arg1 <arg1>)
   (<arg1> ^arg-type waypoint
           ^id <wp>)
-->
   (<s> ^waypoint <wp>)
}

sp {op_go-to-next-waypoint*elaborate*edge
   (state <s> ^name op_go-to-next-waypoint1
              ^waypoint <wp>
              ^world.robot.current-waypoint.edge <edge>)
   (<edge> ^end <wp>)
-->
   (<s> ^edge <edge>)
}

sp {op_go-to-next-waypoint*elaborate*door-object
   (state <s> ^name op_go-to-next-waypoint1
              ^edge.door_handle <dh>
              ^world.objects.object <door>)
   (<door> ^handle <dh>)
-->
   (<s> ^door-object <door>)
}

sp {op_go-to-next-waypoint1*elaborate*terminal*superoperator
   (state <s> ^name op_go-to-next-waypoint1
              ^superstate.operator.terminal <flag>)
-->
   (<s> ^terminal <flag>)
}

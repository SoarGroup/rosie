###############################################################################
#
# Inside the op_go-to-next-waypoint1 substate, this will put a drive-to-waypoint command on the output link
#
###############################################################################

sp {op_go-to-next-waypoint1*propose*send-drive-xy-command*face
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^current-task-segment.position-set.next { <next> <> none }
              ^do-face true
              ^current-task-segment.face-command.status succeeded
             -^current-task-segment.drive-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-drive-xy-command
        ^item-type command-operator
        ^command-name drive-command
        ^position <next>)
}

sp {op_go-to-next-waypoint1*propose*send-drive-xy-command*no-face
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^current-task-segment.position-set.next { <next> <> none }
              ^do-face false
             -^current-task-segment.drive-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-drive-xy-command
        ^item-type command-operator
        ^command-name drive-command
        ^position <next>)
}

sp {op_go-to-next-waypoint1*apply*send-drive-xy-command
   (state <s> ^name op_go-to-next-waypoint1
              ^operator <o>
              ^top-state.io.output-link <out>
              ^io.input-link.self.pose.yaw <yaw>
              ^current-task-segment <seg>)
   (<o> ^name send-drive-xy-command
        ^command-name <cmd-name>
        ^position <pos>)
   (<pos> ^x <x> ^y <y>)
-->
   (<out> ^go-to-pose <gtp>)
   (<gtp> ^item-type output-command
          ^x (* <x> 100.0)
          ^y (* <y> 100.0)
          ^orientation (* <yaw> 57.3) )
   (<seg> ^<cmd-name> <gtp>)
}


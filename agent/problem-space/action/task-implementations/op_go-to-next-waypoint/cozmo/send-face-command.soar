sp {op_go-to-next-waypoint1*propose*send-face-command
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^do-face true
              ^current-task-segment.position-set.next { <next> <> none }
             -^current-task-segment.face-command
             -^current-task-segment.drive-command)
   (<next> ^x <x> ^y <y>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-face-command
        ^item-type command-operator
        ^command-name face-command
        ^position <next>)
}

sp {op_go-to-next-waypoint1*apply*send-face-command
   (state <s> ^name op_go-to-next-waypoint1
              ^operator <o>
              ^io.input-link.self.pose <pose>
              ^top-state.io.output-link <out>
              ^current-task-segment <seg>)
   (<o> ^name send-face-command
        ^command-name <cmd-name>
        ^position <pos>)
   (<pose> ^x <rx> ^y <ry>)
   (<pos> ^x <px> ^y <py>)
-->
   (<out> ^turn-in-place <tip>)
   (<tip> ^item-type output-command
          ^absolute true
          ^angle (* (atan2 (- <py> <ry>) (- <px> <rx>) ) 57.3)
          ^speed 45)
   (<seg> ^<cmd-name> <tip>)
}


sp {op_wait*elaborate*propose-wait
   (state <s> ^name op_wait1)
-->
   (<s> ^propose-wait true)
}

sp {op_wait*elaborate*after-clause
   (state <s> ^name op_wait1
              ^task-operator.after-clause <after>)
-->
   (<s> ^after-clause <after>)
}

### desired satisfied (Will complete wait task)

sp {op_wait*elaborate*desired
   (state <s> ^name op_wait1
              ^task-operator.current-goal wait1goal1
             -^copy-task-goal)
-->
   (<s> ^desired <des>)
   (<des> ^handle wait1goal1 ^pred-count 0)
}

sp {op_wait*elaborate*desired*satisfied*state*relation
   (state <s> ^name op_wait1
              ^desired <des>
              ^after-clause <after>
              ^satisfied <after>)
   (<des> ^handle wait1goal1)
-->
   (<des> ^satisfied true)
}


# satisfy duration clause
sp {op_wait*elaborate*duration*after-clause
   (state <s> ^name op_wait1
              ^after-clause.predicate <pred>)
   (<pred> ^type duration)
-->
   (<s> ^duration <dur>)
   (<dur> ^info <pred>)
}

sp {op_wait*elaborate*duration*seconds
   (state <s> ^name op_wait1
              ^duration <dur>)
   (<dur> ^info <info>)
   (<info> ^unit seconds
           ^number <num>)
-->
   (<dur> ^seconds <num>)
}

sp {op_wait*elaborate*duration*minutes
   (state <s> ^name op_wait1
              ^duration <dur>)
   (<dur> ^info <info>)
   (<info> ^unit minutes
           ^number <num>)
-->
   (<dur> ^seconds (* <num> 60))
}

sp {op_wait*elaborate*duration*hours
   (state <s> ^name op_wait1
              ^duration <dur>)
   (<dur> ^info <info>)
   (<info> ^unit hours
           ^number <num>)
-->
   (<dur> ^seconds (* <num> 3600))
}

sp {op_wait*elaborate*duration*end-time
   (state <s> ^name op_wait1
              ^duration <dur>
              ^current-task-segment.start-time <t>)
   (<dur> ^seconds <secs>)
-->
   (<dur> ^end-time (+ <secs> <t>))
}

sp {op_wait*elaborate*duration*satisfied
   (state <s> ^name op_wait1
              ^duration <dur>
              ^io.input-link.time.clock.elapsed-seconds > <t>)
   (<dur> ^end-time <t>
          ^info <info>)
-->
   (<info> ^satisfied true)
}


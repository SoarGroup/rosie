# This creates a set of points (position-set) to navigate when moving to the next waypoint
sp {op_go-to-next-waypoint1*propose*init-op_go-to-next-waypoint1
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
             -^current-action.position-set)
-->
   (<s> ^operator <o> + >)
   (<o> ^name init-op_go-to-next-waypoint1)
}

sp {op_go-to-next-waypoint*elaborate*init-op_go-to-next-waypoint1*edge
   (state <s> ^name op_go-to-next-waypoint1
              ^operator <o>
              ^waypoint <wp>
              ^world.robot.current-waypoint.edge <edge>)
   (<o> ^name init-op_go-to-next-waypoint1)
   (<edge> ^end <wp>)
-->
   (<o> ^edge <edge>)
}

sp {op_go-to-next-waypoint1*apply*init-op_go-to-next-waypoint1*create*position-set*no*door
   (state <s> ^name op_go-to-next-waypoint1
              ^operator <o>
              ^current-action <a>)
   (<o> ^name init-op_go-to-next-waypoint1
        ^edge <edge>)
   (<edge> ^doorway false
           ^end <end>)
   (<end> ^x <ex> ^y <ey>)
-->
   (<a> ^position-set <pos>)
   (<pos> ^next <p1>)
   (<p1> ^x <ex> ^y <ey> ^next none)
}

sp {op_go-to-next-waypoint1*apply*init-op_go-to-next-waypoint1*create*position-set*ignore*door
   (state <s> ^name op_go-to-next-waypoint1
              ^operator <o>
              ^ignore-doors true
              ^current-action <a>)
   (<o> ^name init-op_go-to-next-waypoint1
        ^edge <edge>)
   (<edge> ^doorway true
           ^end <end>)
   (<end> ^x <ex> ^y <ey>)
-->
   (<a> ^position-set <pos>)
   (<pos> ^next <p1>)
   (<p1> ^x <ex> ^y <ey> ^next none)
}

sp {op_go-to-next-waypoint1*apply*init-op_go-to-next-waypoint1*create*position-set*door
   (state <s> ^name op_go-to-next-waypoint1
              ^operator <o>
              ^ignore-doors false
              ^current-action <a>)
   (<o> ^name init-op_go-to-next-waypoint1
        ^edge <edge>)
   (<edge> ^doorway true
           ^door_sx <dsx>
           ^door_sy <dsy>
           ^door_ex <dex>
           ^door_ey <dey>
           ^end <end>)
   (<end> ^x <ex> ^y <ey>)
-->
   (<a> ^position-set <pos>)
   (<pos> ^next <p1>)
   (<p1> ^x <dsx> ^y <dsy> ^next <p2>)
   (<p2> ^x <dex> ^y <dey> ^next <p3>)
   (<p3> ^x <ex> ^y <ey> ^next none)
}

sp {op_give1*elaborate*held-object*superoperator
   (state <s> ^name op_give1
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^held-object <obj>)
}

sp {op_give1*elaborate*object-info
   (state <s> ^name op_give1
              ^held-object <held>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^wm-obj <held>)
-->
   (<s> ^object-info <obj-info>)
}

sp {op_give1*elaborate*person*arg2
   (state <s> ^name op_give1
              ^action-operator.arg2.{2} <person>)
-->
   (<s> ^person <person>)
}

sp {op_give1*elaborate*desired-distance
   (state <s> ^name op_give1)
-->
   (<s> ^desired-distance 1.6)
}

sp {op_give1*elaborate*executing-command*true
   (state <s> ^name op_give1
              ^current-action.put-down-command.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_give1*elaborate*allow-wait*true
   (state <s> ^name op_give1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

#### GOAL SATISFACTION ####
sp {op_give1*elaborate*desired*satisfied
   (state <s> ^name op_give1
              ^problem-space.execution-type external
              ^current-action.put-down-command.status success)
-->
   (<s> ^desired.satisfied true)
}

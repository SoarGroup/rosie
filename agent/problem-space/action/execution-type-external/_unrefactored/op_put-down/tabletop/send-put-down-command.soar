sp {op_put-down1*tabletop*propose*send-put-down-command
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^projection-result <res>
              ^world.robot.arm.moving-status wait
             -^current-action.put-down-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-put-down-command
        ^destination <res>)
}

# Put the command on the output link
sp {op_put-down1*tabletop*apply*send-put-down-command*send
   (state <s> ^name op_put-down1
              ^operator <o>
              ^io.output-link <out>
              ^current-action <a>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (<out> ^put-down <cmd>)
   (<cmd> ^location <loc>)
   (<loc> ^x <x> ^y <y> ^z <z>)
   (<a> ^put-down-command <cmd>)
}

# Relocate the belief object in svs
sp {op_put-down1*tabletop*apply*send-put-down-command*relocate*object
   (state <s> ^name op_put-down1
              ^operator <o>
              ^held-object.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
   (<o> ^name send-put-down-command
        ^destination <dest>)
   (<dest> ^x <x> ^y <y> ^z <z>)
-->
   (write |Moving object to tracked location| (crlf))
   (<cmd> ^set_transform <setxform>)
   (<setxform> ^id <id>
               ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}

              

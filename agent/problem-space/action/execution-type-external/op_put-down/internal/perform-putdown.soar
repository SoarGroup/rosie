sp {op_put-down1*internal*propose*perform-putdown
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^held-object.predicates.arm-status grabbed)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-putdown)
}

sp {op_put-down1*internal*apply*perform-putdown*change*holding-object
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^operator.name perform-putdown
              ^top-state.perception-monitor.input-link.self.arm <arm>
              ^held-object.handle <h>)
   (<arm> ^holding-object { <old> <> none })
-->
   (<arm> ^holding-object <old> -
          ^holding-object none)
}

sp {op_put-down1*internal*prefer*attend-to-scene-change*over*perform-put-down
   (state <s> ^name op_put-down1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change)
   (<o2> ^name perform-putdown)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_put-down1*internal*apply*perform-putdown*add*predicate
   (state <s> ^name op_put-down1
              ^operator.name perform-putdown
              ^held-object <obj>
              ^predicate-handle <pred-h>
              ^destination <dest>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle <pred-h>)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <obj> ^2 <dest>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <dest>)
}

sp {op_put-down1*internal*apply*perform-putdown*add*predicate*in*current-location
   (state <s> ^name op_put-down1
              ^operator.name perform-putdown
              ^held-object <obj>
              ^world.robot.current-location <loc>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle in1)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <obj> ^2 <loc>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

sp {op_approach1*magicbot*elaborate*problem-space*attend-to-all-perception*yes
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_approach1*elaborate*executing-command*true
   (state <s> ^name op_approach1
              ^current-action.{ << drive-command orient-command >> }.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_approach1*elaborate*allow-wait*true
   (state <s> ^name op_approach1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

#### GOAL SATISFACTION ####
sp {op_approach1*elaborate*desired*satisfied
   (state <s> ^name op_approach1
             -^executing-command true
              ^current-action.orient-command.status success
              ^target-object.predicates.visible true
              ^within-range true)
-->
   (<s> ^desired.satisfied true)
}

sp {op_approach1*magicbot*elaborate*target-object*superoperator
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^action-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

sp {op_approach1*magicbot*elaborate*target-distance
   (state <s> ^name op_approach1)
-->
   (<s> ^target-distance 1.5)
}
   
sp {op_approach1*magicbot*elaborate*within-range*true
   (state <s> ^name op_approach1
              ^target-distance <dist>
              ^calculations.distance <= <dist>)
-->
   (<s> ^within-range true)
}
   
sp {op_approach1*magicbot*elaborate*within-range*false
   (state <s> ^name op_approach1
              ^target-distance <dist>
              ^calculations.distance > <dist>)
-->
   (<s> ^within-range false)
}

sp {op_approach1*magicbot*elaborate*object-info
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^target-object <obj>)
   (<obj-info> ^wm-obj <obj>)
-->
   (<s> ^object-info <obj-info>)
}

sp {op_approach1*elaborate*calculation
   (state <s> ^name op_approach1
              ^robot-position <rp>
              ^object-position <op>)
   (<rp> ^x <rx> ^y <ry>)
   (<op> ^x <px> ^y <py>)
-->
   (<s> ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <px> ^object-y <py>)
}

sp {op_approach1*elaborate*calculations*distance
   (state <s> ^name op_approach1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <px> ^object-y <py>)
-->
   (<c> ^distance (sqrt (+ (* (- <px> <rx>) (- <px> <rx>) )
                           (* (- <py> <ry>) (- <py> <ry>) ) ) ) )
}

sp {op_approach1*elaborate*calculations*theta
   (state <s> ^name op_approach1
              ^calculations <c>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <px> ^object-y <py>)
-->
   (<c> ^theta (atan2 (- <py> <ry>) (- <px> <rx>) ) )
}

sp {op_approach1*elaborate*calcuations*drive-x*drive-y
   (state <s> ^name op_approach1
              ^calculations <c>
              ^target-distance <dist>)
   (<c> ^robot-x <rx> ^robot-y <ry>
        ^object-x <px> ^object-y <py>
        ^theta <theta>)
-->
   (<c> ^drive-x (- <px> (* <dist> (cos <theta>) 0.95) ) )
   (<c> ^drive-y (- <py> (* <dist> (sin <theta>) 0.95) ) )
}

sp {learn-subtask-policy*propose*change-goal-handle
   (state <s> ^name learn-subtask-policy
              ^task-operator <task-op>
              ^goal-handle-subs <subs>)
   (<task-op> ^current-goal <cur-h>)
   (<subs> ^<cur-h> <goal-h>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name change-goal-handle
        ^current-handle <cur-h>
        ^substitute-handle <goal-h>)
}

sp {learn-subtask-policy*prefer*change-goal-handle*over*task-operator
   (state <s> ^name learn-subtask-policy
              ^operator <o1> +
              ^operator <o2> +
              ^task-operator <o2>)
   (<o1> ^name change-goal-handle)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {learn-subtask-policy*apply*change-goal-handle
   (state <s> ^name learn-subtask-policy
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name change-goal-handle
        ^current-handle <cur-h>
        ^substitute-handle <goal-h>)
   (<task-op> ^current-goal <cur-h>)
-->
   (<task-op> ^current-goal <cur-h> -
              ^current-goal <goal-h>)
}

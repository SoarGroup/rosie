
# If the agent has not yet learned an elaboration rule for a goal, force a SNC
sp {learn-subtask-policy*task-operator*elaborate*force-state-no-change*if*unlearned*desired
  (state <s> ^superstate.name learn-subtask-policy
             ^top-retrospective-state true
            -^desired)
-->
   (<s> ^force-state-no-change true)
}

# We want to force an operator tie so that we can have an impasse and learn a policy rule
sp {learn-subtask-policy*task-operator*propose*force-operator-tie
   (state <s> ^problem-space.subproblem retrospective
              ^top-retrospective-state true)
-->
   (<s> ^operator <o> +)
   (<o> ^name force-operator-tie)
}

# Reject all subtasks that are not the one we are trying to learn
sp {learn-subtask-policy*task-operator*reject*other*subtasks
   (state <s> ^problem-space.subproblem retrospective
              ^top-retrospective-state true
              ^subtask-operator.subtask-handle <sub-h>
              ^operator <o> +)
   (<o> ^subtask-handle <> <sub-h>)
-->
   (<s> ^operator <o> -)
}


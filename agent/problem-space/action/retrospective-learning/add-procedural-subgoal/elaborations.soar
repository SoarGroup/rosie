sp {add-procedural-subgoal*elaborate*goal-handle-subs*from*superstate
   (state <s> ^name add-procedural-subgoal
              ^superstate.goal-handle-subs <subs>)
-->
   (<s> ^goal-handle-subs <subs>)
}

sp {add-procedural-subgoal*elaborate*subtask-episode*superoperator
   (state <s> ^name add-procedural-subgoal
              ^superstate.operator.subtask-episode <ep>)
-->
   (<s> ^subtask-episode <ep>)
}

sp {add-procedural-subgoal*elaborate*subtask-operator*from*subtask-episode
   (state <s> ^name add-procedural-subgoal
              ^subtask-episode.task-stack.bottom.task-operator <task-op>)
-->
   (<s> ^subtask-operator <task-op>)
}

sp {add-procedural-subgoal*elaborate*subtask-handle*from*task-operator
   (state <s> ^name add-procedural-subgoal
              ^subtask-operator.subtask-handle <sub-h>)
-->
   (<s> ^subtask-handle <sub-h>)
}

sp {add-procedural-subgoal*elaborate*task-operator*from*subtask-episode
   (state <s> ^name add-procedural-subgoal
              ^subtask-episode.task-stack.bottom.prev.task-operator <task-op>)
-->
   (<s> ^task-operator <task-op>)
}

# current-goal-handle: the handle of the goal we are going to push a procedural goal in front of
#   If there is a handle substitution, use the substituted version
sp {add-procedural-subgoal*elaborate*current-goal-handle*from*task-operator*substitute
   (state <s> ^name add-procedural-subgoal
              ^task-operator.current-goal <goal-h>
              ^goal-handle-subs.<goal-h> <sub-h>)
-->
   (<s> ^current-goal-handle <sub-h>)
}

# Otherwise just use the goal from the task-operator
sp {add-procedural-subgoal*elaborate*current-goal-handle*from*task-operator*no*substitute
   (state <s> ^name add-procedural-subgoal
              ^task-operator.current-goal <goal-h>
             -^goal-handle-subs.<goal-h>)
-->
   (<s> ^current-goal-handle <goal-h>)
}



# Elaborate Entire Goal Graph
sp {add-procedural-subgoal*elaborate*goal-graph-nodes
   (state <s> ^name add-procedural-subgoal)
-->
   (<s> ^goal-graph-nodes <goals>)
}

sp {add-procedural-subgoal*elaborate*goal-graph-nodes*start*handle
   (state <s> ^name add-procedural-subgoal
              ^task-concept-network.goal-graph <start>
              ^goal-graph-nodes <goals>)
   (<start> ^handle <goal-h>)
-->
   (<goals> ^goal-handle <goal-h>)
}

sp {add-procedural-subgoal*elaborate*smem-query*goal-graph-node*from*handle
   (state <s> ^name add-procedural-subgoal
              ^goal-graph-nodes <goals>)
   (<goals> ^goal-handle <goal-h>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 6
        ^destination <goals>
        ^result-name goal)
   (<cue> ^handle <goal-h>)
}

sp {add-procedural-subgoal*elaborate*goal-graph-nodes*next*goal-handle
   (state <s> ^name add-procedural-subgoal
              ^goal-graph-nodes <goals>
             -^current-goal-handle <goal-h>
             -^subtask-goal-node.handle <next-goal-h>)
   (<goals> ^goal <goal-id>)
   (<goal-id> ^handle <goal-h>
              ^next.goal.handle <next-goal-h>)
-->
   (<goals> ^goal-handle <next-goal-h>)
}

sp {add-procedural-subgoal*elaborate*current-goal-id*from*goal-graph-nodes
   (state <s> ^name add-procedural-subgoal
              ^goal-graph-nodes.goal <goal-id>
              ^current-goal-handle <goal-h>)
   (<goal-id> ^handle <goal-h>)
-->
   (<s> ^current-goal-id <goal-id>)
}


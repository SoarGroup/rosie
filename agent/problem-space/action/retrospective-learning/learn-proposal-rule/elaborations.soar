
sp {learn-proposal-rule*elaborate*task-handle*superoperator
   (state <s> ^name learn-proposal-rule
              ^superstate.operator.task-handle <task-handle>)
-->
   (<s> ^task-handle <task-handle>)
}

#### RECREATE THE STATE #####

# deep-copy the world from the start-episode to the state
sp {learn-proposal-rule*elaborate*deep-copy-world*start-episode
   (state <s> ^name learn-proposal-rule
              ^start-episode.world <w>)
-->
   (<s> ^problem-space <ps>)
   (<ps> ^name action
         ^world <ps-world>)
   (<ps-world> ^source <w>
               ^copy-type deep)
}

# deep-copy the task operator with respect to the copied world
sp {learn-proposal-rule*copy-task-operator*reference-operator
   (state <s> ^name learn-proposal-rule
              ^start-episode.task-stack.bottom.task-operator <task-op>
              ^deep-copy-world.copy-objects-map <map>)
-->
   (<s> ^copy-task-operator <cto>)
   (<cto> ^task-operator <task-op>
          ^destination <s>
          ^copy-name reference-operator
          ^object-map <map>)
}


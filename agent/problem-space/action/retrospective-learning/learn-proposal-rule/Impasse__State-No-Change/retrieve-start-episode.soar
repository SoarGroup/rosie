sp {learn-proposal-rule-substate*propose*retrieve-start-episode
   (state <s> ^name learn-proposal-rule-substate
              ^learning-episode
             -^start-episode)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-start-episode)
}

sp {learn-proposal-rule-substate*apply*retrieve-start-episode
   (state <s> ^name learn-proposal-rule-substate
              ^operator.name retrieve-start-episode
              ^learning-episode.operator <o>
              ^epmem.command <cmd>)
   (<o> ^name satisfy-purpose
        ^purpose.parameters.task-segment.task-operator.task-handle <task-handle>)
-->
   (<cmd> ^query <q>)
   (<q> ^task-stack.bottom <bot>)
   (<bot> ^start-of-execution true
          ^task-operator.task-handle <task-handle>)
}

sp {learn-proposal-rule-substate*apply*retrieve-start-episode*success
   (state <s> ^name learn-proposal-rule-substate
              ^operator.name retrieve-start-episode
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^result.retrieved <episode>)
   (<res> ^retrieved <episode>
          ^memory-id <ep-id>
          ^normalized-match-score 1.0)
-->
   (<s> ^start-episode <episode>)
}

sp {learn-proposal-rule-substate*apply*retrieve-start-episode*success*cleanup
   (state <s> ^name learn-proposal-rule-substate
              ^operator.name retrieve-start-episode
              ^epmem <epmem>)
   (<epmem> ^result.success <q>
            ^command <cmd>)
   (<cmd> ^query <q>)
-->
   (<cmd> ^query <q> -)
}

######################### simulate-task-execution #########################

# When we propose a version of the task operator, we want to simulate it
# so that all the necessary state conditions are tested/included in the chunk

# name the substate
sp {learn-proposal-rule-substate*task-operator*elaborate*name*model-task-execution
   (state <s> ^name <task-name>
              ^task-operator.name <task-name>
              ^superstate.name learn-proposal-rule-substate)
-->
   (<s> ^name simulate-task-execution)
}

# simulate the subtasks
sp {simulate-task-execution*elaborate*problem-space
   (state <s> ^name simulate-task-execution
              ^problem-space <ps>)
   (<ps> ^world <w>)
-->
   (<ps> ^execution-type internal)
   (<w> ^copy-type shallow)
}

# Make sure to retrieve and follow all goals
sp {simulate-task-execution*propose*init-current-goal
   (state <s> ^name simulate-task-execution
              ^task-operator <task-op>)
   (<task-op> -^current-goal)
-->
   (<s> ^operator <o> + >)
   (<o> ^name init-current-goal
        ^task-operator <task-op>)
}

sp {simulate-task-execution*prefer*select-next-goal*over*task-operator
   (state <s> ^name simulate-task-execution
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name select-next-goal)
   (<o2> ^item-type task-operator)
-->
   (<s> ^operator <o1> > <o2>)
}


### return-operator-result - return whether the simulation was successful or a failure

### Case 1: The agent hits a state-no-change while trying to simulate the task

sp {simulate-task-execution*state-no-change*propose*return-operator-result*failure
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate.name simulate-task-execution)
-->
   (<s> ^operator <o> + >)
   (<o> ^name return-operator-result
        ^result failure)
}

sp {simulate-task-execution*state-no-change*apply*return-operator-result
   (state <s> ^impasse no-change
              ^attribute state
              ^superstate <ss>
              ^quiescence t
              ^operator <o>)
   (<ss> ^name simulate-task-execution
         ^superstate <sss>)
   (<o> ^name return-operator-result
        ^result <res>)
-->
   (<sss> ^operator-result <res>)
}

### Case 2: If there is a goal with a non-state-based predicate, return failure (can't simulate)
sp {simulate-task-execution*propose*return-operator-result*unlearnable*goal*predicate
   (state <s> ^name simulate-task-execution
              ^desired.{ << 1 2 3 4 5 >> } <gp>
              ^superstate <ss>)
   (<gp> ^type <> state <> relation)
-->
   (<s> ^operator <o> + = !)
   (<o> ^name return-operator-result
        ^result failure)
}

### Case 3: When the terminal goal is satisfied, return success
sp {simulate-task-execution*success*propose*return-operator-result*success
   (state <s> ^name simulate-task-execution
              ^desired <des>
              ^superstate <ss>)
   (<des> ^item-type terminal-goal
          ^satisfied true)
-->
   (<s> ^operator <o> + = !)
   (<o> ^name return-operator-result
        ^result success)
}

# copy result to superstate
sp {simulate-task-execution*apply*return-operator-result
   (state <s> ^name simulate-task-execution
              ^operator <o>
              ^superstate <ss>
              ^quiescence t)
   (<o> ^name return-operator-result
        ^result <res>)
-->
   (<ss> ^operator-result <res>)
}

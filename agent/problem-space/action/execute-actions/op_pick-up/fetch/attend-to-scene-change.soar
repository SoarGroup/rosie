# Don't attend to missing-object on the object being grabbed
sp {op_pick-up1*fetch*reject*attend-to-missing-object
   (state <s> ^name op_pick-up1
              ^operator <o> +
              ^target-object <obj>)
   (<o> ^name attend-to-scene-change
        ^change.type missing-object
        ^change.object-info.wm-obj <obj>)
-->
   (<s> ^operator <o> -)
}

## Don't attend to new objects while the arm is moving
sp {op_pick-up1*fetch*reject*attend-to-new-perception-object
   (state <s> ^name op_pick-up1
              ^operator <o> +
              ^executing-command true)
   (<o> ^name attend-to-scene-change
        ^change.type new-perception-object)
-->
   (<s> ^operator <o> -)
}

# Don't attend at all to the object being picked up while the arm is moving
sp {op_pick-up1*fetch*reject*attend-to-scene-change*grabbed*object
   (state <s> ^name op_pick-up1
              ^operator <o> +
              ^target-object <obj>
              ^executing-command true)
   (<o> ^name attend-to-scene-change
        ^change.object-info.wm-obj <obj>)
-->
   (<s> ^operator <o> -)
}

sp {op_pick-up1*fetch*elaborate*evaluate-new-object*candidate-for-missing-object
   (state <s> ^name op_pick-up1
              ^operator <o> +
              ^target-object.handle <obj-handle>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name attend-to-scene-change
        ^change.type new-object
        ^change <change>)
   (<obj-info> ^object-handle <obj-handle>)
-->
   (<change> ^candidate-for-missing-object <obj-info>)
}


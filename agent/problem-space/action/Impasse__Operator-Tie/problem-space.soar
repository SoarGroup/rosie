########### selection state #########

# If there is an operator-tie in an action problem-space, 
# Name the state selection
sp {problem-space*action*operator-tie*elaborate*name*selection
   (state <s> ^problem-space.name action
              ^impasse tie
              ^attribute operator)
-->
   (<s> ^name selection)
}

sp {problem-space*action*selection*elaborate*problem-space*default-state-copy*no
   (state <s> ^name selection
              ^problem-space <ps>)
   (<ps> ^name action)
-->
   (<ps> ^default-state-copy no)
}

sp {problem-space*action*selection*elaborate*problem-space*world*copy-type*shallow
   (state <s> ^name selection
              ^problem-space <ps>)
   (<ps> ^name action
        -^subproblem waypoint-search
         ^world <world>)
-->
   (<world> ^copy-type shallow)
}

sp {problem-space*action*selection*elaborate*problem-space*subtasks*copy-type*shallow
   (state <s> ^name selection
              ^problem-space <ps>)
   (<ps> ^name action
        -^subproblem waypoint-search
         ^subtasks <subs>)
-->
   (<subs> ^copy-type shallow)
}

sp {problem-space*action*selection*elaborate*evaluate-operator*default-copy*info
   (state <s> ^name selection
              ^problem-space <ps>
              ^operator <o> +)
   (<ps> ^name action
        -^subproblem waypoint-search)
   (<o> ^name evaluate-operator)
-->
   (<o> ^default-desired-copy custom)
   (<o> ^default-operator-copy custom)
}



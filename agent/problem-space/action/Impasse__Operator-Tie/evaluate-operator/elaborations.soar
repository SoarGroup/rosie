## If the desired goal is satisfied, report success
sp {problem-space*action*evaluate-operator*elaborate*successs*desired*satisfied
   (state <s> ^name evaluate-operator
              ^problem-space.name action
              ^desired <d>)
   (<d> ^satisfied true)
-->
   (<s> ^success <d>)
}

sp {problem-space*action*evaluate-operator*elaborate*current-task-segment
   (state <s> ^name evaluate-operator
              ^problem-space.name action)
-->
   (<s> ^current-task-segment <seg>)
}

sp {problem-space*action*evaluate-operator*elaborate*performed-subtask
   (state <s> ^name evaluate-operator
              ^problem-space.name action
              ^superstate.superstate.current-task-segment.performed-subtask <sub-h>
              ^current-task-segment <seg>)
-->
   (<seg> ^performed-subtask <sub-h>)
}

sp {problem-space*action*evaluate-operator*elaborate*plan-length*0
   (state <s> ^name evaluate-operator)
-->
   (<s> ^plan-length 0)
}
              
sp {problem-space*action*evaluate-operator*apply*task-operator*increment*plan-length
   (state <s> ^name evaluate-operator
              ^operator <o>
             -^tried-tied-operator <o>
              ^plan-length <n>)
   (<o> ^item-type task-operator
       -^no-default-application true)
-->
   (<s> ^plan-length <n> -
        ^plan-length (+ <n> 1))
}

sp {evaluate-operator*elaborate*evaluation*plan-length
   (state <s> ^name evaluate-operator
              ^superstate <ss>
              ^success <des>
              ^plan-length { <n> <> 0})
   (<ss> ^operator <so>
         ^evaluation <e>)
   (<so> ^name evaluate-operator
         ^evaluation <e>
         ^evaluation.desired <ss-des>)
   (<des> ^duplicate-of <ss-des>)
-->
   (<e> ^plan-length <n>)
}

sp {problem-space*action*elaborate*subtask-plan-length
   :default
   (state <s> ^name selection
              ^problem-space.name action
              ^superstate <ss>
              ^evaluation <e1>)
   (<ss> ^desired <eb>)
   (<e1> ^symbolic-value success
         ^plan-length <n>
         ^desired <eb>
         ^superoperator <o1>)
-->
   (<o1> ^plan-length <n>)
}


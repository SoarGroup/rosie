# Elaborate: action-stack from superstate (link to top-state.action-stack)
sp {action-space*execute*elaborate*action-stack*from*superstate
   (state <s> ^problem-space.action-space execute
              ^superstate.action-stack <stack>)
-->
   (<s> ^action-stack <stack>)
}

# Elaborate: current-action, the segment from the action-stack for the task-operator
sp {action-space*execute*elaborate*current-action*from*superstate*child-action
   (state <s> ^problem-space.action-space execute
              ^task-operator <o>
              ^superstate.child-action <a>)
   (<a> ^execution-operator <o>)
-->
   (<s> ^current-action <a>)
}

# Elaborate: parent-action, the previous action segment on the action-stack
sp {action-space*execute*elaborate*parent-action
   (state <s> ^problem-space.action-space execute
              ^current-action.prev { <parent> <> none })
-->
   (<s> ^parent-action <parent>)
}

# Elaborate: child-action, the next action segment on the action-stack 
sp {action-space*execute*elaborate*child-action
   (state <s> ^problem-space.action-space execute
              ^current-action.next { <child> <> none })
-->
   (<s> ^child-action <child>)
}



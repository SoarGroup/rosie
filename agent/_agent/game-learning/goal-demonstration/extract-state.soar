
#call from any state, no arguments
# result is ^state-result <res>)
#   (<res> ^relationships <rels> ^objects <os> ^object-desc-set <ods>)
#for now change to this format and make work

sp {extract-state*propose*copy-relationships
   (state <s> ^name extract-state
             -^relationships.done)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relationships)
}
sp {extract-state*apply*copy-relationships*default
   (state <s> ^name extract-state
              ^operator.name copy-relationships
              ^relationships <rels>)
-->
   (<rels> ^done true)
}
sp {extract-state*apply*copy-relationships
   (state <s> ^name extract-state
              ^relationships <rels>
              ^operator.name copy-relationships
              ^top-state.world.predicates.predicate <p>)
   (<p> ^handle <name> ^instance <instance3>)
   (<instance3> ^1 <ida> ^2 <idb> -^3)
-->
   (<rels> ^relations <r>)
   (<r> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb>)
}

sp {extract-state*apply*copy-relationships*3arg
   (state <s> ^name extract-state
              ^relationships <rels>
              ^operator.name copy-relationships
              ^top-state.world.predicates.predicate <p>)
   (<p> ^handle <name> ^instance <instance3>)
   (<instance3> ^1 <ida> ^2 <idb> ^3 <idc>)
-->
   (<rels> ^relations <r>)
   (<r> ^name <name> ^instance <instance>)
   (<instance> ^1 <ida> ^2 <idb> ^3 <idc>)
}

sp {extract-state*propose*copy-objects
   (state <s> ^name extract-state
             -^object-set.done)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-objects)
}
sp {extract-state*apply*copy-objects*default
   (state <s> ^name extract-state
              ^operator.name copy-objects
              ^object-set <set>)
-->
   (<set> ^done true)
}
sp {extract-state*apply*copy-objects
   (state <s> ^name extract-state
              ^relationships.relations <p>
              ^object-set <os>
              ^top-state.world.objects.object <obja>
              ^operator.name copy-objects
              ^relationships <rels>)
   (<p> ^name <name> ^instance <instance>)
   (<instance> ^<< 1 2 3 >> <obja>)
-->
   (<os> ^object <obja>)
}

################################
# 
# extract object predicates
#
################################

sp {extract-state*propose*copy-predicates
   (state <s> ^name extract-state
             -^predicate-set.done)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-predicates)
}

sp {extract-state*apply*copy-predicates*mark*done
   (state <s> ^name extract-state
              ^operator.name copy-predicates
              ^predicate-set <preds>)
-->
   (<preds> ^done true)
}

#JK todo need?
sp {extract-state*apply*copy-predicates*create*objects
   (state <s> ^name extract-state
              ^operator.name copy-predicates
              #^top-state.object <obj>
              ^object-set.object <obj>
              ^predicate-set <preds>)
   (<obj> ^predicates.{<att> <> door1 <> movable <> indicated <> object-handle <> category <> arm-status <> dialog-object <> value <> handle <> type <> visible <> multiple <> number} {<val> <> grid-green})
  # (<obj> ^object-desc.<att> <val>) #huh?
-->
   (<preds> ^predicate <pred>)
   (<pred> ^object <obj> ^name <att> ^value <val>)
}

sp {extract-state*propose*complete-state
   (state <s> ^name extract-state)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-state)
}

sp {extract-state*apply*complete-state
   (state <s> ^name extract-state
              ^object-set <os>
              ^relationships <rels>
              ^predicate-set <preds>
              ^superstate.operator <op>
              ^superstate <ss>
              ^operator.name complete-state)
-->
   (<ss> ^state-result <res>)
   (<res> ^relationships <rels> ^objects <os>
          ^predicate-set <preds>)
}
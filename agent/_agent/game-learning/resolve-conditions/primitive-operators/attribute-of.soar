sp {attribute-of*propose*complete
   (state <s> ^name attribute-of
              ^instance <instance>)
-->
#   (interrupt)
   (<s> ^operator <o> + =)
   (<o> ^name complete
        ^instance <instance>)
}

sp {attribute-of*apply*complete
   (state <s> ^name attribute-of
              ^superstate.condition.result.set <res>
              ^operator <op>
              ^instance <s1>)  
   (<op> ^name complete ^instance <s1>)
-->
  (<res> ^instance <s1>)
}

sp {attribute-of*numeric-evaluate*value
   (state <s> ^name attribute-of
              ^superstate.condition.result.set <res>
              ^superstate.operator <o>)
   (<o> ^args <args> ^parameter.num < 2 ^type <type>)
   (<args> ^num 1
           ^2 <a1>)
   (<a1> -^rtype set ^result.set.instance.{1}.predicates.<type> <value>)
-->
   (<s> ^instance <instance>)
   (<instance> ^1 <value>)
}

sp {attribute-of*numeric-evaluate*value*instance
   (state <s> ^name attribute-of
              ^superstate.condition.result.set <res>
              ^superstate.operator <o>)
   (<o> ^args <args> ^parameter.num 2 ^type <type>)
   (<args> ^num 1
           ^2 <a1>)
   (<a1> -^rtype set ^result.set.instance.{1} <obja>)
   (<obja> ^predicates.<type> <value>)
-->
   (<s> ^instance <instance>)
   (<instance> ^1 <value> ^2 <obja>)
}

sp {attribute-of*numeric-evaluate*value*instance*arg2
   (state <s> ^name attribute-of
              ^superstate.condition.result.set <res>
              ^superstate.operator <o>)
   (<o> ^args <args> ^parameter.num 2 ^type <type> ^negative false)
   (<args> ^num 2
           ^1 <a1>
           ^2 <a2>)
   (<a1> ^result.set.instance.{1} <value>)
   (<a2> -^rtype set ^result.set.instance.{1} <obja>)
   (<obja> ^predicates.<type> <value>)
-->
   (<s> ^instance <instance>)
   (<instance> ^1 <value> ^2 <obja>)
}
sp {attribute-of*numeric-evaluate*value*instance*arg2*negative
   (state <s> ^name attribute-of
              ^superstate.condition.result.set <res>
              ^superstate.operator <o>)
   (<o> ^args <args> ^parameter.num 2 ^type <type> ^negative true)
   (<args> ^num 2
           ^1 <a1>
           ^2 <a2>)
   (<a2> -^rtype set ^result.set.instance.{1} <obja>)
   (<obja> ^predicates.<type> <value>)
  -{(<a1> ^result.set.instance.{1} <value>)}
-->
   (<s> ^instance <instance>)
   (<instance> ^1 <value> ^2 <obja>)
}




sp {attribute-of*numeric-evaluate*value*instance*sets
   (state <s> ^name attribute-of
              ^superstate.condition.rtype set
              ^superstate.operator <o>)
   (<o> ^args <args> ^parameter <pn>)
   #(<pn> ^num 2)
   (<args> ^num 1
           ^2 <a1>)
   (<a1> ^rtype set ^result.set.instance.{1} <set>)
-->
   (<s> ^instance <instance>)
   (<instance> ^1 <set2> ^2 <set>)
}

sp {attribute-of*apply*complete*sets
   (state <s> ^name attribute-of
              ^operator <op>
              ^superstate.condition.rtype set
              ^superstate.condition.result.set.instance <instance>
              ^superstate.operator <o>)
   (<op> ^name complete ^instance <instance>)
   (<instance> ^1 <set2> ^2 <set>)
   (<o> ^args <args> ^parameter <pn> ^type <type>)
  # (<pn> ^num 2 ^{1}.type set)
   (<args> ^num 1
           ^2 <a1>)
   (<a1> ^rtype set ^result.set.instance.{1} <set>)
   (<set> ^object <obj>)
   (<obj> ^predicates.<type> <val>)
-->
   (<set2> ^<random> <val>)
}
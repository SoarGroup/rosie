
sp {interpret-message*propose*goal-description
   (state <s> ^name interpret-message
   	      ^instructor-message <msg>
	      -^new-segment-info)
   (<msg> ^type goal-relation-message)
-->
   (<s> ^operator <o> +) 
   (<o> ^name goal-description
   	^message <msg>)
}

sp {interpret-message*apply*goal-description
   (state <s> ^name interpret-message
   	      ^operator <o>
          -^topstate.current-game.learning.type game-params #JK language hack for describing goal name
	      -^topstate.interaction.stack.segment.purpose.reason)
   (<o> ^name goal-description
   	^message.information <vcomm>)
-->
   (<s> ^new-segment-info <info>) 
   (<info> ^purpose <p>)
   (<p> ^type goal-description
   	^parameters <params>
	^satisfaction <sat>)
   (<sat> ^learning-event <le>)
   (<le> ^type <vcomm>)
   (<params> ^indexable <i>)
   (<i> ^type goal-description
   	^lexical <vcomm>)
}


sp {interpret-message*apply*goal-description*reason
   (state <s> ^name interpret-message
   	      ^operator <o>
          -^topstate.current-game.learning.type game-params #JK language hack for describing goal name
	      ^topstate.interaction.stack.segment.purpose.reason <r>)
   (<o> ^name goal-description
   	^message.information <vcomm>)
-->
   (<s> ^new-segment-info <info>) 
   (<info> ^purpose <p>)
   (<p> ^type goal-description
   	^parameters <params>
	^satisfaction <sat>
	^cause <r>)
   (<sat> ^learning-event <le>)
   (<le> ^type <vcomm>)
   (<params> ^indexable <i>)
   (<i> ^type goal-description
   	^lexical <vcomm>)
}

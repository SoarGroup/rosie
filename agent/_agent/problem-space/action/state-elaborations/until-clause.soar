####################### until-clause ###########################
#
# These rules will check if an until-clause on the state is satisfed
#
# They fire if an until-clause structure is on the state
#   (state <s> ^until-clause <until>)
#   (<until> ^prediate <pred>)  # Can be more than 1 predicate
#   (<pred> ^type << state relation >>
#           ^handle <pred-handle>
#           ^1 <obj1>
#           ^2 <obj2>) # may have 2 or more arguments if a relation
# Will elaborate each predicate with ^satisfied true, 
#   and elaborates the until-clause with ^satisfied true if all predicates are satisfied

#   Also expects the following on the state: 
#     world


# By default, elaborated from the action-operator
sp {action*elaborate*until-clause*from*action-operator
   (state <s> ^problem-space.name action
              ^action-operator.until-clause <until>)
-->
   (<s> ^until-clause <until>)
}

# Check if a visible predicate is satisfied (the object is visible)
sp {execute-primitive-action*elaborate*until-clause*state*predicate*satisfied*visible
   (state <s> ^problem-space.name action
              ^until-clause <until>
              ^world.objects.object <obj>)
   (<until> ^predicate <pred>)
   (<pred> ^type state
           ^handle visible1
           ^{1} <obj>)
   (<obj> ^predicates.visible true)
-->
   (<pred> ^satisfied true)
}

# Check if a state predicate is satisfied (the predicate exists on the object)
sp {execute-primitive-action*elaborate*until-clause*state*predicate*satisfied
   (state <s> ^problem-space.name action
              ^until-clause <until>
              ^world.objects.object <obj>)
   (<until> ^predicate <pred>)
   (<pred> ^type state
           ^handle <pred-handle>
           ^property-handle <prop-handle>
           ^{1} <obj>)
   (<obj> ^predicates.<prop-handle> <pred-handle>)
-->
   (<pred> ^satisfied true)
}

# Check if a relation predicate is satisifed (relation instance exists in the world)
sp {execute-primitive-action*elaborate*until-clause*state*relation*satisfied
   (state <s> ^problem-space.name action
              ^until-clause <until>
              ^world <world>)
   (<until> ^predicate <until-pred>)
   (<until-pred> ^type relation
           ^handle <pred-handle>
           ^1 <obj1>
           ^2 <obj2>)
   (<world> ^predicates.predicate <pred>
            ^objects.object <obj1>
            ^objects.object <obj2>)
   (<pred> ^handle <pred-handle>
           ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
-->
   (<until-pred> ^satisfied true)
}

# until-clause with 1 predicate is satisfied
sp {execute-primitive-action*elaborate*until-clause*satisfied*1*predicate
   (state <s> ^problem-space.name action
              ^until-clause <until>)
   (<until> ^predicate <p1>
           -^predicate { <p2> <> <p1> })
   (<p1> ^satisfied true)
-->
   (<until> ^satisfied true)
}

# until-clause with 2 predicate is satisfied
sp {execute-primitive-action*elaborate*until-clause*satisfied*2*predicates
   (state <s> ^problem-space.name action
              ^until-clause <until>)
   (<until> ^predicate <p1>
            ^predicate <p2> > <p1>
           -^predicate { <p3> <> <p1> <> <p2> })
   (<p1> ^satisfied true)
   (<p2> ^satisfied true)
-->
   (<until> ^satisfied true)
}

# until-clause with 3 predicate is satisfied
sp {execute-primitive-action*elaborate*until-clause*satisfied*3*predicates
   (state <s> ^problem-space.name action
              ^until-clause <until>)
   (<until> ^predicate <p1>
            ^predicate <p2> > <p1>
            ^predicate <p3> > <p2>
           -^predicate { <p4> <> <p1> <> <p2> <> <p3> })
   (<p1> ^satisfied true)
   (<p2> ^satisfied true)
   (<p3> ^satisfied true)
-->
   (<until> ^satisfied true)
}

 
 

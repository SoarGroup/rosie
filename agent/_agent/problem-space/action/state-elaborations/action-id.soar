## action-id
# The smem lti and substructure with information about the current action-operator
# 
# By default elaborated from the superstate (if matches action-operator)
#    Otherwise retrieved from semantic memory

sp {action*elaborate*action-id*from*superstate
   (state <s> ^problem-space.name action
              ^superstate.action-id <id>
              ^action-operator.action-handle <handle>)
   (<id> ^handle <handle>)
-->
   (<s> ^action-id <id>)
}

sp {action*elaborate*action-id*from*superstate*no*action-operator
   (state <s> ^problem-space.name action
              ^superstate.action-id <id>
             -^action-operator)
-->
   (<s> ^action-id <id>)
}

# If no action-id, retrieve from semantic memory
sp {action*problem-space*elaborate*smem-query
   (state <s> ^problem-space.name action
              ^action-operator <action-op>
             -^action-id)
   (<action-op> ^action-handle <action-handle>)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 8)
   (<cue> ^handle <action-handle>)
}

sp {action*composite-action*elaborate*action-id*from*query*result
   :o-support
   (state <s> ^problem-space.name action
              ^smem-query <query>
              ^action-operator.action-handle <action-handle>)
   (<query> ^cue.handle <action-handle>
            ^result <id>)
-->
   (<s> ^action-id <id>)
}


sp {learn-subaction-proposal*propose*create-operator
   (state <s> ^name learn-subaction-proposal
              ^subaction-id <sub>
             -^subaction-operator)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-operator
        ^subaction <sub>)
}

sp {learn-subaction-proposal*elaborate*create-operator*copy*perform
   (state <s> ^name learn-subaction-proposal
              ^operator <o> +
              ^subaction-id <sub-id>)
   (<o> ^name create-operator)
   (<sub-id> ^perform <val>)
-->
   (<o> ^perform <val>)
}

sp {learn-subaction-proposal*elaborate*create-operator*elaborate*perform*default
   (state <s> ^name learn-subaction-proposal
              ^operator <o> +
              ^subaction-id <sub-id>)
   (<o> ^name create-operator)
   (<sub-id> -^perform )
-->
   (<o> ^perform multiple)
}

sp {learn-subaction-proposal*apply*create-operator
   (state <s> ^name learn-subaction-proposal
              ^operator <o>)
   (<o> ^name create-operator
        ^subaction <sub>
        ^perform <perform>)
   (<sub> ^handle <sub-handle>
          ^op_name <name>
          ^action-handle <handle>
          ^action-type <type>)
-->
   (<s> ^subaction-operator <op>)
   (<op> ^subaction-handle <sub-handle>
         ^name <name>
         ^action-proposal true
         ^action-handle <handle>
         ^action-type <type>
         ^perform <perform>)
}

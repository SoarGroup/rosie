sp {action*state*no-change*elaborate*state*name*unmatched-action
   (state <s> ^superstate.problem-space.name action
             -^name << learn-desired-elaboration-rule learn-subaction-proposal >>
              ^impasse no-change
              ^attribute state)
-->
   (<s> ^name unmatched-action)
}

sp {action*state*no-change*elaborate*state*name*learn-desired-elaboration-rule
   (state <s> ^superstate.problem-space.name action
              ^superstate.action-operator <action>
              ^impasse no-change
              ^attribute state
             -^superstate.desired)
   (<action> ^action-type composite)
-->
   (<s> ^name learn-desired-elaboration-rule)
}

sp {action*state*no-change*elaborate*state*name*learn-subaction-proposal
   (state <s> ^superstate.problem-space.name action
              ^superstate.desired
              ^impasse no-change
              ^attribute state
              ^superstate <ss>)
   (<ss> ^action-id.procedural.subaction <sub>)
   (<sub> ^handle <handle>)
  -{(<ss> ^operator <o> +)
    (<o> ^subaction-handle <handle>)}
-->
   (<s> ^name learn-subaction-proposal
        ^subaction-id <sub>)
}


# Attend to perception during drive commands
sp {op_go-to1*elaborate*problem-space*attend-to-all-perception*during*commands
   (state <s> ^name op_go-to1
              ^current-action.{ << drive-command drive-forward-command >> } <cmd>
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

# If arg2 is an object, then elaborate it as target-object
sp {op_go-to*elaborate*target-object
   (state <s> ^name op_go-to1
              ^action-operator.arg2 <arg2>)
   (<arg2> ^arg-type predicate
           ^2 <obj>)
   (<obj> -^predicates.spatial-shape end1
          -^linked-to
          ^predicates.category object)
-->
  (<s> ^target-object <obj>)
}

# If arg2 is a location, then elaborate it as desired-location
sp {op_go-to*elaborate*desired-location
   (state <s> ^name op_go-to1
              ^action-operator.arg2 <arg2>)
   (<arg2> ^arg-type predicate
           ^2 <loc>)
   (<loc> -^predicates.spatial-shape end1
          -^linked-to
          ^predicates.category location)
-->
   (<s> ^desired-location <loc>)
}

sp {op_go-to*elaborate*desired-location*linked-to
   (state <s> ^name op_go-to1
              ^action-operator.arg2 <arg2>)
   (<arg2> ^arg-type predicate
           ^{2}.linked-to <loc>)
   (<loc> -^predicates.spatial-shape end1
           ^predicates.category location)
-->
   (<s> ^desired-location <loc>)
}

# If arg2 is a waypoint, then elaborate it as desired-waypoint
sp {op_go-to*elaborate*desired-waypoint*arg2*waypoint
   (state <s> ^name op_go-to1
              ^action-operator.arg2 <arg2>
              ^top-state.maps.map.waypoint <wp>)
   (<arg2> ^arg-type predicate
           ^2 <wp>)
-->
   (<s> ^desired-waypoint <wp>)
}

sp {op_go-to*elaborate*desired-waypoint
   (state <s> ^name op_go-to1
              ^desired-location.waypoint <wp-handle>
              ^top-state.maps.map.waypoint <wp>)
   (<wp> ^handle <wp-handle>)
-->
   (<s> ^desired-waypoint <wp>)
}

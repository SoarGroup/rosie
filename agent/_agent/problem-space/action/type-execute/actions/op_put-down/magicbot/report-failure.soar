#### Different failure cases that can arise during put-down

sp {op_put-down1*magicbot*propose*report-failure*svs-filter-error
   (state <s> ^name op_put-down1
              ^current-action.execution-operator.action-handle <handle>
              ^{ << object-position-filter robot-position-filter >> <type> } <pos-filter>)
   (<pos-filter> ^result <res>)
   (<res> -^record.value)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type svs-filter-error
        ^failure-info <type>)
}

# missing-object: no input-link rep for the object
sp {op_put-down1*magicbot*propose*report-failure*missing-object
   (state <s> ^name op_put-down1
             -^object-info.input-link-obj)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type missing-object
        ^failure-info <handle>)
}

# command-error: The put-down command got an error status
sp {op_put-down1*magicbot*propose*report-failure*command-error
   (state <s> ^name op_put-down1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << face-command drive-command put-down-command >> }.status error)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type command-error
        ^failure-info <handle>)
}

# execution-failed: The put-down command got a failure status
sp {op_put-down1*magicbot*propose*report-failure*execution-failed
   (state <s> ^name op_put-down1
              ^current-action.execution-operator.action-handle <handle>
              ^current-action.{ << face-command drive-command put-down-command >> }.status failure)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type execution-failed
        ^failure-info <handle>)
}

sp {op_put-down1*magicbot*propose*report-failure*execution-failed*not-in-destination
   (state <s> ^name op_put-down1
              ^current-action.put-down-command.status success
              ^current-action.execution-operator.action-handle <handle>
              ^destination <dest>
              ^held-object <obj>
              ^top-state.world.predicates.predicate <in-pred>)
   (<in-pred> ^handle in1)
   -{(<in-pred> ^instance <i>)
     (<i> ^1 <obj> ^2 <dest>)}
-->
   (<s> ^operator <o> +)
   (<o> ^name report-failure
        ^failure-type execution-failed
        ^failure-info <handle>)
}

sp {op_pick-up1*magicbot*propose*send-drive-to-xy-command
   (state <s> ^name op_pick-up1
               ^object-position <pos>
              -^current-action.drive-command
              ^top-state.world.robot.arm.state external)
-->
   (<s> ^operator <o> +)
   (<o> ^name send-drive-to-xy-command
        ^position <pos>)
}

sp {op_pick-up1*magicbot*apply*send-drive-to-xy-command*send
   (state <s> ^name op_pick-up1
              ^operator <o>
              ^io.output-link <out>
              ^current-action <a>)
   (<o> ^name send-drive-to-xy-command
        ^position <pos>)
   (<pos> ^x <x> ^y <y>)
-->
    (<out> ^do-control-law <law>)
    (<law> ^name drive-xy
           ^parameters <params>
           ^termination-condition.name stabilized)
    (<params> ^x <x> ^y <y>)
    (<a> ^drive-command <law>)
}

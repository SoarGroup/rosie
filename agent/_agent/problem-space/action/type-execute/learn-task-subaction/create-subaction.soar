# Create the subaction-structure and copy the basic information onto it

sp {learn-task-subaction*propose*create-subaction
  (state <s> ^name learn-task-subaction
            -^subaction-structure.op_name)
-->
  (<s> ^operator <o> + =)
  (<o> ^name create-subaction)
}

# Create the basic structure
sp {learn-task-subaction*apply*create-subaction*create*structure
  (state <s> ^name learn-task-subaction
             ^operator.name create-subaction)
-->
  (<s> ^subaction-structure <struct>)
}

# Copy the operator name
sp {learn-task-subaction*apply*create-subaction*copy*name
  (state <s> ^name learn-task-subaction
             ^operator.name create-subaction
             ^subaction.name <op-name>
             ^subaction-structure <struct>)
-->
  (<struct> ^op_name <op-name>)
}

# Copy the action handle and create a new subaction handle
sp {learn-task-subaction*apply*create-subaction*copy*handle
  (state <s> ^name learn-task-subaction
             ^operator.name create-subaction
             ^subaction.action-handle <handle>
             ^subaction-structure <struct>)
-->
  (<struct> ^action-handle <handle>
            ^handle (make-constant-symbol |subaction|))
}

# Copy the action-type
sp {learn-task-subaction*apply*create-subaction*copy*action-type
  (state <s> ^name learn-task-subaction
             ^operator.name create-subaction
             ^subaction.action-type <type>
             ^subaction-structure <struct>)
-->
   (<struct> ^action-type <type>)
}

# Copy the perform flag
sp {learn-task-subaction*apply*create-subaction*copy*perform
  (state <s> ^name learn-task-subaction
             ^operator.name create-subaction
             ^subaction-id.perform <perf>
             ^subaction-structure <struct>)
-->
  (<struct> ^perform <perf>)
}



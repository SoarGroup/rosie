sp {op_pick-up1*tabletop*propose*report-success
   (state <s> ^name op_pick-up1
              ^problem-space.action.type execute
              ^current-action.pick-up-command.status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name report-success)
}

sp {op_pick-up1*tabletop*apply*report-success*mark*finished
   (state <s> ^name op_pick-up1
              ^operator.name report-success
              ^current-action <a>)
-->
   (<a> ^status complete)
}

sp {op_pick-up1*tabletop*apply*report-success*add*grabbed
   (state <s> ^name op_pick-up1
              ^operator.name report-success
              ^target-object.predicates <preds>)
   (<preds> ^arm-status moving)
-->
   (<preds> ^arm-status moving -
            ^arm-status grabbed)
}

sp {op_pick-up1*tabletop*apply*report-success*move*belief*to*arm
   (state <s> ^name op_pick-up1
              ^operator.name report-success
              ^target-object.svs-obj.id <id>
              ^io.input-link.self.pose <pose>
              ^top-state.svs.command <cmd>)
   (<pose> ^x <x> ^y <y> ^z <z>)
-->
   (<cmd> ^set_transform <xform>)
   (<xform> ^id <id>
            ^position <pos>)
   (<pos> ^x <x> ^y <y> ^z <z>)
}

sp {op_pick-up1*tabletop*apply*report-success*remove*commands
   (state <s> ^name op_pick-up1
              ^current-action <a>
              ^operator.name report-success
              ^io.output-link <out>)
   (<a> ^{ <cmd-type> << pick-up-command reset-command >> } <cmd>)
   (<out> ^<cmd-name> <cmd>)
-->
   (<a> ^<cmd-type> <cmd> -)
   (<out> ^<cmd-name> <cmd> -)
}

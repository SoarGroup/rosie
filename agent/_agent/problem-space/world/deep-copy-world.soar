sp {anystate*deep-copy-world*elaborate*copy
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^source <src>)
-->
   (<dcw> ^copy <new-world>
          ^copy-objects-map <objs-map>
          ^copy-predicates-map <preds-map>
          ^copy-set-map <set-map>)
   (<new-world> ^objects <objs>
                ^predicates <preds>
                ^robot <robot>)
   (<robot> ^arm <arm>)
}

###### COPY ROBOT ######

sp {anystate*deep-copy-world*copy*robot*current-location
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^source.robot.current-location <src-loc>
          ^copy.robot <robot>
          ^copy-objects-map.entry <e>)
   (<e> ^source <src-loc>
        ^copy <loc>)
-->
   (<robot> ^current-location <loc>)
}

sp {anystate*deep-copy-world*copy*robot*arm
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^source.robot.arm.<att-name> <att-val>
          ^copy.robot.arm <arm>)
-->
   (<arm> ^<att-name> <att-val>)
}

##### COPY OBJECTS #####

sp {anystate*deep-copy-world*copy*object
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^source.objects.object <src-obj>
          ^copy.objects <objs>
          ^copy-objects-map <map>)
   (<src-obj> ^item-type <type>)
-->
   (<objs> ^object <new-obj>)
   (<new-obj> ^item-type <type>
              ^predicates <preds>)
   (<map> ^entry <e>)
   (<e> ^source <src-obj>
        ^copy <new-obj>)
}

sp {anystate*deep-copy-world*copy*object*handle
   (state <s> ^deep-copy-world.copy-objects-map.entry <e>)
   (<e> ^source <src-obj>
        ^copy <obj>)
   (<src-obj> ^handle <handle>)
-->
   (<obj> ^handle <handle>)
}

sp {anystate*deep-copy-world*copy*object*linked-to
   (state <s> ^deep-copy-world.copy-objects-map <map>)
   (<map> ^entry <e1> <e2>)
   (<e1> ^source <src-obj1>
        ^copy <obj1>)
   (<src-obj1> ^linked-to <src-obj2>)
   (<e2> ^source <src-obj2>
         ^copy <obj2>)
-->
   (<obj1> ^linked-to <obj2>)
}

sp {anystate*deep-copy-world*copy*object*predicates
   (state <s> ^deep-copy-world.copy-objects-map.entry <e>)
   (<e> ^source.predicates <src-preds>
        ^copy.predicates <preds>)
   (<src-preds> ^<pred-name> <pred-val>)
-->
   (<preds> ^<pred-name> <pred-val>)
}

##### COPY PREDICATES #####

sp {anystate*deep-copy-world*copy*predicate
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^source.predicates.predicate <src-pred>
          ^copy.predicates <preds>
          ^copy-predicates-map <map>)
-->
   (<preds> ^predicate <new-pred>)
   (<map> ^entry <e>)
   (<e> ^source <src-pred>
        ^copy <new-pred>)
}

sp {anystate*deep-copy-world*copy*predicate*handle
   (state <s> ^deep-copy-world.copy-predicates-map.entry <e>)
   (<e> ^source.handle <pred-handle>
        ^copy <pred>)
-->
   (<pred> ^handle <pred-handle>)
}

sp {anystate*deep-copy-world*copy*predicate*instance*size*1*set
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^copy-predicates-map.entry <e>
          ^copy-set-map <set-map>)
   (<e> ^source <src-pred>
        ^copy <pred>)
   (<src-pred> ^instance <src-instance>)
   (<src-instance> ^1 <src-set> -^2)
   (<src-set> -^item-type)
-->
   (<pred> ^instance <new-instance>)
   (<new-instance> ^1 <new-set>)
   (<set-map> ^entry <new-e>)
   (<new-e> ^source <src-set>
             ^copy <new-set>)
}

sp {anystate*deep-copy-world*copy*predicate*instance*size*1*set*object
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^copy-set-map.entry <set-e>
          ^copy-objects-map.entry <obj-e>)
   (<set-e> ^source <src-set>
            ^copy <set>)
   (<src-set> ^<elem> <src-obj>)
   (<obj-e> ^source <src-obj>
            ^copy <obj>)
-->
   (<set> ^<elem> <obj>)
}

sp {anystate*deep-copy-world*copy*predicate*instance*size*2
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^copy-predicates-map.entry <pred-e>
          ^copy-objects-map.entry <obj1-e>
          ^copy-objects-map.entry <obj2-e>)
   (<pred-e> ^source <src-pred>
             ^copy <pred>)
   (<src-pred> ^instance <src-i>)
   (<src-i> ^1 <src-obj1> ^2 <src-obj2> -^3)
   (<obj1-e> ^source <src-obj1>
             ^copy <obj1>)
   (<obj2-e> ^source <src-obj2>
             ^copy <obj2>)
-->
   (<pred> ^instance <new-i>)
   (<new-i> ^1 <obj1> ^2 <obj2>)
}

sp {anystate*deep-copy-world*copy*predicate*instance*size*3
   (state <s> ^deep-copy-world <dcw>)
   (<dcw> ^copy-predicates-map.entry <pred-e>
          ^copy-objects-map.entry <obj1-e>
          ^copy-objects-map.entry <obj2-e>
          ^copy-objects-map.entry <obj3-e>)
   (<pred-e> ^source <src-pred>
             ^copy <pred>)
   (<src-pred> ^instance <src-i>)
   (<src-i> ^1 <src-obj1> ^2 <src-obj2> ^3 <src-obj3> -^4)
   (<obj1-e> ^source <src-obj1>
             ^copy <obj1>)
   (<obj2-e> ^source <src-obj2>
             ^copy <obj2>)
   (<obj3-e> ^source <src-obj3>
             ^copy <obj3>)
-->
   (<pred> ^instance <new-i>)
   (<new-i> ^1 <obj1> ^2 <obj2> ^3 <obj3>)
}
# init-smem.soar
# Preloads information into semantic memory that has to do with learning about nouns and adjectives

sp {top-state*propose*store-object-properties-smem
   (state <s> ^top-state <s>
              ^name <name>
              ^agent-info <info>)
   (<info> -^stored-object-properties)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name store-object-properties-smem)
}

sp {top-state*elaborate*agent-info*stored-properties
   (state <s> ^top-state <s>
              ^agent-info <info>)
-->
   (<info> ^stored-properties <props>)
}

# !!!!!!!!!!!!!!!!!! README !!!!!!!!!!!!!!!!!!!!!!!
# This is the only rule you should edit for object properties
# Simple add or remove adjectives from each property
# The correct representations will be placed into smem automatically

sp {top-state*elaborate*store-object-properties-smem*info
   (state <s> ^top-state <s>
              ^operator <o> +)
   (<o> ^name store-object-properties-smem)
-->
   (<o> ^properties-to-store <p>)
   (<p> ^property <color> <shape> <size> <name> <generic> <weight> <temp>)
   (<color> ^property-name color
            ^property-type visual)
   (<shape> ^property-name shape
            ^property-type visual)
   (<size> ^property-name size
           ^property-type visual)
   (<name> ^property-name name
           ^property-type linguistic)
   (<generic> ^property-name generic
              ^property-type linguistic)
   (<weight> ^property-name weight
             ^property-type measurable
             ^measure-location scale
             ^superlative.highest heavy-est 
             ^superlative.lowest light-est)
   (<temp> ^property-name temperature
           ^property-type measurable
           ^measure-location thermometer
           ^superlative.highest hot-est
           ^superlative.lowest cold-est)
   # EDIT THESE VALUES BELOW:
	(<color> ^adjective red orange yellow green blue purple white)
 	(<shape> ^adjective rectangle triangle arch square sphere l-block crescent)
  	(<size> ^adjective small medium large)
    (<name> ^adjective stove pantry garbage table dishwasher scale thermometer
           ^adjective grid-red grid-orange grid-yellow grid-green grid-blue
           ^adjective home goal open-spot
           ^adjective bin-1 bin-2 bin-3 bin-4 bin-5 bin-6
           ^adjective steak plate)
    (<generic> ^adjective one object block)
    (<weight> ^adjective heavy moderate light)
    (<temp> ^adjective hot warm cool cold)
}
   
# Generate smem representation for visual and linguistic properties
sp {top-state*elaborate*store-object-properties-smem*visual*linguistic*property*rep
   (state <s> ^top-state <s>
              ^operator <o> +)
   (<o> ^name store-object-properties-smem
        ^properties-to-store.property <p>)
   (<p> ^property-name <name>
        ^property-type { <type> << visual linguistic >> })
-->
   (<p> ^smem-rep <rep>)
   (<rep> ^wme-type property ^name <name> ^type <type>)
}

# Generate smem representations for measurable properties
sp {top-state*elaborate*store-object-properties-smem*measurable*property*rep
   (state <s> ^top-state <s>
              ^operator <o> +)
   (<o> ^name store-object-properties-smem
        ^properties-to-store.property <p>)
   (<p> ^property-name <name>
        ^property-type measurable
        ^measure-location <loc>)
-->
   (<p> ^smem-rep <rep>)
   (<rep> ^wme-type property ^name <name> ^type measurable ^location <loc>)
}


# Store the properties in smem
sp {top-state*apply*store-object-properties-smem*store*properties
   (state <s> ^top-state <s>
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-object-properties-smem
        ^properties-to-store.property.smem-rep <prop>)
-->
   (<cmd> ^store <prop>)
}

# Generate and store smem representations for linguistic adjectives
sp {top-state*apply*store-object-properties-smem*store*adjectives*linguistic
   (state <s> ^top-state <s>
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-object-properties-smem
        ^properties-to-store.property <prop>)
   (<prop> ^smem-rep <prop-rep>
           ^property-type linguistic
           ^adjective <adj>)
-->
   (<cmd> ^store <word> <sense>)
   (<word> ^wme-type word ^word <adj> ^sense <sense>)
   (<sense> ^wme-type sense ^word <word> ^type adjective ^property <prop-rep>)
}

# Generate and store smem representations for visual and measurable adjectives
sp {top-state*apply*store-object-properties-smem*store*adjectives*visual*measurable
   (state <s> ^top-state <s>
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-object-properties-smem
        ^properties-to-store.property <prop>)
   (<prop> ^smem-rep <prop-rep>
           ^property-name <prop-name>
           ^property-type << measurable visual >>
           ^adjective <adj>)
-->
   (<cmd> ^store <word> <sense>)
   (<word> ^wme-type word ^word <adj> ^sense <sense>)
   (<sense> ^wme-type sense ^word <word> ^type adjective ^property <prop-rep> ^label (concat <prop-name> |-| <adj>))
}


# Generate and store smem representations for superlatives
sp {top-state*apply*store-object-properties-smem*store*superlatives
   (state <s> ^top-state <s>
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-object-properties-smem
        ^properties-to-store.property <prop>)
   (<prop> ^smem-rep <prop-rep>
           ^property-name <prop-name>
           ^property-type measurable
           ^superlative.<type> <sup>)
-->
   (<cmd> ^store <word> <sense>)
   (<word> ^wme-type word ^word <sup> ^sense <sense>)
   (<sense> ^wme-type sense ^word <word> ^type superlative ^property <prop-rep> ^value <type>)
}

# Mark that the properties have been stored
sp {top-state*apply*store-object-properties-smem*mark*stored
   (state <s> ^top-state <s>
              ^operator.name store-object-properties-smem
              ^smem.result.success
              ^agent-info <info>)
-->
   (<info> ^stored-object-properties true)
}

sp {top-state*apply*store-object-properties-smem*copy*lti
   (state <s> ^top-state <s>
              ^operator.name store-object-properties-smem
              ^agent-info.stored-properties <props>
              ^smem.result.success <lti>)
-->
   (<props> ^retrieved <lti>)
}


sp {learn-goal-demonstration*elaborate*copy-down-attrs
   (state <s> ^name learn-goal-demonstration
              ^superstate.operator <o>)
   (<o> ^type <type>)
-->
   (<s> ^type <type>)
}

sp {convert-TCN-to-NL*elaborate*operator-attrs
   (state <s> ^name convert-TCN-to-NL
              ^superstate.operator <o>)
   (<o> ^name convert-TCN-to-NL
        ^param <params>
        ^state-rep <state-rep>
        ^tcn <tcn>)
-->
   (<s> ^state-rep <state-rep>
        ^description <d>
        ^param <params>
        ^count 1
        ^tcn <tcn>)
}

sp {convert-state-to-TCN*elaborate*operator-attrs
   (state <s> ^name convert-state-to-TCN
              ^superstate.operator <o>)
   (<o> ^name convert-state-to-TCN
        ^state-rep <state-rep>)
-->
   (<s> ^state-rep <state-rep>
        ^tcn <tcn>
        ^param <p>
        ^arg 1)
   (<tcn> ^conditions <co> ^parameters <pa> ^name default)
   (<co> ^cond <cond>)
}

sp {extract-state*elaborate*operator-attrs
   (state <s> ^name extract-state
              ^superstate.operator <o>)
   (<o> ^name extract-state
        ^type <type>
        ^category svs)
-->
   (<s> ^relationships <rels>
        ^object-desc-set <ods>
        ^object-set <objects>)
}

sp {state-diff*elaborate*operator-attrs
   (state <s> ^name state-diff
              ^superstate.operator <o>)
   (<o> ^name state-diff
        ^initial <istate>
        ^final <fstate>)
-->
   (<s> ^initial <istate>
        ^final <fstate>
        ^relationships <rels>
        ^extra-relationships <erels>
        ^object-set <os>
        ^object-desc-set <ods>)
}
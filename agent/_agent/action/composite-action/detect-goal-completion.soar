### goal success for track-relation
sp {composite-success*detect*track-relation*success
   (state <s> ^problemspace action
   	      ^desired <d>
   	      ^track-relation <tr>
	      -^action-status learning)
   (<d> ^track-relation <dtr>
   	-^achieve-state)
   (<dtr> ^p1 <val1>
   	  ^p2 <val2>
	  ^relation <prep>)
   (<tr> ^object1 <val3>
	 ^object2 <val4>
	 ^vpredicate <prep>
	 ^complete success)
   (<val1> ^focus <fval1>)
   (<val2> ^focus <fval2>)
   (<val3> ^focus <fval1>)
   (<val4> ^focus <fval2>)
-->
   (<s> ^success <d>)
}

sp {new-operator*detect*two*achieve-state-predicate*success
   (state <s> ^problemspace action
   	      ^desired <d>
   	      ^<state> <sobj>
	      ^<state2> <sobj2>
	      ^track-relation <tr>
	      -^action-status learning)
   (<d> ^achieve-state <asp>
	^achieve-state {<asp2> <> <asp>}
	^track-relation <dtr>)
   (<asp> ^p1 <obj>
   	  ^state <state>)
   (<asp2> ^p1 <obj2>
   	  ^state <state2>)
   (<dtr> ^p1 <val12>
   	  ^p2 <val22>
	  ^relation <prep>)
   (<tr> ^object1 <val32>
	 ^object2 <val42>
	 ^vpredicate <prep>
	 ^complete success)
   (<sobj> ^focus <f1>)
   (<obj> ^focus <f1>)
   (<sobj2> ^focus <f2>)
   (<obj2> ^focus <f2>)
   (<val12> ^focus <fval12>)
   (<val22> ^focus <fval22>)
   (<val32> ^focus <fval12>)
   (<val42> ^focus <fval22>)
-->
   (<s> ^success <d>)
}

sp {new-operator*detect*achieve-state-predicate*success
   (state <s> ^problemspace action
   	      ^desired <d>
   	      ^<state> <sobj>
	      -^action-status learning)
   (<d> ^achieve-state <asp>
   	-^track-relation
	-^achieve-state {<asp2> <> <asp>})
   (<asp> ^p1 <obj>
   	  ^state <state>)
   (<sobj> ^focus <f1>)
   (<obj> ^focus <f1>)
-->
   (<s> ^success <d>)
}

sp {new-operator*detect*achieve-state-predicate*track-relation-success
   (state <s> ^problemspace action
   	      ^desired <d>
   	      ^<state> <sobj>
	      ^track-relation <tr>
	      -^action-status learning)
   (<d> ^achieve-state <asp>
   	^track-relation <dtr>
        -^achieve-state {<asp2> <> <asp>})
   (<asp> ^p1 <obj>
   	  ^state <state>)
   (<sobj> ^focus <f1>)
   (<obj> ^focus <f1>)
   (<dtr> ^p1 <val1>
   	  ^p2 <val2>
	  ^relation <prep>)
   (<tr> ^complete success
   	 ^object1 <val3>
	 ^object2 <val4>
	 ^vpredicate <prep>)
   (<val1> ^focus <fval1>)
   (<val2> ^focus <fval2>)
   (<val3> ^focus <fval1>)
   (<val4> ^focus <fval2>)
-->
   (<s> ^success <d>)
}

sp {op_put-down*propose*internal-put-down
   (state <s> ^name op_put-down1
              ^held-object <obj>
              ^top-state.world.robot.arm.state internal)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-put-down
        ^object <obj>)
}

sp {op_put-down*skip*ask
   (state <s> ^name op_put-down1
               ^top-state.agent-params.confirm-arm-actions no
              ^current-action <a>)
-->
   (<a> ^received-yes true)
}

sp {op_put-down*apply*internal-put-down*send-confirmation
   (state <s> ^name op_put-down1
              ^operator <o>
               ^top-state.agent-params.confirm-arm-actions no
              ^current-action.received-yes true
              ^top-state.interaction.changes <c>
              ^top-state.agent-params.arm-simulation-level none)
   (<o> ^name internal-put-down)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type simple-message
          ^message-type confirm-put-down)
}

sp {op_put-down*apply*internal-put-down*send-message
   (state <s> ^name op_put-down1
              ^operator <o>
               ^top-state.agent-params.confirm-arm-actions yes
              ^current-action <a>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-put-down
        ^object.predicates.{ << name shape >> } <shape>)
   (<a> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type give-item-request
          ^item <shape>)
   (<a> ^sent-message true)
}

sp {op_put-down*apply*internal-put-down*mark*complete
   (state <s> ^name op_put-down1
              ^operator.name internal-put-down
              ^current-action <a>
              ^top-state.action-stack.changes <c>)
   (<a> ^received-yes true)
-->
   (<c> ^pop <a>)
}

sp {op_put-down*apply*internal-put-down*remove*grabbed
   (state <s> ^name op_put-down1
              ^operator <o>
              ^current-action.received-yes true)
   (<o> ^name internal-put-down
        ^object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -)
}

sp {op_put-down*apply*internal-put-down*add*in*relation
   (state <s> ^name op_put-down1
              ^operator <o>
              ^top-state.world <world>)
   (<o> ^name internal-put-down
        ^object <obj>)
   (<world> ^robot.current-location <loc>
            ^predicates.predicate <in-pred>)
   (<in-pred> ^handle in1)
-->
   (<in-pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}

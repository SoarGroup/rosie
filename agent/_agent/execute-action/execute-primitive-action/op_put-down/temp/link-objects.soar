sp {op_put-down*propose*link-objects
   (state <s> ^name op_put-down1
              ^held-object <obj>)
   (<obj> ^handle <dest-handle>
          ^found-as <src>)
   (<src> ^handle <src-handle>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^type link
          ^destination-handle <dest-handle>
          ^source-handle <src-handle>)
}
          
sp {op_put-down*apply*link-objects*send*modify-scene*command
   (state <s> ^name op_put-down1
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
-->
   (<out> ^modify-scene <cmd>)
}

sp {op_put-down*apply*link-objects*found-as*mark-for-deletion
   (state <s> ^name op_put-down1
              ^operator.name link-objects
              ^held-object.found-as <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^wm-obj <obj>)
-->
   (<obj-info> ^mark-for-deletion true)
}
   

sp {op_put-down*apply*link-objects*remove*missing*flag
   (state <s> ^name op_put-down1
              ^operator.name link-objects
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^held-object <obj>)
   (<obj-info> ^wm-obj <obj>
               ^missing true)
-->
   (<obj-info> ^missing true -)
}

sp {op_put-down*apply*link-objects*remove*found-as*flag
   (state <s> ^name op_put-down1
              ^operator.name link-objects
              ^held-object <obj>)
   (<obj> ^found-as <other>)
-->
   (<obj> ^found-as <other> -)
}

sp {op_put-down*apply*link-objects*monitor*perception
   (state <s> ^name op_put-down1
              ^operator <o>
              ^top-state.agent-params.debug-mode on
              ^top-state.agent-params.debug-perception true)
   (<o> ^name link-objects
        ^modify-scene-command <cmd>)
   (<cmd> ^source-handle <src>
          ^destination-handle <dest>)
-->
   (write |Linking object | <src> | to | <dest> (crlf))
}
        
        

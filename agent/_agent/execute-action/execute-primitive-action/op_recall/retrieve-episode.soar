sp {op_recall*propose*retrieve-episode
   (state <s> ^name op_recall1
              ^epmem-cue <q>
             -^retrieved-episode)
-->
   (<s> ^operator <o> + )
   (<o> ^name retrieve-episode
        ^query <q>)
}
sp {op_recall*apply*retrieve-episode*do*query
   (state <s> ^name op_recall1
              ^operator <o>
              ^epmem.command <cmd>)
   (<o> ^name retrieve-episode
        ^query <q>)
-->
   (<cmd> ^query <q>)
}

sp {op_recall*apply*retrieve-episodie*print*result
   (state <s> ^name op_recall1
              ^operator.name retrieve-episode
              ^agent-params.debug-actions true
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^command <cmd>)
  (<res> ^normalized-match-score)
-->
  (write (cmd print <res> -d 2) (crlf))
}

sp {op_recall*apply*retrieve-episode*success
   (state <s> ^name op_recall1
              ^operator.name retrieve-episode
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^command <cmd>)
   (<res> ^success <q>
          ^retrieved <episode>
          ^graph-match 1
          ^normalized-match-score 1.0)
   (<cmd> ^query <q>)
-->
   (<s> ^retrieved-episode <episode>)
   (<cmd> ^query <q> -)
}

sp {op_recall*apply*retrieve-episode*success*elaborate*object*epmem*mapping
   (state <s> ^name op_recall1
              ^operator.name retrieve-episode
              ^query-object <qo>
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^command <cmd>)
   (<res> ^success <q>
          ^retrieved <episode>
          ^mapping.node <node>
          ^graph-match 1
          ^normalized-match-score 1.0)
   (<node> ^cue <cue>
           ^retrieved <ret>)
   (<qo> ^cue <cue>)
-->
   (<qo> ^epmem <ret>)
}


sp {op_recall*apply*retrieve-episode*failure*no*exact*match
   (state <s> ^name op_recall1
              ^operator.name retrieve-episode
              ^epmem <epmem>)
   (<epmem> ^result.normalized-match-score < 1.0
            ^command <cmd>)
   (<cmd> ^query <q>)
-->
   (<s> ^retrieved-episode none)
   (<cmd> ^query <q> -)
}

sp {op_recall*apply*retrieve-episode*failure*no*graph-match
   (state <s> ^name op_recall1
              ^operator.name retrieve-episode
              ^epmem <epmem>)
   (<epmem> ^result.graph-match 0
            ^command <cmd>)
   (<cmd> ^query <q>)
-->
   (<s> ^retrieved-episode none)
   (<cmd> ^query <q> -)
}
        
sp {op_recall*apply*retrieve-episode*failure*no*match
   (state <s> ^name op_recall1
              ^operator.name retrieve-episode
              ^epmem <epmem>)
   (<epmem> ^result.failure
            ^command <cmd>)
   (<cmd> ^query <q>)
-->
   (<s> ^retrieved-episode none)
   (<cmd> ^query <q> -)
}

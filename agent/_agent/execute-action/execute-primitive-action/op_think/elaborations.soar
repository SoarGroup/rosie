sp {op_think1*elaborate*target-object*arg1
   (state <s> ^name op_think1
              ^action-operator.arg1.{2} <obj>)
-->
   (<s> ^target-object <obj>)
}

sp {op_think1*elaobrate*link-name*arg2
   (state <s> ^name op_think1
              ^action-operator.arg2.{2}.predicates.{ << spatial-shape modifier1 >> } <name>)
-->
   (<s> ^link-name <name>)
}

sp {op_think1*elaborate*smem-query*smem-object*instance
   (state <s> ^name op_think1
              ^target-object <obj>)
   (<obj> ^in-smem true
          ^handle <handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^depth 2 
        ^cue <cue>)
   (<cue> ^handle <handle>)
}

sp {op_think1*elaborate*smem-object*from*query*result
   :o-support
   (state <s> ^name op_think1
              ^smem-query.result <res>
              ^target-object.handle <handle>)
   (<res> ^handle <handle>)
-->
   (<s> ^smem-info <res>)
}

sp {op_think1*elaborate*smem-query*smem-object*instance*predicate
   (state <s> ^name op_think1
              ^target-object <obj>)
   (<obj> -^in-smem true
          ^predicates.{ << spatial-shape shape >> } <pred>)
-->
   (<s> ^smem-query <q>)
   (<q> ^depth 2 
        ^cue <cue>)
   (<cue> ^item-type object-info ^shape <pred>)
}

sp {op_think1*elaborate*smem-object*from*query*result*instance
   :o-support
   (state <s> ^name op_think1
              ^smem-query.result <res>)
   (<res> ^item-type object-info)
-->
   (<s> ^smem-info <res>)
}

sp {op_think1*elaborate*location*from*smem-info
   (state <s> ^name op_think1
              ^link-name <name>
              ^smem-info.<name> <loc>)
   (<loc> ^item-type object
          ^object-instance true)
-->
   (<s> ^retrieved-location <loc>)
}


sp {op_follow-wall1*elaborate*until-clause*from*superoperator
   (state <s> ^name op_follow-wall1
              ^action-operator.until-clause <term>)
-->
   (<s> ^until-clause <term>)
}

### direction-handle
# Which wall to follow (left or right)

sp {op_follow-wall1*elaborate*direction-handle*from*action-operator
   (state <s> ^name op_follow-wall1
              ^action-operator.arg1.predicates.relative-direction1 <dir-handle>)
-->
   (<s> ^direction-handle <dir-handle>)
}
                       
# default is left
sp {op_follow-wall1*elaborate*direction-handle*default
   (state <s> ^name op_follow-wall1
              -^action-operator.arg1.predicates.relative-direction1 <dir-handle>)
-->
   (<s> ^direction-handle left1)
}

### direction-id
# The LTI with information about the wall direction
sp {op_follow-wall1*elaborate*smem-query-cue*direction
   (state <s> ^name op_follow-wall1
              ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
	(<cue> ^handle <dir-handle>)
}

sp {op_follow-wall1*elaborate*direction-id*from*retrieval
   (state <s> ^name op_follow-wall1
			  ^direction-handle <dir-handle>
              ^smem-query.result <dir-id>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}

### wall-side
# Elaborated from the direction-id, either 1 or -1
sp {op_follow-wall*elaborate*wall-side*from*direction-id
   (state <s> ^name op_follow-wall1
              ^direction-id.side <side>)
-->
   (<s> ^wall-side <side>)
}

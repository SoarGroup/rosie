sp {ground-referent*propose*index-object-properties
   (state <s> ^name ground-referent
              ^candidate-set <cs>
              ^property-constraint <pc>)
   (<cs> ^generated-candidates true
        -^satisfied-property <pc>)
-->
   #(interrupt)
   (<s> ^operator <op> + =)
   (<op> ^name index-object-properties
         ^property-constraint <pc>)
}

sp {ground-referent*apply*index-object-properties*violates
   (state <s> ^operator <o>
              ^candidate-set <cs>)
   (<cs> ^proto-referent <pr>)
   (<o> ^name index-object-properties
        ^property-constraint <pc>)
   (<pc> ^property { <prop-name> <> generic }
         ^value <prop-val>)
   (<pr> ^object.predicates <preds>)
   (<preds> -^<prop-name> <prop-val>)
-->
   (<pr> ^violates <pc>)
   (<cs> ^fail-tested <pc>)
}

sp {ground-referent*apply*index-object-properties*passes
   (state <s> ^operator <o>
              ^candidate-set <cs>)
   (<cs> ^proto-referent <pr>)
   (<o> ^name index-object-properties
        ^property-constraint <pc>)
   (<pc> ^property <prop-name>
         ^value <prop-val>)
   (<pr> ^object.predicates.<prop-name> <prop-val>)
-->
   (<pr> ^passes <pc>)
   (<cs> ^success-tested <pc>)
}

sp {ground-referent*apply*index-object-properties*mark*checked
   (state <s> ^candidate-set <cs>
              ^operator <o>)
   (<o> ^name index-object-properties
        ^property-constraint <pc>)
   (<cs> ^success-tested <pc>)
-->
   (<cs> ^satisfied-property <pc>)
}

sp {ground-referent*apply*index-object-properties*mark*checked*failed
   (state <s> #^constraint-count <cc>
              ^candidate-set <cs>
              ^operator <o>)
   (<o> ^name index-object-properties
        ^property-constraint <pc>)
   (<cs> -^success-tested <pc>
         ^fail-tested <pc>)
-->
   (<cs> ^satisfied-property <pc>)
}

sp {ground-referent*apply*index-object-properties*mark*satisfied
   (state <s> ^candidate-set.proto-referent <pr>
              ^operator <o>)
   (<o> ^name index-object-properties
        ^property-constraint <pc>)
   (<pr> ^passes <pc>
         ^constraint-count <cc>)
-->
   (<pr> ^constraint-count <cc> - (- <cc> 1))
}

sp {ground-referent*apply*index-object-properties*remove-violated
   (state <s> ^candidate-set <cs>
              ^operator <o>)
   (<o> ^name index-object-properties
        ^property-constraint <pc>)
   (<cs> ^proto-referent <pr>)
   (<pr> ^violates <pc>)
-->
   (<cs> ^proto-referent <pr> -)
}
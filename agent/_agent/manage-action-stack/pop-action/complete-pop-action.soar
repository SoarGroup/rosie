# complete-pop-action - takes care of thing that needs to be done when popping the action

sp {pop-action*propose*complete-pop-action
   (state <s> ^name pop-action)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-pop-action)
}

# Remove the action from the top of the stack
sp {pop-action*apply*complete-pop-action*pop
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-stack <stack>
              ^action-to-pop <action>)
   (<stack> ^top <action>)
   (<action> ^prev <prev>)
-->
   (<stack> ^top <action> -
            ^top <prev>)
  (<action> ^next none -)
  (<prev> ^next <action> -
           ^next none)
}

# If the action was the bottom, change it to none
sp {pop-action*apply*complete-pop-action*change*bottom
  (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-stack <stack>
              ^action-to-pop <action>)
  (<stack> ^bottom <action>)
-->
  (<stack> ^bottom <action> -
            ^bottom none)
}


# Remove the pop change from the changes list
sp {pop-action*apply*complete-pop-action*clear*changes
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-stack.changes <changes>
              ^action-to-pop <action>)
   (<changes> ^pop <action>)
-->
   (<changes> ^pop <action> -)
}

# Report an action-event as having been performed if the action was successful
sp {pop-action*apply*complete-pop-action*report*performed*action-event
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-to-pop <action>
              ^top-state.interaction.status <status>)
   (<action> ^status success
             ^execution-operator.action-handle <action-handle>)
-->
   (<status> ^performed.action-event.type <action-handle>)
}

# Report an execution failure if the action failed (bottom action)
sp {pop-action*apply*complete-pop-action*report*execution*failure*bottom
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-to-pop <action>
              ^action-stack.bottom <action>
              ^top-state.interaction.status <status>)
   (<action> ^status failure
             ^execution-failure <type>
             ^failure-info <info>)
-->
   (<status> ^failure <f>)
   (<f> ^type execution-failure
        ^failure-type <type>
        ^failure-info <info>
        ^bottom-action true)
}

# Report an execution failure if the action failed (not bottom action)
sp {pop-action*apply*complete-pop-action*report*execution*failure*not*bottom
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-to-pop <action>
              ^action-stack.bottom <> <action>
              ^top-state.interaction.status <status>)
   (<action> ^status failure
             ^execution-failure <type>
             ^failure-info <info>)
-->
   (<status> ^failure <f>)
   (<f> ^type execution-failure
        ^failure-type <type>
        ^failure-info <info>
        ^bottom-action false)
}

# If there is a policy-learning-needed flag, 
#   push a retrospective learning segment onto the interaction stack
sp {pop-action*apply*complete-pop-action*push*learning*segment
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-to-pop <action>
              ^top-state.interaction.changes <changes>)
   (<action> ^policy-learning-needed true)
-->
   (<changes> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type retrospective-learning
        ^parameters <params>
        ^satisfaction.learning-event.type retrospective)
   (<params> ^action <action>)
}

# Remove anything remembered during the action
sp {pop-action*apply*complete-pop-action*remove*remembered*things
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-to-pop.remembered <rem>)
   (<rem> ^object.predicates <preds>
          ^attribute <att>
          ^value <val>)
   (<preds> ^<att> <val>)
-->
   (<preds> ^<att> <val> -)
}

sp {pop-action*apply*complete-pop-action*remove*object-set*bottom
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-to-pop <a>
              ^action-stack.bottom <a>
              ^action-stack.object-set <objs>)
   (<objs> ^object <obj>)
-->
   (<objs> ^object <obj> -)
}
   
   
## Remove any objects added to the world during the action
#sp {pop-action*apply*complete-pop-action*remove*added*objects
#   (state <s> ^name pop-action
#              ^operator.name complete-pop-action
#              ^action-to-pop.added <obj>
#              ^top-state.world.objects <objs>)
#   (<objs> ^object <obj>)
#-->
#   (<objs> ^object <obj> -)
#}
#
## Remove any predicates related to that added object
#sp {pop-action*apply*complete-pop-action*remove*added*predicates
#   (state <s> ^name pop-action
#              ^operator.name complete-pop-action
#              ^action-to-pop.added <obj>
#              ^top-state.world.predicates.predicate <pred>)
#   (<pred> ^instance <i>)
#   (<i> ^<any> <obj>)
#-->
#   (<pred> ^instance <i> -)
#}

# If the execution operator has the flag ^perform once,
#   mark it performed in the parent action
sp {pop-action*apply*complete-pop-action*mark*performed*once*subaction
   (state <s> ^name pop-action
              ^operator.name complete-pop-action
              ^action-to-pop.execution-operator <op>
              ^action-to-pop.prev.execution-operator <parent-op>)
   (<op> ^subaction-handle <sub-handle>
         ^perform once)
-->
   (<parent-op> ^performed <sub-handle>)
}

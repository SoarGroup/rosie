sp {evaluate-stability*propose*mark-stable
   (state <s> ^name evaluate-stability
              ^obj-is-stable true
              ^monitor-object <mon-obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-stable
        ^category perception
        ^monitor-object <mon-obj>)
}

sp {evaluate-stability*apply*mark-stable*mark*unidentified
   (state <s> ^name evaluate-stability
              ^operator <o>)
   (<o> ^name mark-stable
        ^monitor-object <mon-obj>)
   (<mon-obj> ^status uncertain)
-->
   (<mon-obj> ^status uncertain -
              ^status unidentified)
}

sp {evaluate-stability*apply*mark-stable*cleanup*object
   (state <s> ^name evaluate-stability
              ^operator <o>)
   (<o> ^name mark-stable
        ^monitor-object <mon-obj>)
   (<mon-obj> ^number-checks <num>
             ^stability-check-time <t>)
-->
   (<mon-obj> ^number-checks <num> -
             ^stability-check-time <t> -)
}

sp {evaluate-stability*apply*mark-stable*remove*filters
   (state <s> ^name evaluate-stability
              ^operator <o>
              ^topstate.svs.command <cmd>)
   (<o> ^name mark-stable
        ^monitor-object <obj>)
   (<obj> ^perception-obj.id <id>
          ^position-monitor <old-pos>
          ^volume-monitor <old-vol>)
-->
   (<cmd> ^extract <old-pos> -
          ^extract <old-vol> -)
   (<obj> ^position-monitor <old-pos> -
          ^volume-monitor <old-vol> -)   
}
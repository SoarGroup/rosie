sp {attend-to-new-object*propose*complete-attend-to-new-object
   (state <s> ^name attend-to-new-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-attend-to-new-object
        ^category perception)
}

sp {attend-to-new-object*apply*complete-attend-to-new-object*add*to*object
   (state <s> ^name attend-to-new-object
              ^operator.name complete-attend-to-new-object
              ^io.input-link.time.seconds <t>
              ^candidates <c>
              ^monitor-object <obj>
              ^topstate.agent-params.stability-check-duration <dur>)
-->
   (<obj> ^status uncertain
          ^stability-check-time (+ <t> <dur>)
          ^number-checks 0
          ^stale-candidates <c>)
}

sp {attend-to-new-object*apply*complete-attend-to-new-object*create*filters
   (state <s> ^name attend-to-new-object
              ^operator.name complete-attend-to-new-object
              ^perception-obj.id <id>
              ^topstate.svs.command <cmd>
              ^monitor-object <obj>)
-->
   (<cmd> ^extract <pos>)
   (<pos> ^type monitor_position
          ^a <pa>)
   (<pa> ^type node
         ^id <id>)
   (<obj> ^position-monitor <pos>)
   
   (<cmd> ^extract <vol>)
   (<vol> ^type monitor_volume
          ^a <va>)
   (<va> ^type node
         ^id <id>)
   (<obj> ^volume-monitor <vol>)
}

sp {clean-house*propose*record-area
  #interrupt
   (state <s> ^name robot
              ^parameters.areas-held-in wm
              ^io.input-link.self.area <area-id>)
   (<s> ^areas.area <area>)
   (<area> -^visited yes
            ^id { <> -1 <area-id> })
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name record-area
     #    ^actions.stop <stop>
         ^area <area>)
}
 
# Set cleaned -> yes to eliminate spin.
sp {apply*record-area*room*wall*not*open
   (state <s> ^operator <op>
              ^io.input-link.area-description <ad>)
   (<ad> ^wall <w1>
         ^type room)
   (<op> ^name record-area
         ^area <area>)
   (<w1> -^open true
         ^x <x>
         ^y <y>
         ^direction <dir>)
-->
   (<area> ^wall <nw1>
           ^visited yes
                    no -
           ^cleaned no)    ### NO JEL
   (<nw1>  ^x <x> ^y <y> ^direction <dir> )
}
sp {apply*record-area*room*wall*open
   (state <s> ^operator <op>
              ^io.input-link.area-description <ad>)
   (<ad> ^wall <w1>
         ^type room)
   (<op> ^name record-area
         ^area <area>)
   (<w1> ^open true
         ^to <to>
         ^x <x>
         ^y <y>
         ^direction <dir>)
-->
   (<area> ^wall <nw1>
           ^type room
           ^visited yes
                    no -
           ^cleaned no)    ### NO JEL
   (<nw1>  ^x <x>
           ^y <y>
           ^direction <dir>
           ^open true
           ^to <to> )
}

sp {apply*record-area*door*wall
   (state <s> ^operator <op>
              ^io.input-link.area-description <ad>)
   (<ad> ^wall <w1>
         ^type door)
   (<op> ^name record-area
         ^area <area>)
   (<w1> ^x <x> ^y <y> ^direction <dir> )
-->
   (<area> ^wall <nw1>
           ^visited yes
                    no -
           ^cleaned yes)  
  (<nw1>  ^x <x> ^y <y> ^direction <dir> )
}

# voigtjr 5/13/10: These two commented out lines were in the 2009 agent. I added
# <> <wid1> when debugging this agent initially. I think my change and the lines
# from 2009 might have the same effect...
sp {apply*record-area-gateway
   (state <s> ^operator <op>
              ^io.input-link.area-description.gateway <gateway>)
   (<op> ^name record-area
         ^area <area>)
   (<area> ^id <aid>)
   (<gateway> ^x <x> ^y <y> ^direction <dir> ^id <wid1> ^to { <> <wid1> <> <aid> <tid> })
#  -{(<area> ^gateway <prev-gw>)
#    (<prev-gw> ^to <tid>)}
-->
   (<area> ^gateway <ngateway>)
   (<ngateway> ^x <x> ^y <y> ^direction <dir> ^id <wid1> ^to <tid>)
}

sp {apply*record-area-type
   (state <s> ^operator <op>
              ^io.input-link.area-description.type <type>)
   (<op> ^name record-area
         ^area <area>)
   (<area> ^id <aid>)
-->
   (<area> ^type <type>)
}

# Trying out versions below to simplify associating x y with gateways
# no need to record x y for rooms - will always go through doorway to room
sp {apply*record-area-gateway*initialize*connected-area*door
   (state <s> ^operator <op>
              ^parameters.delete-old-areas false
              ^current-location <ca>
              ^areas <areas>
              ^io.input-link.area-description <ad>)
   (<ad> ^gateway <gateway>
         ^type door)
  -{(<ad> ^wall.to <gid>)}   ## Avoid bugs in map
   (<op> ^name record-area)
   (<gateway> ^id <gid>
              ^to { <> <caid> <aid> })
  -(<areas> ^area.id <aid>)
   (<ca> ^id <caid>)
-->
   (<areas> ^area <area>)
   (<area> ^id <aid>
           ^visited no
           ^neighbor-area <ca>)
   (<ca> ^neighbor-area <area>)
}


## Record location as x, y of gateway to door
sp {apply*record-area-gateway*initialize*connected-area
   (state <s> ^operator <op>
              ^parameters.delete-old-areas false
              ^current-location <ca>
              ^areas <areas>
              ^io.input-link.area-description <ad>)
   (<ad> ^gateway <gateway>
         ^type room)
   (<op> ^name record-area)
   (<gateway> ^to { <> <caid> <aid> }
              ^x <x>
              ^y <y>)
  -(<areas> ^area.id <aid>)
   (<ca> ^id <caid>)
-->
   (<areas> ^area <area>)
   (<area> ^id <aid>
           ^cleaned  no    ### NO JEL
           ^visited no
           ^gateway-x <x>
           ^gateway-y <y>
           ^neighbor-area <ca>)
   (<ca> ^neighbor-area <area>)
}

sp {apply*record-area-gateway*initialize*connected-area*wall
   (state <s> ^operator <op>
              ^parameters.delete-old-areas false
              ^current-location <ca>
              ^areas <areas>
              ^io.input-link.area-description <ad>)
   (<ad> ^wall <wall>
         ^type room)
   (<op> ^name record-area)
   (<wall> ^to { <> <caid> <aid> }
           ^x <x>
           ^y <y>)
  -(<areas> ^area.id <aid>)
   (<ca> ^id <caid>)
-->
   (<areas> ^area <area>)
   (<area> ^id <aid>
           ^type room
           ^temp-x <x>  ## JEL 1/28/2011 - room without x y
           ^temp-y <y>
           ^gateway-x <x>
           ^gateway-y <y>
           ^cleaned no    ### NO JEL
           ^visited no
           ^neighbor-area <ca>)
   (<ca> ^neighbor-area <area>)
}

sp {apply*record-area-gateway*initialize*connected-area*already-visited*know*about
   (state <s> ^operator <op>
              ^current-location <ca>
              ^areas <areas>
              ^io.input-link.area-description <ad>)
   (<ad> ^gateway <gateway>
         ^type room)
   (<op> ^name record-area)
   (<gateway> ^id <gid>
              ^to { <> <caid> <aid> }
              ^x <x> ^y <y>)
   (<areas> ^area <area>)
   (<area> ^id <aid>)
   (<ca> ^id <caid>)
  -(<ca> ^neighbor-area <area>)
-->
   (<area> ^neighbor-area <ca>)
   (<ca> ^neighbor-area <area>)
}


sp {apply*record-area-gateway*initialize*connected-area*already-visited*know*about*through-wall
   (state <s> ^operator <op>
              ^current-location <ca>
              ^areas <areas>
              ^io.input-link.area-description <ad>)
   (<ad> ^wall <wall>
         ^type room)
   (<op> ^name record-area)
   (<wall> ^to { <> <caid> <aid> }
           ^x <x> ^y <y>)
   (<areas> ^area <area>)
   (<area> ^id <aid>)
   (<ca> ^id <caid>)
  -(<ca> ^neighbor-area <area>)
-->
   (<area> ^neighbor-area <ca>)
   (<ca> ^neighbor-area <area>)
}

sp {apply*record-area*in-gateway
   (state <s> ^operator <o>
              ^io.input-link.area-description <area>)
   (<area> ^type door)
   (<o> ^name record-area
        ^area <narea>)
-->
   (<narea> ^cleaned yes
                     no -)}

## Remove old areas if random
sp {clean-house*apply*record-area*remove
   (state <s> ^operator.name record-area
              ^parameters.delete-old-areas true
              ^top-state.areas <areas>
              ^io.input-link.self.area <area-id>)
   (<areas> ^area <area>)
   (<area> ^id <> <area-id>)
-->
   (<areas> ^area <area> -)
}
   
sp {clean-house*compare*record-area
   (state <s> ^name robot
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name << record-object record-area >>)
   (<op2> ^name << get-object deposit-object >>)
-->
   (<s> ^operator <op1> > <op2>)
}

# Send a message to the environment saying that
# this agent has seen this area.
# MMT
sp {clean-house*apply*record-area*send-text-message
   (state <s> ^name robot
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name record-area
        ^area.id <area-id>)
-->
   (<ol> ^send-message <sm>)
   (<sm> ^destination user
         ^first <w1>)
   (<w1> ^word i-see
         ^next <w2>)
   (<w2> ^word area
         ^next <w3>)
   (<w3> ^word <area-id>
         ^next nil)
}

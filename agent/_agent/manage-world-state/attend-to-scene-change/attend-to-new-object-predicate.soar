# Adds the new predicate to the wm-obj
sp {anystate*apply*attend-to-scene-change*new-object-predicate*add
   (state <s> ^operator <o>)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type new-object-predicate
        ^object-info.wm-obj.predicates <wm-preds>
        ^property-handle <prop-handle>
        ^predicate-handle <pred-handle>)
-->
   (<wm-preds> ^<prop-handle> <pred-handle>)
}

# Remove other predicates from the object with the same property
sp {anystate*apply*attend-to-scene-change*new-object-predicate*remove
   (state <s> ^operator <o>)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type new-object-predicate
        ^object-info.wm-obj.predicates <wm-preds>
        ^property-handle <prop-handle>
        ^predicate-handle <pred-handle>)
   (<wm-preds> ^<prop-handle> { <other-pred> <> <pred-handle> })
-->
   (<wm-preds> ^<prop-handle> <other-pred> -)
}

# if debug-perception is on, print a message
sp {anystate*apply*attend-to-scene-change*new-object-predicate*debug-perception*print
   (state <s> ^operator <o>
              ^top-state.agent-params.debug-perception true)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type new-object-predicate
        ^object-info.object-handle <handle>
        ^object-info.wm-obj.predicates <wm-preds>
        ^property-handle <prop-handle>
        ^predicate-handle <pred-handle>)
-->
   (write |Object | <handle> |: Adding predicate | <prop-handle> |=| <pred-handle> (crlf))
}

# if debug-perception is on, print a message for removing
sp {anystate*apply*attend-to-scene-change*new-object-predicate*remove*debug-perception*print
   (state <s> ^operator <o>
              ^top-state.agent-params.debug-perception true)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type new-object-predicate
        ^object-info.object-handle <handle>
        ^object-info.wm-obj.predicates <wm-preds>
        ^property-handle <prop-handle>
        ^predicate-handle <pred-handle>)
   (<wm-preds> ^<prop-handle> { <other-pred> <> <pred-handle> })
-->
   (write |Object | <handle> |: Removing predicate | <prop-handle> |=| <other-pred> (crlf))
}
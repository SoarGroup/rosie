# Elaborate the source object information from the superoperator
sp {create-new-object*elaborate*source*info*from*superoperator
   (state <s> ^name create-new-object
              ^superstate.operator <ssop>)
   (<ssop> ^source-type <type>
           ^source-obj <obj>)
-->
   (<s> ^source-type <type>
        ^source-obj <obj>)
}

# Elaborate smem-obj from superoperator
sp {create-new-object*elaborate*smem-obj*from*superoperator
   (state <s> ^name create-new-object
              ^superstate.operator <ssop>)
   (<ssop> ^smem-obj <obj>)
-->
   (<s> ^smem-obj <obj>)
}

sp {create-new-object*elaborate*smem-obj*source-object
   (state <s> ^name create-new-object
              ^source-type smem-obj
              ^source-obj <obj>)
-->
   (<s> ^smem-obj <obj>)
}

# Elaborate epmem-obj from source
sp {create-new-object*elaborate*epmem-obj*source-object
   (state <s> ^name create-new-object
              ^source-type epmem-obj
              ^source-obj <obj>)
-->
   (<s> ^epmem-obj <obj>)
}

# Elaborate object-monitor from superstate
sp {create-new-object*elaborate*object-monitor
   (state <s> ^name create-new-object
              ^top-state.perception-monitor.object-monitor <obj-mon>)
-->
   (<s> ^object-monitor <obj-mon>)
}

# Elaborate object-info from object-monitor
sp {create-new-object*elaborate*input-link-obj*matching*perception-obj
   (state <s> ^name create-new-object
              ^source-type perception-obj
              ^source-obj <pobj>
              ^object-monitor.new-object-info <obj-info>)
   (<obj-info> ^perception-obj <pobj>)
-->
   (<s> ^object-info <obj-info>)
}


### object-handle

# Priority 1: Use handle from smem object
sp {create-new-object*elaborate*object-handle*from*smem-obj
   (state <s> ^name create-new-object
              ^smem-obj.handle <obj-handle>)
-->
   (<s> ^object-handle <obj-handle>)
}

# Priority 2: Use handle from source object
sp {create-new-object*elaborate*object-handle*from*source-obj
   (state <s> ^name create-new-object
             -^smem-obj.handle
              ^source-obj.handle <obj-handle>)
-->
   (<s> ^object-handle <obj-handle>)
}

# Use perception id instead of handle
sp {create-new-object*elaborate*object-handle*from*perception-obj
   (state <s> ^name create-new-object
             -^smem-obj.handle
              ^source-type perception-obj
              ^source-obj.id <obj-handle>)
-->
   (<s> ^object-handle <obj-handle>)
}

### ignore-predicate
# HARDCODED: need to be better at this
sp {create-new-object*elaborate*ignore-predicate
   (state <s> ^name create-new-object)
-->
   (<s> ^ignore-predicate handle item-type object-instance predicates in-smem room1 storage1 arm-status object-status visible dialog-object)
}



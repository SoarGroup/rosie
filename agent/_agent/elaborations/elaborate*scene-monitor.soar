##########################################################################
#
# scene-monitor: 
#   A topstate set of elaborations that monitor the scene via
#   the svs link and compare it to the current representations 
#   in working memory. If discrepancies are reported they are recorded
#
##########################################################################


sp {topstate*elaborate*scene-monitor
   (state <s> ^topstate <s>
   	      -^operator.name learning)
-->
   (<s> ^scene-monitor <sm>)
   (<sm> ^scene-changes <sc>)
}

sp {topstate*elaborate*scene-monitor*object
   (state <s> ^topstate <s>
              ^scene-monitor <sm>
              ^svs.spatial-scene.child <svs-obj>)
   (<svs-obj> ^id-int <id>)
-->
   (<sm> ^object <obj>)
   (<obj> ^svs-obj <svs-obj>)
}

sp {topstate*elaborate*scene-monitor*wm-object
   (state <s> ^topstate <s>
              ^scene-monitor <sm>
              ^object <obj>)
   (<sm> ^object <sm-obj>)
   (<sm-obj> ^svs-obj.id-int <id>)
   (<obj> ^id <id>)
-->
   (<sm-obj> ^wm-obj <obj>)
}

sp {topstate*elaborate*scene-monitor*property
   (state <s> ^topstate <s>
              ^scene-monitor.object <obj>)
   (<obj> ^svs-obj.<prop-name> <prop>)
   (<prop> ^type {<type> << visual state >>})
-->
   (<obj> ^property <p>)
   (<p> ^name <prop-name>
        ^type <type>
        ^svs-prop <prop>)
}

sp {topstate*elaborate*scene-monitor*wm-prop
   (state <s> ^topstate <s>
              ^scene-monitor.object <obj>)
   (<obj> ^property <prop>
          ^wm-obj.property <wm-prop>)
   (<prop> ^name <prop-name>)
   (<wm-prop> ^name <prop-name>)
-->
   (<prop> ^wm-prop <wm-prop>)
}

sp {topstate*elaborate*scene-monitor*hypothesis*visual
   (state <s> ^topstate <s>
              ^scene-monitor.object.property <prop>)
   (<prop> ^type visual
           ^svs-prop.{<label> <> type} {<conf> > .1})
-->
   (<prop> ^hypothesis <h>)
   (<h> ^label <label>
        ^conf <conf>)
}

sp {topstate*elaborate*scene-monitor*hypothesis*state
   (state <s> ^topstate <s>
              ^scene-monitor.object.property <prop>)
   (<prop> ^type state
           ^svs-prop.value <label>)
-->
   (<prop> ^hypothesis <h>)
   (<h> ^label <label>
        ^conf 1.0)
}

sp {topstate*elaborate*scene-monitor*hypothesis*best
   (state <s> ^topstate <s>
              ^scene-monitor.object.property <prop>)
   (<prop> ^hypothesis <h1>
          -^hypothesis.conf > <best>)
   (<h1> ^conf <best>)
-->
   (<h1> ^best true)
}

sp {topstate*elaborate*scene-monitor*hypothesis*lower-threshold
   (state <s> ^topstate <s>
              ^scene-monitor.object.property <prop>)
   (<prop> ^hypothesis <h>)
   (<h> ^best true
        ^conf <conf>)
-->
   (<h> ^lower-threshold (* <conf> .8))
}

sp {topstate*elaborate*scene-monitory*case*none*no-hypothesis
   (state <s> ^topstate <s>
              ^scene-monitor.object.property <prop>)
   (<prop> -^hypothesis)
-->
   (<prop> ^case none)
}

#sp {topstate*elaborate*scene-monitor*case*none*low*best
#   (state <s> ^topstate <s>
#              ^scene-monitor.object.property <prop>)
#   (<prop> ^hypothesis <h>)
#   (<h> ^best true
#        ^conf < .05)
#-->
#   (<prop> ^case none)
#}

sp {topstate*elaborate*scene-monitor*case*uncertain
   (state <s> ^topstate <s>
              ^scene-monitor.object.property <prop>)
   (<prop> ^hypothesis <h1>
           ^hypothesis {<h2> <> <h1>})
   (<h1> ^best true
         ^lower-threshold <thresh>)
   (<h2> ^conf > <thresh>)
-->
   (<prop> ^case uncertain)
}

sp {topstate*elaborate*scene-monitor*case*certain
   (state <s> ^topstate <s>
              ^scene-monitor.object.property <prop>)
   (<prop> ^hypothesis <h1>)
   (<h1> ^best true
         ^lower-threshold <thresh>)
   -{(<prop> ^hypothesis {<h2> <> <h1>})
     (<h2> ^conf > <thresh>)}
-->
   (<prop> ^case certain)
}
           
sp {action-goal-definiton*elaborate*indexed-operator
   (state <s> ^name goal-definition
   	      ^topstate.interaction.stack.segment.next.purpose.parameters.indexable.result <ia>)
   (<ia> ^_metadata.map-id.goal <gid>)
-->
   (<s> ^indexed-action <ia>
   	^goal-id <gid>)
}

sp {action-goal-definiton*elaborate*goal
   (state <s> ^name goal-definition
   	      ^superstate.operator.purpose.parameters.indexable.result <goal>)
-->
   (<s> ^goal <goal>)
}

sp {action-goal-definition*elaborate*goal-id*relation-predicate
   (state <s> ^name goal-definition
   	      ^goal.{<pred> << relation-predicate state-predicate >>} <rel>
	      ^indexed-action._metadata.map-id.goal <gid>
	      ^smem.command <scomm>)
-->
   (<gid> ^<pred> <nrel>)
   (<nrel> ^identifier (make-constant-symbol))
   (<s> ^pairing <p>)
   (<p> ^smem <nrel>
   	^wmem <rel>)
   (<scomm> ^store <nrel> <gid>)
}

### elaborate objects that can be generalized away
sp {action-goal-learning*objects*generalizable*level1
   (state <s> ^name goal-definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^relation-predicate <relp>)
   (<ia> ^<argument> <obj>)
   (<relp> ^<relarg> <obj>)
   (<proc-id> ^<argument> <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
}

sp {action-goal-learning*objects*generalizable*level2*argument*object
   (state <s> ^name goal-definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^relation-predicate <relp>)
   (<ia> ^<argument> <arg-obj>)
   (<arg-obj> ^argument <obj>)
   (<relp> ^<relarg> <obj>)
   (<proc-id> ^<argument> <sarg-obj>)
   (<sarg-obj> ^argument <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <obj>
   	  ^lt-id <sobj>)
}

sp {action-goal-learning*objects*generalizable*level2*relation
   (state <s> ^name goal-definition
   	      ^indexed-action <ia>
	      ^goal <goal>
	      ^smem.command <scomm>)
   (<ia> ^_metadata.map-id <map>)
   (<map> ^procedural <proc-id>
   	  ^goal <gid>)
   (<goal> ^relation-predicate <relp>)
   (<ia> ^<argument> <arg-obj>)
   (<arg-obj> ^relation.smem-rep <obj>)
   (<relp> ^relation <xrel>)
   (<xrel> ^smem-rep <obj>)
   (<proc-id> ^<argument> <sarg-obj>)
   (<sarg-obj> ^relation <sobj>)
-->
   (<s> ^generalizable <gen>)
   (<gen> ^wme-id <xrel>
   	  ^lt-id <sobj>)
}
### initialize comprehend-action-command
sp {comprehend-action-command*initialize
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <i>
	      -^initialized)
-->
   (<s> ^operator <op> + !)
   (<op> ^name initialize-comprehend-action-command)
}


### index direct object
sp {comprehend-action-command*index-argument*direct-object
   (state <s> ^name comprehend-action-command
	      ^indexable-parameter <i>)
   (<i> ^direct-object <do>)
   (<do> ^type object
   	 ^lexical <do-lex>
	 -^result)
   -->
   (<s> ^operator <op> +)
   (<op> ^name index-parameter
	 ^parameter <do>)
   (<do> ^index-info <ii>)
   (<ii> ^return-all true
         ^use-interaction true
         ^infer-match true)
}

### index pp-object
sp {comprehend-action-command*index-argument*pp-object
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <i>)
   (<i> ^pp-object <po>)
   (<po> ^type pp-object
	 -^result)
-->
   (<s> ^operator <op> +)
   (<op> ^name comprehend-pp-object
   	 ^parameter <po>)
}

### index verb
sp {comprehend-action-command*index-argument*verb
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <ip>)
   (<ip> ^verb <i>)
   (<i> ^type verb
   	^lexical <verbword>
	-^result)
-->
   (<s> ^operator <op> +)
   (<op> ^name index-verb
   	 ^verb <verbword>
	 ^parameter <i>)
}

### generated indexed-operator
sp {comprehend-action-command*generate-indexed-operator	
   (state <s> ^name comprehend-action-command
   	      ^indexable-parameter <ip>)
   (<ip> -^result
   	 ^verb.result <result>)
-->
   (<s> ^operator <op> +)
   (<op> ^name generate-indexed-operator
   	 ^parameter <ip>
	 ^action-concept-network <result>)
}


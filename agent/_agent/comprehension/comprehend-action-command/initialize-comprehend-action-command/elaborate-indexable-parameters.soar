### elaborate direct-object
sp {elaborate*direct-object*comprehend-action-command
   (state <s> ^name initialize-comprehend-action-command
   	      ^indexable-parameter <i>
   	      ^operator.name elaborate-indexable-parameters)
   (<i> ^lexical <info>)
   (<info> ^verb.direct-object.object <do>)
 -{(<s> ^indexable-parameter <i>)
   (<i> ^direct-object <dox>)
   (<dox> ^lexical <do>
   	 ^result)}
-->
   (<i> ^direct-object <ido>)
   (<ido> ^type object
   	  ^lexical <do>)
}

### elaborate pp-object
sp {elaborate*pp-object*comprehend-action-command
   (state <s> ^name initialize-comprehend-action-command
   	      ^operator.name elaborate-indexable-parameters
   	      ^indexable-parameter <i>)
   (<i> ^lexical <info>)
   (<info> ^verb.preposition <do>)
   (<do> ^word <preposition>
   	 ^object <np>)
 -{(<s> ^indexable-parameter <i>)
   (<i> ^pp-object <prepobj>)
   (<prepobj> ^lexical <do>
   	     ^result)}
-->
   (<i> ^pp-object <ido>)
   (<ido> ^preposition <prep>
   	  ^type pp-object
	  ^object <object>
	  ^lexical <do>)
   (<object> ^type object
   	     ^lexical <np>)
   (<prep> ^type preposition
   	   ^lexical <preposition>)
}

### elaborate verb
sp {elaborate*verb*comprehend-action-command
   (state <s> ^name initialize-comprehend-action-command
   	      ^indexable-parameter <i>
   	      ^operator.name elaborate-indexable-parameters)
 -{(<s> ^indexable-parameter <i>)
   (<i> ^verb <vbx>)
   (<vbx> ^lexical <verbword>
   	  ^result)}
   (<i> ^lexical.verb <info>)
   (<info> ^word <verbword>)
-->
   (<i> ^verb <verb>)
   (<verb> ^lexical <verbword>
   	   ^type verb)
}


sp {initialize-comprehend-action-command*mark
   (state <s> ^name initialize-comprehend-action-command
   	      ^operator.name elaborate-indexable-parameters
	      ^superstate <ss>)
-->
   (<ss> ^initialized yes)
}

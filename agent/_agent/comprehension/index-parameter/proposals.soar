# This is a recursive call of index-parameter to any children of the current parameter that should be indexed
sp {index-parameter*propose*index-parameter*recurse
	(state <s> ^name index-parameter
           ^parameter.indexable <i>)
	(<i> ^lexical
	    -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-parameter
        ^parameter <i>)
}

sp {index-parameter*propose*index-property
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type property
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-property
        ^property-param <p>)
}

sp {index-parameter*propose*index-word
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type word
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-word
        ^word-param <p>)
}

sp {index-parameter*propose*index-object
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type object
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^object-param <p>)
}

#JK
sp {index-parameter*propose*lookup-preposition
   (state <s> ^name index-parameter
              ^parameter <p>)
   (<p> ^type preposition
        ^lexical <prep>
       -^result)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-preposition
   	 ^type <prep>)
}

sp {index-parameter*elaborate*lookup-preposition
   (state <s> ^name index-parameter
              ^found-prep <fprep>
              ^parameter <p>)
   (<fprep> ^predicate <pre> ^type <prep>)
   (<p> ^type preposition
        ^lexical <prep>)
-->
   (<p> ^result <prep>)
}

#####################################################
# teaching-request
#   Ask the instructor to provide more information
#####################################################
sp {index-parameter*apply*lookup-preposition*failure
   (state <s> ^name index-parameter
              ^operator.name lookup-preposition
              ^topstate.interaction.status <status>
              ^found-prep <fprep>
              ^parameter <p>)
   (<fprep> -^predicate <pre> ^type <prep>)
   (<p> ^type preposition
        ^lexical <prep>)
-->
   (<status> ^failure <f>)
   (<f> ^type index-preposition-failure
        ^preposition <prep>)
}

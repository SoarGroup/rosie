#################################################
# First: Only look at the created/delete objects
#        In case they need to be used as candidates
#################################################

sp {index-object*propose*attend-to-scene*create-delete
   (state <s> ^name index-object
             -^did-create-delete)
-->
   (<s> ^operator <o> +)
   (<o> ^name attend-to-scene
        ^category indexing
        ^type create-delete
        ^create-delete-only true)
}

sp {index-object*apply*attend-to-scene*create-delete
   (state <s> ^name index-object
              ^operator <o>)
   (<o> ^name attend-to-scene
        ^type create-delete
        ^status complete)
-->
   (<s> ^did-create-delete true)
}

###################################################
# Second, after we have the candidates attend to them
#     using only the properties we are concerned about
###################################################
sp {index-object*propose*attend-to-scene*candidates
   (state <s> ^name index-object
              ^did-create-delete true
             -^attended-to-candidates)
-->
   (<s> ^operator <o>)
   (<o> ^name attend-to-scene
        ^category indexing
        ^type candidates
        ^use-all-objects false
        ^use-all-properties false)
}

sp {index-object*elaboreate*attend-to-scene*candidates
   (state <s> ^name index-object
              ^operator <o> +
              ^object-to-index.candidates.candidate.object.id <id>)
   (<o> ^name attend-to-scene
        ^type candidates)
-->
   (<o> ^object <id>)
}

sp {index-object*elaborate*attend-to-scene*properties
   (state <s> ^name index-object
              ^operator <o> +
              ^object-to-index.word.sense.property.name <prop-name>)
   (<o> ^name attend-to-scene
        ^type candidates)
-->
   (<o> ^property <prop-name>)
}

sp {index-object*apply*attend-to-scene*candidates
   (state <s> ^name index-object
              ^operator <o>)
   (<o> ^name attend-to-scene
        ^type candidates
        ^status complete)
-->
   (<s> ^attended-to-candidates true)
}
###############################################################################
#
# closest-command
# (<s> ^operator <o>)
# (<o> ^name closest-command
#      ^object-id <id>
#      ^candidate-set <cand>)
# 
# The id's given should be svs id's
# The candidate set is optional, and defaults to all nodes
#    Should be the node filter from which to check for closest node
# 
# The id's given should be svs id's
#
# Returns either ^closest-command.result <id> or ^occlusion-command.result none
# (NOTE! the id returned is the svs id, so probably a string and not an integer)
###############################################################################
sp {anystate*closest-command*elaborate*used-candidate-set*default
   (state <s> ^operator <o> +)
   (<o> ^name closest-command
       -^candidate-set)
-->
   (<o> ^used-candidate-set.type all_nodes)
}

sp {anystate*closest-command*elaborate*used-candidate-set*copy
   (state <s> ^operator <o> +)
   (<o> ^name closest-command
        ^candidate-set <cand>)
-->
   (<o> ^used-candidate-set <cand>)
}

# Create the extract command
sp {anystate*closest-command*apply*create-closest-filter
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name closest-command
        ^object-id <id>
        ^used-candidate-set <candidates>)
-->
   (<cmd> ^extract_once <closest>)
   (<closest> ^type closest
              ^distance_type centroid
               ^a <obj>
               ^b <candidates>)
   (<obj> ^type node
          ^id <id>)
   (<s> ^closest-filter <closest>)
}

# Remove the command from the svs.command link
sp {anystate*closest-command*apply*create-closest-filter*cleanup
   (state <s> ^operator.name closest-command
              ^closest-filter <closest>
              ^top-state.svs.command <cmd>)
   (<cmd> ^extract_once <closest>)
   (<closest> ^result <res>)
-->
   (<cmd> ^extract_once <closest> -)
}

# Copy result of the successful command to the superstate
sp {anystate*closest-command*apply*create-result*success
   (state <s> ^operator <o>
              ^closest-filter.result.record.params.b {<closest-id> <> <id>})
   (<o> ^name closest-command
        ^object-id <id>)
-->
   (<s> ^closest-command.result <closest-id>)
}

# The object returned was the one given, return as a none result
sp {anystate*closest-command*apply*create-result*failure*same-id
   (state <s> ^operator <o>
              ^closest-filter.result.record.params.b <id>)
   (<o> ^name closest-command
        ^object-id <id>)
-->
   (<s> ^closest-command.result none)
}

# There was no valid result given, return none
sp {anystate*closest-command*apply*create-result*failure*no-result
   (state <s> ^operator <o>
              ^closest-filter.result <res>)
   (<o> ^name closest-command
        ^object-id <id>)
   (<res> -^record.params.b <foo>)
-->
   (<s> ^closest-command.result none)
}

sp {anystate*closest-command*apply*create-result*success*no-result
   (state <s> ^operator.name closest-command
              ^closest-filter <filter>)
   (<filter> ^status success
            -^result)
-->
   (<s> ^closest-command.result none)
}
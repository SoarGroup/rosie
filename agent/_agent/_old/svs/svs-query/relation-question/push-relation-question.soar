sp {propose*push-relation-question
   (state <s> ^topstate <s>
              ^instructor-message <msg>)
   (<msg> ^type relation-question)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-relation-question
        ^category communication
        ^message <msg>)
}

sp {push-relation-question*elaborate*empty*true
   (state <s> ^operator <o> +)
   (<o> ^name push-relation-question
        ^message.information.relation.{<< p1 p2 >>}.object <obj>)
   (<obj> -^word <> UNKNOWN)
-->
   (<obj> ^empty true)
}

sp {push-relation-question*elaborate*empty*false
   (state <s> ^operator <o> +)
   (<o> ^name push-relation-question
        ^message.information.relation.{<< p1 p2 >>}.object <obj>)
   (<obj> ^word <> UNKNOWN)
-->
   (<obj> ^empty false)
}

sp {apply*push-relation-question*what
   (state <s> ^operator <o>)
   (<o> ^name push-relation-question
        ^message <msg>)
   (<msg> ^type relation-question
          ^information.question-word what 
          ^information.relation <rel>)
   (<rel> ^p1.object <obja> ^p2.object <objb> ^word <prep>)
-->
   (<msg> ^purpose <p>)
   (<p> ^type relation-question
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^object1 <obja>
             ^object2 <objb>
             ^prep <prep>)
   (<sat> ^action-event.type describe)
}


sp {apply*push-relation-question*which
   (state <s> ^operator <o>)
   (<o> ^name push-relation-question
        ^message <msg>)
   (<msg> ^type relation-question
          ^information.question-word which 
          ^information.relation <rel>)
   (<rel> ^p1.object <obja> ^p2.object <objb> ^word <prep>)
  -{(<obja> ^empty false)
   (<objb> ^empty false)}
-->
   (<msg> ^purpose <p>)
   (<p> ^type relation-question
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^object1 <obja>
             ^object2 <objb>
             ^project prj
             ^prep <prep>)
   (<sat> ^action-event.type describe)
}
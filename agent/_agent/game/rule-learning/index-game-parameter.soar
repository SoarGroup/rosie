#todo handle conceptual spatial rels
sp {index-game-parameter*elaborate*copy-down-attrs
   (state <s> ^name index-game-parameter
              ^superstate.operator <o>)
   (<o> ^conditions <cond>
	    ^param <num>)
-->
   (<s> ^conditions <cond2>
	    ^param <num>)
}
sp {index-game-parameter*elaborate*copy-down-attrs*type
   (state <s> ^name index-game-parameter
              ^conditions <cond>
              ^superstate.operator <o>)
   (<o> ^conditions <cond2>)
   (<cond2> ^word {<word> << location block >>})
-->
   (<cond> ^type <word>)
}
sp {index-game-parameter*elaborate*copy-down-attrs*typeboth
   (state <s> ^name index-game-parameter
              ^conditions <cond>
              ^superstate.operator <o>)
   (<o> ^conditions <cond2>)
   (<cond2> -^word << location block >>)
-->
   (<cond> ^type either)
}
sp {index-game-parameter*elaborate*copy-down-attrs*word
   (state <s> ^name index-game-parameter
              ^conditions <cond>
              ^superstate.operator <o>)
   (<o> ^conditions <cond2>)
   (<cond2> ^word {<word> <> location <> block <> object <> null})
-->
   (<cond> ^word <word>)
}


#need to handle concepts like clear
sp {index-game-parameter*propose*indexed-parameter
   (state <s> ^name index-game-parameter
              ^conditions <co>
              ^topstate.current-game <cg>)
   (<cg> ^learning <l>)
   (<l> ^arg <arg> ^learn-link <link>)
   (<link> ^parameters.{<arg3> <= <arg>}.type <type>)
   (<co> ^type <type>)
-{(<co> ^word <word>)
  -{(<link> ^conditions.<arg3>.attribute.attr <word>)}}
-{(<co> ^concept.name <cname>)
  -{(<link> ^conditions.<arg3>.concept.name <cname>)}}
-->
   (<s> ^operator <o> + =)
   (<o> ^name indexed-parameter
        ^param <arg3>) 
}
#todo worry about multiple on onestate
sp {index-game-parameter*apply*index-parameter
   (state <s> ^name index-game-parameter
              ^superstate <ss>
              ^superstate.operator <op>
              ^operator <o>)
   (<o> ^name indexed-parameter
        ^param <arg3>)
-->
   (<ss> ^indexed <ind>)
   (<ind> ^index <arg3>)
   (<op> ^index <arg3>)
}

sp {index-game-parameter*pref*recency*indexed-parameter
   (state <s> ^name index-game-parameter
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name indexed-parameter ^param <arg1>) 
   (<o2> ^name indexed-parameter ^param {<arg2> < <arg1>}) 
-->
   (<s> ^operator <o1> > <o2>)
}
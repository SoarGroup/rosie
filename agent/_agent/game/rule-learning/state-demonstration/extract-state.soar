

sp {extract-state*propose*copy-relationships
   (state <s> ^name extract-state
             -^relationships.relations)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relationships
        ^category svs)
}
sp {extract-state*apply*copy-relationships
   (state <s> ^name extract-state
              ^relationships <rels>
              ^operator.name copy-relationships
              ^topstate.prepositions.<< prep2 prep >> <p>)
   (<p> ^name <name> ^pair <pair3>)
   (<pair3> ^a <ida> ^b <idb> -^c)
-->
   (<rels> ^relations <r>)
   (<r> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb>)
}

sp {extract-state*apply*copy-relationships*3arg
   (state <s> ^name extract-state
              ^relationships <rels>
              ^operator.name copy-relationships
              ^topstate.prepositions.<< prep2 prep >> <p>)
   (<p> ^name <name> ^pair <pair3>)
   (<pair3> ^a <ida> ^b <idb> ^c <idc>)
-->
   (<rels> ^relations <r>)
   (<r> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb> ^c <idc>)
}

sp {extract-state*propose*copy-objects
   (state <s> ^name extract-state
             -^object-set.object)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-objects
        ^category svs)
}
sp {extract-state*apply*copy-objects
   (state <s> ^name extract-state
              ^relationships.relations <p>
              ^object-set <os>
              ^topstate.object <obj>
              ^operator.name copy-objects
              ^relationships <rels>)
   (<p> ^name <name> ^pair <pair>)
   (<pair> ^<< a b c >> <id>)
   (<obj> ^id <id>)
-->
   (<os> ^object <obj>)
}


sp {extract-state*propose*describe-object
   (state <s> ^name extract-state
              ^object-set.object <obj>
             -^object-desc-set.obj-desc.id <id>)
   (<obj> ^id <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name describe-object
        ^object <obj>)
}
sp {extract-state*apply*describe-object
   (state <s> ^name extract-state
              ^operator <o>
              ^object-set.object <obj>
              ^object-desc-set <ods>)
   (<obj> ^id <id> ^category <type>)
   (<o> ^name describe-object
         ^description <desc>
        ^object <obj>)
-->
   (<ods> ^obj-desc <desc>)
   (<desc> ^id <id> ^type <type>)
}


sp {extract-state*propose*complete-state
   (state <s> ^name extract-state)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-state
        ^category svs)
}
sp {extract-state*apply*complete-state*remove-object-fromdesc
   (state <s> ^name extract-state
              ^object-desc-set.obj-desc <o>
              ^operator.name complete-state)
   (<o> -^type either ^word {<obj> object} ^type <type>)
-->
   (<o> ^word <obj> -
        ^word <type>)
}

sp {extract-state*apply*complete-state
   (state <s> ^name extract-state
              ^object-set <os>
              ^relationships <rels>
              ^object-desc-set <ods>
              ^superstate.operator <op>
              ^operator.name complete-state)
-->
#   (interrupt)
   (<op> ^state-result <res>)
   (<res> ^relationships <rels> ^objects <os> ^object-desc-set <ods>)
}
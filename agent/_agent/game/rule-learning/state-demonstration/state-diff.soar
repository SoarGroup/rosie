

sp {state-diff*propose*copy-relationships
   (state <s> ^name state-diff
             -^relationships.relations)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relationships
        ^category svs)
}
sp {state-diff*apply*copy-relationships
   (state <s> ^name state-diff
              ^relationships <r>
              ^operator.name copy-relationships
              ^initial.relationships <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb> -^c)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^pair <pair2>)
    (<pair2> ^a <ida> ^b <idb>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^pair <pair>)
}

sp {state-diff*apply*copy-relationships*3arg
   (state <s> ^name state-diff
              ^relationships <r>
              ^operator.name copy-relationships
              ^initial.relationships <rels>
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb> ^c <idc>)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^pair <pair2>)
    (<pair2> ^a <ida> ^b <idb> ^c <idc>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^pair <pair>)
}

sp {state-diff*propose*copy-objects
   (state <s> ^name state-diff
             -^object-set.object)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-objects
        ^category svs)
}
sp {state-diff*apply*copy-objects
   (state <s> ^name state-diff
              ^relationships.relations <p>
              ^object-set <os>
              ^object-desc-set <ods>
              ^final.object-desc-set.obj-desc <desc>
              ^final.objects.object <obj>
              ^operator.name copy-objects
              ^relationships <rels>)
   (<p> ^name <name> ^pair <pair>)
   (<pair> ^<< a b c >> <id>)
   (<obj> ^id <id>)
   (<desc> ^id <id>)
-->
   (<os> ^object <obj>)
   (<ods> ^obj-desc <desc>)
}

sp {state-diff*propose*copy-extra-relationships
   (state <s> ^name state-diff
             -^extra-relationships.done)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-extra-relationships
        ^category svs)
}
sp {state-diff*apply*copy-extra-relationships*default
   (state <s> ^name state-diff
              ^extra-relationships <r>
              ^relationships <rels>
              ^operator.name copy-extra-relationships)
-->
   (<r> ^done true)
}
   
sp {state-diff*apply*copy-extra-relationships
   (state <s> ^name state-diff
              ^extra-relationships <r>
              ^object-set.object.id <ida>
              ^object-set.object.id <idb>
              ^relationships <rels>
              ^operator.name copy-extra-relationships
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb> -^c)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^pair <pair2>)
    (<pair2> ^a <ida> ^b <idb>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^pair <pair>)
}

sp {state-diff*apply*copy-extra-relationships*3args
   (state <s> ^name state-diff
              ^extra-relationships <r>
              ^object-set.object.id <ida>
              ^object-set.object.id <idb>
              ^object-set.object.id <idc>
              ^relationships <rels>
              ^operator.name copy-extra-relationships
              ^final.relationships.relations <rel2>)
   (<rel2> ^name <name> ^pair <pair>)
   (<pair> ^a <ida> ^b <idb> ^c <idc>)
  -{(<rels> ^relations <rel>)
    (<rel> ^name <name> ^pair <pair2>)
    (<pair2> ^a <ida> ^b <idb> ^c <idc>)}
-->
   (<r> ^relations <r1>)
   (<r1> ^name <name> ^pair <pair>)
}



sp {state-diff*propose*complete-state
   (state <s> ^name state-diff)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-state
        ^category svs)
}

sp {state-diff*apply*complete-state*copyrels
   (state <s> ^name state-diff
              ^object-set <os>
              ^extra-relationships.relations <rel>
              ^relationships <rels>
              ^operator.name complete-state)
-->
   (<rels> ^relations <rel>)
}

sp {state-diff*apply*complete-state
   (state <s> ^name state-diff
              ^object-set <os>
              ^relationships <rels>
              ^object-desc-set <ods>
              ^extra-relationships <rels2>
              ^superstate.operator <op>
              ^operator.name complete-state)
  -{(<rels2> ^relations <rr>)
    (<rr> ^name <name> ^pair <pair>)
    -{(<rels> ^relations.pair <pair>)}}

-->
   (<op> ^state-result <res>)
   (<res> ^relationships <rels> ^objects <os> ^object-desc-set <ods>)
}
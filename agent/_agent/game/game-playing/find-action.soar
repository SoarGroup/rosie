
sp {find-action*propose*found-next-action
   (state <s> ^name find-action
              ^game-name <name>
              ^topstate <ts>
             -^no-actions
              ^topstate.flound-action <fa>
              -^valid-arg)
   (<fa> ^obj1 <oa> ^obj2 <ob> ^dcount <dc> -^mark)
  -{(<ts> ^flound-action <fa2>)
    (<fa2> -^mark ^dcount < <dc>)}          
-->
   (<s> ^operator <o> +)
   (<o> ^name found-next-action
        ^category svs
        ^action <fa>)
}

sp {find-action*propose*no-actions
   (state <s> ^name find-action
              ^game-name <name>
             -^no-actions
              -^topstate.flound-action
              -^valid-arg)    
-->
   (<s> ^operator <o> +)
   (<o> ^name no-actions
        ^category svs)
}
sp {find-action*apply*no-actions
   (state <s> ^name find-action
              ^game-name <name>
              -^topstate.flound-action
              ^operator <o>)
   (<o> ^name no-actions
        ^category svs)
-->
   (<s> ^no-actions true)
}


sp {find-action*propose*completed-all-actions
   (state <s> ^name find-action
              ^topstate <ts>
             -^valid-arg
              ^topstate.flound-action <fa>)
   (<fa> ^obj1 <oa> ^obj2 <ob> ^dcount <dc> ^mark)
  -{(<ts> ^flound-action <fa2>)
    (<fa2> -^mark)}          
-->
   (<s> ^operator <o> + =)
   (<o> ^name completed-all-actions
        ^category svs)
}
sp {find-action*apply*completed-all-actions
   (state <s> ^name find-action
              ^superstate <ss>
              ^operator <o>)
   (<o> ^name completed-all-actions)
-->
   (<ss> ^completed-all-actions true)
}
#sp {find-action*apply*completed-all-actions*remove-found-action
#   (state <s> ^name find-action
#              ^topstate <ts>
#              ^operator <o>)
#   (<o> ^name completed-all-actions)
#   (<ts> ^flound-action <fa>)
#-->
#   (<ts> ^flound-action <fa> -)
#}

sp {find-action*apply*found-next-action
   (state <s> ^name find-action
              ^game-name <name>
              ^topstate <ts>
              ^topstate.found-prep <fprep>
              ^topstate.flound-action <fa>
              ^operator <o>)
   (<o> ^name found-next-action ^action <fa>)
   (<fa> ^obj1 <oa> ^obj2 <ob> ^dcount <dc> ^pname <pname>)
   (<fprep> ^type <pname> ^predicate <p>)          
-->
   (<s> ^action <a>
        ^valid-arg <va>
        ^predicate <p>
        ^done <dc>
        ^valid-arg <vb>)
   (<va> ^1 <oa>)
   (<vb> ^2 <ob>)
   (<a> ^verb move ^pname <pname>)
   (<fa> ^mark done)
}

sp {find-action*propose*find-parameters
   (state <s> ^name find-action
#              ^found-prep
              ^no-actions true
              ^game-name <name> 
             -^valid-arg)
-->
   (<s> ^operator <o> +)
   (<o> ^name find-parameters
        ^category svs
        ^game-name <name>)
}


sp {find-action*propose*complete-find-action
   (state <s> ^name find-action       
#              ^found-prep.predicate <p1>
              ^topstate.found-prep <fp>
              ^action <a>
#              ^predicate <p1>
              ^valid-arg <va>
              ^valid-arg <vb>)
   (<va> ^1 <o1>)
   (<vb> ^2 <o2>)
   (<a> ^pname <pname>)
   (<fp> ^type <pname> ^predicate <p1>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-find-action
        ^category svs
        ^action <a>
        ^predicate <p1>
        ^obja <o1>
        ^objb <o2>)
}

sp {find-action*apply*complete-find-action
   (state <s> ^name find-action
              ^operator <o>
              ^object <o1>
              ^object <o2>
              ^superstate <ss>)
   (<o1> ^id <id1>)
   (<o2> ^id <id2>)
   (<o> ^name complete-find-action
        ^obja <id1>
        ^objb <id2>
        ^predicate <p>
        ^action <a>)
-->
   (<ss> ^action <a>
         ^predicate <p>
         ^obj1 <o1>
         ^obj2 <o2>)
}

sp {find-action*apply*complete-find-action*solved
   (state <s> ^name find-action
              ^operator <o>
              ^object <o1>
              ^object <o2>
              ^solved <t>
              ^superstate <ss>)
   (<o1> ^id <id1>)
   (<o2> ^id <id2>)
   (<o> ^name complete-find-action
        ^obja <id1>
        ^objb <id2>
        ^predicate <p>
        ^action <a>)
-->
   (<ss> ^solved <t>)
}




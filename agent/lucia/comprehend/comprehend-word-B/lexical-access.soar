##!
# @file
#
# @created plindes 20181024
# File: lexical-access.soar     System B version
# Author: Peter Lindes
# Created: 24 October 2018
#
# Desc: Retrieves a new input word and puts it on the state.
#
#   This operator retrieves a lexical item from semantic memory.
#

#    Propose an operator to process a new word
sp {comprehend-word*propose*lexical-access
   (state <s> ^problem-space comprehend-word
              ^input <input>
             -^lexical-access done)
   #    Find the word to query for
   (<input> ^comprehended false
            ^word.orth <orth>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lexical-access
        ^type construction
        ^orth <orth>
        ^debug-info (concat |Select an appropriate sense of '| <orth> |'.|))
}

#   Put a retrieval query on the smem link
sp {comprehend-word*apply*lexical-access*do-query
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^input <input>
              ^smem.command <command>)
   (<o> ^name lexical-access
        ^orth <orth>)
-->
   #    Add a command to the smem link
   (<command> ^query <q>)
   (<q> ^orth <orth>)
}

#   Create a cxn node and a construction set to be used later
#   and mark this operator as done
sp {comprehend-word*apply*lexical-access*create-nodes
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name lexical-access)
-->
   (<s> #^cxn <cxn>
        ^cxn-set <c-set>
        ^lexical-access done)
}

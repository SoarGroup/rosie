##!
# @file
#
# @created plindes 20181105
# File: add-basics.soar     in System B
# Author: Peter Lindes
# Created: 5 November 2018
#
# Desc: Adds basic attributes to a new construction.
#
#   This operator is intended to be selected
#   in a substate under build-cxn, but it ignores
#   build-cxn and works with everything
#   on the comprehend-word superstate.
#

#    Propose an operator to add basics to a new construction
sp {build-cxn*propose*add-basics
   (state <s> ^superstate <ss>
             -^basics done) #   Retract when this operator completed
   (<ss> ^problem-space comprehend-word
         ^operator <ss-o>
         ^cxn <cxn>)
   (<cxn> ^cxn-name <name>)
   #    Insure o-support
   (<ss-o> ^name build-cxn) 
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-basics
        ^type construction
        ^cxn <cxn>
        ^debug-info (concat |Add basic attributes to a '| <name> |' construction.|))
}

#   Add basic attributes to a new construction
sp {build-cxn*apply*add-basics
   (state <s> ^superstate <ss>
              ^operator <o>)
   (<o> ^name add-basics
        ^cxn <cxn>)
   (<cxn> ^cxn-name <name>)
-->
   #    Put the basic attributes on the cxn
   (<cxn> ^cxn-name <name>
          ^f <f>
          ^m <m>
          ^is <a>)
   (<a> ^a <name>)
   #    Mark this operator as completed
   (<s> ^basics done)
}

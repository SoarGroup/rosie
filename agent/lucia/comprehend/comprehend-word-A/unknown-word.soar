##!
# @file
#
# @created plindes 20150612
# File: unknown-word.soar
# Author: Peter Lindes
# Created: 22 July 2015
#
# Desc: This rule fires when no words matched
#       for lexical access.  It creates an
#       UNKNOWN-WORD lexical construction.
#

#   UNKNOWN-WORD
sp {lexical-access*apply*UNKNOWN-WORD
   (state <s> ^attribute operator
              ^impasse no-change
              ^superstate <ss>)
   (<ss> ^name comprehend-word
         ^operator <o>
         ^input <input>
         ^superstate <sss>)
   (<o> ^name lexical-access)
   (<input> ^word <word>)
   (<word> ^word-count <count>
           ^content <text>)
   #	Find where to attach this to the stack
   (<sss> ^comprehension.cxn-stack <stack>
          ^comprehension.lex-stack <lex-stack>)
-->
   #   Add an UNKOWN-WORD structure to the superstate
   (<ss> ^cxn <lex>
         ^lexical <lex>
         ^lexical-access done)
   (<lex> ^cxn-name UNKNOWN-WORD
          ^is <a>
          ^text <text>
          ^previous <stack>
          ^previous-lex <lex-stack>
          ^source <source>
          ^word <word>)
   (<source> ^left <count>
             ^right <count>)
   (<a> ^a UNKNOWN-WORD)
   #   Create a construction set and add this new construction to it
   (<ss> ^cxn-set <c-set>)
   (<c-set> ^cxn <lex>)
}

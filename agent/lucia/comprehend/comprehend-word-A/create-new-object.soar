##!
# @file
#
# @created plindes 20160302
# File: create-new-object.soar
# Author: Peter Lindes
# Created: 2 March 2016
#
# Desc:  This handles three kinds of situations:
#
# Case 1:When an object reference resolves to NONE,
#        create a new object with its properties.
#
# Case 2:For a PropertySet with an indefinite determiner
#        create a new object with its properties.
#        Added PL 6/13/2018
#        There is a lot of duplication in the code,
#        it could maybe be simplified.
#
# Case 3:For an unresolved object reference when
#        the hypothetical flag is set.
#        The operation is the same as in Case 1.
#        This requires a preference rule to make
#        it get selected over ground-reference.
#        Added PL 4/2/2020
#

################  For a RefDesc with ^referent NONE  #############

#   Propose an operator to create a new object from a RefDesc
#   This is the normal version after a ground-reference
#   that got NONE.
sp {comprehend-word*propose*create-new-object*no-referent
   (state <s> ^problem-space comprehend-word
              ^lexical-access done
              ^match-construction done
              ^cxn <cxn>
              ^reference resolved
              ^top-state <ts>)
   (<cxn> ^is.a RefExpr
          ^m <ref-desc>)
   (<ref-desc> ^is.a RefDesc
               ^category <rosie-ref-desc>
               ^referent NONE
               ^modifiers <modifiers>)
   (<rosie-ref-desc> ^rosie-category { <category> <> nil })
   #    Find the object list in the world
   (<ts> ^world.objects <object-list>)
-->
   (<s> ^operator <o> + >	#	This must happen before other operators
        ^modifiers <modifiers>)
   (<o> ^name create-new-object
        ^type resolution
        ^meaning <ref-desc>
        ^category <category>
        ^object-list <object-list>
        ^debug-info (concat |Create a new object from a given RefDesc.|))
}

#   Create an object with a given Rosie category
#   and add it to the world
#   This is used for the normal (non-hypothetical) case.
#    This rule should fire only when NOT in SIMULATED PERCEPTION mode
sp {comprehend-word*apply*create-new-object*category*normal
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^superstate.comprehension.hypothetical false
             -^top-state.agent-params.simulate-perception true)
   (<o> ^name create-new-object
        ^category <category>
        ^object-list <object-list>)
-->
   #    Create the object on the state
   (<s> ^new-object <object>)
   (<object> ^handle (make-constant-symbol |new-object-id|)
             ^item-type object
             ^predicates <predicates>
             ^predicates-copied true)
   (<predicates> ^category <category>
                 ^dialog-object true
                 ^is-visible1 not-visible1)
   #    Add it to the world
   (<object-list> ^object <object>)
}

#   Create an object with a given Rosie category
#   and add it to the world
#   This is used for the normal (non-hypothetical) case.
#   This rule should fire only when IN SIMULATED PERCEPTION mode
#   This rule does NOT add the object to the world.
sp {comprehend-word*apply*create-new-object*category*sim
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^superstate.comprehension.hypothetical false
              ^top-state.agent-params.simulate-perception true)
   (<o> ^name create-new-object
        ^category <category>
        ^meaning <ref-desc>)
   #    Find the root category
   (<ref-desc> ^category.category <root-category>)
-->
   #    Create the object on the state
   (<s> ^new-object <object>)
   (<object> ^handle (make-constant-symbol |new-object-id|)
             ^item-type object
             ^predicates <predicates>
             ^predicates-copied true
             ^root-category <root-category>)    # Needed to get reference-info
   (<predicates> ^category <root-category>
                 ^dialog-object true
                 ^is-visible1 not-visible1)
}

################  For a RefDesc in a hypothetical situation  #############

#   Propose an operator to create a new object from a RefDesc
#   This is the hypothetical version to run instead of ground-reference
#   but only for an indefinite RefDesc.
#   In this mode, we have ^referent nil instead of NONE
sp {comprehend-word*propose*create-new-object*hypothetical
   (state <s> ^problem-space comprehend-word
              ^lexical-access done
              ^match-construction done
              ^cxn <cxn>
             -^reference resolved
              ^superstate.comprehension.hypothetical true
              ^top-state <ts>)
   (<cxn> ^is.a RefExpr
          ^m <ref-desc>)
   (<ref-desc> ^is.a RefDesc
               ^givenness indefinite
               ^category <rosie-ref-desc>
               ^referent nil
               ^modifiers <modifiers>)
   (<rosie-ref-desc> ^rosie-category { <category> <> nil })
   #    Find the object list in the world
   (<ts> ^world.objects <object-list>)
-->
   (<s> ^operator <o> + >   #   This must happen before other operators
        ^modifiers <modifiers>)
   (<o> ^name create-new-object
        ^type resolution
        ^meaning <ref-desc>
        ^category <category>
        ^object-list <object-list>
        ^debug-info (concat |Create a new object from a given RefDesc.|))
}

#   Create an object with a given Rosie category
#   and add it to the world
#   This is used for a hypothetical RefDesc.
sp {comprehend-word*apply*create-new-object*category*hypothetical*ref-desc
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^superstate.comprehension.hypothetical true
              ^top-state <ts>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>
        ^category <category>
        ^object-list <object-list>)
   (<ref-desc> ^is.a RefDesc)
-->
   #    Create the object on the state
   (<s> ^new-object <object>)
   (<object> ^handle (make-constant-symbol |new-object-id|)
             ^item-type object
             ^predicates <predicates>
             ^predicates-copied true)
   (<predicates> ^category <category>
                 ^dialog-object true
                 ^is-visible1 not-visible1)
   #    Add it to the world
   (<object-list> ^object <object>)
}

#   Create an object with a given Rosie category
#   and add it to the world
#   This is used for a hypothetical PropertySetDescriptor.
sp {comprehend-word*apply*create-new-object*category*hypothetical*property-set
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^superstate.comprehension.hypothetical true
              ^top-state <ts>)
   (<o> ^name create-new-object
        ^meaning <ps-desc>
        ^category <category>
        ^object-list <object-list>)
   (<ps-desc> ^is.a PropertySetDescriptor)
-->
   #    Create the object on the state
   (<s> ^new-object <object>)
   (<object> ^handle (make-constant-symbol |new-object-id|)
             ^item-type object
             ^predicates <predicates>
             ^predicates-copied true)
   (<predicates> ^category <category>
                 ^dialog-object true
                 ^is-visible1 not-visible1)
   #    Add it to the world
   (<object-list> ^object <object>)
}

################  Details for a RefDesc   #############

#   Post properties from the ref-desc
#   This applies only to the RefDesc case
sp {comprehend-word*apply*create-new-object*post-name-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the name
   (<ref-desc> ^is.a RefDesc
               ^category <rosie-ref-desc>
               ^referent << NONE nil >>)
   (<rosie-ref-desc> ^name { <name> <> nil })
-->
   #    Post a name property
   (<predicates> ^name <name>)
   #    Mark it on the state
   (<s> ^post name)
}

#   This applies only to the RefDesc case
sp {comprehend-word*apply*create-new-object*post-shape-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the shape
   (<ref-desc> ^is.a RefDesc
               ^category <rosie-ref-desc>
               ^referent << NONE nil >>)
   (<rosie-ref-desc> ^shape { <shape> <> nil })
-->
   #    Post a shape property
   (<predicates> ^shape <shape>)
   #    Mark it on the state
   (<s> ^post shape)
}

#   This applies only to the RefDesc case
sp {comprehend-word*apply*create-new-object*post-color-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the color
   (<ref-desc> ^is.a RefDesc
               ^category <rosie-ref-desc>
               ^referent << NONE nil >>)
   (<rosie-ref-desc> ^color { <color> <> nil })
-->
   #    Post a color property
   (<predicates> ^color <color>)
   #    Mark it on the state
   (<s> ^post color)
}

#   This applies only to the RefDesc case
sp {comprehend-word*apply*create-new-object*post-size-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the size
   (<ref-desc> ^is.a RefDesc
               ^category <rosie-ref-desc>
               ^referent << NONE nil >>)
   (<rosie-ref-desc> ^size { <size> <> nil })
-->
   #    Post a size property
   (<predicates> ^size <size>)
   #    Mark it on the state
   (<s> ^post size)
}

#   This applies only to the RefDesc case
sp {comprehend-word*apply*create-new-object*post-spatial-shape-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the spatial shape
   (<ref-desc> ^is.a RefDesc
               ^category <rosie-ref-desc>
               ^referent << NONE nil >>)
   (<rosie-ref-desc> ^spatial-shape { <spatial-shape> <> nil })
-->
   #    Post a spatial-shape property
   (<predicates> ^spatial-shape <spatial-shape>)
   #    Mark it on the state
   (<s> ^post spatial-shape)
}

#   This applies only to the RefDesc case
sp {comprehend-word*apply*create-new-object*post-state-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the state
   (<ref-desc> ^is.a RefDesc
               ^category <rosie-ref-desc>
               ^referent << NONE nil >>)
   (<rosie-ref-desc> ^state { <state> <> nil })
-->
   #    Post a state property
   (<predicates> ^state <state>)
   #    Mark it on the state
   (<s> ^post state)
}

#   This applies only to the RefDesc case
#   For a RefDesc built from a noun, if that noun
#   is plural set ^multiple true on the object.
sp {comprehend-word*apply*create-new-object*multiple-property
   (state <s> ^problem-space comprehend-word
              ^cxn <cxn>
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Look for a plural feature
   (<cxn> ^is.a RefExpr
          ^noun <noun>)
   (<noun> ^is.a Noun NominalFeatures
           ^features <features>)
   (<features> ^is.a NominalAgreementFeatures
               ^number << pl plural >>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
-->
   #    Post a state property
   (<predicates> ^multiple true)
}

#   Like above, but only for number quantifiers
sp {comprehend-word*apply*create-new-object*add-number
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #   Find the quantifier
   (<ref-desc> ^quantified { <quantifier> <> nil })
   (<quantifier> ^is.a Concept
                 ^concept-type number
                 ^concept-handle <handle>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
-->
   #    Add the quantification
   (<predicates> ^number (strlen <handle>))
}

#   Remove the post markers
sp {comprehend-word*apply*create-new-object*remove-post
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^post <post>)
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   #    Find the object
   (<s> ^new-object <object>)
-->
   #    Remove this marker
   (<s> ^post <post> -)
}

#   Post any modifiers
#   This applies only to the RefDesc case
sp {comprehend-word*apply*create-new-object*modifiers
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^modifiers { <modifier> <> nil })
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   (<ref-desc> ^is.a RefDesc
               ^category <prop-desc>)
   #    Get the details of this modifier
   (<modifier> ^class <class>
               ^name <name>
               ^predicate <predicate>
               ^next <next>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
-->
   #    Post a new property
   (<predicates> ^<class> <name>)
   #    Get the next modifier
   (<s> ^modifiers <modifier> -
        ^modifiers <next>)
}

#   All posts and modifiers done, set the referent
sp {comprehend-word*apply*create-new-object*all-done
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^modifiers nil
             -^post )
   (<o> ^name create-new-object
        ^meaning <ref-desc>)
   (<ref-desc> ^is.a RefDesc
               ^category <prop-desc>
               ^referent <none-or-nil>)
   #    Find the object
   (<s> ^new-object <object>)
-->
   #    Set the referent
   (<ref-desc> ^referent <none-or-nil> -
               ^referent <object>)
   #    Clean up the state
   (<s> ^modifiers nil -
        ^reference resolved)
}


################  For an indefinite PropertySet  #############

#    Propose an operator to create a new object from a PropertySet
sp {comprehend-word*propose*create-new-object*property-set
   (state <s> ^problem-space comprehend-word
              ^lexical-access done    # May need to see comprehend-word-done as proposed
              ^match-construction done
              ^cxn <cxn>
              ^top-state <ts>)
   (<cxn> ^is.a PropertySet
          ^m <ps-desc>)
   (<ps-desc> ^is.a PropertySetDescriptor
              ^givenness indefinite
              ^predicate <predicate>
              ^property <property>
             -^referent )   # Will be added below
   (<predicate> ^handle <category>)
   #    Find the object list in the world
   (<ts> ^world.objects <object-list>)
-->
   (<s> ^operator <o> + >)   #   This must happen before other operators
   (<o> ^name create-new-object
        ^type resolution
        ^meaning <ps-desc>
        ^category <category>
        ^object-list <object-list>
        ^debug-info (concat |Create a new object from a given PropertySet.|))
}

#   This applies only to the PropertySet case
sp {comprehend-word*apply*create-new-object*property-set*post-shape-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ps-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the shape
   (<ps-desc> ^is.a PropertySetDescriptor
               ^property <property>)
   (<property> ^class shape
               ^name { <shape> <> nil })
-->
   #    Post a shape property
   (<predicates> ^shape <shape>)
   #    Mark as done
   (<ps-desc> ^referent <object>)
}

#   This applies only to the PropertySet case
sp {comprehend-word*apply*create-new-object*property-set*post-color-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ps-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the color
   (<ps-desc> ^is.a PropertySetDescriptor
               ^property <property>)
   (<property> ^class color
               ^name { <color> <> nil })
-->
   #    Post a color property
   (<predicates> ^color <color>)
   #    Mark as done
   (<ps-desc> ^referent <object>)
}

#   This applies only to the PropertySet case
sp {comprehend-word*apply*create-new-object*property-set*post-size-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ps-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the size
   (<ps-desc> ^is.a PropertySetDescriptor
               ^property <property>)
   (<property> ^class size
               ^name { <size> <> nil })
-->
   #    Post a size property
   (<predicates> ^size <size>)
   #    Mark as done
   (<ps-desc> ^referent <object>)
}

#   This applies only to the PropertySet case
sp {comprehend-word*apply*create-new-object*property-set*post-spatial-shape-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ps-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the spatial shape
   (<ps-desc> ^is.a PropertySetDescriptor
               ^property <property>)
   (<property> ^class spatial-shape
               ^name { <spatial-shape> <> nil })
-->
   #    Post a spatial-shape property
   (<predicates> ^spatial-shape <spatial-shape>)
   #    Mark as done
   (<ps-desc> ^referent <object>)
}

#   This applies only to the PropertySet case
#   Not sure if this is really correct, but it seems
#   to help for "clear" and "covered."
sp {comprehend-word*apply*create-new-object*property-set*post-property-property
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ps-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the spatial shape
   (<ps-desc> ^is.a PropertySetDescriptor
               ^<< property property2 >> <property>)
   (<property> ^class property
               ^name { <property-name> <> nil })
-->
   #    Post a property property
   (<predicates> ^property <property-name>)
   #    Mark as done
   (<ps-desc> ^referent <object>)
}

#   This applies only to the PropertySet case
sp {comprehend-word*apply*create-new-object*property-set*nothing-to-post
   (state <s> ^problem-space comprehend-word
              ^operator <o>)
   (<o> ^name create-new-object
        ^meaning <ps-desc>)
   #    Find the object and its predicates
   (<s> ^new-object <object>)
   (<object> ^predicates <predicates>)
   #    Get the spatial shape
   (<ps-desc> ^is.a PropertySetDescriptor
               ^property nil)
-->
   #    Mark as done
   (<ps-desc> ^referent <object>)
}

##!
# @file
#
# @created plind 20160826
# File: lexical-selection.soar
# Author: Peter Lindes
# Created: 26 August 2016
#
# Desc: This file has rules that do selection on multiple
#       lexical items in a general way.
#

#    Delete an un-selected alternative for attachment and resolution operators
sp {comprehend-word*apply*lexical-selection*attachment-resolution
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^cxn <lexical2>
              ^lexical <lexical2>
              ^cxn-set <cxn-set>)
   (<o> ^type << attachment resolution >>
        ^selected { <lexical1> <> <lexical2> })
   #    We'll clear it out of the cxn-set as well
   (<cxn-set> ^cxn <lexical2>)
-->
   #    Delete the deictic one as both a regular cxn and a lexical
   (<s> ^cxn <lexical2> -
        ^lexical <lexical2> -)
   (<cxn-set> ^cxn <lexical2> -)
   #    Add it as an alternative to the selected one for future reference
   (<lexical1> ^alternative-lexical <lexical2>)
      #   Write a message for the snip.
   (write (crlf) (concat |Deleting the lexical option | <lexical2> |.|))
}

#    Delete an un-selected alternative for a match-construction operator
sp {comprehend-word*apply*lexical-selection*match-construction-unused
   (state <s> ^problem-space comprehend-word
              ^operator <o>
              ^cxn <lexical2>
              ^lexical <lexical1>
              ^cxn-set <cxn-set>)
   (<o> ^name match-construction
        ^<constituent> <lexical1> <> <lexical2>)
   #    We'll clear it out of the cxn-set as well
   (<cxn-set> ^cxn <lexical2>)
-->
   #    Delete the non-constituent one as both a regular cxn and a lexical
   (<s> ^cxn <lexical2> -
        ^lexical <lexical2> -)
   (<cxn-set> ^cxn <lexical2> -)
   #    Add it as an alternative to the selected one for future reference
   (<lexical1> ^alternative-lexical <lexical2>)
   #   Write a message for the snip.
   (write (crlf) (concat |Deleting the lexical option | <lexical2> |.|))
}

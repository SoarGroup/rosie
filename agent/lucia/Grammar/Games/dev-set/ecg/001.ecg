//  This file contains new ECG data needed for sentence
//  001 of the Games dev-set.

//  This is sentence #1 of 01.01_blocks-world script.txt.
//  The sentence is:
//      The name of the puzzle is blocks-world.

//  Concepts
construction NAME
   subcase of CommonNoun
   form
      constraints
         self.f.orth <-- "name"
   meaning: Name

schema Name
   subcase of Concept
   constraints
      rosie-category <-- concept
      concept-handle <-- name1

construction PUZZLE
   subcase of CommonNoun
   form
      constraints
         self.f.orth <-- "puzzle"
   meaning: Puzzle

schema Puzzle
   subcase of Concept
   constraints
      rosie-category <-- concept
      concept-handle <-- puzzle1

//  Prepositions
construction OF
   subcase of Preposition
   form
      constraints
         self.f.orth <-- "of"
   meaning
      constraints
         self.m.name <-- "of1"

//  A very specific version of a SpecifierNP
//  This competes with SpecNoun for the same
//  constituents, and they have the same span.
//  To fix this a new feature has been added
//  to all composite constructions: ^lexicals,
//  and a new preference rule has been added
//  that prefers more lexicals when the spans
//  are the same.
construction TheName
//  Can't use this subcase because it propagates up
//  and creates a RefExprPrepPhrase, which leaves
//  no way to distinguish this specific cxn.
//   subcase of SpecifierNP
   constructional
      constituents
         spec : THE
         name-lex: NAME
   meaning: Name

// Types of definitions
construction NameDefinitionSentence
   constructional
      constituents
         name-np : TheName
         prep-phrase: SimplePrepPhrase
         tobe : FiniteToBe
         word : UNKNOWN-WORD
   meaning: NameDefinition
      evokes Salient as salient
      constraints
         self.m.name <--> name-np.m
         self.m.relation <--> prep-phrase.m
         self.m.word <--> word.word.orth
         self.salient.reference <--> prep-phrase.object.m

schema NameDefinition
   roles
      word
      relation
      name

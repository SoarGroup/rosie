//  This file contains new ECG data needed for sentence
//  004 of the Games dev-set.

//  This is sentence #4 of 01.01_blocks-world script.txt.
//  The sentence is:
//      You can move a clear block onto a clear object.

//  Prepositions
construction ONTO
   subcase of Preposition
   form
      constraints
         self.f.orth <-- "onto"
   meaning
      constraints
         self.m.name <-- "on1"

//  Function Words
construction CAN-verb
   subcase of EnablingVerb
   form
      constraints
         self.f.orth <-- "can"
   meaning
      constraints
         self.m.type <-- "can"

//  A general construction for agents
general construction Agent

//  A general construction for verbal auxiliaries
//  that can serve an enabling function
general construction EnablingVerb
   meaning: AuxiliaryFeatures

//  A construction that recognizes an agent
//  followed by an enabling verb
construction EnablerPhrase
   constructional
      constituents
         agent: Agent
         verb: EnablingVerb
   meaning: Enabler
      constraints
         self.m.agent <--> agent.m
         self.m.ability <--> verb.m.type

//  A sentence that enables a command
construction EnabledCommandSentence
   subcase of Declarative
   constructional
      constituents
         enabler: EnablerPhrase
         command: Imperative
   meaning: EnabledCommand
      constraints
         self.m.enabler <--> enabler.m
         self.m.command <--> command.m

//  Meaning schemas needed for enabled commands
schema AuxiliaryFeatures
   roles
      type
      
schema Enabler
   roles
      agent
      ability

schema EnabledCommand
   roles
      enabler
      command



//  This file contains all the constructions and schemas
//  related to sentences containing "if" in the form
//  they were before being refactored 15 June 2020.
//
//  The constructions moved here are:
//      Conditional, IfCondition, IfConditionThen,
//      IfConditionCommand, IfConditionThenCommand,
//      CheckCondition, and RememberConditionasB
//
//  Several related schemas were also moved here:
//      Condition, IfCondition, IfThenCommand, ThenResult,
//      and CheckIt
//      (It appears that IfCondition and ThenResult were
//       never used.)
//      

// A whole new system for handle "If <condition> then <imperative>" sentences
general construction Conditional

construction IfCondition
   constructional
      constituents
         if: IF
         declarative: Declarative
   meaning: Condition
      constraints
         self.m.statement <--> declarative.m

// Conditional commands
construction IfConditionThen
   subcase of Conditional
   constructional
      constituents
         condition: IfCondition
         then: THEN

construction IfConditionThenCommand
   subcase of Conditional
   constructional
      constituents
         conditionPart: IfConditionThen
         command: Imperative
   meaning: IfThenCommand
      constraints
         self.m.condition <--> conditionPart.condition.declarative.m
         self.m.command <--> command.m

construction IfConditionCommand
   subcase of Conditional
   constructional
      constituents
         condition: IfCondition
         command: Imperative
   meaning: IfThenCommand
      constraints
         self.m.condition <--> condition.declarative.m
         self.m.command <--> command.m

//	Constructions to handle sentences like "Check if <Declarative>."
construction CheckCondition
   constructional
      constituents
	     action: CHECK
         condition: IfCondition
   meaning: CheckIt
      constraints
         self.m.action <--> action.m
         self.m.condition <--> condition.declarative.m
 
//	Constructions to handle sentences like "Remember if <Declarative> as the message."
construction RememberConditionasB
   subcase of RememberAasB
   constructional
      constituents
	     verb: REMEMBER
         condition : IfCondition
         connector : AS
         target : RefExpr
   meaning: DoTransfer
      constraints
         self.m.action <--> verb.m
         self.m.object <--> condition.m
         self.m.object2 <--> target.m

//  Meaning schemas:
// A schema for holding some condition
schema Condition
   roles
      statement

schema IfCondition
   roles
      condition

schema IfThenCommand
   roles
      condition
      command

schema ThenResult
   roles
      command

// A schema for checking the value of some condition
schema CheckIt
   subcase of Action
   roles
      condition



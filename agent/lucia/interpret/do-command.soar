##!
# @file
#
# @created plindes 20150625
# File: do-command.soar
# Author: Peter Lindes
# Created: 25 June 2015
#
# Desc: This operator will create one or more actions
#       to perform a command.
#
#       3/2/2016: This is rather obsolete.  It does not
#       apply to actions for Rosie.  It is used for commands
#       to the Comprehender itself.
#

#   Propose an operator to decide to perform a command
sp {interpret*propose*do-command
   (state <s> ^top-state <ts> 
              ^problem-space interpret
              ^comprehension <c>
             -^action <a>)
   (<c> ^cxn-stack.meaning <m>)
   (<m> ^basic Command)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-command
        ^type interpretation
        ^meaning <m>
        ^debug-info (concat |Decide to perform a Command.|))
}

#   Create an action to execute the command
sp {interpret*apply*do-command
   (state <s> ^top-state <ts> 
              ^problem-space interpret
              ^operator <o>)
   (<o> ^name do-command
        ^meaning <m>)
   (<m> ^is-a Command
        ^roles <roles>)
   (<roles> ^verb <verb>
            ^object <obj>)
   (<obj> ^is-a RefDesc)
-->
   #   Add an action to assert the relation
   (<s> ^action <a-command>)
   (<a-command> ^action COMMAND
                ^arg1 <verb>
                ^arg2 <obj>
                ^next nil)
}

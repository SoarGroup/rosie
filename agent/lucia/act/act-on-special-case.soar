##!
# @file
#
# @created plind 20161222
# File: act-on-special-case.soar
# Author: Peter Lindes
# Created: 22 December 2016
#
# Desc:  Send a message to perform a special case action.
#

#   Propose an operator to perform a finished action.
sp {act*propose*act-on-special-case*finished
   (state <s> ^problem-space act
              ^action <a>)
   (<a> ^action SPECIAL-CASE
        ^rosie-action finished
        ^text <text>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name act-on-special-case
        ^rosie-action finished
        ^text <text>
        ^debug-info (concat |Create a new "finished" action message.|))
}

#   Build a Rosie message for this action
sp {act*apply*act-on-special-case*finished
   (state <s> ^problem-space act
              ^operator <o>
              ^action <a>)
   (<o> ^name act-on-special-case
        ^rosie-action finished
        ^text <text>)
   (<a> ^next <next>)
-->
   #    Create a message
   (<s> ^result <message>)
   (<message> ^message-type finished)
   #   Go on to the next
   (<s> ^action <a> -
        ^action <next>)
}

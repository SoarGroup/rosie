### If there are multiple groundings, search the ^dialog-object-list-access
### to see if there is one in that list can be used.
### The access list is a separate pointer from from ^dialog-object-list
###  to avoid GDS issues when something is added to the list in the middle of sentence processing.

sp {ground-referent*propose*process-multiple-groundings
#:interrupt
   (state <s> ^name ground-referent             
              ^proto-referents multiple
             -^processed-multiple true
              ^candidate-set <cs>
              ^constraint-processing-finished true)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name process-multiple-groundings
         ^candidate-set <cs>)
}

sp {ground-referent*apply*process-multiple-groundings
   (state <s> ^operator <op>)
   (<op> ^name process-multiple-groundings
         ^candidate-set <cs>)
   (<cs> ^proto-referent <pr-g>
         ^proto-referent <pr-d>)
   (<pr-g> ^object.predicates.status live)
   (<pr-d> ^object.predicates.is-visible1 not-visible1)
-->
   (<cs> ^proto-referent <pr-d> -)
   (write (crlf) |Remove not visible referent: | <pr-d> | when live one is avaialble: | <pr-g>)
}

sp {process-multiple-groundings*hypothetical-remove*for-multiples
   (state <s> ^operator <op>
              ^constraint.multiple true
             -^constraint-lt.specifier << it definitive >>
              ^segment.hypothetical true)
   (<op> ^name process-multiple-groundings
         ^candidate-set <cs>)
   (<cs> ^proto-referent <pr>)
-->
   (<cs> ^proto-referent <pr> -)
}

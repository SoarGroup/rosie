sp {elaborate*top-state*pass-down-evaluate
   (state <s> ^name evaluate-operator
              ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)}

                   ### NEEDS WORK
sp {evaluate-operator*elaborate*state*merge
   (state <s> ^name evaluate-operator
              ^superstate <ss>)
   (<ss> ^operator.superoperator <so>
         ^top-state.world <world>
         ^superstate <sss>)
   (<so> ^name merge
         ^assigner <assigner>
         ^receiver <receiver>)
   (<sss> ^segment.last-merge <lm>)
   (<lm> ^receiver <r1>
         ^assigner <a1>
         ^structure-type <st>)
   (<assigner> ^parent-receiver <aps>)
-->
   (<s> ^world <world>
        ^id-mapping <im>
        ^assigner-parent-receiver <apsi>
        ^segment <seg>)
   (<seg> ^last-merge <lmx>
          ^not-merged-receiver <ri>
          ^not-merged-assigner <ni>)
   (<lmx> ^receiver <r1>
          ^assigner <a1>
          ^structure-type <st>)
   (<im> ^<assigner> <ni>
         ^<receiver> <ri>
         ^<aps> <apsi>)
   (<ni> ^parent-receiver <apsi>)
}

sp {evaluate-operator*elaborate*state*receiver
   (state <s> ^name evaluate-operator
              ^superstate <ss>
              ^id-mapping <im>)
   (<ss> ^operator.superoperator.name merge
         ^superstate.segment <sss>)
   (<sss> ^not-merged-receiver <receiver>)
   (<receiver> ^<att> <value>)
   (<im> ^<receiver> <ri>)
-->
   (<ri> ^<att> <value>)
}

sp {evaluate-operator*elaborate*state*assigner
   (state <s> ^name evaluate-operator
              ^superstate <ss>
              ^id-mapping <im>)
   (<ss> ^operator.superoperator.name merge
         ^superstate.segment <sss>)
   (<sss> ^not-merged-assigner <assigner>)
   (<im> ^<assigner> <ai>)
   (<assigner> ^{ <> parent-receiver <att> } <value>)
-->
   (<ai> ^<att> <value>)
}

sp {evaluate-operator*elaborate*state*receiver*parent
   (state <s> ^name evaluate-operator
              ^superstate <ss>
              ^id-mapping <im>)
   (<ss> ^operator.superoperator.name merge
         ^superstate.segment <sss>)
   (<sss> ^not-merged-assigner <uai>)
   (<uai> ^parent-receiver <aps>)
   (<aps> ^<att> <value>)
   (<im> ^<aps> <apsi>
         ^<uai> <nai>)
-->
   (<apsi> ^<att> <value>)
}

production excise evaluate-operator*elaborate*state*look-ahead-operator*no-copy
sp {evaluate-operator*elaborate*state*look-ahead-operator*merge*copy
   :default
   (state <s> ^name evaluate-operator
              ^superstate.operator <so>
              ^id-mapping <im>)
   (<so> ^name evaluate-operator
         ^superoperator <o>)
   (<o> ^name merge
        ^receiver <receiver>
        ^assigner <assigner>)
   (<im> ^<assigner> <ni>
         ^<receiver> <ri>)
-->
   (<s> ^look-ahead-operator <copy-o>)
   (<s> ^untried-tied-operator <copy-o>)
   (<copy-o> ^name merge
             ^receiver <ri>
             ^assigner <ni>)}


sp {top-state*better-preferences*lookup-preposition
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name lookup-preposition)
   (<o2> ^name << interaction generate-action-operator comprehension manage-action-stack satisfy-purpose action-learning execute-action attend-to-scene-change wait >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*interaction
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name interaction)
   (<o2> ^name << generate-action-operator comprehension manage-action-stack satisfy-purpose action-learning execute-action attend-to-scene-change wait >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*comprehension
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name comprehension)
   (<o2> ^name << generate-action-operator manage-action-stack satisfy-purpose action-learning execute-action attend-to-scene-change wait >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*generate-action-operator
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name generate-action-operator)
   (<o2> ^name << manage-action-stack satisfy-purpose action-learning execute-action attend-to-scene-change wait >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*manage-action-stack
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name manage-action-stack)
   (<o2> ^name << satisfy-purpose action-learning execute-action attend-to-scene-change wait >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*manage-action-stack*prefer*push-action*higher*first
    (state <s> ^superstate nil
               ^operator <o1> +
               ^operator <o2> +)
    (<o1> ^name manage-action-stack
          ^change-name push
          ^change-info.order <i1>)
    (<o2> ^name manage-action-stack
          ^change-name push
          ^change-info.order { <i2> < <i1> })
-->
    (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*satisfy-purpose
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << satisfy-purpose execute-action action-learning >>)
   (<o2> ^name << attend-to-scene-change wait >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*execute-action
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name execute-action)
   (<o2> ^name satisfy-purpose)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*attend-to-scene-change
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change)
   (<o2> ^name << wait >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {top-state*better-preferences*init-agent
   (state <s> ^superstate nil
   		  	  ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name init-agent)
   (<o2> -^name init-agent)
-->
   (<s> ^operator <o1> > <o2>)
}


sp {retrieve-synonym-version*elaborate*handle-spelling
   (state <s> ^name retrieve-synonym-version
              ^superstate.operator <sop>)
   (<sop> ^handle <handle>)
-->
   (<s> ^handle-spelling <handle>)
}
#hmm dont want to build synonym with empty version...
sp {retrieve-synonym-version*elaborate*default-version
   (state <s> ^name retrieve-synonym-version
              ^superstate.operator <sop>)
   (<sop> ^default-version true)
-->
   (<s> ^default-version true)
}
sp {retrieve-synonym-version*elaborate*smem-query
   (state <s> ^name retrieve-synonym-version
              ^smem.command <c>
			 -^default-version true
			  ^handle-spelling <handle>)
-->	
   (<c> ^query <cue>
        ^neg-query <ncue> 
        ^depth 5)
   (<cue> ^handle <x> ^handle-spelling <handle>)
   (<ncue> ^multiple true)
}
#TODO but dont retrieve empty language concepts as synonyms..
sp {retrieve-synonym-version*elaborate*smem-query*default
   (state <s> ^name retrieve-synonym-version
              ^smem.command <c>
			  ^default-version true
			  ^handle-spelling <handle>)
-->	
   (<c> ^query <cue>
        ^neg-query <ncue> #TODO add neg cue for handle-spelling
        ^depth 5)
   (<cue> ^handle <handle>)
   (<ncue> ^multiple true)
}
sp {retrieve-synonym-version*elaborate*smem-query*prohibit
   (state <s> ^name retrieve-synonym-version
              ^smem.command <com>
              ^superstate.synonym-retrieved <pr>)
-->	
   (<com> ^prohibit <pr>)
}
sp {retrieve-synonym-version*elaborate*smem-query*failure
:o-support
   (state <s> ^name retrieve-synonym-version
   		  	  ^superstate <ss>
			  ^handle-spelling <ch>
              ^smem.result.failure <rr>)
-->
   (<ss> ^completed-synonym <ch>)
}
sp {retrieve-synonym-version*elaborate*smem-query*synonym-retrieved
:o-support
   (state <s> ^name retrieve-synonym-version
   		  	  ^superstate <ss>
              ^smem.result.retrieved <rr>)
   (<rr> ^handle <cn>)
-->
   (<ss> ^synonym-retrieved <rr>)
}
sp {retrieve-synonym-version*elaborate*compeleted-true
:o-support
   (state <s> ^name retrieve-synonym-version
   		  	  ^superstate <ss>
              ^smem.result.retrieved <rr>)
   (<rr> ^handle <cn>)
   (<ss> ^operator <o>)
-->
   (<o> ^completed true)
}
sp {retrieve-synonym-version*elaborate*compeleted-true*failure
:o-support
   (state <s> ^name retrieve-synonym-version
   		  	  ^superstate <ss>
              ^smem.result.failure <rr>)
   (<ss> ^operator <o>)
-->
   (<o> ^completed true)
}

#######################
####     DEBUG     ####
#######################
sp {retrieve-synonym-versions*elaborate*smem-query*prohibitPRINT
   (state <s> ^name retrieve-synonym-versionsXX
              ^smem.command <c>)
   (<c> ^query <q>)
# (<q> ^handle-spelling empty) 
-->	
   (interrupt)
   (write (cmd print <c> -d 3))
}
sp {store-predicate-structure*elaborate*attributes
   (state <s> ^name store-predicate-structure
              ^superstate.operator <o>)
   (<o> ^storing <t> ^handle <h> ^message <msg> ^type <y>)
-->
   (<s> ^storing <t> ^handle <h> ^message <msg> ^ptype <y>)
}

sp {store-predicate-structure*elaborate*multiplayer
   (state <s> ^name store-predicate-structure
              ^superstate.operator <o>)
   (<o> ^multiplayer <m>)
-->
   (<s> ^multiplayer <m>)
}

sp {store-predicate-structure*propose*complete
   (state <s> ^name store-predicate-structure)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete)
}

sp {store-predicate-structure*apply*complete*game-name
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^ptype game
              ^top-state.interaction.status <status>
              ^top-state.interaction.changes <changes>)
-->
   (<changes> ^outgoing-message <msg>)
   (<msg> ^type describe-game
          ^complete-message true)
   (<status> ^performed.learning-event <de>)
   (<de> ^type game
         ^originator agent)
}

sp {store-predicate-structure*apply*complete*puzzle-name
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^ptype puzzle
              ^top-state.interaction.status <status>
              ^top-state.interaction.changes <changes>)
-->
   (<changes> ^outgoing-message <msg>)
   (<msg> ^type describe-puzzle
          ^complete-message true)
   (<status> ^performed.learning-event <de>)
   (<de> ^type game
         ^originator agent)
}

sp {store-predicate-structure*apply*complete*goal-name
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^found-transferable-knowledge false
              ^ptype goal
              ^top-state.interaction.status <status>
              ^top-state.interaction.changes <changes>)
-->
   (<changes> ^outgoing-message <msg>)
   (<msg> ^type setup-goal
          ^complete-message true)
   (<status> ^performed.learning-event <de>)
   (<de> ^type game-name
         ^originator agent)
}

sp {store-predicate-structure*apply*complete*failure-name
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^found-transferable-knowledge false
              ^ptype failure
              ^top-state.interaction.status <status>
              ^top-state.interaction.changes <changes>)
-->
   (<changes> ^outgoing-message <msg>)
   (<msg> ^type setup-failure
          ^complete-message true)
   (<status> ^performed.learning-event <de>)
   (<de> ^type game-name
         ^originator agent)
}

sp {store-predicate-structure*apply*complete*action-name
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^found-transferable-knowledge false
              ^ptype action
              ^top-state.interaction.status <status>
              ^top-state.interaction.changes <changes>)
-->
   (<changes> ^outgoing-message <msg>)
   (<msg> ^type describe-action
          ^complete-message true)
   (<status> ^performed.learning-event <de>)
   (<de> ^type game-name
         ^originator agent)
}

sp {store-predicate-structure*apply*complete*action-goal-failure-transfer
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^found-transferable-knowledge true
              ^copied <r>
              ^handle <handle>
              ^ptype {<ptype> << action goal failure >>}
              ^top-state.interaction.status <status>
              ^top-state.interaction.changes <changes>)
-->
   (<changes> ^outgoing-message <msg>)
   (<msg> ^type already-know
          ^complete-message true
          ^fields <f>)
   (<f> ^concept-name <handle> ^type <ptype>)
   (<status> ^performed.learning-event <de>)
   (<de> ^type game
         ^originator agent)
}

sp {store-predicate-structure*apply*complete*remove-learn-link
   (state <s> ^name store-predicate-structure
              ^operator.name complete
             -^message.predicate <ll>
              ^top-state <ts>)
   (<s> ^storing name)
   (<ts> ^dialog-context <dc>)
   (<dc> ^learn-link <ll>)
-->
   (<dc> ^learn-link <ll> -)
}

sp {store-predicate-structure*elaborate*top-state*dialogue-context*puzzle
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^top-state <ts>)
   (<s> ^storing name ^ptype puzzle ^handle <handle> ^message.predicate <pred>)
-->
   (<ts> ^dialog-context <dc>)
   (<dc> ^type game-learning ^name <handle> ^structure <pred> ^learning true ^needs-setup true ^synonyms <sy> ^antonyms <ay>)
   (<pred> ^actions <act> ^goals <go> ^failures <fa> ^heuristics <heu> ^multiplayer false ^task-type puzzle)
}

sp {store-predicate-structure*elaborate*top-state*dialogue-context*game
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^top-state <ts>)
   (<s> ^storing name ^ptype game ^handle <handle> ^message.predicate <pred> )
-->
   (<ts> ^dialog-context <dc>)
   (<dc> ^type game-learning ^name <handle> ^structure <pred> ^learning true ^needs-setup true ^synonyms <sy> ^antonyms <ay>)
   (<pred> ^actions <act> ^goals <go> ^failures <fa> ^heuristics <heu> ^multiplayer true ^task-type game)
}

sp {store-predicate-structure*elaborate*top-state*dialogue-context*goal
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^top-state <ts>)
   (<s> ^storing name ^ptype goal ^handle <handle> ^message.predicate <pred2> ^found-transferable-knowledge false)
   (<ts> ^dialog-context <dc>)
   (<dc> ^structure <pred>)
   (<pred> ^goals <go>)
-->
   (<go> ^goal <pred2>)
   (<pred2> ^predicate-structure <ps> ^type goal)
   (<dc> ^learn-link <pred2> ^needs-goal-setup true)
}

sp {store-predicate-structure*elaborate*top-state*dialogue-context*failure
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^top-state <ts>)
   (<s> ^storing name ^ptype failure ^handle <handle> ^message.predicate <pred2> ^found-transferable-knowledge false)
   (<ts> ^dialog-context <dc>)
   (<dc> ^structure <pred>)
   (<pred> ^failures <go>)
-->
   (<go> ^failure <pred2>)
   (<pred2> ^predicate-structure <ps> ^type failure)
   (<dc> ^learn-link <pred2> ^needs-failure-setup true)
}

sp {store-predicate-structure*elaborate*top-state*dialogue-context*action
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^top-state <ts>)
   (<s> ^storing name ^ptype action ^handle <handle> ^message.predicate <pred2> ^found-transferable-knowledge false)
   (<ts> ^dialog-context <dc>)
   (<dc> ^structure <pred>)
   (<pred> ^actions <act>)
-->
   (<act> ^action <pred2>)
   (<pred2> ^predicate-structure <ps> ^type action)
   (<dc> ^learn-link <pred2>)
}

sp {store-predicate-structure*elaborate*query*find-transferable*knowlege
   (state <s> ^name store-predicate-structure
              ^smem.command <sc>)
   (<s> ^storing name ^ptype << action goal failure >> ^handle <handle>)# ^message.predicate <pred2>) #Jk action fix
-->
   (<sc> ^query <pred2> ^depth 25)
   (<pred2> ^handle <handle> ^item-type predicate)
}

sp {store-predicate-structure*elaborate*found-transferable-knowledge*true
    :o-support
   (state <s> ^name store-predicate-structure
              ^smem.result.retrieved)
-->
   (<s> ^found-transferable-knowledge true)
}

sp {store-predicate-structure*propose*transfer-action-goal
   (state <s> ^name store-predicate-structure
              ^smem.result.retrieved <struc>
              ^found-transferable-knowledge true
              ^storing name 
              ^ptype <type> 
              ^handle <handle>)
-->
    (<s> ^operator <o> + >)
    (<o> ^name transfer-action-goal
         ^check yes
         ^type <type>
         ^structure <struc>
         ^handle-name <handle>)
}
sp {store-predicate-structure*apply*transfer-action-goal
   (state <s> ^name store-predicate-structure
              ^smem.result.retrieved <struc>
              ^found-transferable-knowledge true
              ^storing name 
              ^ptype <type> 
			  ^top-state.dialog-context <dc>
              ^handle <handle>)
    (<s> ^operator <o>)
    (<o> ^name transfer-action-goal
         ^check yes
         ^type <type>
         ^structure <struc>
         ^handle-name <handle>)
    (<struc> ^predicate-structure <ps>)
-->
#(write (cmd print <o> -d 2))
#(write (cmd print <dc> -d 2))
	(<s> ^new-handle (make-constant-symbol <handle>))
	(<s> ^ps-copy (deep-copy <ps>))
}

sp {store-predicate-structure*elaborate*found-transferable-knowledge*false
    :o-support
   (state <s> ^name store-predicate-structure
              ^smem.result.failure)
-->
   (<s> ^found-transferable-knowledge false)
}

sp {store-predicate-structure*elaborate*top-state*dialogue-context*multiplayer
   (state <s> ^name store-predicate-structure
              ^operator.name complete
              ^top-state <ts>)
   (<s> ^multiplayer <mult>)
   (<ts> ^dialog-context <dc>)
   (<dc> ^structure <pred>)
-->
   (<pred> ^multiplayer <mult>)
}

sp {evaluate-operator*propose*look-ahead
   :default
   (state <s> ^look-ahead-operator <o>
              ^untried-tied-operator <o>)
   -->
   (<s> ^operator <o> +)}

sp {evaluate-operator*propose*look-ahead*reject-all-others
   :default
   (state <s> ^look-ahead-operator <o>
              ^untried-tied-operator <o>
              ^operator { <> <o> <o1> } +)
   -->
   (<s> ^operator <o1> -)}

### Detect that the operator has been tried.

# AM 12/19: Added detection of a ^no-default-application flag
#    where if it is present, the user is responsible for 
#    changing the untried-tied-operator to tried-tied-operator

sp {evaluate-operator*elaborate*state*tried-tied-operator
    :default
    (state <s> ^operator <o> 
               ^look-ahead-operator <o>)
    (<o> -^no-default-application true)
-->
    (<s> ^tried-tied-operator <o>)
}

# SBW 5/07: replaced all occurences of -^tried-tied-operator with ^untried-tied-operator
# this takes care of lots of local negations
 sp {evaluate-operator*elaborate*state*remove*untried-tied-operator2
    :default
    (state <s> ^look-ahead-operator <o> 
               ^operator <o>
               ^untried-tied-operator <o>)
    (<o> -^no-default-application true)
 -->
    (<s> ^untried-tied-operator <o> -)
 }

sp {describe-message*propose*lookup-preposition-list
   (state <s> ^name describe-message
             -^prep-list)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-preposition-list
        ^category svs)
}

sp {describe-message*propose*lookup-preposition
   (state <s> ^name describe-message
             -^found-prep.predicate.name <prep>
              ^prep-list.prep <prep>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-preposition
        ^category svs
        ^type <prep>)
}

sp {describe-message*propose*find-relations
   (state <s> ^name describe-message
              ^topstate.svs.spatial-scene <sc>
              ^found-prep <fprep>)
   (<fprep> ^predicate.name <prep>)
   (<sc> ^child <C>)
   (<C> ^id <obja> ^id-int <id>)
   -{(<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <prep>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-relations
        ^category svs
        ^obj <id>
        ^ind 2
        ^found-prep <fprep>
        ^prep <prep>)
}

sp {describe-message*propose*describe-object
   (state <s> ^name describe-message
              -^described <obj>
               ^topstate.svs.spatial-scene <sc>
               ^topstate.object <obj>)
   (<obj> ^id <id>
         -^property.word << pantry stove table garbage >>)
   (<sc> ^child <C>)
   (<C> ^id-int <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name describe-object
        ^category svs
        ^object <obj>)
}

sp {describe-message*apply*describe-object
   (state <s> ^name describe-message
              ^operator <o>)
   (<o> ^name describe-object
        ^object <obj>
        ^description <desc>)
   (<s> ^descriptions <d>
        ^obj-descs <descs>)
-->
   (<s> ^described <obj>)
   (<descs> ^object <desc>)
   (<d> ^obj-desc <objd>)
   (<objd> ^desc <desc> ^object <obj>)
}

sp {describe-message*propose*complete-describe-message*send-message
   (state <s> ^name describe-message
              ^relationships <rels>
              ^topstate.io.output-link <out>
              ^descriptions <d>)
   (<d> ^obj-desc <objd1> ^obj-desc <objd2>)
   (<objd1> ^desc <desc1> ^object <obj1>)
   (<objd2> ^desc <desc2> ^object <obj2>)
   (<obj1> ^id <id1>)
   (<obj2> ^id {<id2> <> <id1>})
   (<rels> ^preposition <p> ^with <id2> ^relations <rel>)
   (<rel> ^matched true ^obj <id1>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type describe-scene
          ^fields <f>)
   (<f> ^object1 <desc1> ^object2 <desc2> ^prep <p>)
   (<s> ^operator <o> + =)
   (<o> ^name complete-describe-message
        ^category svs)
}

sp {describe-message*propose*complete-describe-message*no-preps
   (state <s> ^name describe-message
             -^prep-list.prep
              ^topstate.io.output-link <out>
              ^descriptions <d>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-describe-message
        ^category svs)
}

sp {describe-message*propose*complete-describe-message*no-objs
   (state <s> ^name describe-message
             -^topstate.object
              ^topstate.io.output-link <out>
              ^descriptions <d>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-describe-message
        ^category svs)
}

sp {describe-message*propose*complete-describe-message*no-matches
   (state <s> ^name describe-message
              ^relationships <rels>
              ^topstate.io.output-link <out>
              ^descriptions <d>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-describe-message
        ^category svs)
}
sp {describe-message*apply*complete-describe-message
   (state <s> ^name describe-message
              ^topstate.status <status>
              ^obj-descs <descs>
              ^topstate.io.output-link <out>
              ^operator <o>)
   (<o> ^name complete-describe-message)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type describe-scene-objects
          ^fields.objects <descs>)
   (<status> ^performed.learning-event.type explanation)
}


#################### get-next-subaction ######################
# Parameters:
#    task-operator - the task-operator representing the task
#                      for which we are asking for the goal

# Push a new segment to get the goal from the instructor
sp {anystate*apply*initiate-interaction*get-next-subaction*push*segment
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-next-subaction)
-->
   (<chgs> ^new-segment-info <info>)
   (<info> ^purpose <p>
         ^originator agent)
   (<p> ^type get-next-subaction
      ^satisfaction.dialog-event <de>)
   (<de> ^type command
         ^type goal-description
         ^originator instructor)
}

# Send an outgoing message to ask for the next action to take
sp {anystate*apply*initiate-interaction*get-next-subaction*send*message*get-next-subaction
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-next-subaction)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type get-next-subaction
          ^complete-message true
          ^fields <f>)
}

#################### get-find-help ######################
# Parameters:
#    object - the object the agent can't find

# Push a new segment to find out if the instructor knows where the object is
sp {anystate*apply*initiate-interaction*get-find-help*push*segment
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-find-help
        ^object <obj>)
-->
   (<chgs> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type get-find-help
        ^parameters <params>
        ^satisfaction.dialog-event <de>)
   (<params> ^object <obj>)
   (<de> ^type ANY
         ^originator instructor)
}

# Send an outgoing message to ask for help about the object
sp {anystate*apply*initiate-interaction*get-find-help*send*message*get-goal-info
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-find-help
        ^object <obj>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type cant-find-object
          ^complete-message true
          ^fields.object <obj>)
}


# Push a new segment to get the goal from the instructor
sp {anystate*apply*initiate-interaction*get-predicate-info*push*segment
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-predicate-info)
-->
   (<chgs> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type get-predicate-info
        ^satisfaction.dialog-event <de>)
   (<de> ^type adjective-definition
         ^originator instructor)
}

# Send an outgoing message to ask for the goal
sp {anystate*apply*initiate-interaction*get-predicate-info*send*message*get-goal-info
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-predicate-info
        ^predicate <pred>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type get-predicate-info
          ^complete-message true
          ^fields.predicate-name <pred>)
}

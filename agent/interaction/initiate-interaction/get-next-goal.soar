#################### get-next-goal ######################
# Parameters:
#    task-operator - the task-operator representing the task
#                      for which we are asking for the goal

# Push a new segment to get the goal from the instructor
sp {anystate*apply*initiate-interaction*get-next-goal*push*segment
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-next-goal
        ^task-operator <aop>)
-->
   (<chgs> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type get-next-goal
        ^parameters.task-operator <aop>
        ^satisfaction.dialog-event <de>)
   (<de> ^type goal-description command
         ^originator instructor)
}

# Send an outgoing message to ask for the goal
sp {anystate*apply*initiate-interaction*get-next-goal*send*message*get-next-goal
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-next-goal)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type get-next-goal
          ^complete-message true
          ^fields <f>)
}

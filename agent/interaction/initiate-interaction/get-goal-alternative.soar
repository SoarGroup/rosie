#################### get-goal-alternative ######################
# Parameters:
#    task-operator - the task-operator representing the task
#                      for which we are asking for another goal

# Push a new segment to get the goal from the instructor
sp {anystate*apply*initiate-interaction*get-goal-alternative*push*segment
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-goal-alternative
        ^task-operator <aop>)
-->
   (<chgs> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type get-goal-alternative
        ^parameters.task-operator <aop>
        ^satisfaction.dialog-event <de>)
   (<de> ^type goal-description command
         ^originator instructor)
}

# Send an outgoing message to ask for the goal
sp {anystate*apply*initiate-interaction*get-goal-alternative*send*message*get-goal-alternative
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type get-goal-alternative
        ^task-operator.task-handle <task-h>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type get-goal-alternative
          ^complete-message true
          ^fields <f>)
   (<f> ^task-handle <task-h>)
}

###################################################################
#
# interpret-object-description
#   Occurs when the intructor wishes to give additional information about an object
#   Construction: *-DP-is-ADJ/DP/PP-T
#   	E.g. 'The ball is red', 'The ball is a sphere', 'The ball is in the pantry'
#   Construction: *-This-is-DP-T
#   	E.g. 'This is a large red block', 'This is to the right of the sphere'
#
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*interpret-object-description
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type object-description
          -^interaction-status.failure
         -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message <msg>)
}

######
# OPERATOR ELABORATIONS
######

# Elaborate useful information from the message onto the operator

# goal: OPTIONAL - the robot is the object
sp {interpret-message*elaborate*interpret-object-description*goal
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.arg1 <obj>)
   (<obj> ^handle goal)
-->
   (<o> ^goal <obj>)
}

# robot: OPTIONAL - the robot is the object
sp {interpret-message*elaborate*interpret-object-description*robot
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.arg1 <obj>)
   (<obj> ^handle rosie)
-->
   (<o> ^robot <obj>)
}

# object: REQUIRED - the object being described
sp {interpret-message*elaborate*interpret-object-description*object
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.arg1 <obj>
       -^goal <obj>
       -^robot <obj>)
   (<obj> ^item-type object)
-->
   (<o> ^object <obj>)
}

# object2: REQUIRED - the object being told is equivalent
sp {interpret-message*elaborate*interpret-object-description*object2
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.arg2 <obj>)
   (<obj> ^item-type object)
-->
   (<o> ^object2 <obj>)
}

# predicate: OPTIONAL - tells something that is true about the object (e.g. color=red)
sp {interpret-message*elaborate*interpret-object-description*predicate
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.predicate <pred>)
   (<pred> -^{2} <obj2>)
-->
   (<o> ^predicate <pred>)
}

# relation: OPTIONAL - how the object is in relation to another
sp {interpret-message*elaborate*interpret-object-description*relation
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<rel> -^property.handle << color shape size >>)
   (<o> ^name interpret-object-description
        ^message.{ << relation predicate >> } <rel>)
   (<rel> ^2 <other>)
-->
   (<o> ^relation <rel>)
}

sp {interpret-message*elaborate*interpret-object-description*arg2
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.arg2 <rel>)
   (<rel> ^item-type predicate)
-->
   (<o> ^relation <rel>)
}

sp {interpret-message*elaborate*interpret-object-description*subclause
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.subclause <sub>)
-->
   (<o> ^subclause <sub>)
}

sp {interpret-message*elaborate*interpret-object-description*subclause*level*2
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^subclause.subclause <sub>)
-->
   (<o> ^subclause <sub>)
}

#####
# MESSAGE CATEGORIZATION
#####

sp {interpret-message*elaborate*interpret-object-description*purpose*train-on-object
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^object <obj>
        ^predicate <pred>)
      -{(<s> #^instructor-message.predicate.handle initial 
          ^top-state.dialog-context.type game-learning)}
   (<obj> -^handle message1)
-->
   (<o> ^purpose train-on-object)
}

sp {interpret-message*elaborate*interpret-object-description*purpose*give-answer
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^message.arg1.predicates.category message
        ^message.arg2.sentence <sent>)
-->
   (<o> ^purpose give-answer)
}

sp {interpret-message*elaborate*interpret-object-description*purpose*learn-goal-definition
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^goal <goal>
        ^subclause <sub>)
-->
   (<o> ^purpose learn-goal-definition)
}

sp {interpret-message*elaborate*interpret-object-description*purpose*task-completed
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^robot <rob>
        ^predicate.handle task-completed)
-->
   (<o> ^purpose task-completed)
}
   
sp {interpret-message*elaborate*interpret-object-description*purpose*teach-location
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^robot <rob>
        ^relation <rel>)
-->
   (<o> ^purpose teach-location)
}
   
sp {interpret-message*elaborate*interpret-object-description*purpose*teach-relation
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^object <obj>
        ^relation <rel>)
   -{(<s> #^instructor-message.predicate.handle initial 
          ^top-state.dialog-context.type game-learning)}
   (<obj> -^handle message1)
-->
   (<o> ^purpose teach-relation)
}
   
sp {interpret-message*elaborate*interpret-object-description*purpose*equate-objects
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^object <obj>
        ^object2 <obj2>)
-->
   (<o> ^purpose equate-objects)
}


#####
# OPERATOR APPLICATION
#####

###########################
# Purpose: train-on-object

sp {interpret-message*apply*interpret-object-description*create*segment*train-on-object
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^purpose train-on-object
        ^message <msg>
        ^object <obj>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type train-on-object 
        ^parameters <params>
        ^satisfaction.learning-event.type object)
   (<params> ^object <obj>)
}

sp {interpret-message*apply*interpret-object-description*copy*predicates
   (state <s> ^name interpret-message
              ^operator <o>
              ^new-segment-info.purpose.parameters <params>)
   (<o> ^name interpret-object-description
        ^purpose train-on-object
        ^predicate <pred>)
-->
   (<params> ^predicate <pred>)
}

##########################
# Purpose: give-answer

sp {interpret-message*apply*interpret-object-description*mark*answer
   (state <s> ^name interpret-message
              ^operator <o>
              ^current-purpose.type wait-for-answer
              ^top-state.action-stack.top <top-action>)
   (<o> ^name interpret-object-description
        ^purpose give-answer
        ^message <msg>)
  (<msg> ^arg2.sentence <sentence>)
-->
   (<s> ^mark-interpreted <msg>)
   (<top-action> ^answer <sentence>)
}

#  TODO JAMES continue
## and make non-if based predicate sentences to test out...
#purpose: teach new predicate
sp {interpret-message*apply*interpret-object-description*create*segment*teach-new-predicate
#:interrupt
   (state <s> ^name interpret-message
              ^top-state.dialog-context.learning-predicate.name <lpred>
              ^top-state.interaction.status <status>
              ^operator <o>)
   (<o> ^name interpret-object-description
        -^purpose
        ^message <msg>)
   (<msg> ^arg2.{2}.handle <lpred>)
-->
   (<status> ^performed.dialog-event <de>)
   (<de> ^type process-unknown-word
         ^originator instructor)
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-property-predicate
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}
sp {interpret-message*apply*interpret-object-description*create*segment*teach-new-predicate*single
#:interrupt
   (state <s> ^name interpret-message
              ^top-state.dialog-context.learning-predicate.name <lpred>
              ^top-state.interaction.status <status>
              ^operator <o>)
   (<o> ^name interpret-object-description
        -^purpose
        ^message <msg>)
   (<msg> ^predicate.handle <lpred> -^arg2)
-->
   (<status> ^performed.dialog-event <de>)
   (<de> ^type process-unknown-word
         ^originator instructor)
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-property-predicate
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}
sp {interpret-message*apply*interpret-object-description*copy-old-link
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc>
              ^top-state.dialog-context.learning-predicate.name <lpred>
              ^operator <o>)
   (<o> ^name interpret-object-description
       -^purpose
        ^message <msg>)
   (<msg> ^arg2.{2}.handle <lpred>)
   (<dc> ^learn-link <ll>)
   (<ll> ^type << action goal failure >>)
-->
    (<dc> ^store-learn-link <ll>)
    (<dc> ^learn-link <ll> -)
}
sp {interpret-message*apply*interpret-object-description*copy-old-link*arg1
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc>
              ^top-state.dialog-context.learning-predicate.name <lpred>
              ^operator <o>)
   (<o> ^name interpret-object-description
       -^purpose
        ^message <msg>)
   (<msg> ^predicate.handle <lpred> -^arg2)
   (<dc> ^learn-link <ll>)
   (<ll> ^type << action goal failure >>)
-->
    (<dc> ^store-learn-link <ll>)
    (<dc> ^learn-link <ll> -)
}

##>> here
sp {interpret-message*apply*interpret-object-description*create*context-links
#:interrupt
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc>
              ^operator <o>)
   (<o> ^name interpret-object-description
        -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^predicate <pred> -^arg2)
   (<pred> ^handle <name> -^2 ^property <p>)
   (<lp> ^name <name>)
-->
#(interrupt)
   (<dc> ^learn-link <ll>
         ^input-object-1 <obj>)
   (<ll> ^handle <name> ^item-type predicate ^property <p>)
}
sp {interpret-message*apply*interpret-object-description*create*context-links*visual-unary
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc> 
              ^operator <o>)
   (<o> ^name interpret-object-description
       -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^predicate <pred> -^arg2)
   (<pred> ^handle <name> -^2 ^item-type predicate -^property)
   (<lp> ^name <name>)
-->
#(interrupt)
   (<dc> ^learn-link <ll>
         ^input-object-1 <obj>)
   (<ll> ^handle <name> ^item-type predicate ^property <prop>)
   (<prop> ^handle property ^item-type property ^type visual)
}
sp {interpret-message*apply*interpret-object-description*create*context-links*spelling
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc> 
              ^operator <o>)
   (<o> ^name interpret-object-description
       -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^predicate <pred> -^arg2)
   (<pred> ^handle <name> -^2 ^property <p>)
   (<lp> ^spelling <name> ^name <name2>)
-->
#(interrupt)
   (<dc> ^learn-link <ll>
         ^input-object-1 <obj>)
   (<ll> ^handle <name2> ^item-type predicate ^property <p>)
}
sp {interpret-message*apply*interpret-object-description*create*context-links*2args*spelling
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc> 
              ^operator <o>)
   (<o> ^name interpret-object-description
       -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^predicate <pred> -^arg2)
   (<pred> ^handle <name> ^2 <obj2> -^handle of1)
   (<lp> ^spelling <name> ^name {<name2> <> <name>})
-->
#(interrupt)
   (<dc> ^learn-link <ll>
         ^input-object-1 <obj>
         ^input-object-2 <obj2>)
   (<ll> ^handle <name2> ^item-type predicate)
} 
sp {interpret-message*apply*interpret-object-description*create*context-links*2args
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc> 
              ^operator <o>)
   (<o> ^name interpret-object-description
       -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)   
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^predicate <pred> -^arg2)
   (<pred> ^handle <name> ^2 <obj2> -^handle of1)
   (<lp> ^name <name>)
   (<dc> -^learn-link.handle <name>)
-->
#(interrupt)
   (<dc> ^learn-link <ll>#<pred>
         ^input-object-1 <obj>
         ^input-object-2 <obj2>)
   (<ll> ^handle <name> ^item-type predicate)
} 
sp {interpret-message*apply*interpret-object-description*create*context-links*2args*attribute-of
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc> 
              ^operator <o>)
   (<o> ^name interpret-object-description
        -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^predicate <pred> -^arg2)
   (<pred> ^handle of1 ^2 <pred2> ^item-type predicate)
   (<pred2> ^handle <name> ^2 <obj2>)
   (<lp> ^name <name>)
   (<dc> -^learn-link.handle <name>)
-->
#(interrupt)
   (<dc> ^learn-link <ll>#<pred>
         ^output-object-1 <obj> #not sure if want or not... still want this to be arg1, but want to not ignore tests..
         ^input-object-2 <obj2>)
   (<ll> ^handle <name> ^item-type predicate)
}

sp {interpret-message*apply*interpret-object-description*create*context-links*2args*x-of
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc> 
              ^operator <o>)
   (<o> ^name interpret-object-description
        -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^arg2 <pred> ^arg2 <p3> -^predicate)
   (<pred> ^handle of1 ^2 <pred2> ^item-type predicate)
   (<pred2> ^handle <name> ^2 <o2>)
   (<p3> ^handle <name> ^relation <relp>)
   (<relp> ^handle of1 ^2 <o2>)
   (<lp> ^name <name>)
   (<dc> -^learn-link.handle <name>)
-->
   (<dc> ^learn-link <ll>#<pred>
         ^output-object-1 <obj> #not sure if want or not... still want this to be arg1, but want to not ignore tests..
         ^input-object-2 <o2>)
   (<ll> ^handle <name> ^item-type predicate)
}

sp {interpret-message*apply*interpret-object-description*create*context-links*2args*x-of*2
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc> 
              ^operator <o>)
   (<o> ^name interpret-object-description
        -^purpose
        ^message <msg>)
   (<dc> ^learning-predicate <lp>)
   #(<msg> ^then-subclause <tc>)
   (<msg> ^arg1 <obj> ^arg2 <pred>  -^predicate)
   (<pred> ^handle of1 ^2 <pred2> ^item-type predicate)
   (<pred2> ^handle <name> ^2 <o2>)
   (<lp> ^name <name>)
   (<dc> -^learn-link.handle <name>)
  -{(<msg> ^arg2 <ff> <> <pred>)}
-->
   (<dc> ^learn-link <ll>#<pred>
         ^output-object-1 <obj> #not sure if want or not... still want this to be arg1, but want to not ignore tests..
         ^input-object-2 <o2>)
   (<ll> ^handle <name> ^item-type predicate)
}
##


###########################
# Purpose: teach-relation

# Object description is a relationship (preposition) example between two objects
sp {interpret-message*apply*interpret-object-description*create*segment*teach-relation
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^purpose teach-relation
        ^message <msg>
        ^object <obj>
        ^relation <rel>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type teach-relation
        ^parameters <params>
        ^satisfaction.learning-event.type relation)
   (<params> ^object <obj>
             ^relation <rel>)
}
 
###########################
# Purpose: task-completed

sp {interpret-message*apply*interpret-object-description*task-completed*terminate-segment
   (state <s> ^name interpret-message
              ^operator <o>
              ^top-state.interaction.stack.segment.context.parameters.action-operator <a>)
   (<o> ^name interpret-object-description
        ^purpose task-completed
        ^message <msg>)
-->
   (<s> ^terminate-segment true
        ^mark-interpreted <msg>
        ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type retrospective-learning
        ^parameters <params>
        ^satisfaction.learning-event.type retrospective)
   (<params> ^action-operator <a>)
}

###########################
# Purpose: teach-location

sp {interpret-message*apply*interpret-object-description*create*segment*teach-location
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^purpose teach-location
        ^message <msg>
        ^robot <robot>
        ^relation.{2} <loc>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type teach-location
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type location)
   (<params> ^location-id <loc>)
}


sp {interpret-message*apply*interpret-object-description*teach-location*terminate-segment
   (state <s> ^name interpret-message
              ^operator <o>
	      		^current-purpose.satisfaction.dialog-event <de>)
   (<o> ^name interpret-object-description
        ^purpose teach-location)
   (<de> ^type location-description
         ^originator instructor)
-->
   (<s> ^terminate-segment true)
}
#JK teach Rosie your name
sp {interpret-message*apply*interpret-object-description*create*segment*learn-teacher-name
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^arg1 <q> ^predicate <p>)
   (<q> ^handle name1)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-teacher-name
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}


#JK 'the name of the game is x"
sp {interpret-message*apply*interpret-object-description*create*segment*learn-game-name
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^arg1.{2} <q>)
   (<q> ^handle name1 ^{2}.handle game1)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-game-name
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}


sp {interpret-message*apply*interpret-object-description*puzzle-name*store*smem
   (state <s> ^name interpret-message
              ^smem.command <sc>
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^arg1.{2} <q> ^predicate.word-structure <ws>)
   (<ws> ^referent <ref>)
   (<q> ^handle name1 ^{2}.<< game handle >> << game1 puzzle1 >>)
-->
    (<sc> ^store <ws> <ref> <prop>)
    (<ws> ^structure-type N ^number singular ^object-feature handle)
    (<ref> ^property <prop>)
    (<prop> ^handle concept ^type conceptual)
}

#'the name of the puzzle is x"
sp {interpret-message*apply*interpret-object-description*create*segment*learn-puzzle-name
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^arg1.{2} <q>)
   (<q> ^handle name1 ^{2}.<< game handle >> puzzle1)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-puzzle-name
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}

#'the name of an action is x"
sp {interpret-message*apply*interpret-object-description*create*segment*learn-action-name
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^arg1.{2} <q>)
   (<q> ^handle name1 ^{2}.<< action handle >> action)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-action-name
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game-name)
}
#'the name of the goal is x"
sp {interpret-message*apply*interpret-object-description*create*segment*learn-goal-name
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^arg1.{2} <q>)
   (<q> ^handle name1 ^{2}.<< goal handle >> goal)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-goal-name
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game-name)
}

#'the name of the failure is x"
sp {interpret-message*apply*interpret-object-description*create*segment*learn-failure-name
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^arg1.{2} <q>)
   (<q> ^handle name1 ^{2}.<< failure handle >> failure)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-failure-name
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game-name)
}

sp {interpret-message*apply*interpret-object-description*create*segment*learn-goal-initiate-state
   (state <s> ^name interpret-message
              ^top-state.dialog-context.type game-learning
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^predicate.handle initial)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-goal-initial-state
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}

sp {interpret-message*apply*interpret-object-description*create*segment*learn-goal-final-state
   (state <s> ^name interpret-message
              ^top-state.dialog-context.type game-learning
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<msg> ^predicate.handle final)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-goal-final-state
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}

sp {interpret-message*apply*interpret-object-description*create*segment*learn-goal-predicate
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc>
              ^operator <o>)
   (<dc> -^needs-goal-setup ^type game-learning ^learn-link)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^message <msg>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-goal-predicate
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}



sp {interpret-message*apply*interpret-object-description*create*segment*learn-goal-predicate*setup
   (state <s> ^name interpret-message
              ^top-state.interaction.changes <changes>
              ^top-state.dialog-context <dc>
              ^operator <o>)
   (<dc> ^needs-goal-setup ^type game-learning ^learn-link)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^message <msg>)
-->
    (<dc> ^parsed-goal <msg>)
    (<msg> ^interpreted true)
   (<changes> ^outgoing-message <msg2>)
   (<msg2> ^type setup-goal
              ^complete-message true)
}

sp {interpret-message*apply*interpret-object-description*create*segment*learn-property-predicate
   (state <s> ^name interpret-message
              ^top-state.dialog-context <dc>
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^message <msg>)
   (<dc> ^type game-learning ^learning-predicate <lp>)
   (<lp> ^name <name>)
   (<msg> ^arg1.predicates.property <name>)
-->
  # (<dc> 
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-property-predicate
        ^parameters.message <msg>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type game)
}

###########################
# Purpose: equate-objects

sp {interpret-message*apply*interpret-object-description*create*segment*equate-objects
   (state <s> ^name interpret-message
              -^top-state.dialog-context.type game-learning
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^purpose equate-objects
        ^object <obj1>
        ^object2 <obj2>
        ^message <msg>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type equate-objects
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type object)
   (<params> ^object1 <obj1>
             ^object2 <obj2>)
}


###########################
# Purpose: learn-goal-definition

sp {interpret-message*apply*interpret-object-description*create*segment*learn-goal-definition
   (state <s> ^name interpret-message
              -^top-state.dialog-context.type game-learning
              ^operator <o>)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^message <msg>)
-->
   (<s> ^new-segment-info <info>
        ^mark-interpreted <msg>)
   (<info> ^purpose <p>
           ^originator instructor)
   (<p> ^type learn-goal-definition
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^learning-event.type goal)
}

sp {interpret-message*apply*interpret-object-description*learn-goal-definition*elaborate*action*segment
   (state <s> ^name interpret-message
              ^operator <o>
              ^current-purpose.parameters.action-operator <action>
              ^new-segment-info.purpose.parameters <params>)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition)
-->
   (<params> ^action-operator <action>)
}

sp {interpret-message*interpret-object-description*learn-goal-definition*elaborate*subclause*object1
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^subclause <sub>)
   (<sub> ^arg1 <obj>)
   (<obj> -^linked-to)
-->
   (<sub> ^object1 <obj>)
}

sp {interpret-message*interpret-object-description*learn-goal-definition*elaborate*subclause*object1*linked-to
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^subclause <sub>)
   (<sub> ^arg1 <obj>)
   (<obj> ^linked-to <link>)
-->
   (<sub> ^object1 <link>)
}

sp {interpret-message*interpret-object-description*learn-goal-definition*elaborate*relation*subclause*object2
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^subclause <sub>)
   (<sub> ^{ << arg2 predicate >>} <rel>)
   (<rel> ^2 <obj2>)
   (<obj2> -^linked-to)
-->
   (<rel> ^object2 <obj2>)
}

sp {interpret-message*interpret-object-description*learn-goal-definition*elaborate*subclause*relation*object2*linked-to
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^subclause <sub>)
   (<sub> ^{ << arg2 predicate >>} <rel>)
   (<rel> ^2 <obj2>)
   (<obj2> ^linked-to <link>)
-->
   (<rel> ^object2 <link>)
}

sp {interpret-message*apply*interpret-object-description*learn-goal-definition*elaborate*goal-predicate*state
   (state <s> ^name interpret-message
              ^operator <o>
              ^new-segment-info.purpose.parameters <params>)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^subclause <sub>)
   (<sub> ^object1 <obj>
          ^predicate.handle <pred-handle>
         -^predicate.{2} <any>)
-->
   (<params> ^goal-predicate <pred>)
   (<pred> ^type state
           ^handle <pred-handle>
           ^1 <obj>)
}

sp {interpret-message*apply*interpret-object-description*learn-goal-definition*elaborate*goal-predicate*relation
   (state <s> ^name interpret-message
              ^operator <o>
              ^new-segment-info.purpose.parameters <params>)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^subclause <sub>)
   (<sub> ^object1 <obj1>
          ^{ << arg2 predicate >>} <rel>)
   (<rel> ^handle <pred-handle>
          ^object2 <obj2>)
-->
   (<params> ^goal-predicate <pred>)
   (<pred> ^type relation
           ^handle <pred-handle>
           ^1 <obj1>
           ^2 <obj2>)
}

sp {interpret-message*apply*interpret-object-description*learn-goal-definition*elaborate*goal-predicate*relation*person
   (state <s> ^name interpret-message
              ^operator <o>
              ^new-segment-info.purpose.parameters <params>)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition
        ^subclause <sub>)
   (<sub> ^action-operator.handle heard
          ^agent <obj1>
          ^arg1 <obj2>)
-->
   (<params> ^goal-predicate <pred>)
   (<pred> ^type relation
           ^handle heard2
           ^1 <obj1>
           ^2 <obj2>)
}

sp {interpret-message*apply*interpret-object-description*learn-goal-definition*terminate*segment
   (state <s> ^name interpret-message
              ^operator <o>
	      		^current-purpose.satisfaction.dialog-event <de>)
   (<o> ^name interpret-object-description
        ^purpose learn-goal-definition)
   (<de> ^type goal-description
         ^originator instructor)
-->
   (<s> ^terminate-segment true)
}

sp {interpret-message*apply*interpret-object-description*remove*unknown-word*failure
   (state <s> ^name interpret-message
              ^operator <o>
              ^top-state.interaction.status <status>)
   (<o> ^name interpret-object-description
        ^predicate.spelling <word>)
   (<status> ^failure <f>)
   (<f> ^type unknown-word
        ^word <word>)
-->
   (<status> ^failure <f> -)
}

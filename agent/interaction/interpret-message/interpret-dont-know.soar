###################################################################
#
# interpret-dont-know
#   Occurs when the intructor wants to start the interaction over
#   Construction: *-dont-know-T
#
###################################################################

#######
# OPERATOR PROPOSAL
#######
sp {interpret-message*propose*interpret-dont-know
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^message-type dont-know
         -^interpreted true)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-dont-know
        ^message <msg>)
}

#####
# OPERATOR APPLICATION
#####

# Don't even need to do any special processing, just create a response
sp {interpret-message*apply*interpret-dont-know*mark
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name interpret-dont-know
        ^message <msg>)
-->
   (<msg> ^interpreted true)
}

sp {interpret-message*apply*interpret-dont-know*mark*stop-asking
   (state <s> ^name interpret-message
              ^operator.name interpret-dont-know
              ^top-state.interaction.stack.segment.purpose.type get-find-help
              ^top-state.task-stack.top <seg>)
-->
   (<seg> ^stop-asking true)
}

sp {interpret-message*apply*interpret-dont-know*missing-object*mark*stop-asking
   (state <s> ^name interpret-message
              ^operator.name interpret-dont-know
              ^message-context.type find-missing-object
              ^message-context.parameters <params>
              ^top-state.task-stack.top <seg>)
-->
   (<seg> ^stop-asking true)
   (<params> ^stop-asking true)
   (<s> ^terminate-segment true)
}

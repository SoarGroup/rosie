# Elaborations for top-state and substates

# name
# Name a state using the selected superstate operator's name
sp {all*elaborate*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

# top-state
# Copy direct top-state link to all substates
sp {top-state*elaborate*top-state
   (state <s> ^superstate nil)
-->
   (<s> ^top-state <s>)
}

#   This rules is not used.
#   It has been commented out here for a long time.
#   PL  5/15/2020
#duplicate of elaborate*state*top-state
#sp {all*elaborate*top-state*top-state
#   (state <s> ^superstate <ss>)
#   (<ss> ^top-state <ts>
#        -^operator.name evaluate-operator)
#-->
#   (<s> ^top-state <ts>)
#}

#   This rule used to live inside agent/language-comprehension in
#   rosie\agent\language-comprehension\comprehension
#   \Impasse__Operator_Tie\evaluate-operator\elaborations.soar
#   It is necessary to make game-search work.
#   It is being moved here to be readily available
#   regardless of the parser.
#   PL  5/15/2020
sp {elaborate*top-state*pass-down-evaluate
   (state <s> ^name evaluate-operator
              ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)
}


# io
# Copy the io link to all substates
sp {all*elaborate*io
   (state <s> ^superstate.io <io>
	      -^name selection
	      -^problem-space.state.dont-copy io
             -^name learn-param
             -^name setup-search
   	      -^name action-policy)
-->
   (<s> ^io <io>)
}

# agent-params
# Copy agent-params to all substates
sp {all*elaborate*agent-params
   (state <s> ^superstate.agent-params <ap>)
-->
   (<s> ^agent-params <ap>)
}
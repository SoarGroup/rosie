sp {lookup-smem*preposition*query
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^smem.command <sc>)
-->
   (<sc> ^query <q>)
   (<s> ^found-preposition <fprep>)
   (<fprep> ^type <type>)
   (<q> ^prep-name <type>)
}


sp {lookup-smem*preposition*query*failure
   (state <s> ^name lookup-preposition
              ^found-preposition <fprep>
              ^smem.result.failure <cue>)
-->
   (<fprep> ^stat default)
}

sp {lookup-smem*preposition*apply*complete-failure
   (state <s> ^name lookup-preposition
              ^superstate <ss>
             -^interaction true
              ^found-preposition <fprep>)
   (<fprep> ^stat default)
-->
   (<ss> ^found-prep <fprep>)
}

sp {lookup-smem*preposition*apply*complete-failure*after-interaction
   (state <s> ^name lookup-preposition
              ^superstate <ss>
              ^interaction true
              ^topstate.interaction.stack.segment.info.status.no-teaching true
              ^found-preposition <fprep>)
   (<fprep> ^stat default)
-->
   (<ss> ^found-prep <fprep>)
}

sp {lookup-smem*preposition*query*success
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^smem.result.success <cue>
              ^smem.result <res>)
   (<res> ^retrieved <ret>)
-->
   (<s> ^predicate <ret>
        ^status old)
}

sp {lookup-smem*preposition*propose*lookup-relations
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^predicate <p1>
              ^status old
             -^relation <rel>)
   (<p1> ^relation <rel>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-relations
        ^category svs
        ^lte <rel>)
}


sp {lookup-smem*preposition*propose*complete-lookup-relations
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^predicate <p1>
              ^status old)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-lookup-relations
        ^category svs)
}
sp {lookup-smem*preposition*copy*relations
   (state <s> ^name lookup-preposition
              ^found-preposition <fprep>
              ^relation <rel>
              ^status old)
-->
   (<fprep> ^relation <rel>)
}

sp {lookup-smem*preposition*copy*predicate
   (state <s> ^name lookup-preposition
              ^found-preposition <fprep>
              ^predicate <p1>
              ^status old)
   (<s> ^operator.name complete-lookup-relations)
-->
   (<fprep> ^predicate <p1>
            ^stat old)
}

sp {lookup-smem*preposition*apply*complete-lookup-relations
   (state <s> ^name lookup-preposition
              ^superstate <ss>
              ^found-preposition <fprep>
              ^status old)
   (<s> ^operator.name complete-lookup-relations)
   (<fprep> ^stat old)
-->
   (<ss> ^found-prep <fprep>)
}


#find ids for both objects then process in svs
sp {relation-question*propose*index-object
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
         -^info.obj2)
   (<p> ^type relation-question
        ^parameters.object2 <obj2>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^use-interaction true
        ^infer-match true
		^allow-ties true
        ^obj 2
        ^category object-processing
	    ^object <obj2>)
}


sp {relation-question*apply*index-object*results
   (state <s> ^operator <o>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
          ^info <i>)
   (<p> ^type relation-question)
   (<o> ^name index-object
        ^obj 2
        ^match.id <id>)
-->
   (<i> ^obj2 <id>)
}

sp {relation-question*apply*index-object*failure
   (state <s> ^operator <o>
              ^interaction.stack.segment.purpose.type relation-question
              ^io.output-link <out>
              ^topstate.status <status>)
   (<o> ^name index-object
        ^obj 2
        ^match failure)              
-->
   (<out> ^send-message <msg>)
   (<msg> ^type no-object
          ^fields.object failure)
   (<status> ^performed.action-event.type describe)
}

sp {relation-question*propose*process-svs-question
   (state <s> ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
          ^info.obj2 <id2>)
   (<p> ^type relation-question
       -^parameters.project
        ^parameters.prep <prep>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-svs-question
        ^category svs
	    ^message <m>)
   (<m> ^fields <f> ^type svs-query)
   (<f> ^objects <object>)
   (<object> ^obj <obja>
             ^type <prep>)
   (<obja> ^id <id2> ^index 2)
}

sp {relation-question*propose*process-svs-projection
   (state <s> ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
          ^info.obj2 <id2>)
   (<p> ^type relation-question
        ^parameters.project
        ^parameters.prep <prep>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-svs-projection
        ^category svs
	    ^message <m>)
   (<m> ^fields <f> ^type svs-project)
   (<f> ^objects <object>)
   (<object> ^obj <obja>
             ^type <prep>)
   (<obja> ^id <id2> ^index 2)
}

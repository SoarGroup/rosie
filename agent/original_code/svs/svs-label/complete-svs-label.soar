
sp {svs-label*propose*complete-svs-label*direct
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^smem.command <sc>
              ^superstate.superstate <sss>
              ^stat default
              ^label-svs <l>)
   (<l> ^result <r>)
   (<r> ^type <t> ^axis <axis> ^dist <dist>
        -^type x-aligned
        -^type y-aligned
        -^type z-aligned)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>)
-->
   (<p1> ^relation <r1>)
   (<r1> ^type <t> ^axis <axis> ^dist <dist> ^count 1 ^sum <dist> 
         ^squaredsum (* <dist> <dist>) ^avg <dist> ^top (* 2 <dist>) ^bottom 0)
   (<sc> ^store <r1>)
   (<s> ^operator <o> + =)
   (<o> ^name complete-svs-label
        ^category svs
        ^type new-add)
}

sp {svs-label*propose*complete-svs-label*all-aligned
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^smem.command <sc>
              ^superstate.superstate <sss>
              ^label-svs <l>)
   (<l> ^result.type x-aligned ^result.type y-aligned ^result.type z-aligned)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>)
   (<p1> ^all-aligned {<val> false})
-->
   (<s> ^operator <o> + =)
   (<o> ^name remove-relation
        ^remove <val>
        ^category svs)
}

sp {svs-label*apply*remove-relation
   (state <s> ^name svs-label
              ^predicate <p1>
              ^operator <o>)
   (<o> ^name remove-relation
        ^remove <val>)
-->
   (<p1> ^all-aligned <val> -)
   (<p1> ^all-aligned true)
}


sp {svs-label*propose*complete-svs-label*direct*old
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^smem.command <sc>
              ^superstate.superstate <sss>
              ^stat old
              ^label-svs <l>)
   (<l> ^result <r>)
   (<r> ^type <t> ^axis <axis> ^dist <dist>
        -^type x-aligned
        -^type y-aligned
        -^type z-aligned)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>
         ^relation <r1>)
   (<r1> ^axis <axis> -^type <t> -^dist <dist>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-to-relation
        ^relation <r1>
        ^dist <dist>
        ^type <t>
        ^category svs)
}

sp {svs-label*propose*complete-svs-label*direct*old3
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^smem.command <sc>
              ^superstate.superstate <sss>
              ^stat old
              ^label-svs <l>)
   (<l> ^result <r>)
   (<r> ^type <t> ^axis <axis> ^dist <dist>
        -^type x-aligned
        -^type y-aligned
        -^type z-aligned)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>
         ^relation <r1>)
   (<r1> ^axis <axis> -^type <t> ^dist <dist>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-to-relation
        ^relation <r1>
        ^type <t>
        ^category svs)
}

sp {svs-label*propose*complete-svs-label*direct*old2
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^smem.command <sc>
              ^superstate.superstate <sss>
              ^stat old
              ^label-svs <l>)
   (<l> ^result <r>)
   (<r> ^type <t> ^axis <axis> ^dist <dist>
        -^type x-aligned
        -^type y-aligned
        -^type z-aligned)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>
         ^relation <r1>)
   (<r1> ^axis <axis> ^type <t> -^dist <dist>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-to-relation
        ^relation <r1>
        ^dist <dist>
        ^category svs)
}
sp {svs-label*apply*add-to-relation
   (state <s> ^name svs-label
              ^smem.command <sc>
              ^predicate <p1>
              ^operator <o>)
   (<o> ^name add-to-relation
        ^relation <r1>
        ^dist <dist>
        ^type <t>)
   (<r1> ^count {<count> > 0} ^sum <sum> ^squaredsum <sqsum> ^avg <avg> ^top <top> ^bottom <bot>)
-->
   (<r1> ^count <count> -)
   (<r1> ^sum <sum> -)
   (<r1> ^avg <avg> -)
   (<r1> ^top <top> -)
   (<r1> ^bottom <bot> -)
   (<r1> ^squaredsum <sqsum> -)
   (<r1> ^dist <dist> 
         ^type <t>
         ^count (+ 1 <count>) 
         ^sum (+ <sum> <dist>) 
         ^squaredsum (+ <sqsum> (* <dist> <dist>))
         ^avg (/ (+ <sum> <dist>) (+ 1 <count>))
         ^top  (+ (/ (+ <sum> <dist>) (+ 1 <count>)) 
                  (* 1.5 (sqrt (- (/ (+ <sqsum> (* <dist> <dist>)) <count>) 
                           (* (/ (+ 1 <count>) <count>) (* (/ (+ <sum> <dist>) (+ 1 <count>))
                                                           (/ (+ <sum> <dist>) (+ 1 <count>))))))))
         ^bottom (- (/ (+ <sum> <dist>) (+ 1 <count>)) 
                  (* 1.5 (sqrt (- (/ (+ <sqsum> (* <dist> <dist>)) <count>) 
                           (* (/ (+ 1 <count>) <count>) (* (/ (+ <sum> <dist>) (+ 1 <count>))
                                                           (/ (+ <sum> <dist>) (+ 1 <count>)))))))))
    (<sc> ^store <r1>)
}


sp {svs-label*apply*add-to-relation*count-zero
   (state <s> ^name svs-label
              ^smem.command <sc>
              ^predicate <p1>
              ^operator <o>)
   (<o> ^name add-to-relation
        ^relation <r1>
        ^dist <dist>
        ^type <t>)
   (<r1> ^count {<count> 0} ^sum <sum> ^squaredsum <sqsum> ^avg <avg> ^top <top> ^bottom <bot>)
-->
   (<r1> ^count <count> -)
   (<r1> ^sum <sum> -)
   (<r1> ^avg <avg> -)
   (<r1> ^top <top> -)
   (<r1> ^bottom <bot> -)
   (<r1> ^squaredsum <sqsum> -)
   (<r1> ^dist <dist> 
         ^type <t>
         ^count (+ 1 <count>) 
         ^sum (+ <sum> <dist>) 
         ^squaredsum (+ <sqsum> (* <dist> <dist>))
         ^avg (/ (+ <sum> <dist>) (+ 1 <count>))
         ^top (* 2 <dist>)
         ^bottom 0)
    (<sc> ^store <r1>)
}

sp {svs-label*apply*add-to-relation*no-type
   (state <s> ^name svs-label
              ^smem.command <sc>
              ^predicate <p1>
              ^operator <o>)
   (<o> ^name add-to-relation
        ^relation <r1>
       -^type
        ^dist <dist>)
   (<r1> ^count <count> ^sum <sum> ^squaredsum <sqsum> ^avg <avg> ^top <top> ^bottom <bot>)
-->
   (<r1> ^count <count> -)
   (<r1> ^sum <sum> -)
   (<r1> ^top <top> -)
   (<r1> ^bottom <bot> -)
   (<r1> ^avg <avg> -)
   (<r1> ^squaredsum <sqsum> -)
   (<r1> ^dist <dist> 
         ^count (+ 1 <count>) 
         ^sum (+ <sum> <dist>) 
         ^squaredsum (+ <sqsum> (* <dist> <dist>))
         ^avg (/ (+ <sum> <dist>) (+ 1 <count>))
         ^top  (+ (/ (+ <sum> <dist>) (+ 1 <count>)) 
                  (* 1.5 (sqrt (- (/ (+ <sqsum> (* <dist> <dist>)) <count>) 
                           (* (/ (+ 1 <count>) <count>) (* (/ (+ <sum> <dist>) (+ 1 <count>))
                                                           (/ (+ <sum> <dist>) (+ 1 <count>))))))))
         ^bottom (- (/ (+ <sum> <dist>) (+ 1 <count>)) 
                  (* 1.5 (sqrt (- (/ (+ <sqsum> (* <dist> <dist>)) <count>) 
                           (* (/ (+ 1 <count>) <count>) (* (/ (+ <sum> <dist>) (+ 1 <count>))
                                                           (/ (+ <sum> <dist>) (+ 1 <count>)))))))))
#         ^stdev (sqrt (- (/ (+ <sqsum> (* <dist> <dist>)) <count>) 
#                            (* (/ (+ 1 <count>) <count>) (* (/ (+ <sum> <dist>) (+ 1 <count>))
#                                                            (/ (+ <sum> <dist>) (+ 1 <count>)))))))
   (<sc> ^store <r1>)
}

sp {svs-label*apply*add-to-relation*no-dist
   (state <s> ^name svs-label
              ^smem.command <sc>
              ^predicate <p1>
              ^operator <o>)
   (<o> ^name add-to-relation
        ^relation <r1>
        ^type <t>
       -^dist)
-->
   (<r1> ^type <t>)
   (<sc> ^store <r1>)
}

sp {svs-label*propose*complete-svs-label*direct*aligned
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^smem.command <sc>
              ^stat default
              ^superstate.superstate <sss>
              ^label-svs <l>)
   (<l> ^result <r>)
   (<r> ^type <t> << x-aligned z-aligned y-aligned >>
        ^axis <axis> ^dist <dist>)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>)
-->
   (<p1> ^relation <r1>)
   (<r1> ^type <t> ^axis <axis> ^count 0 ^sum 0 ^squaredsum 0 ^avg 0 ^top 0 ^bottom 0)
   (<sc> ^store <r1>)
   (<s> ^operator <o> + =)
   (<o> ^name complete-svs-label
        ^category svs
        ^type new-add)
}

sp {svs-label*propose*complete-svs-label*direct*aligned*old
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^smem.command <sc>
              ^stat old
              ^superstate.superstate <sss>
              ^label-svs <l>)
   (<l> ^result <r>)
   (<r> ^type <t> << x-aligned z-aligned y-aligned >>
        ^axis <axis> ^dist <dist>)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>
         ^relation <r1>)
   (<r1> ^axis <axis> -^type <t>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-to-relation
        ^relation <r1>
        ^type <t>
        ^category svs)
}


#sp {svs-label*complete-svs-label*simplify
#   (state <s> ^name svs-label
#              ^superstate.operator.message.fields.objects <objs>
#              ^superstate.superstate <sss>
#              ^topstate.filters.filter <rtype>
#              ^label-svs <l>)
#   (<objs> ^type <type>)
#   (<s> ^predicate <p1>)
#   (<p1> ^name <type>
#         ^obj <o1>
#         ^obj <o2>
#         ^obj <o3>)
#   (<o3> ^index <i3>)
#   (<o2> ^index <i2>)
#   (<o1> ^index <i1>)
#   (<o1> ^index < <i2>)
#   (<o2> ^index < <i3>)
#   (<p1> ^relation <r1> ^relation { <r2> <> <r1> } ^relation { <r3> <> <r1> })
#   (<p1> ^relation { <r3> <> <r2> })
#   (<r1> ^type <rtype>
#         ^a <i1>
#         ^b <i2>)
#   (<r2> ^type <rtype>
#         ^a <i2>
#         ^b <i3>)
#   (<r3> ^type <rtype>
#         ^a <i1>
#         ^b <i3>)
#-->
#   (<p1> ^relation <r3> -)
#}


#sp {svs-label*propose*remove-relation
#   (state <s> ^name svs-label
#              ^superstate.operator.message.fields.objects <objs>
#              ^superstate.superstate <sss>
#             -^topstate.track-relation.tracking-extract_once <E>
#              ^stat old)
#   (<sss> ^svs.command <cmd>)
#   (<sss> ^svs.spatial-scene <sc>)
#   (<objs> ^type <type>
#           ^obj <o1>
#           ^obj <o2>)
#   (<o2> ^index <i2> ^id <objb>)
#   (<o1> ^index <i1> ^id <obja>)
#   (<o1> ^index < <i2>)
#   (<cmd> ^extract_once <E>)
#   (<E> ^type <t> ^result.negative.atom <A>)
#   (<sc> ^child <C> ^child <C2>)
#   (<C> ^id <obja1> ^id-int <obja>)
#   (<C2> ^id <objb1> ^id-int <objb>)
#   (<A> ^a <obja1> 
#        ^b <objb1>)
#   (<s> ^predicate <p1>)
#   (<p1> ^name <type>
#         ^relation <r1>)
#   (<r1> ^type <t> ^a <i1> ^b <i2>)
#-->
#   (<s> ^operator <o> + =)
#   (<o> ^name remove-relation
#        ^remove <r1>
#        ^category svs)
#}

#sp {svs-label*apply*remove-relation
#   (state <s> ^name svs-label
#              ^predicate <p1>
#              ^operator <o>)
#   (<o> ^name remove-relation
#        ^remove <r1>)
#-->
#   (<p1> ^relation <r1> -)
#}



sp {svs-label*propose*complete-svs-label*old*default
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <obj>
              ^stat old)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-svs-label
        ^category svs
        ^type old)
}

sp {svs-label*propose*complete-svs-label*direct*false
   (state <s> ^name svs-label
              ^superstate.operator.message.fields.objects <objs>
              ^label-svs <l>)
   (<l> ^done true)
   (<objs> ^type <type>)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-svs-label
        ^category svs
        ^type false)
}


sp {svs-label*apply*complete-svs-label*direct*mark-processed
   (state <s> ^name svs-label
              ^operator.name complete-svs-label
              ^topstate.status <status>
              ^smem.command <sc>
             -^stat default
              ^superstate.operator.message.fields.objects <objs>
              ^io.output-link <ol>
              ^superstate <ss>)
   (<ss> ^operator.message <m>)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>)
   (<ss> ^superstate <sss>)
-->
   (<sc> ^store <p1>)
   (<status> ^performed.learning-event.type preposition)
   (<ol> ^send-message <msg>)
   (<msg> ^type confirmation)
}

sp {svs-label*apply*complete-svs-label*direct*mark-processed*new-prep
   (state <s> ^name svs-label
              ^operator.name complete-svs-label
              ^topstate.status <status>
              ^smem.command <sc>
              ^stat default
              ^prep-list <p>
              ^superstate.operator.message.fields.objects <objs>
              ^io.output-link <ol>
              ^superstate <ss>)
   (<ss> ^operator.message <m>)
   (<objs> ^type <type>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>) #todo change to prep-name?
   (<ss> ^superstate <sss>)
-->
   (<p> ^prep <type>)
   (<p1> ^prep-name <type>)
   (<sc> ^store <p1> ^store <p>)  
   (<status> ^performed.learning-event.type preposition)
   (<ol> ^send-message <msg>)
   (<msg> ^type confirmation)
}


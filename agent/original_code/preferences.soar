##########################----------Preference rules interpretation------------------########################

### setting preferences of kind interpretation wrt primitive actions
sp {preference*interpretation*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^kind << interpretation message-interpretation >>)
   (<op1> ^category << primitive-action sub-primitive-action composite-action object-processing svs smem communication natural-language indexing >>)
-->
   (<s> ^operator <op2> > <op1>)
}


##########################----------Preference rules wait-for-instructor wait-for-arm ------------------########################
### set preferences wait-for-instructor and others

sp {preference*waiting*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^name << wait-for-instructor wait-for-arm >>)
   (<op1> ^category << primitive-action sub-primitive-action composite-action >>)
-->
   (<s> ^operator <op2> > <op1>)
}

sp {preference*waiting*worse-than*category
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op1> ^category << object-processing indexing svs smem communication natural-language interaction-stack >>)
   (<op2> ^name << wait-for-instructor wait-for-arm >>)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {preference*waiting*worse-than*kind
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op1> ^kind << interpretation message-interpretation >>)
   (<op2> ^name << wait-for-instructor wait-for-arm >>)
-->
   (<s> ^operator <op1> > <op2>)
}


#########################-----------Preference rules category: object-processing----------------------#######################
# Note: object-processing and indexing have the same preference

### prefer interaction-stack operators over primitive-actions
sp {preference*object-processing*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category object-processing)
   (<op1> ^category << primitive-action sub-primitive-action composite-action >>)
-->
   (<s> ^operator <op2> > <op1>)
}

#########################-----------Preference rules category: indexing ----------------------------#######################

sp {preference*indexing*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category indexing)
   (<op1> ^category << primitive-action sub-primitive-action composite-action object-processing >>)
-->
   (<s> ^operator <op2> > <op1>)
}

#########################-----------Preference rules category: svs ----------------------#######################

sp {preference*svs*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category svs)
   (<op1> ^category << primitive-action sub-primitive-action composite-action object-processing indexing >>) #JK removed object-processing
-->
   (<s> ^operator <op2> > <op1>)
}


#########################-----------Preference rules category: smem ----------------------#######################

sp {preference*smem*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category smem)
   (<op1> ^category << primitive-action sub-primitive-action composite-action object-processing indexing svs >>)
-->
   (<s> ^operator <op2> > <op1>)
}

##########################----------Preference rules category: communication------------------########################

### setting preferences wrt primitive actions
sp {preference*communicate*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category communication)
   (<op1> ^category << primitive-action sub-primitive-action composite-action object-processing indexing svs smem >>)
-->
   (<s> ^operator <op2> > <op1>)
}


##########################----------Preference rules category: nl------------------########################

### setting preferences wrt nl and primitive actions
sp {preference*natural-language*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category natural-language)
   (<op1> ^category << primitive-action sub-primitive-action composite-action object-processing indexing svs smem communication >>)
-->
   (<s> ^operator <op2> > <op1>)
}

##########################----------Preference rules category: interaction-stack----------------------########################

### prefer interaction-stack operators over primitive-actions
sp {preference*interaction-stack*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category interaction-stack)
   (<op1> ^category << primitive-action composite-action object-processing indexing svs smem communication natural-language explanation >>)
-->
   (<s> ^operator <op2> > <op1>)
}

sp {preference*interaction-stack*better-than*interpretation
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category interaction-stack)
   (<op1> ^kind << interpretation message-interpretation >>)
-->
   (<s> ^operator <op2> > <op1>)
}


### prefer pop-segment to push-segment
sp {preference*pop-segment*push-segment
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name pop-segment)
   (<op2> ^name push-segment)
-->
   (<s> ^operator <op1> > <op2>)
}
   	 

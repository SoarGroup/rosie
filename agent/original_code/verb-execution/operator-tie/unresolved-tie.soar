### if the operator-tie cannot be resolved with the internal knowledge/reasoning mechanisms, write to topstate
### ask instructor to break operator-tie if the agent is not in explain problem-space; where it will learn an action

sp {topstate*operator-tie*propose*write-unresolved-tie-topstate
   (state <s> -^superstate.superstate.name explain
   	      ^name selection
	      -^learning-operator <any>
	      -^problem-space << lgsoar message-interpretation >>
          -^work-space << games >>
	      -^name preprocess-sentence)
-->
   (<s> ^operator <op> + >)
   (<op> ^name indicate-unresolved-tie)
   (dont-learn <s>)
}


### new

sp {operator-tie*apply*write-unresolved-tie-topstate
   (state <s> ^operator <op>
	      ^superstate <top>
	      ^topstate <top>)
   (<top> ^status <status>
   	  -^failed explanation)
   (<op> ^name indicate-unresolved-tie)
-->
   (<status> ^type unresolved-tie
   	     ^state-name topstate)
}

sp {operator-tie*apply*write-unresolved-ties-no-topstate
   (state <s> ^superstate <ss>
   	      ^topstate <top> <> <ss>
	      ^operator <op>)
   (<top> ^status <status>
   	  -^failed explanation)
   (<op> ^name indicate-unresolved-tie)
   (<ss> ^name <ssname>)
-->
   (<status> ^type unresolved-tie
   	     ^state-name <ssname>)
}

sp {operator-tie*apply*topstate*failed-explanation
   (state <s> ^topstate <top>
   	      ^superstate <top>
	      ^operator <op>)
   (<top> ^status <status>
   	  ^failed explanation)
   (<op> ^name indicate-unresolved-tie)
-->
   (<status> ^performed.learning-event <e>)
   (<e> ^type explanation
   	^information failure)
}
	      
 






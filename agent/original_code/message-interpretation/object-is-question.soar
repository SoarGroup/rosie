# Rules that handle when an object-is-question is sent by the user

# Proposal
sp {propose*push-object-is-question*object-question
   (state <s> ^topstate <s>
              ^instructor-message <msg>)
   (<msg> ^type object-is-question
         -^information.relation.questioned true
         -^processed)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-object-is-question
        ^category communication
        ^message <msg>)
}

# Elaborate useful information from the message to the operator
sp {elaborate*push-object-is-question*object
   (state <s> ^operator <o> +)
   (<o> ^name push-object-is-question
        ^message <msg>)
   (<msg> ^information.object <obj>)
-->
   (<o> ^object <obj>)
}

sp {elaborate*push-object-is-question*relation
   (state <s> ^operator <o> +)
   (<o> ^name push-object-is-question
        ^message <msg>)
   (<msg> ^information.relation <rel>)
-->
   (<o> ^relation <rel>)
}

sp {elaborate*push-object-is-question*questioned
   (state <s> ^operator <o> +)
   (<o> ^name push-object-is-question
        ^message <msg>)
   (<msg> ^information.object.questioned <q>)
-->
   (<o> ^questioned <q>)
}

sp {apply*push-object-is-question*object-is-question*no-relation
   (state <s> ^operator <o>)
   (<o> ^name push-object-is-question
        ^questioned <q>
        ^message <msg>
        ^object <obj>
       -^relation)
-->
   (<msg> ^purpose <p>)
   (<p> ^type object-is-question
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^dialog-event.type single-word-response)
   (<params> ^object <obj>
             ^questioned <q>)
}

sp {apply*push-object-is-question*object-is-question*relation
   (state <s> ^operator <o>)
   (<o> ^name push-object-is-question
        ^questioned <q>
        ^message <msg>
        ^relation <rel>)
   (<rel> ^p1.object <obja> ^p2.object <objb> ^word <prep>)
-->
   (<msg> ^purpose <p>)
   (<p> ^type object-is-question
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^dialog-event.type single-word-response)
   (<params> ^relation <rel2>
             ^questioned <q>)
   (<rel2> ^object1 <obja>
          ^object2 <objb>
          ^word <prep>)
}

sp {apply*push-object-is-question*mark*processed
   (state <s> ^operator <o>)
   (<o> ^name push-object-is-question
        ^message <msg>)
-->
   (<msg> ^processed true)
}
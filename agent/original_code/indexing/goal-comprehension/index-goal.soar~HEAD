sp {elaborate*state*index-goal
   (state <s> ^name index-goal
   	      ^superstate.operator <sop>)
   (<sop> ^name index-goal
   	  ^information <info>
	  ^indexed-operator <iop>
	 ^learning-event <le>)
   (<iop> ^_metadata.map-id <met>)
   (<met> ^goal <gid>)
   -->
   (<s> ^information <info>
   	^map-id <met>
	^goal-id <gid>
	^indexed-operator <iop>
	^bit flip
	^learning-event <le>)
}

### store goal-id with the verb schema
#sp {index-goal*store*goal-id
#   (state <s> ^name index-goal
#   	      ^map-id <met>
#	      ^smem.command <scomm>)
#-->
#   (<s> ^goal-id <gid>)
#   (<met> ^goal-id <gid>)
#   (<scomm> ^store <gid>
#   	    ^store <met>)
#}


### create goal predicates for all relations in the goal statement
sp {propose*create-goal-predicate*relation
   (state <s> ^name index-goal
   	      ^goal-id <gid>
	      ^information.relation <rel>
	      ^bit flip
	      ^indexed-operator <iop>
	      ^map-id <map>
	      ^learning-event <le>)
   (<le> -^completed <rel>)
   (<rel> ^word <pword>)
  -{(<s> ^_relation <trel>)
    (<trel> ^preposition <pword>
     	   ^_ltid <lid>)
    (<gid> ^<lid> <any>)}
-->
   (<s> ^operator <op> + =)
   (<op> ^name create-goal-predicate-relation
   	 ^goal-id <gid>
	 ^information <rel>
	 ^map-id <map>
	 ^indexed-operator <iop>
	 ^predicate-type relation)
}

sp {propose*create-goal-predicate
   (state <s> ^name index-goal
   	      ^goal-id <gid>
	      ^information.state-predicate <statp>
	      ^bit flip
	      ^indexed-operator <iop>
	      ^map-id <map>
	      -^success <statp>
	      ^learning-event <le>)
   (<le> -^completed <statp>)
   (<statp> ^word <sword>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name create-goal-predicate-state
   	 ^goal-id <gid>
	 ^information <statp>
	 ^map-id <map>
	 ^indexed-operator <iop>
	 ^predicate-type state)
}
   
   

### store goal predicates
sp {store*goal-predicate
   (state <s> ^name index-goal
   	      ^goal-id <gid>
	      ^smem.command <scomm>)
   (<gid> ^<predicate> <rel>)
-->
   (<scomm> ^store <rel> <gid>)
}

### result success
sp {write*status*topstate
   (state <s> ^name index-goal
   	      ^goal-id <gid>
	      ^information <info>
	      ^indexed-operator <iop>
	      ^learning-event <le>)
  -{(<le> -^completed <val>)
    (<info> ^<any> <val>)}
-->
   (<iop> ^goal <gid>)
}

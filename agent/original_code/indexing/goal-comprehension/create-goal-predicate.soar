#### retrieve goal-id, this state will retract if this knowledge already exists, no need to re-learn this knowledge
# sp {retrieve*goal-id
#    (state <s> ^name create-goal-predicate
#    	      ^smem.command <scomm>
# 	      ^superstate.operator.goal-id <gid>)
# -->
#    (<scomm> ^retrieve <gid>)
# }

### delete retrieve command
#sp {delete*retrieve
#   (state ^name create-goal-predicate
#   	  ^smem.command <scomm>	  ^superstate.operator.goal-id <gid>)
#-->
#   (<scomm> ^retrieve <gid>)
#}



#### create state
sp {elaborate*create-goal-predicate
   (state <s> ^name create-goal-predicate
   	      ^superstate <ss>)
   (<ss> ^operator <sop>)
   (<sop> ^name create-goal-predicate
   	  ^goal-id <gid>
	  ^information <pred>
	  ^map-id <map>
	  ^indexed-operator <iop>)
-->
   (<s> ^preposition-id <id>	
   	^information <pred>
	^map-id <map>
	^bit flip
	^indexed-operator <iop>
	^relation <rel>
	^goal-id <gid>)
}	


### elaborate preposition-id
sp {elaborate*preposition-id
   (state <s> ^preposition-id <id>
   	      ^information <info>)
   (<info> ^word <prep-word>)
-->
   (<id> ^word <prep-word>)
}

### index preposition
sp {propose*index-preposition*goal-predicate
   (state <s> ^name create-goal-predicate
   	      ^preposition-id.word <prep>
	      -^_relation.preposition <prep>)
 -->
   (<s> ^operator <op> +)
   (<op> ^name index-preposition
   	 ^preposition <prep>
	 ^category indexing)
}

sp {index-preposition*goal-predicate*write-to-superstate
   (state <s> ^name create-goal-predicate
   	      ^preposition-id.word <prep>
	      ^_relation <rel>
	      ^superstate <ss>)
   (<rel> ^preposition <prep>)
-->
   (<ss> ^_relation <rel>)
}

#### index arguments
sp {propose*index*argument
   (state <s> ^name create-goal-predicate
   	      ^information <info>
	      ^bit flip
	      ^preposition-id <id>
	      ^_relation.preposition)
   (<info> ^{<argattr> <> word} <obj>)
   (<obj> ^object <arg>)
   (<id> -^<argattr> <anything>
   	 ^word <prep>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-argument
   	 ^category indexing	
	 ^connector <argattr>
	 ^cue <id>
	 ^argument <arg>)
}


#### using dialog context, resolve the referent
#### improper dialog context

sp {create*relation*visual-objects
   (state <s> ^name create-goal-predicate
   	      ^preposition-id <id>
	      ^relation <rel>
	      ^indexed-operator <iop>
	      ^map-id.procedural <map>)
   (<id> ^{<arg> <> word} <obj>)
   (<iop> ^<op-arg> <obj>)
   (<map> ^<op-arg> <sobj>)
-->
   (<rel> ^<arg> <sobj>)
}

sp {create*relation*semantic-object
   (state <s> ^name create-goal-predicate
   	      ^preposition-id <id>
	      ^relation <rel>
	      ^indexed-operator <iop>
	      ^map-id.procedural <map>
	      ^smem.command <scomm>)
   (<id> ^{<arg> <> word} <obj>)
   (<iop> -^<op-arg> <obj>)
-->
   (<rel> ^<arg> <obj>)
   (<scomm> ^store <obj>)
}


sp {create*relation*semantic-object*substructure
   (state <s> ^name create-goal-predicate
	      ^relation <rel>
	      ^smem.command <scomm>)   
   (<rel> ^<arg> <obj>)
   (<scomm> ^store <obj>)
   (<obj> ^<oarg> <oval>)
   (<oval> ^<oval-arg> <ovval>)
-->
   (<scomm> ^store <oval>
   	    ^store <ovval>)
}



### write to the superstate
sp {write*superstate
   (state <s> ^superstate <ss>
   	      ^impasse no-change
	      ^attribute state
	      ^smem.command <scomm>)
   (<ss> ^name create-goal-predicate
   	 ^relation <rel>
	 ^goal-id <gid>
	 ^superstate <sss>
	 ^preposition-id.word <pword>
	 ^_relation <trel>)
   (<trel> ^preposition <pword>
   	  ^_ltid <cid>)
   (<rel> ^<sub> <structure>)
-->
   (<gid> ^predicate <rel>)
   (<rel> ^type <cid>
   	  ^g_prep_name <pword>)
   (<scomm> ^store <gid>
   	    ^store <rel>
	    ^store <cid>)
   (<sss> ^bit flip)
}


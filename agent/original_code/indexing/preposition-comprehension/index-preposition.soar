### map the preposition to a long-term id in semantic memory.
### mapping failure should result in preposition learning
### preposition 'to' is not a spatial-relationship, hence special handling.


sp {index*preposition*elaborate*state
   (state <s> ^name index-preposition
   	      ^superstate.operator <sop>)
   (<sop> ^name index-preposition
   	  ^preposition <p>)
-->
   (<s> ^preposition <p>)
}


sp {propose*index-preposition*lookup-preposition
   (state <s> ^name index-preposition
   	      ^preposition {<p> <> to}
	      -^found-prep <any>)
 -->
    (<s> ^operator <op> + >)
    (<op> ^name lookup-preposition
    	  ^type <p>
	  ^category svs
	  ^interaction true)
}

sp {create*_relation*structure
   (state <s> ^name index-preposition
   	      ^found-prep <cid>
	      ^preposition <p>)
   (<cid> ^predicate <pred>)
 -->
   (<s> ^_relation <rel>)
   (<rel> ^preposition <p>
   	  ^_ltid <pred>
	  ^type spatial)
}

sp {create*_relation*structure*to
   (state <s> ^name index-preposition
	      ^preposition to)
 -->
   (<s> ^_relation <rel>)
   (<rel> ^preposition to
   	  ^_ltid to
	  ^type duration)
}

sp {write*_relation*superstate
   (state <s> ^name index-preposition
   	      ^superstate <ss>
	      ^_relation <rel>)
-->
   (<ss> ^_relation <rel>)
}



sp {attend-to-scene*propose*create-visual-value
   (state <s> ^name attend-to-scene
              ^object-pair <op>
              ^parameters.create-delete-only false)
   (<op> ^il <il-obj>
         ^ts <ts-obj>)
   (<il-obj> ^visual-prop <il-prop>)
   (<il-prop> ^category <prop-name>
              ^{<label> <> category <> unknown})
   (<ts-obj> ^property <ts-prop>)
   (<ts-prop> ^name <prop-name>
             -^value.label <label>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-visual-value
        ^category object-processing
        ^level value
        ^property <ts-prop>
        ^value <label>)
}

sp {attend-to-scene*reject*create-visual-value*not-specified
   (state <s> ^name attend-to-scene
              ^parameters <p>
              ^operator <o> +)
   (<o> ^name create-visual-value
        ^property.name <name>)
   (<p> ^use-all-properties false
       -^use-property <name>)
-->
   (<s> ^operator <o> -)
}

sp {attend-to-scene*apply*create-visual-value
   (state <s> ^name attend-to-scene
              ^operator <o>)
   (<o> ^name create-visual-value
        ^property <ts-prop>
        ^value <label>)
-->
   (<ts-prop> ^value.label <label>)
}
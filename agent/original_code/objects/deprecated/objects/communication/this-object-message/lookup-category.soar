# Fetch the category for the given word from memory
sp {this-object-message*propose*lookup-smem*category
   (state <s> ^name this-object-message
              ^this-object {<obj> <> failure}
              ^nounjective <nj>)
   (<nj> -^category
          ^word <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^lookup-type category
        ^ask-user true
        ^query <q>
        ^desired.category true)
   (<q> ^type word2cat ^word <word> ^category <cat>)
}

# go through words in alphabetical order
sp {this-object-message*prefer*lookup-smem*category*lower-word
   (state <s> ^name this-object-message
              ^operator <o1> + <o2> +)
   (<o1> ^name lookup-smem
         ^lookup-type category
         ^query.word <w1>)
   (<o2> ^name lookup-smem
         ^lookup-type category
         ^query.word <w2> < <w1>)
   
-->
   (<s> ^operator <o1> < <o2>)
}

# Successful retrieval - single 
sp {this-object-message*apply*lookup-smem*category*success
   (state <s> ^name this-object-message
              ^operator <o>
              ^nounjective <nj>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result {<cat> <> failure}
       -^multiple-results)
   (<nj> ^word <word>)
-->
   (<nj> ^category <cat>)
}

# Failed retrieval
sp {this-object-message*apply*lookup-smem*category*failure
   (state <s> ^name this-object-message
              ^operator <o>
              ^nounjective <nj>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result failure)
   (<nj> ^word <word>)
-->
   (<nj> ^category failure)
}

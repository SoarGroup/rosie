# When there is a new object on the input link initialize a new object sturcture in working memory
sp {propose*lookup-object
   (state <s> ^topstate <s>
              ^io.input-link.objects.object.id <id>
             -^object.id <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-object
        ^category object-processing
        ^id <id>)
}

# Failed lookup, create a new object structure
sp {apply*lookup-object*object
	(state <s> ^operator <o>)
   (<o> ^name lookup-object
        ^match failure
        ^id <id>)
-->
   (<o> ^object <obj>)
}


# Adds the name to the object
sp {apply*lookup-object*name
   (state <s> ^operator <o>)
   (<o> ^name lookup-object
        ^match failure
        ^object <obj>)
-->
   (<obj> ^property <prop>)
   (<prop> ^name name
           ^type linguistic)
}

# Copies the id of the object
sp {apply*lookup-object*id
   (state <s> ^operator <o>)
   (<o> ^name lookup-object
        ^match failure
        ^object <obj>
        ^id <id>)
-->
   (<obj> ^id <id>
          ^wme-type object)
}

# Adds the object structure to the topstate
sp {apply*lookup-object*copy*object
   (state <s> ^operator <o>)
   (<o> ^name lookup-object
        ^{<< match object >>} {<obj> <> failure})
-->
   (<s> ^object <obj>)
}
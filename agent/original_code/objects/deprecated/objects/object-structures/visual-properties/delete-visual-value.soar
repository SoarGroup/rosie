# If a label disappears from the input link this will remove the corresponding value structure

# There is a label on the input-link not in working memory
sp {propose*delete-visual-value
   (state <s> ^topstate <s>
              ^object <obj>
              ^io.input-link.objects.object <io-obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <prop-name>
           ^type visual
           ^value <val>)
   (<val> ^label <label>)
   (<io-obj> ^id <id>
             ^visual-prop <vis-prop>)
   (<vis-prop> ^category <prop-name>
         		-^<label>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-visual-value
        ^category object-processing
        ^object <obj>
        ^property <prop>
        ^value <val>)
}

# Remove the value from the property
sp {apply*delete-visual-value*remove*value
   (state <s> ^operator <o>)
   (<o> ^name delete-visual-value
        ^property <prop>
        ^value <val>)
-->
   (<prop> ^value <val> -)
}
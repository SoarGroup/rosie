# elaborations.soar
# Elaborations used to construct the index-object substate including:
#   Creating a label-query for each object.property
# 	Copying parameter values (or creating default values if missing)
#	Modifying the final-matches structure with the result of the index-object

# If the object is the desired object from a relation, mark it desired
sp {index-object*elaborate*object*desired*relation
   (state <s> ^name index-object
              ^index-object-context <context>)
   (<context> ^object <obj>
              ^relation.object1 <obj>)
-->
   (<obj> ^desired true)
}

sp {index-object*elaborate*object*desired*no-relation
   (state <s> ^name index-object
              ^index-object-context <context>)
   (<context> ^object <obj>
             -^relation)
-->
   (<obj> ^desired true)
}

sp {index-object*elaborate*operator*default*context-id
   (state <s> ^operator <o>)
   (<o> ^name index-object
       -^context-id <> default)
-->
   (<o> ^context-id default)
}




# For each property of an object, create a label-query structure to 
# look up the label for that word

sp {index-object*elaborate*label-query
   (state <s> ^name index-object
              ^index-object-context.object.property <prop>)
   (<prop> ^word <word>
          -^label <label>)
-->
   (<s> ^label-query.word <word>)
}

# Elaborations for copying parameters from the top state

sp {index-object*elaborate*boolean*parameter*copy
   (state <s> ^name index-object
              ^superstate.operator <ss-op>)
   (<ss-op> ^{<par> << use-interaction allow-ties return-all context-id infer-match >>} <val>)
-->
   (<s> ^<par> <val>)
}

sp {index-object*elaborate*use-interaction*default
   (state <s> ^name index-object
              -^superstate.operator.use-interaction)
-->
   (<s> ^use-interaction false)
}

sp {index-object*elaborate*allow-ties*default
   (state <s> ^name index-object
              -^superstate.operator.allow-ties)
-->
   (<s> ^allow-ties false)
}

sp {index-object*elaborate*return-all*default
   (state <s> ^name index-object
              -^superstate.operator.return-all)
-->
   (<s> ^return-all false)
}

sp {index-object*elaborate*infer-match*default
   (state <s> ^name index-object
             -^superstate.operator.infer-match)
-->
   (<s> ^infer-match false)
}

# final-matches structure on the substate
sp {index-object*elaborate*final-matches
   (state <s> ^name index-object)
-->
   (<s> ^final-matches <fm>)
}

# If an exact match is given, then copy it right away
sp {index-object*elaborate*object-matches*exact*match
   (state <s> ^name index-object
              ^topstate.object <obj>
              ^index-object-context.object <i-obj>)
   (<i-obj> ^match-id <id>)
   (<obj> ^id <id>)
-->
   (<s> ^object-matches <matches>)
   (<matches> ^object <i-obj>
              ^match <obj>)
}

# Add the desired object to the final-matches
sp {index-object*elaborate*final-matches*desired
   (state <s> ^name index-object
              ^final-matches <fm>
              ^index-object-context.object <obj>)
   (<obj> ^desired)
-->
   (<fm> ^object <obj>)
}

# Object only - Copy the match from the identify-object 
sp {index-object*elaborate*final-match*no-relation
   (state <s> ^name index-object
              ^final-matches <fm>
              ^object-matches.match {<m> <> failure}
             -^index-object-context.relation)
-->
   (<fm> ^match <m>)
}

# If one of the identify-object queries resulted in failure, and
#   use-interaction is false, then the result is a failure
sp {index-object*elaborate*match*failure*use-interaction*false
   (state <s> ^name index-object
              ^final-matches <fm>
              ^index-object-context.use-interaction false
              ^object-matches <matches>)
   (<matches> -^match <> failure)
-->
   (<fm> ^match failure)
}

# If one of the identify-object queries resulted in failure, and
#	no-teaching has been set on that object, then the result is failure
sp {index-object*elaborate*match*failure*no-teaching
   (state <s> ^name index-object
              ^final-matches <fm>
              ^object-matches <matches>)
   (<matches> -^match <> failure
               ^object.no-teaching)
-->
   (<fm> ^match failure)
}

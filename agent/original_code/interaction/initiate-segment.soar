### initiate a segment in the interaction-stack by proposing a push-segment operator.


### propose push-segment (purpose determined by the agent)
sp {propose*push-segment*agent-purpose
   (state <s> ^status <status>)
   (<status> ^purpose <p>)
--> 
   (<s> ^operator <op> +)
   (<op> ^name push-segment
   	 ^originator agent
	 ^purpose <p>
	 ^category interaction-stack)
}

sp {apply*push-segment*agent-purpose
   (state <s> ^status <status>
   	      ^operator <op>
	      ^success-operator <op>)
   (<op> ^name push-segment)
-->
   (<s> ^status <status> -
   	^status <new-status>)
}



### propose push-segment (associate purpose to instructor dialog-event)

sp {propose*push-segment*instructor-purpose
   (state <s> ^instructor-message <m>)
   (<m> ^purpose <p>)
-->
   (<s> ^operator <op> +)
   (<op> ^name push-segment
   	 ^originator instructor
	 ^purpose <p>
	 ^category interaction-stack)
}

# AM: Automatically copy the message's context to the push-segment operator
sp {elaborate*push-segment*instructor-purpose*context
   (state <s> ^instructor-message <m>
              ^operator <o> +)
   (<o> ^name push-segment
        ^purpose <p>)
   (<m> ^context <context>)
-->
   (<p> ^context <context>)
}


sp {apply*push-segment*instructor-purpose
   (state <s> ^operator <op>
   	      ^instructor-message <m>
	      ^success-operator <op>)
   (<op> ^name push-segment)
-->
   (<s> ^instructor-message <m> -)
}
   	

### clean up
sp {topstate*clean-up
   (state <s> ^topstate <ts>
   	      ^success-operator <sop>
	      ^operator <op>)
   (<op> -^name push-segment)
-->
   (<s> ^success-operator <sop> -)
}
### put down an object in a location
sp {propose*put-down-object-location
   (state <s>  ^grabbed <obj>
	      ^object <loc>
       -^work-space << games >> #JK dont fire in game learning work space
	     -^problem-space << lgsoar message-interpretation >>
	     ^name {<any> <> evaluate-operator})
   (<loc> ^category location)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down-object-location
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^category primitive-action)
}

### if an operator is trying to achieve what track-relation has tracked to be success
sp {retract*put-down-object-location
   (state <s> ^operator <op>)
   (<op> ^name << op_put-down-object-location op_put-down-object-object >>
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^spatial-relationship <rel>)
   (<s> ^track-relation <tr>)
   (<tr> ^object1 <obj>
     	 ^object2 <loc>
	 ^complete success
	 ^vpredicate <rel>)
-->
  (<s> ^operator <op> -)
}


### control in close loop with SVS
## elaborate the operator state
sp {elaborate*op_put-down-object-location
   (state <s> ^name  << op_put-down-object-location op_put-down-object-object >>
   	      ^superstate <ss>
	      ^io
	      ^smem.command <scomm>)
   (<ss> ^operator <sop>)
   (<sop> ^name << op_put-down-object-location op_put-down-object-object >>
   	  ^argument1 <obj>
	  ^argument2 <loc>
	  ^spatial-relationship <rel>)
-->
   (<s> ^argument1 <obj>
   	^argument2 <loc>
	^spatial-relationship <rel>)
   (<scomm> ^retrieve <rel>)
}

## determine the location x,y,z using SVS
sp {propose*svs*find-projection
   (state <s> ^name << op_put-down-object-location op_put-down-object-object >>
   	      ^io
   	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      -^projection-result)
-->
   (<s> ^operator <op> + >)
   (<op> ^name find-projection
         ^interaction true
   	 ^a <obj>
	 ^b <loc>
	 ^predicate <rel>
	 ^category svs)
}

## set up tracking
## SM 06/17/2012: changed to work with tracking in the superstate
sp {set*svs*tracking
   (state <s> ^name << op_put-down-object-location op_put-down-object-object >>
   	      ^projection-result <res>
	      ^io
	      ^argument1 <obj>
	      ^argument2 <loc>
	      ^spatial-relationship <rel>
	      ^topstate <ts>)
   (<ts> ^object <obj>
         ^object <loc>)
-->
   (<ts> ^track-relation <relation>)
   (<relation> ^object1 <obj>
   	       ^object2 <loc>
	       ^predicate <rel>
	       ^vpredicate <rel>)
}

## put actions on the output-link
sp {elaborate*put-down*object*location*action
   (state <s> ^name << op_put-down-object-location op_put-down-object-object >>
   	      ^superstate <ss>
	      ^projection-result <res>)
   (<ss> ^operator <sop>)
   (<sop> ^name << op_put-down-object-location op_put-down-object-object >>)
-->
   (<sop> ^actions.put-down.location <res>		 
   	 ^category primitive-action)
   (<sop> ^kind non-atomic)
}



##############################################################################
# clean up track-result
sp {delete*track-result
   (state <s> ^topstate <s>
   	      ^track-relation <any>
   	      ^operator <op>)
   (<op> ^category << interaction-stack communication >>)
-->
   (<s> ^track-relation <any> -)
}


################################################################################
### action model : action model
sp {apply*put-down*location*action-model
   (state <s> ^operator <op>	
	     -^io
	     ^grabbed <obj>)
   (<op> ^name << op_put-down-object-location op_put-down-object-object >>
   	 ^argument1 <obj>
	 ^argument2 <loc>
	 ^spatial-relationship <rel>)
-->
   (<s> ^grabbed <obj> -
   	^track-relation <trel>)
   (<trel> ^object1 <obj>
          ^object2 <loc>
          ^predicate <rel>
          ^complete success
	  ^vpredicate <rel>)
}


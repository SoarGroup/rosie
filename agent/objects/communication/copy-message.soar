sp {propose*copy-message
   (state <s> ^topstate <s>
              ^io.input-link.message <m>
             -^processed <m>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-message-il
        ^category communication
        ^message <m>)
}

sp {apply*copy-message
   (state <s> ^operator <o>)
   (<o> ^name copy-message-il
        ^message <m>)
-->
   (<s> ^instructor-message <m>
        ^processed <m>)
}

sp {apply*copy-message*remove*processed
   (state <s> ^operator <o>
              ^processed {<pm> <> <m>})
   (<o> ^name copy-message-il
        ^message <m>)
-->
   (<s> ^processed <pm> -)
}

sp {apply*pop-segment*copy*context*to*instructor-message
   (state <s> ^topstate <s> 
              ^operator.name pop-segment
              ^interaction.stack.segment <seg>
              ^instructor-message <msg>)
        # AM: Changed the satisfaction structure
   (<seg> ^purpose.satisfaction.dialog-event.type <response-type>
          ^context <context>)
   (<msg> ^type <response-type>)
-->
   (<msg> ^context <context>)
}

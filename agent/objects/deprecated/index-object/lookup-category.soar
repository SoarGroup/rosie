# For each object.property, look up the category for the word
# Copies the category result onto the object and the label-query

# Fetch the category for the given word from memory
sp {index-object*propose*lookup-smem*category
   (state <s> ^name index-object
              ^index-object-context
              ^label-query <lq>
              ^use-interaction <ask>)
   (<lq> -^category
          ^word <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^ask-user <ask>
        ^lookup-type category
        ^query <q>
        ^desired.category true)
   (<q> ^type word2cat ^word <word> ^category <cat>)
}

# go through words in alphabetical order
sp {index-object*prefer*lookup-smem*category*lower-word
   (state <s> ^name index-object
              ^index-object-context
              ^label-query <lq>
              ^use-interaction <ask>
              ^operator <o1> + <o2> +)
   (<o1> ^name lookup-smem
         ^query.word <w1>)
   (<o2> ^name lookup-smem
         ^query.word <w2> < <w1>)
   
-->
   (<s> ^operator <o1> < <o2>)
}

# Copy the result
sp {index-object*apply*lookup-smem*copy*result
   (state <s> ^name index-object
              ^operator <o>
              ^label-query <lq>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result <result>
       -^multiple-results)
   (<lq> ^word <word>)
-->
   (<lq> ^category <result>)
}

# Copy the result to the object
sp {index-object*apply*lookup-smem*success*copy*result*object
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context.object.property <prop>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result {<result> <> failure <> generic}
       -^multiple-results)
   (<prop> ^word <word>)
-->
   (<prop> ^category <result>)
}

# Failed Retrieval and wont ask user, remove the property from the object
sp {index-object*apply*lookup-smem*category*failure*remove*property
   (state <s> ^name index-object
              ^operator <o>
              ^index-object-context <context>)
   (<context> ^object <obj>
              ^use-interaction false)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result << failure generic >>)
   (<obj> ^property <prop>)
   (<prop> ^word <word>)
-->
   (<obj> ^property <prop> -)
}
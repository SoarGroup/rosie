# Uses the confidence information to give a estimate of how accurate the visual label is

sp {elaborate*visual-properties*confidences*high
   (state <s> ^topstate <s>
              ^object <obj>
              ^io.input-link.objects.object <io-obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <cat-name>
           ^value <val>)
   (<val> ^label <label>)
   (<io-obj> ^id <id>
             ^visual-prop.category <cat-name>
             ^visual-prop.<label> {<conf> > .7})
-->
   (<val> ^confidence high)
}

sp {elaborate*visual-properties*confidences*medium
   (state <s> ^topstate <s>
              ^object <obj>
              ^io.input-link.objects.object <io-obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <prop-name>
           ^value <val>)
   (<val> ^label <label>)
   (<io-obj> ^id <id>
             ^visual-prop.category <cat-name>
             ^visual-prop.<label> {<conf> > .3 < .7})
-->
   (<val> ^confidence medium)
}

sp {elaborate*visual-properties*confidences*low
   (state <s> ^topstate <s>
              ^object <obj>
              ^io.input-link.objects.object <io-obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <prop-name>
           ^value <val>)
   (<val> ^label <label>)
   (<io-obj> ^id <id>
             ^visual-prop.category <cat-name>
             ^visual-prop.<label> {<conf> < .3})
-->
   (<val> ^confidence low)
}

# Decide when a label is the correct one (only one with high confidence)

sp {elaborate*object-labels*high-confidence
   (state <s> ^topstate <s>
              ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^value <v1>)
   (<v1> ^label <label>
         ^confidence high)
   -{(<prop> ^value <v2>)
     (<v2> ^label <> <label>
           ^confidence high)}
-->
   (<prop> ^label <label>)
}

sp {elaborate*object-words
   (state <s> ^topstate <s>
              ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^value <v1>
           ^label <label>)
   (<v1> ^label <label>
         ^word <word>)
-->
   (<prop> ^word <word>)
}

sp {elaborate*object-labels*medium
   (state <s> ^topstate <s>
              ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^value <v1>
          -^value <> <v1>)
   (<v1> ^label <label>
         ^confidence medium)
-->
   (<prop> ^label <label>)
}
# When a new label comes in on the input link this creates a value structure on the 
# object for the appropriate property (example, will add color-red label to the color property)

# There is a label on the input-link not in working memory
sp {propose*create-visual-value
   (state <s> ^topstate <s>
              ^io.input-link.objects.object <io-obj>
              ^object <obj>)
   (<io-obj> ^id <id>
             ^visual-prop <vis-prop>)
   (<vis-prop> ^{<label> <> category <> unknown} <conf>
          ^category <cat-name>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <cat-name>
          -^value.label <label>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-visual-value
        ^category object-processing
        ^property <prop>
        ^label <label>)
}

# Create the value structure on the operator
sp {apply*create-visual-value*init*value
   (state <s> ^operator <o>)
   (<o> ^name create-visual-value
        ^label <label>
        ^property <prop>)
-->
   (<prop> ^value.label <label>)
}
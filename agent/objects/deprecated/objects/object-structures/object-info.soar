# Elaborates Flexible structures on the object
# Includes pose and bbox

sp {elaborate*object*pose
   (state <s> ^topstate <s>
              ^io.input-link.objects.object <il-obj>
              ^object <obj>)
   (<il-obj> ^id <id>
             ^pose <pose>)
   (<obj> ^id <id>)
-->
   (<obj> ^pose <pose>)
}

sp {elaborate*object*bbox
   (state <s> ^topstate <s>
              ^io.input-link.objects.object <il-obj>
              ^object <obj>)
   (<il-obj> ^id <id>
             ^bbox <pose>)
   (<obj> ^id <id>)
-->
   (<obj> ^bbox <pose>)
}

# AM: Uncomment to automatically name locations
sp {elaborate*object*name
   (state <s> ^topstate <s>
              ^io.input-link.objects.object <il-obj>
              ^object <obj>)
   (<il-obj> ^id <id>
             ^name <name>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name name)
-->
   (<prop> ^word <name>)
}

sp {elaborate*object*name*no-name
   (state <s> ^topstate <s>
              ^object.property <prop>)
   (<prop> ^name name
          -^word <> object)
-->
   (<prop> ^word object)
}

sp {elaborate*object*shape-name
   (state <s> ^topstate <s>
              ^object <obj>)
   (<obj> ^property <shape-prop>
          ^property <name-prop>)
   (<name-prop> ^name name)
   (<shape-prop> ^name shape
                 ^word <word>)
-->
   (<name-prop> ^word <word>)
}
           
# FOCUS-STACK CANDIDATES

sp {index-object*propose*add-candidates*focus
   (state <s> ^name index-object
              ^object-to-index <obj>)
   (<obj> ^use-focus true
         -^did-focus)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-candidates
        ^category indexing
        ^candidate-type focus
        ^object-to-index <obj>)
}

sp {index-object*apply*add-candidates*focus
   (state <s> ^name index-object
              ^operator <o>)
   (<o> ^name add-candidates
        ^candidate-type focus
        ^object-to-index <obj>)
-->
   (<obj> ^did-focus true)
}

sp {index-object*apply*add-candidates*focus*add*candidates
   (state <s> ^name index-object
              ^operator <o>
              ^topstate.interaction.referents.objects.object <obj>)
   (<o> ^name add-candidates
        ^candidate-type focus
        ^object-to-index.candidates <c>)
-->
   (<c> ^candidate <candidate>)
   (<candidate> ^object <obj>
                ^pref 0)
}
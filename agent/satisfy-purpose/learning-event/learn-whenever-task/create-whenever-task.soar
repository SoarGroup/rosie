# When we have generalized the task and conditions, 
#   create a whenever-task structure and store it
sp {learn-whenever-task*propose*create-whenever-task
   (state <s> ^name learn-whenever-task
              ^generalized-task <task>
              ^generalized-conditions <conds>
             -^whenever-task)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-whenever-task
        ^task <task>
        ^conditions <conds>)
}

# Make the structure and mark it to store
sp {learn-whenever-task*apply*create-whenever-task
   (state <s> ^name learn-whenever-task
              ^operator <o>)
   (<o> ^name create-whenever-task
        ^task <task>
        ^conditions <conds>)
-->
   (<s> ^whenever-task <when>)
   (<when> ^handle (make-constant-symbol |whenever|)
           ^item-type whenever-task
           ^unlearned true
           ^task <task>
           ^conditions <conds>)
   (<task> ^handle (make-constant-symbol |whenever-task|))

   # Using task-utils/store-task-operator.soar to store the entire structure
   (<s> ^store-concept <when>
        ^store-task-operator <task>
        ^store-predicate-set <conds>)
}




# Delete any task objects that are not confirmed and are not on any other task segment
sp {learn-whenever-task*elaborate*old-objects
   (state <s> ^name learn-whenever-task
              ^top-state.world.objects.object <obj>
              ^top-state.task-stack.segments <segs>)
   (<obj> -^predicates.is-confirmed1)
  -{(<segs> ^segment { <other-seg> <> <seg> })
    (<other-seg> ^task-objects.object <obj>)}
-->
   (<s> ^created-object <obj>)
}

sp {learn-whenever-task*elaborate*created-object-info
   (state <s> ^name learn-whenever-task
              ^created-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^world-obj <obj>)
-->
   (<s> ^created-object-info <obj-info>)
}

sp {learn-whenever-task*propose*change-world-state*delete-object
   (state <s> ^name learn-whenever-task
              ^created-object-info <obj-info>
              ^whenever-task <when>
              ^stored <when>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name change-world-state
        ^delete-object <obj-info>)
}

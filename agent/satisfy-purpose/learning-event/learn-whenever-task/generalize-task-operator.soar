### generalize the task-operator
sp {learn-whenever-task*elaborate*generalize-task-operator
   (state <s> ^name learn-whenever-task
              ^task-concept-network <tcn>
              ^task-operator <task-op>)
   (<tcn> ^procedural <proc>)
-->
   (<s> ^generalize-task-operator <gto>)
   (<gto> ^task-operator <task-op>
          ^task-concept-network <tcn>)
}

# Copy the result onto the state as generalized-task
sp {learn-whenever-task*elaborate*generalize-task-operator*result*as*generalized-task
   (state <s> ^name learn-whenever-task
              ^generalize-task-operator.general-task-rep <gen-task>)
-->
   (<s> ^generalized-task <gen-task>)
}


### generalize the conditions
sp {learn-whenever-task*elaborate*generalize-task-argument*conditions
   (state <s> ^name learn-whenever-task
              ^conditions <conds>
              ^task-concept-network <tcn>
              ^generalized-task <gen-task>)
-->
   (<s> ^generalize-task-argument <gen-arg>)
   (<gen-arg> ^arg-type predicate-set
              ^type task-conditions
              ^instantiation <conds>)
}

# Elaborate the reference task-operator and TCN used by generalize-task-argument
sp {learn-whenever-task*elaborate*generalize-task-argument*goal-conditions*reference-info
   (state <s> ^name learn-whenever-task
              ^task-operator <task-op>
              ^generalized-task <gen-task>)
-->
   (<s> ^reference-task-op <task-op>
        ^reference-tcn-proc <gen-task>)
}

# Elaborate the result as generalized-conditions
sp {learn-whenever-task*elaborate*generalize-task-argument*generalized-conditions
   (state <s> ^name learn-whenever-task
              ^generalize-task-argument <gen-arg>)
   (<gen-arg> ^type task-conditions
              ^general-arg-rep <gen>)
-->
   (<s> ^generalized-conditions <gen>)
}

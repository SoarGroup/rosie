
# If there is an applied operator that matches the one we are learning, mark as learned
sp {setup-whenever-state*apply*whenever-task*mark*learned-proposal*true
   (state <s> ^name setup-whenever-state
              ^whenever-task.handle <when-h>
              ^operator <o>)
   (<o> ^whenever-handle <when-h>)
-->
   (<s> ^learned-proposal true)
}

sp {setup-whenever-state*reject*whenever-task*once*learned
   (state <s> ^name setup-whenever-state
              ^whenever-task.handle <when-h>
              ^operator <o> +
              ^learned-proposal true)
   (<o> ^whenever-handle <when-h>)
-->
   (<s> ^operator <o> -)
}

# Once the rule is learned, return
sp {setup-whenever-state*propose*complete-setup-whenever-state
   (state <s> ^name setup-whenever-state
              ^learned-proposal true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name complete-setup-whenever-state)
}

sp {setup-whenever-state*apply*complete-setup-whenever-state
   (state <s> ^name setup-whenever-state
              ^operator.name complete-setup-whenever-state
              ^superstate <ss>)
-->
   (<ss> ^learned-proposal true)
}
